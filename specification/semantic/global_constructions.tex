\section{Modules}%
\label{sec:modules}

A module is a first-level symbol that can contain other types of symbols. Any
other type of symbol is necessarily contained within a module.

\subsection{File module}

A source file declares a module symbol. Its name can be specified explicitly
with the keyword \texttt{in} as the first line of code. The purpose of this line
is for documentation purposes only, any comment above this line is considered
part of the documentation of the module symbol. The documentation syntax is
described in Chapter~\ref{chap:documentation}. The name of the module must
be the same as the file it is described in. For example, the file
\textit{foo.yr} should start with the line \texttt{in foo;}. This line is
optional.


\begin{lstlisting}[style=coloredverbatim]
/**
 * Documentation of the /foo/ module.
 * @Authors: John Doe
 * @Licence: GPLv3
 */
in foo;
\end{lstlisting}

A module can enclose other modules with the keyword \texttt{mod}. The name of
the enclosed module follows the keyword, and must be the name of the file being
enclosed. It can be a sibling file, or a child file (in the subdirectory with
the same name as the parent module). If both files exist (child and sibling),
the compiler will generate an error. Depending on whether they are child files
(in a directory whose name is the name of the enclosing module) or sibling
files, enclosed modules are either child or sibling. For example, assuming the
file hierarchy shown in Figure~\ref{fig:file_hierarchy}, the module located in
the file \textit{./foo/bar.yr} and enclosed by the module \textit{./foo.yr} will
be named \texttt{foo::bar} whereas the module located in \textit{./baz.yr}, even
if enclosed by the module \texttt{foo}, will be named \texttt{baz}. A complete
example is given below for the file hierarchy shown in
figure~\ref{fig:file_hierarchy}.

\input{images/file_hierarchy}

\noindent 1) Root directory :

\vspace{-5pt}%
\begin{minipage}[t][][t]{0.47\linewidth}
\begin{lstlisting}[caption=\textit{./foo.yr}, style=coloredverbatim]
in foo;

mod baz;
pub mod bar;
mod qux;

\end{lstlisting}
\end{minipage}\hspace{5pt}%
\begin{minipage}[t][][t]{0.47\linewidth}
\begin{lstlisting}[caption=\textit{./baz.yr}, style=coloredverbatim]
in baz;

pub fn pubFuncInBaz () {}
fn prvFuncInBaz () {}
\end{lstlisting}
\end{minipage}


\noindent 2) Subdirectory \textit{foo/}:

\vspace{-5pt}%
\begin{minipage}[t][][t]{0.47\linewidth}
\begin{lstlisting}[caption=\textit{./foo/bar.yr}, style=coloredverbatim]
in bar;

pub fn pubFuncInBar () {}
fn prvFuncInBar () {}
\end{lstlisting}
\end{minipage}\hspace{5pt}%
\begin{minipage}[t][][t]{0.47\linewidth}
\begin{lstlisting}[caption=\textit{./foo/qux.yr}, style=coloredverbatim]
in qux;

pub fn pubFuncInQux () {}
fn prvFuncInQux () {}
\end{lstlisting}
\end{minipage}


To compile all the sources of the above example, only one file has to be passed
to the compiler, here the file \textit{./foo.yr}. All inner modules declared in
the \texttt{foo} module will be reached and validated by the compiler. A file
hierarchy of source files is called a package, and always starts with a root
file. This is the main reason for sibling modules, as only one file is the entry
point of a package, whereas sometimes multiple root modules would be needed. The
following bash command line is used to compile the package. The
\texttt{-fdump-ymir} option is used to dump files representing what the compiler
generated at each stage, and \texttt{-nostdinc} to not automatically include the
standard library and runtime.

\begin{lstlisting}[style=intermediateVerb]
$ gyc foo.yr -fdump-ymir -nostdinc
\end{lstlisting}

The file \textit{foo.yr.ydump-decls.1} shows the symbol tree declared during the
declaration phase (before validation, so without template symbols -
\textit{foo.yr.ydump-decls.2} contains all declared symbols after validation, so
including the templates).

\begin{lstlisting}[style=intermediateVerb]
baz
    baz::prvFuncInBaz
    pub baz::pubFuncInBaz
pub foo
    pub foo::bar
        foo::bar::prvFuncInBar
        pub foo::bar::pubFuncInBar
    foo::qux
        foo::qux::prvFunctionInQux
        pub foo::qux::pubFunctionInQux
\end{lstlisting}

\subsection{Local module}

A module can be declared directly inside another module without using another
file. In this case, the module is considered a child module, and therefore the
result is identical to a file module located in a subdirectory. For example, the
\texttt{foo} module described above could have been written as follows. The
generated symbol tree is exactly the same.

%\begin{minipage}{\linewidth}
\begin{lstlisting}[caption=\textit{./foo.yr}, style=coloredverbatim]
in foo;

mod baz;

pub mod bar {
  pub fn pubFuncInBar () {}
  fn prvFuncInBar () {}
}

mod qux {
  pub fn pubFuncInQux () {}
  fn prvFuncInQux () {}
}
\end{lstlisting}
%\end{minipage}

\subsection{Protection}

The keywords \texttt{pub} (for public) and \texttt{prv} (for private) are used
to define the protection of a symbol. By default, a symbol is private. A private
symbol is accessible only by the module that declares it and by the other child
symbols declared in the same module. In other words, a module has access to all
the symbols it has declared and all the symbols declared by its parents. But it
does not have access to the private symbols declared by its child modules, or
those declared by siblings or cousins.

To clarify the situation, let's look at the symbol lookup algorithm. This
algorithm always starts with the symbol requesting access, then looks at its
siblings, then at the parent symbol and its siblings, and then at the
grandparents and their siblings. For example, if the symbol
\texttt{foo::bar::pubFuncInBar} makes a symbol lookup request, then three levels
of symbols are visible as described in the figure~\ref{fig:symbol_privacy}. In
addition to these levels, public symbols declared within visible symbols are
also visible, recursively.

\input{images/symbol_privacy}

As a result, the symbol \texttt{foo::bar::pubFuncInBar} has access to the symbol
\texttt{foo::qux::pubFuncInQux}, even if the symbol \texttt{qux} is declared
private by the parent module \texttt{foo}. On the other hand, the symbol
\texttt{foo::bar::pubFuncInBar} does not have access to
\texttt{foo::qux::prvFuncInQux}, nor does the symbol \texttt{baz} have access to
the module \texttt{foo::qux}.

\subsection{Package importation}

As we have seen, a package is compiled by passing the path to its root file to
the compiler. But a package may depend on other packages, which need to be
imported in order to use the symbols they describe. This is done by using the
compiler's \texttt{-I} option to import an external package and declare its
symbols in the symbol table. All symbols imported by this method are not
validated by the compiler (except for template symbols which are generated on
invocation - see Chapter~\ref{chap:templates}). So they have to be validated
manually and linked during symbol linking.

In the following example, let's consider two packages, the package \texttt{foo},
located in the directory \textit{/home/alice/mypackage/foo.yr}, which declares a
sub-module \texttt{bar}, and a second package \texttt{qux}, located in the path
\textit{/home/alice/external/qux.yr}, and which declares a module \texttt{baz}.

\begin{lstlisting}[caption=\textit{/home/alice/mypackage/foo.yr}, style=coloredverbatim]
in foo;

mod bar {
  pub fn tryAccessToBaz () {
    qux::baz::funcInBaz ();
  }
}
\end{lstlisting}

\begin{lstlisting}[caption=\textit{/home/alice/external/qux.yr}, style=coloredverbatim]
in qux;

pub mod baz {
  pub fn funcInBaz () {
    std::io::println ("Success !");
  }
}
\end{lstlisting}

In order for the module \texttt{foo::bar} to access the module
\texttt{qux::baz}, the following command line must be written. It will declare
the symbol \texttt{qux} as a sibling module of the root module \texttt{foo} and
thus with the same privacy protection as described above. Therefore, the module
\texttt{qux::baz} must be declared public to be accessible by the module
\texttt{foo} and its children.

\begin{lstlisting}[style=intermediateVerb]
$ gyc foo.yr -I ../external/qux.yr
\end{lstlisting}

Because the \texttt{qux} package is not validated, the above command will result
in a linking error stating that the symbol \texttt{qux::baz::funcInBaz} was not
found. This is a linking error, not a validation error. To correct this error,
the \texttt{qux} package must first be validated and then linked when compiling
the \texttt{foo} package.

\begin{minipage}{\linewidth}
\begin{lstlisting}[style=intermediateVerb]
$ cd /home/alice/external
$ gyc qux.yr -c // -c to create an object .o file
$ cd ../mypackage
$ gyc foo.yr -I ../external/qux.yr ../external/qux.o // add qux.o for the symbol linking
\end{lstlisting}
\end{minipage}

\textbf{Note:} The symbol \texttt{qux::baz::funcInBaz} has access to the symbol
\texttt{std::io::println} because the compiler includes the standard library
package by default. This import follows the same rules as any other package
import, and this package could be imported manually using the \texttt{-I}
option, followed by the path to the standard library installation, and
\texttt{-nostdinc} to disable automatic import. This can be useful for testing
new or custom versions of the standard library. For more information on the
standard library and runtime, see Chapter~\ref{chap:std_and_core_runtime}.

\subsection {Using a module}

As you may have noticed, the full path of symbols has to be written to access
them, for example in \texttt{foo::bar:tryAccessToBaz} the access to the symbol
\texttt {qux::baz::funcInBaz} was in full letter. This can be cumbersome, so the
construction \texttt{use} was introduced. This construction is followed by the
ymir path of a module, in order to describe that a symbol name written in the
current context can come from this module. For example, let's say you need to
use the \texttt{println} often, then the \texttt{use std::io;} construction can
be useful. The use construction is enclosed in the symbol that makes the
statement and its children.

%\begin{minipage}{\linewidth}
\begin{lstlisting}[caption=\textit{./foo.yr}, style=coloredverbatim]
in foo;

mod bar {
  use qux::baz;
  use std::io;

  pub fn tryAccessToBaz () {
    funcInBaz ();
    println ("io without full name");
  }
}


fn funcInFoo () {
  std::io::println ("Need full name, foo did not use std::io");
}
\end{lstlisting}
%\end{minipage}

The declaration dump file \textit{foo.yr.ydump-decls.1} contains the list of
modules used within a given symbol. The core modules are the modules that are
automatically imported and used in every module when the \texttt{-nostdinc}
option is not used. They contain symbols used by the runtime and referenced by
the compiler to perform high-level operations (such as deep copies of slices,
exception throwing, etc.). More information about the runtime and the standard
library is presented in Chapter~\ref{chap:std_and_core_runtime}.

\begin{minipage}{\linewidth}
\begin{lstlisting}[caption=\textit{foo.yr.ydump-decls.1}, style=intermediateVerb]
pub foo - use {core, core::array, core::range, core::exception, core::typeinfo, core::duplication, core::math}
  foo::bar - use {core, core::array, core::range, std::io, qux::baz, core::exception, core::typeinfo, core::duplication, core::math}
      pub foo::bar::tryAccessToBaz
  foo::funcInFoo
\end{lstlisting}
\end{minipage}

The path described in a \texttt{use} statement can be more complex, describing a
tree of modules to use. For example, you may want to use the modules
\texttt{std::io}, \texttt{std::fs::path}, and \texttt{std::fs::file}. This can
be done in a single line.

\begin{lstlisting}[style=coloredverbatim]
in foo;

use std::{io, fs::{path, file}};
\end{lstlisting}

\subsubsection{Use statement locality}

As explained above, \texttt{use} statements are enclosed by the symbol that
makes them and by its children. However, in addition to this, use statements are
local to a file, meaning that child modules declared in files other than their
parent module don't have access to the use statements of their parent modules.
This is enforced to avoid cluttering up child modules with use statements they
may not need or even want, and to make use statements clearer since they're
always in the same file as the file they affect. As a result, there is a slight
difference between a child module declared locally in the same file, and a child
module declared in a subdirectory file.
\smallskip

1) When using a single file:

\begin{lstlisting}[caption=\textit{./foo.yr}, style=coloredverbatim]
in foo;
use std::io;

mod bar {
  fn inBar () {
    println("In bar."); // ok, with the use statement from /foo/.
  }
}
\end{lstlisting}

2) When using two separate files:

\begin{minipage}[t][][t]{0.3\linewidth}
\begin{lstlisting}[caption=\textit{./foo.yr}, style=coloredverbatim]
in foo;
use std::io;

mod bar;
\end{lstlisting}
\end{minipage}%
\hfill%
\begin{minipage}[t][][t]{0.65\linewidth}
\begin{lstlisting}[caption=\textit{./foo/bar.yr}, style=coloredverbatim]
in bar;

fn inBar () {
  println("In bar."); // error, parent use statement is hidden
}
\end{lstlisting}
\end{minipage}%
\smallskip


\section{Functions}%
\label{sec:functions}

A function is a piece of code that can be called to perform a defined behavior.
A function is declared using the keyword \texttt{fn} followed by the name of the
function to be described. A function is enclosed in a module like any other
global symbol.

\subsection{Parameters}

The parameters of a function are declared in parentheses after the function
identifier. Parameters serve as the input of the function, allowing to pass data
to the function when calling it. A parameter declaration follows the syntax
\texttt{ident : T}, where \texttt{ident} is the parameter's identifier and
\texttt{T} is its type. Parameters are separated by a comma. The lifetime of the
parameters are limited to the scope of the function body.

\begin{lstlisting}[style=coloredverbatim]
fn foo (a : i32, b : i32) {
  std::io::println (a, " ", b);
}
\end{lstlisting}

The type of the parameter is mandatory, but its identifier can be the token
\texttt{\_} to define the the parameter is not used by the function. If it can
seem useless, it has its purpose for function overriding (for example in class
method override, cf. Chapter~\ref{chap:custom_types}).

\subsubsection {Calling a function}

A function is called using the syntax \texttt{ident (arg1, arg2, ...)}, where
\texttt{ident} is the identifier of the function to call (path to the function
symbol), followed by a list of arguments enclosed in parentheses and separated
by commas. A function argument is a value passed either positionnally or by
keyword as the value to be taken by the parameter of the function when called.
To construct a keyword argument, the parameter identifier is used and associated
with a value using the \texttt{->} token. The following example shows the call
of the function \texttt{foo} using both possibilities.

\begin{lstlisting}[style=coloredverbatim]
foo (1, 2); // positional /a/, then /b/

foo (b-> 2, 1); // keyword for /b/, then position for /a/
foo (b-> 2, a-> 1); // keyword for both parameters
\end{lstlisting}

A keyword argument can be placed anywhere in the argument list. The argument
parameter assignment algorithm starts by removing the keyword arguments from the
equation and locking their assignment, and then iterates over the parameters
that do not yet have an assignment to assign them to the positional arguments.
The result of all the above calls is the same, and produce the following Ymir
Intermediate Language code (for the function \texttt{foo} in the module
\texttt{main}).

\begin{lstlisting}[style=intermediateVerb]
_Y4main3fooFi32i32Zv(1, 2);
_Y4main3fooFi32i32Zv(1, 2);
_Y4main3fooFi32i32Zv(1, 2);
\end{lstlisting}

\textbf{Important note:} the arguments of the function are constructed in the order of
the parameters, and not in the order they appear in the function call. For
example, if the function \texttt{foo} is called as described in the following
source code, the function \texttt{bar} will always be called before the function
\texttt{baz}. If the order of construction of the arguments is defined and
predictible, it is not a good habit to count on it, and the use of intermediate
variables is strongly recommanded to avoid any errors.

\begin{lstlisting}[style=coloredverbatim]
fn bar ()-> i32 {
  println ("In bar");
  12
}

fn baz ()-> i32 {
  println ("In baz");
  15
}

foo (a-> bar (), b-> baz ());
foo (b-> baz (), bar ());
// if the above code perform the same order of operations, it is recommanded to make it clear

let x = bar ();
let y = baz (); // it is more clear that bar is called before baz
foo (a-> x, b-> y);
\end{lstlisting}

The above source code produces the following Ymir Intermediate Language. At this
level, there is no difference between the three calls, but the third one is
preferred for code clarity.

\begin{lstlisting}[style=intermediateVerb]
// variable declarations ...
YI_1(#1) = _Y4main3barFZi32();
YI_2(#2) = _Y4main3bazFZi32();
_Y4main3fooFi32i32Zv(YI_1(#1), YI_2(#2)); // first call
YI_3(#3) = _Y4main3barFZi32();
YI_4(#4) = _Y4main3bazFZi32();
_Y4main3fooFi32i32Zv(YI_3(#3), YI_4(#4)); // second call
x(#5) = _Y4main3barFZi32();
y(#6) = _Y4main3bazFZi32();
_Y4main3fooFi32i32Zv(x(#5), y(#6)); // third call
\end{lstlisting}

\subsubsection {Uniform call syntax}

The Uniform Call Syntax (\textit{UCS}) allows to call any function with the same
syntax as method calls (see Chapter~\ref{chap:custom_types}). The primary use of
this syntax is to chain calls and provide pipe handling to a value by placing
the first argument (associated with the first parameter) before the function
name and concatenating it with the \texttt{.} token. There is no need to change
the function definition to allow for \textit{UCS}.

\begin{lstlisting}[style=coloredverbatim]
fn add (a : i32, b : i32)-> i32 {
  a + b
}

let a = 12;
let b = a.add (13).add (125); // rewritten into add (add (a, 13), 125)
\end{lstlisting}

\textit{UCS} calls are simply rewritten by placing the left operand of the
binary operation \texttt{.} as the first positional argument of the call. It is
impossible to use a keyword argument as this left operand. The above source code
produces the following Ymir Intermediate Language result.

\begin{lstlisting}[style=intermediateVerb]
let a(#1) : i32;
let YI_2(#2) : i32;
let b(#3) : i32;
a(#1) = 12;
YI_2(#2) = _Y4main3addFi32i32Zi32(a(#1), 13);
b(#3) = _Y4main3addFi32i32Zi32(YI_2(#2), 125);
\end{lstlisting}

\subsubsection {Optional parameters}

A parameter within a function can have a default value, making it optional to
provide an argument when calling the function. This default value is specified
by appending the \texttt{=} token after the parameter type, followed by the
desired value. The optional parameter can be placed anywhere in the parameter
list.

\begin{lstlisting}[style=coloredverbatim]
fn foo (a : i32 = 12, b : i32) {
  std::io::println (a, " ", b);
}
\end{lstlisting}

To call the \texttt{foo} function described in the above code, only one argument
is needed to associate it with the \texttt{b} parameter. The value of the
parameter can be changed using a keyword argument.

\begin{lstlisting}[style=coloredverbatim]
foo (3); // defining parameter /b/

foo (3, a-> 18); // defining parameter /a/ and /b/
foo (a-> 18, 3); // defining parameter /a/ and /b/
\end{lstlisting}

If no other value is associated with an optional parameter, its default value is
constructed at the place of the call. This means that for a complex default
value (for example, a function call), the value is constructed before entering
the function. For example, let's look at the following code where the parameter
\texttt{a} has a default value constructed by calling the function \texttt{bar},
then the function \texttt{bar} is called before the function \texttt{foo} when
calling the function \texttt{foo}. The result is described in the YIL code
below.

\begin{lstlisting}[style=coloredverbatim]
fn bar ()-> i32 {
  println ("In bar.");
  12
}

fn foo (a : i32 = bar ()) {
  println ("In foo : ", a);
}

foo ();
\end{lstlisting}

\begin{lstlisting}[style=intermediateVerb]
let YI_1(#1) : i32;
YI_1(#1) = _Y4main3barFZi32();
_Y4main3fooFi32Zv(YI_1(#1));
\end{lstlisting}

The default value of an optional parameter can refer to a symbol that is
accessible within the context of the function for which it is a parameter, but
because the function has not yet been entered and because it would create
complex parameter order dependencies, this value cannot refer to the other
parameters of the function.

\begin{lstlisting}[style=coloredverbatim]
mod bar {
  pub fn foo (a : i32 = prvInBar ()) {
    println (a);
  }

  fn prvInBar ()-> i32 {
    12
  }
}

bar::foo (); // ok, no need to have access to prvInBar from here because foo has the access

bar::prvInBar (); // error, prvInBar is private within this context
\end{lstlisting}

\subsubsection{Recursive optional parameter}

An optional parameter can be constructed by a function, it can even be
constructed by calling the function in which it is a parameter. However, since
simply calling the function without changing the value of this default parameter
would result in an infinite recursion, in such contexts default values might be
disabled and made mandatory. For example, in the following source code the
validation of the function \texttt{foo} fails because the parameter \texttt{a}
is self-dependent. For the same reason, the functions \texttt{bar} and
\texttt{baz} are not allowed.

\begin{lstlisting}[style=coloredverbatim]
fn foo (a : i32 = foo ())-> i32 {
  a + 1
}

fn bar (a : i32 = baz ())-> i32 {
  a + 1
}

fn baz (a : i32 = bar ())-> i32 {
  a - 1
}
\end{lstlisting}

To solve this problem, the value of the parameter \texttt{a} can be set inside
the recursive calls. For example, the following code shows a solution for the
functions \texttt{bar} and \texttt{baz}. This fix may seem abrupt, and one could
argue that only one of the two functions needs to stop the infinite recursion.
However, it was decided to force this, as it seems to be a fairly niche problem
that needs to be avoided anyway.

\begin{lstlisting}[style=coloredverbatim]
// setting the option parameter a in baz, to stop the infinite recursion
fn bar (a : i32 = baz (a-> 1))-> i32 {
  a + 1
}

// setting the option parameter a in bar, to stop the infinite recursion
fn baz (a : i32 = bar (a-> 1))-> i32 {
  a - 1
}

bar ();
\end{lstlisting}

The above source code produce the following YIL result.

\begin{lstlisting}[style=intermediateVerb]
let YI_1(#1) : i32;
let YI_2(#2) : i32;
YI_1(#1) = _Y4main3bazFi32Zi32(1);
YI_2(#2) = _Y4main3barFi32Zi32(YI_1(#1));
\end{lstlisting}


\subsubsection {Mutable parameters}

A parameter whose type is borrowing data can be described as mutable. In this
case, because the parameter is borrowing mutable data, it must be explicitly
accepted when the function is called using the \texttt{alias} keyword. The
behavior is similar to assigning mutable borrowed data to a mutable variable,
and must follow the same rules regarding mutability levels.

\begin{lstlisting}[style=coloredverbatim]
fn foo (dmut x : [i32]) {
  x [0] = 12; // ok, a is mutable
}

let dmut a = copy [1, 2, 3];
foo (alias a); // Ok, contract has been accepted

assert (a == [12, 2, 3]);

let dmut b = copy [1, 2, 3];
foo (b); // forbidden, a should be passed by alias

let c = copy [1, 2, 3];
foo (alias c); // error, the mutability level of c is not high enough
\end{lstlisting}

The parameter itself is not mutable, and we cannot change the value to which it
is associated. This is because mutable data is not passed by reference, but by
value. Therefore, in the following example, modifying the value borrowed by the
variable \texttt{x} at line 3 would have no effect on the value borrowed by the
value \texttt{a}. However, on line 2, the value borrowed by \texttt{x} is the
same as the value borrowed by \texttt{a}, so modifying it will modify the value
of both variables. It was decided that the parameters should not be lvalues in
order to completely separate the concept of reference parameters and value
parameters.

\begin{lstlisting}[style=coloredverbatim]
fn foo (dmut x : [i32]) {
  x = [4, 5, 6]; // forbidden, x is not a lvalue
  x [0] = 3; // ok, x borrows mutable datas
}
\end{lstlisting}

To be allowed to be mutable, the mutability of the parameter must be deep enough
to actually touch borrowed data - and therefore imply an explicit alias of the
value associated with it during the call. For example, in the next example, even
if the parameter actually borrows data, the mutable modifier is not deep enough
and does not affect it. In this case, the compiler will reject the code and
return an error to avoid useless mutable decorators for parameter variables that
cannot change data. This rule is specific to function value parameters and does
not apply to default variables or reference parameters.

\begin{lstlisting}[style=coloredverbatim]
// forbidden, a [0] is not mutable, and neither is a
fn foo (mut a : [i32]);

// forbidden as well, a [0] is not borrowed, and a[0][0] is const
fn bar (mut a : [mut [i32] ; 2]);

// obviously forbidden, no data are borrowed
fn baz (mut a : i32);
\end{lstlisting}


If a parameter is constant, it is forbidden to associate a mutable variable with it using the keyword \texttt{alias}. This keyword is used to indicate that the contract for a data movement of borrowed mutable data has been accepted. Approving code that uses this keyword when no mutable data is being moved would have the undesirable effect of encouraging the keyword to be used even when unnecessary, and thus as a kind of mandatory decorator, which would ultimately defeat its main purpose of preventing unwanted mutable movement.

In the example below, the keyword \texttt{alias} is used in two different
contexts, both of which are incorrect. The first instance is on line 5, where
the variable \texttt{x} does not have mutable access to the data it borrows.
Therefore, it does not have the right to give this permission to another
variable, and the keyword \texttt{alias} is not allowed. In the second case, on
line 6, variable \texttt{y} has mutable access to the borrowed data, but it is
being passed to a variable that does not need mutable access. Therefore, the
keyword is unnecessary and not allowed. It is important to avoid using it for
every data movement without considering its usefulness, as this is a bad
practice, for that reason it is enforced by the compiler.

\begin{lstlisting}[style=coloredverbatim]
fn foo (mut a : [mut i32]);
fn bar (a : [i32]);

let x = copy [1, 2, 3];
let dmut y = copy [1, 2, 3];

foo (alias x); // forbidden, x is const
bar (alias y); // forbidden, passing to a const does not require /alias/
\end{lstlisting}

\subsubsection {Reference parameters}

The \texttt{ref} keyword can be used to decorate a parameter variable. This
decorator is used to pass a value by reference to a function. A reference value
is equivalent to a pointer value, except that it can never be null and is always
associated with a memory segment, making it safe to use. This keyword can only
be used to describe a variable, a type cannot be described as a reference, and
it is impossible to store a reference inside another value. To pass a value by
reference to a function, nothing has to be done on the calling side if the
variable is not mutable and the referencing is made implicitely.

\begin{lstlisting}[style=coloredverbatim]
use std::io;
fn foo (ref x : i32) {
  println (x);
}

let a = 12;
foo (a); // ok, x is immutable in foo
\end{lstlisting}

\begin{lstlisting}[style=intermediateVerb]
frame :  _Y4main3fooFRi32Zv (let x(#1) : *(i32))-> void {
    _Y3std2io7printlnNi32Fi32Zv(*x(#1));
}

frame :  _Y4main4mainFZv ()-> void {
    let a(#1) : i32;
    a(#1) = 12;
    _Y4main3fooFRi32Zv(&a(#1));
}
\end{lstlisting}

In the above example, the variable \texttt{a} is passed by reference to the
function \texttt{foo} however this reference is constant, and the function
\texttt{foo} cannot modified its value. It can be useful to pass a variable by
reference to avoid copying big datas, for example an array containing a lot of
elements and avoid copying all of them.

\begin{lstlisting}[style=coloredverbatim]
fn foo (ref x : [i32 ; 10]) {
  println (x [5]);
}

fn bar (x : [i32 ; 10]) {
  println (x [5]);
}

let a = [0 ; 10];
foo (a);
bar (a);
\end{lstlisting}

In the previous example, the value is not copied when passed to the \texttt{foo}
function, but is copied when passed to the \texttt{bar} function. The abstract
result of both functions is the same, but calling \texttt{foo} should be faster.

\begin{lstlisting}[style=intermediateVerb]
frame :  _Y4main3barFA10_3i32Zv (let x(#1) : [i32;10])-> void {
    _Y3std2io7printlnNi32Fi32Zv(x(#1)[5]);
}
frame :  _Y4main3fooFRA10_3i32Zv (let x(#1) : *([i32;10]))-> void {
    _Y3std2io7printlnNi32Fi32Zv(*x(#1)[5]);
}
frame :  _Y4main4mainFZv ()-> void {
    let YI_1(#1) : [i32;10];
    let a(#2) : [i32;10];
    YI_1(#1) = [0, 0, 0, 0, 0, 0, 0, 0, 0, 0];
    a(#2) = YI_1(#1);
    _Y4main3fooFRA10_3i32Zv(&a(#2));
    _Y4main3barFA10_3i32Zv(a(#2));
}
\end{lstlisting}

Only a \textit{lvalue} can be passed to a function as a reference, otherwise it
does not really have a memory location, and therefore an address cannot be
assigned to it.

\begin{lstlisting}[style=coloredverbatim]
fn foo (ref x : i32);

foo (12); // error, 12 is not a lvalue
\end{lstlisting}

\subsubsection{Mutable reference parameter}

By default, a reference parameter is not mutable, but it can be made mutable
using the \texttt{mut} and \texttt{dmut} decorators. Unlike mutable value
parameters, a mutable reference parameter can be mutable even if it does not
borrow any mutable data. In fact, as we saw in the previous section, a reference
parameter is a pointer that can never be null, so it actually borrows data from
another frame.

The contract of a function that presents a mutable reference parameter must be
accepted as it was mandatory for mutable parameters, but this time instead of
the keyword \texttt{alias} the keyword \texttt{ref} is used.

\begin{lstlisting}[style=coloredverbatim]
fn foo (ref mut x : i32) {
  x = 12;
}

let mut a = 1;
foo (ref a);
\end{lstlisting}

\begin {lstlisting}[style=intermediateVerb]
frame :  _Y4main3fooFRi32Zv (let x(#1) : *(i32))-> void {
    *x(#1) = 12;
}
frame :  _Y4main4mainFZv ()-> void {
    let a(#1) : i32;
    a(#1) = 1;
    _Y4main3fooFRi32Zv(&a(#1));
}
\end{lstlisting}

 As for the keyword
\texttt{alias}, this keyword \texttt{ref} cannot be used in vain and must be
associated with a mutable reference parameter.

\begin{lstlisting}[style=coloredverbatim]
fn foo (ref x : i32);

let mut a = 12;
foo (ref a); // forbidden, unecessary mutable reference

let b = 12;
foo (ref b); // forbidden, cannot create a mutable reference from a data that is not mutable
\end{lstlisting}

\subsection {Body, expressions and statements}

A function body is an expression. There are several types of expressions in
Ymir, some of which have already been described in previous chapters, such as
literal values, binary expressions, etc. A common expression that is generally
used to describe the body of a function is a block. A block is a list of
expressions separated by semicolons and enclosed in curly braces. The last
expression contained in a block may or may not end with a semicolon, 1) if not,
then the value of the block is described by that last expression, 2) otherwise
the block value is a unit value of type \texttt{void}.

\begin{lstlisting}[style=coloredverbatim]
fn foo (a : i32)-> i32
  a + 1 // body of /foo/

fn bar ()-> i32
{ // start of a block
  let a = 12;
  let b = 34;
  a + b // value of the block
}

fn baz ()-> void {
  let a = 12;
  let b = 34;
  std::io::println (a + b);
} // block value is <unit>

\end{lstlisting}

We can distinguish two kinds of expressions, those that have a value and those
that don't (or actually have the value \texttt{unit} of type \texttt{void}). An
expression that has no value is called a statement. Ending an expression with a
semicolon inside a block construction turns it into a statement.

\begin{lstlisting}[style=coloredverbatim]
fn foo () {
  let a = (let b = 12); // error, /let b = 12/ is of type void

  let y = {
    let x = 1;
    x + 1
  };
}
\end{lstlisting}

In the above example at line 2, the statement \texttt{let b = 12;} does not have
a value, so it cannot be used as the initial value in the variable declaration
of \texttt{a}. On line 4, however, the expression \texttt{\{let x = 1; x + 1\}}
has a value and is evaluated to the value \texttt{2} of type \texttt{i32}, so it
can be used to describe the initial value of \texttt{y}. All expressions and
statements are listed in Chapter~\ref{chap:control_flows}.

\subsubsection{Empty function}

A function with no body defines a function that is described externally. This
feature is is used to provide header only module files, where source code is
hidden but still callable externally. The actual code of the function has to be
linked at compilation. Let's suppose the two packages \texttt{foo} and
\texttt{bar}, where the full source code of the package \texttt{bar} is
described in \texttt{bar/src/bar.yr} and a header file was created to be
imported by the package \texttt{foo} in \texttt{bar/include/bar.yri}. A
\textit{yri} file described a header file that has been automatically generated
by the compiler (cf. Section\ref{sec:external_decls}).


\begin{lstlisting}[caption=\textit{./bar/src/bar.yr}, style=coloredverbatim]
in bar;
use std::io;

pub fn funcInBar (a : i32)-> i32 {
  println ("Hello from bar");
  a + 1
}
\end{lstlisting}

%\begin{minipage}[t][][t]{0.47\linewidth}
\begin{lstlisting}[caption=\textit{./bar/include/bar.yri}, style=coloredverbatim]
in bar;

pub fn funcInBar (a : i32)-> i32;
\end{lstlisting}
%% \end{minipage}\hspace{5pt}
%% \begin{minipage}[t][][t]{0.47\linewidth}
\begin{lstlisting}[caption=\textit{foo.yr}, style=coloredverbatim]
in foo;

use bar;
use std::io;

pub fn main () {
  println ("Hello from foo");
  println ("Res : ", funcInBar (12));
}
\end{lstlisting}
%\end{minipage}


The file \textit{./bar/include/bar.yri} can be published alonside with the
binary file of the \texttt{bar} package, that way the source code of the package
\texttt{bar} is hidden. It has the same purpose as header files that could be
found in C/C++ languages.

\begin{lstlisting}[style=intermediateVerb]
$ cd bar/src/bar.yr
$ gyc -c bar.yr -o ../../libbar.o
$ cd ../../
$ gyc foo.yr -I bar/include/bar.yri libbar.o -o foo.exe
$ ./foo.exe
Hello from foo
Hello from bar
Res : 13
\end{lstlisting}

\subsection {Return value}

The return type of a function is described at the end of the prototype of a
function declaration, after the parameters, with the syntax \texttt{-> T}, where
\texttt{T} is the type returned by the function. This information can be omitted
if the function does not return a value and is therefore of type \texttt{void}.
The body of the function must produce a value of the same type as that defined
in its prototype.

\begin{lstlisting}[style=coloredverbatim]
fn foo ()-> i32 {
  12
}
fn bar () {}
fn baz ()-> void {}
\end{lstlisting}

The keyword \texttt{return} can be used to return early, it is associated with a
value written after the keyword, defining the value to return. The type of this
value must be the same as the type of the function.

\begin{lstlisting}[style=coloredverbatim]
fn foo ()-> i32 {
  return 12;
}

fn bar () {
  return; // return with /unit/ value
}
\end{lstlisting}

The value of the function is retreived from the caller of the function, as the
result of the call expression.

\begin{lstlisting}[style=coloredverbatim]
let resultOfFoo = foo ();

// It can also be used inside another expression, as any expression
let add = foo () + 12;
\end{lstlisting}

\subsubsection {Early exits}

In case of early return, the type of the body of the function must be
\texttt{void} if any branches lead to an early exit statement. Any statement and
expressions that are not atteignable because they are written after an early
exit are not allowed.

\begin{lstlisting}[style=coloredverbatim]
fn foo (b : bool)-> i32 {
  if (b) return 12;
  else return 0;

  println ("Unreachable !"); // error, unreachable statement
  87 // unreachable as well
}
\end{lstlisting}

There are three other ways to exit a function early, 1) throw an exception (see
the next subsection), 2) fail an assertion, or 3) panic. All of these early
exits are taken into account by the compiler to calculate the exit nodes of the
flow graph and send unreachable statement errors.

\subsubsection{Mutable return value}

When a function is called, its return value is retrievable from the caller
function. If the return value borrows mutable data, it does not need to be
explicitly aliased, in fact it is already supposed to be done in the body of the
callee in order to return the value by alias, so it would only be redondant to
force the alias inside the caller as well.

\begin{lstlisting}[style=coloredverbatim]
// Function that return a dmut slice
fn foo (a : i32)-> dmut [i32] {
  let dmut x = copy [0 ; a]; // allocating a slice of size /a/

  // explicitely return a mutable alias of the value, to respect the return value contract
  alias x
}

let dmut x = foo (12); // no need for explicit alias the call of /foo/
\end{lstlisting}

\subsubsection {Reference return value}

A function cannot return a reference. There is a guarantee in Ymir that all
aliasable and mutable borrowed values are on the heap, which is not the case for
reference values, which can be on the stack and therefore cease to exist when
the function is returned. To avoid this, it was decided not to allow reference
returns.

\begin{lstlisting}[style=coloredverbatim]
fn foo ()-> ref i32 { // error, cannot return a reference
                      // actually, /ref/ cannot describe a type
  let a = 12;
  a
}
\end{lstlisting}

\subsection {Exceptions}

\subsection {Effects}

\subsection{The main function}
A program always starts with the function named \texttt{main}



\section{Global variable}%
\label{sec:global_variables}

Global var

\section{Global alias names}
\label{sec:global_alias_names}

\section{Unit tests}%
\label{sec:unit_test}

unit test

\section{External declaration}%
\label{sec:extern_var}

External variable
