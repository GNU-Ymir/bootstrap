class Z {
    pub let mut i : i32;
    pub self (i : i32) with i = i {}
}

class A {
    let dmut _items = copy [Z::new (1), Z::new (2), Z::new (3)];

    pub self () {}

    pub fn begin (self)-> dmut &CIterator {
        CIterator::new (0, self)
    }

    pub fn begin (mut self)-> dmut &MIterator {
        MIterator::new (0, alias self)
    }

    pub fn end (self)-> usize {
        self._items.len
    }

    @field
    pub fn values (self)-> [&Z] {
        self._items
    }

    @field
    pub fn values (mut self)-> dmut [&Z] {
        alias self._items
    }

}

class CIterator {

    let _x : &A;
    let mut _index : usize;


    pub self (index : usize, x : &A)
        with _index = index
        , _x = x
    {}

    pub fn get {0us, 1us} (mut self)-> &Z {
        self._x.values [self._index]
    }

    pub fn get {1us, 2us} (mut self)-> &Z {
        self._x.values [self._index]
    }

    pub fn get {0us, 2us} (mut self)-> usize {
        self._index
    }

    pub fn opEquals (self, o : usize)-> bool {
        self._index == o
    }

    impl Iterator {
        pub over next (mut self) {
            self._index += 1;
        }
    }


}

class MIterator {

    let dmut _x : &A;
    let mut _index : usize;


    pub self (index : usize, dmut x : &A)
        with _index = index
        , _x = alias x
    {}


    pub fn get {0us, 1us} (mut self)-> dmut &Z {
        alias self._x:.values [self._index]
    }

    pub fn get {1us, 2us} (mut self)-> dmut &Z {
        alias self._x:.values [self._index]
    }

    pub fn get {0us, 2us} (mut self)-> usize {
        self._index
    }

    impl Iterator {
        pub over next (mut self) {
            self._index += 1;
        }
    }

    pub fn opEquals (self, o : usize)-> bool {
        self._index == o
    }

}


fn main () {
    let dmut a = A::new ();
    for i, dmut j in alias a {
        j.i = cast!i32 (i) + 9;
    }

    for i in a {
        std::io::println (i.i);
    }
}
