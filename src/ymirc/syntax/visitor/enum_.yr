/*
 * Declares the syntax visitor in charge of reading an enum declaration.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::visitor::enum_
 * @File: ymirc/syntax/visitor/enum_.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-13
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in enum_;

use ymirc::errors::_;
use ymirc::syntax::{_,
                    visitor::_,
                    declaration::_,
                    expression::{empty, instruction}};


use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::_;

use std::io;


/**
 * Visitor in charge of reading enumerations
 * */
pub record EnumVisitor {
    
    pub self () {}

    /**
     * Read a declaration introduced by the keyword `enum`
     *
     * @params:
     *    - loc:  the location of the `enum` keyword
     *    - comm: the comment above the `enum` keyword
     *
     * @grammar
     * 
     * ```
     * EnumDecl :=
     *     TemplateEnumDecl
     *   | EnumDecl
     *
     * EnumDecl :=
     *   'enum' Identifier (':' ExpressionSimple(0))? EnumParams* ';'
     *
     * TemplateEnumDecl :=
     *   'enum' TemplateTest? Identifier TemplateParams  (':' ExpressionSimple(0))? EnumParams* ';'
     * ```
     *    
     * */
    pub fn read (self, dmut context: &SyntaxVisitor, loc: &Word, comm: [c8])-> &Declaration
        throws ErrorMsg
    {                
        let tryTemplateLoc = context.getSeek ();
        let mut normErr: (&ErrorMsg)? = none;
        let mut templateErr: (&ErrorMsg)? = none;
        { // Read a normal enum
            return self.readNormalEnum (alias context, loc, comm);
        } catch {
            err => {
                normErr = err?;
            }
        }

        // Go back to the `enum` keyword
        context:.rewindToSeek (tryTemplateLoc);
        { // Read a template enum
            return self.readTemplateEnum (alias context, loc, comm);
        } catch {
            err => {
                templateErr = err?;
            }
        }

        if let (Ok (ne), Ok (te)) = (normErr, templateErr) {
            throw copy ErrorMsg::list (copy [ne, te]);
        }

        eprintln ("internal enum syntax visitor: empty error messages");
        panic;
        
    }

    /**
     * Read an enumeration without template parameters
     *
     * @params:
     *    - context: the context of the reading
     *    - loc:     the location of the `enum` keyword
     *    - comm:    the comment above the enum declaration
     *
     * @grammar: cf. `self.read`
     * */
    pub fn readNormalEnum (self, dmut context: &SyntaxVisitor, loc: &Word, comm: [c8])-> &Declaration
        throws ErrorMsg
    {
        // `enum` Identifier
        let (ident, _) = context:.readIdentifier ();

        // ':' Type
        let mut type: &Expression = EMPTY_EXPR;
        let (hasType, _) = context:.readIf (Tokens::COLON);
        if !hasType.isEof {
            type = context.getExpressionVisitor ().readExpressionSimple (alias context);
        }

        // ('|' Identifier '=' Value)* ';'
        let mut vars: [&VarDeclExpr] = [];
        let mut comms: [[c8]] = [];
        while let (n, vcomm) = context:.readTokens (Tokens::SEMI_COLON, Tokens::PIPE) && n.str == Tokens::PIPE {
            let (vident, _) = context:.readIdentifier ();
            context:.readTokens (Tokens::EQUAL);

            let value = context.getExpressionVisitor ().readExpressionSimple (alias context);

            let var = copy VarDeclExpr (loc-> n,
                                        name-> vident,
                                        type-> type,
                                        value-> value,
                                        isMutOrDmut-> EOF_WORD,
                                        isLazyOrRef-> EOF_WORD);

            vars ~= [var];
            comms ~= [vcomm];
        }

        return copy EnumDecl (loc-> loc,
                              name-> ident,
                              comm-> comm,
                              type-> type,
                              fields-> vars,
                              fieldComms-> comms);
    }

    /**
     * Read an enumeration with template parameters
     *
     * @params:
     *    - context: the context of the reading
     *    - loc:     the location of the `enum` keyword
     *    - comm:    the comment above the enumeration
     *
     * @grammar: cf. `self.read`
     * */
    pub fn readTemplateEnum (self, dmut context: &SyntaxVisitor, loc: &Word, comm: [c8])-> &Declaration
        throws ErrorMsg
    {
        // `enum` `if` Value Identifier '{...}'
        let (ifLoc, test) = context:.readTemplateTest ();        
        let (ident, _) = context:.readIdentifier ();
        let templates = context:.readTemplateParameters (ifLoc-> ifLoc);

        // ':' Type
        let mut type: &Expression = EMPTY_EXPR;
        let (hasType, _) = context:.readIf (Tokens::COLON);
        if !hasType.isEof {
            type = context.getExpressionVisitor ().readExpressionSimple (alias context);
        }

        // ('|' Identifier '=' Value)* ';'
        let mut vars: [&VarDeclExpr] = [];
        let mut comms: [[c8]] = [];
        while let (n, vcomm) = context:.readTokens (Tokens::SEMI_COLON, Tokens::PIPE) && n.str == Tokens::PIPE {
            let (vident, _) = context:.readIdentifier ();
            context:.readTokens (Tokens::EQUAL);

            let value = context.getExpressionVisitor ().readExpressionSimple (alias context);

            let var = copy VarDeclExpr (loc-> n,
                                        name-> vident,
                                        type-> type,
                                        value-> value,
                                        isMutOrDmut-> EOF_WORD,
                                        isLazyOrRef-> EOF_WORD);

            vars ~= [var];
            comms ~= [vcomm];
        }

        let en = copy EnumDecl (loc-> loc,
                                name-> ident,
                                comm-> comm,
                                type-> type,
                                fields-> vars,
                                fieldComms-> comms);

        return copy TemplateDecl (loc-> ident,
                                  comm-> comm,
                                  params-> templates,
                                  decl-> en,
                                  guard-> test);
    }
    
}
