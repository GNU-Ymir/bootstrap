/*
 * Syntax visitor of `def` symbols.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::visitor::def_
 * @File: ymirc/syntax/visitor/def_.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-13
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in def_;

use ymirc::errors::_;
use ymirc::syntax::{_,
                    visitor::_,
                    declaration::_,
                    expression::empty};

use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::_;

use std::io;

/**
 * Visitor in charge of reading a `def` global symbol.
 * 
 */
pub record DefVisitor {

    pub self () {}

    /**
     * Read a declaration introduced by the keyword `def`
     * 
     * @params:
     *    - loc: the location of the `def` keyword
     *    - comm: the comment above the `def` declaration
     * 
     * @grammars:
     * ```
     * DefDecl :=
     *     TemplateDefDecl     
     *   | DefDecl     
     *
     * TemplateDefDecl :=
     *   TemplateTest? Identifier TemplateParams ('=' | ':') Expression(0) ';'
     *
     * DefDecl :=
     *   Identifier ('=' | ':') Expression (0) ';'
     *   
     * ```
     */
    pub fn read (self, dmut context: &SyntaxVisitor, loc: &Word, comm: [c8])-> &Declaration
        throws ErrorMsg
    {        
        let tryTemplateLoc = context.getSeek ();
        let mut normErr : (&ErrorMsg)? = none;
        let mut templateErr : (&ErrorMsg)? = none;
        
        { // Try reading a normal `def` decl
            return self.readNormalDef (alias context, loc, comm);                 
        } catch {
            err => {
                normErr = (err)?;
            }
        }

        // Go back to try reading a template version of the def
        context:.rewindToSeek (tryTemplateLoc);
        { // Try a template `def`
            self.readTemplateDef (alias context, loc, comm);
        } catch {
            err => {
                templateErr = (err)?;
            }
        }

        // Both failed, throwing the errors
        if let (Ok (ne), Ok (te)) = (normErr, templateErr) {
            throw copy ErrorMsg::list (copy [ne, te]);
        }

        eprintln ("internal syntax visitor error: error messages are empty");
        panic;        
    }
    
    /**
     * Read a `def` symbol without templates
     *
     * @params:
     *   - context: the context of the reading
     *   - loc: the location of the `def` keyword
     *   - comm: the comment above the `def` keyword
     *
     * @grammar: cf. `self.read`
     * 
     * */
    fn readNormalDef (self, dmut context: &SyntaxVisitor, loc: &Word, comm: [c8])-> &Declaration
        throws ErrorMsg
    {
        let (ident, _) = context:.readIdentifier ();
        let (kind, _) = context:.readTokens (Tokens::EQUAL, Tokens::COLON);
        let value = context.getExpressionVisitor ().readExpressionSimple (alias context);
        
        context:.readTokens (Tokens::SEMI_COLON);

        return copy DefDecl (loc-> loc,
                             name-> ident,
                             comm-> comm,
                             content-> value,
                             tokDecl-> kind);
    }
    
    /**
     * Read a `def` symbol with templates
     *
     * @params:
     *   - context: the context of the reading
     *   - loc: the location of the `def` keyword
     *   - comm: the comment above the `def` keyword
     *
     * @grammar: cf. `self.read`
     * 
     * */
    fn readTemplateDef (self, dmut context: &SyntaxVisitor, loc: &Word, comm: [c8])-> &Declaration
        throws ErrorMsg
    {
        let (ifLoc, test) = context:.readTemplateTest ();
        let (ident, _) = context:.readIdentifier ();
        let templates = context:.readTemplateParameters (ifLoc-> ifLoc);
        
        let (kind, _) = context:.readTokens (Tokens::EQUAL, Tokens::COLON);
        let value = context.getExpressionVisitor ().readExpressionSimple (alias context);
        
        context:.readTokens (Tokens::SEMI_COLON);

        let aka = copy DefDecl (loc-> loc,
                                name-> ident,
                                comm-> comm,
                                content-> value,
                                tokDecl-> kind);
        
        return copy TemplateDecl (loc-> ident,
                                  comm-> comm,
                                  params-> templates,
                                  decl-> aka,
                                  guard-> test);
    }    
}
