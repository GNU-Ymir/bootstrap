/*
 * Declares the syntax visitor in charge of reading an union declaration.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::visitor::union_
 * @File: ymirc/syntax/visitor/union_.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-15
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in union_;


use ymirc::errors::_;
use ymirc::syntax::{_,
                    visitor::_,
                    declaration::_,
                    expression::{empty, instruction}};


use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::_;

use std::io;


/**
 * Visitor in charge of reading unions
 * */
pub record UnionVisitor {
    pub self () {}

    /**
     * Read a declaration introduced by the keyword `union`
     *
     * @params:
     *    - loc: the location of the `union` keyword
     *    - comm: the comment above the `enum` keyword
     *
     * @grammar
     * ```
     * UnionDecl :=
     *     TemplateUnionDecl
     *   | NormalUnionDecl
     *
     * NormalUnionDecl :=
     *    'union' Identifier UnionParams* ';'
     *
     * TemplateUnionDecl :=
     *    'union' TemplateTest? Identifier TemplateParams UnionParams* ';'
     *
     * UnionParams :=
     *    '|' ExpressionSimple (0)
     * ```
     * */
    pub fn read (self, dmut context: &SyntaxVisitor, loc: &Word, comm: [c8])-> &Declaration
        throws ErrorMsg
    {
        let tryTemplateLoc = context.getSeek ();
        let mut normErr: (&ErrorMsg)? = none;
        let mut templateErr: (&ErrorMsg)? = none;
        { // Read a normal union
            return self.readNormalUnion (alias context, loc, comm);
        } catch {
            err => {
                normErr = err?;
            }
        }

        // Go back to the `union` keyword
        context:.rewindToSeek (tryTemplateLoc);
        { // Read a template union
            return self.readTemplateUnion (alias context, loc, comm);
        } catch {
            err => {
                templateErr = err?;
            }
        }

        if let (Ok (ne), Ok (te)) = (normErr, templateErr) {
            throw copy ErrorMsg::list (copy [ne, te]);
        }

        eprintln ("internal union syntax visitor: empty error messages");
        panic;
    }

    /**
     * Read an union without template parameters
     *
     * @params:
     *    - context: the context of the reading
     *    - loc:     the location of the `union` keyword
     *    - comm:    the comment above the declaration
     *
     * @grammar: cf `self.read`
     * */
    fn readNormalUnion (self, dmut context: &SyntaxVisitor, loc: &Word, comm: [c8])-> &Declaration
        throws ErrorMsg
    {
        let (ident, _) = context:.readIdentifier ();

        let mut types: [&Expression] = [];
        let mut typeComms: [[c8]] = [];
        while let (n, vcomm) = context:.readTokens (Tokens::SEMI_COLON, Tokens::PIPE) && n.str == Tokens::PIPE {
            types ~= [context.getExpressionVisitor ().readExpressionSimple (alias context)];
            typeComms ~= [vcomm];
        }

        return copy UnionDecl (loc-> loc,
                               name-> ident,
                               comm-> comm,
                               types-> types,
                               typeComms-> typeComms);
    }

    /**
     * Read an union with template parameters
     *
     * @params:
     *    - context: the context of the reading
     *    - loc:     the location of the `union` keyword
     *    - comm:    the comment above the declaration
     *
     * @grammar: cf `self.read`
     * */
    fn readTemplateUnion (self, dmut context: &SyntaxVisitor, loc: &Word, comm: [c8])-> &Declaration
        throws ErrorMsg
    {
        let (ifLoc, test) = context:.readTemplateTest ();  
        let (ident, _) = context:.readIdentifier ();
        let templates = context:.readTemplateParameters (ifLoc-> ifLoc);

        let mut types: [&Expression] = [];
        let mut typeComms: [[c8]] = [];
        while let (n, vcomm) = context:.readTokens (Tokens::SEMI_COLON, Tokens::PIPE) && n.str == Tokens::PIPE {
            types ~= [context.getExpressionVisitor ().readExpressionSimple (alias context)];
            typeComms ~= [vcomm];
        }

        let un = copy UnionDecl (loc-> loc,
                                 name-> ident,
                                 comm-> comm,
                                 types-> types,
                                 typeComms-> typeComms);

        return copy TemplateDecl (loc-> ident,
                                  comm-> comm,
                                  params-> templates,
                                  decl-> un,
                                  guard-> test);
    }
    
    
}
