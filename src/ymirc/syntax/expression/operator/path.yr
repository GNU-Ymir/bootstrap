/*
 * Declares the PathExpr class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::expression::operator::path
 * @File: ymirc/syntax/expression/operator/path.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2021-11-20
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in path;

use std::stream;

use ymirc::lexing::{word, tokens};
use ymirc::utils::format;
use ymirc::syntax::expression::literal; // VarExpr

use std::{stream, fs::path};


/**
 * Syntaxic representation of a path expression.
 * 
 */
@final
pub class PathExpr over OperatorExpr {

    /*! Public fields */
    pub {
        // The left operand
        let left: &Expression;

        // The right operand
        let right: &Expression;
    }
    
    /**
     * @params: 
     *    - loc: the location and operator of the expression
     *    - left: the left operand of the binary expression
     *    - right: the right operand of the binary expression
     */
    pub self (loc: &Word, left: &Expression, right: &Expression)
        with super (loc)
        , left = left
        , right = right
    {}

    /**
     * @returns: a std path
     */
    pub fn toStdPath (self)-> Path {
        let mut res = Path ("");
        match self.left {
            p: &PathExpr => {
                res = res.push (p.toStdPath ());
            }
            v: &VarExpr => {
                res = res.push (v.name.str);
            }
            _ => {
                res = res.push (self.left.loc.str);
            }
        }

        match self.right {
            p: &PathExpr => {
                res = res.push (p.toStdPath ());
            }
            v: &VarExpr => {
                res = res.push (v.name.str);
            }
            _ => {
                res = res.push (self.right.loc.str);
            }
        }

        res
    }

    /**
     * @returns: the location of the different component of the path
     * */
    pub fn getLocs (self)-> [&Word] {
        let l = match self.left {
            p: &PathExpr => {
                p.getLocs ()
            }
            _ => {
                copy [self.left.loc]
            }
        };

        let r = match self.right {
            p: &PathExpr => {
                p.getLocs ()
            }
            _ => {
                copy [self.right.loc]
            }
        };

        l ~ r
    }

    /**
     * @returns: the location of the file part of the path
     * */
    pub fn getFileLoc (self)-> &Word {
        match self.right {
            p: &PathExpr => {
                p.getFileLoc ()
            }            
            _ => { self.right.loc }
        }
    }
        
    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream: &Formatter) {
            self.left.format (alias stream);
            stream:.write (Tokens::DCOLON);
            self.right.format (alias stream);
        }
    }

}
