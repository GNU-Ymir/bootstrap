/*
 * Declares the MultOperatorExpr class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::expression::operator::mult
 * @File: ymirc/syntax/expression/operator/mult.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2021-11-20
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in mult;

use std::stream;

use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;

/**
 * Transform `str` into a MultOperators ()
 * */
fn toOp (str: [c8])-> MultOperators {
    if str == Tokens::LPAR return MultOperators::CALL;
    if str == Tokens::LCRO return MultOperators::INDEX;
    if str == MultOperators::CALL return MultOperators::CALL;
    if str == MultOperators::INDEX return MultOperators::INDEX;

    std::io::eprintln ("internal syntax error: invalid mult operator, ", str);    
    panic;
}

/**
 * A mult operator is an operator with on left operand, and a list of right
 * operands (for example call is a mult operator, array access is also)
 *
 */
@final
pub class MultOperatorExpr over OperatorExpr {

    /*! Public fields */
    pub {
        // The operator used
        let op: MultOperators;
        
        // The end of the expression
        let end: &Word;

        // The left operand
        let left: &Expression;

        // The operands inside the mult operator
        let rights: [&Expression];

        // True iif it can be a dot call
        let canBeDotCall: bool;
    }
    
    /**
     * @params: 
     *    - op:           the location of the operator (first '(' or '[')
     *    - end:          the location of the closing token of the operator
     *    - left:         the left operand
     *    - rights:       the right operand
     *    - canBeDotCall: true, if this expression can be used as a dot call, false otherwise (sub part of a Dot operator)
     */        
    pub self (op:           &Word,
              end:          &Word,              
              left:         &Expression,
              rights:       [&Expression],
              canBeDotCall: bool = true)
        
        with super (op)
        , end = end
        , op = toOp (op.str)
        , left = left
        , rights = rights
        , canBeDotCall = canBeDotCall
    {}
        
    impl Streamable;
    impl Formattable {
        
        pub over format (self, dmut stream: &Formatter) {
            self.left.format (alias stream);
            stream:.entabing ();            
            stream:.write (' ', self.loc.str);            
            for i, elem in self.rights {
                if (i != 0) stream:.write (", ");
                elem.format (alias stream);                
            }
            stream:.detabing ();
            stream:.write (self.end.str);
        }        
    }

}
