/*
 * Declares the BinaryExpr class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::expression::operator::binary
 * @File: ymirc/syntax/expression/operator/binary.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2021-11-20
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */
    
in binary;

use std::stream;

use ymirc::lexing::{word, keys};
use ymirc::utils::format;


/**
 * Transform the string into a binary operator
 * @warning: panic if does not exists
 * */
fn toOp (str: [c8])-> BinaryOperators {
    for o in BinaryOperators::__members__ {
        if o == str {
            return o;
        }
    }

    std::io::eprintln ("internal syntax error: invalid binary operator, ", str);
    panic;
}

/**
 * Syntaxic representation of a binary expression
 *
 */
@final
pub class BinaryExpr over OperatorExpr {

    /*! Public fields */
    pub {
        // The operator
        let op: BinaryOperators;
        
        // The left operand
        let left: &Expression;

        // The right operand
        let right: &Expression;
    }
        
    /**
     * @params: 
     *    - op:    the location and operator of the expression     
     *    - left:  the left operand of the binary expression
     *    - right: the right operand of the binary expression
     */
    pub self (op: &Word, left: &Expression, right: &Expression)
        with super (op)
        , op = toOp (op.str)
        , left = left
        , right = right
    {}
    
    impl Streamable;
    impl Formattable {
    
        pub over format (self, dmut stream: &Formatter) {
            self.left.format (alias stream);
            match self.op {
                BinaryOperators::DOT | BinaryOperators::DOT_AND | BinaryOperators::DCOLON => 
                    stream:.write (self.op);
                _ =>
                    stream:.write (' ', self.op, ' ');
            }
            
            self.right.format (alias stream);
        }
    }
}
