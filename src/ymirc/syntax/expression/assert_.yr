in assert_;

use ymirc::syntax::expression::empty;
use std::stream;
use ymirc::syntax::keys;
use ymirc::lexing::{word, tokens};
use ymirc::utils::format;

/**
 * Syntaxic node representation of an assertion
 * @verbatim: 
 * ==============
 * assert_expr := Keys::ASSERT param_expr_list
 * ==============
 */
@final
pub class AssertExpr over Expression {
    
    // The test of the assertion
    let _test : &Expression;

    // The message of the assertion
    let _msg : &Expression;

    /**
     * @params : 
     *    - loc: the location of the assert
     *    - test: the test of the assertion
     *    - msg: the message to display, if the test failed
     */
    pub self (loc : &Word, test : &Expression, msg : &Expression)
        with super (loc), _test = test, _msg = msg
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */
    
    /**
     * @returns: the test of the assertion
     */
    pub fn getTest (self)-> &Expression {
        self._test
    }

    /**
     * @preturns: the message to display if the assertion fails
     */
    pub fn getMessage (self)-> &Expression {
        self._msg
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Expression)-> bool {
        match o {
            a : &AssertExpr => {
                a._test == self._test && a._msg == self._msg
            }
            _ => { false }
        }
    }    

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    
    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the assertion
         */
        pub over format (self, dmut stream : &Formatter) {
            stream:.write (Keys::ASSERT, " (");
            stream:.entabing ();
            self._test.format (alias stream);
            match self._msg {
                EmptyExpression () => {}
                _ => {
                    stream:.write (", ");
                    self._msg.format (alias stream);
                }
            }
            stream:.detabing ();
            stream:.write (")");
        }        
    }
    
}
