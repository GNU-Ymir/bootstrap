/*
 * Declares the FloatExpr class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::expression::literal::float
 * @File: ymirc/syntax/expression/literal/float.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2021-11-20
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in float;

use std::stream;

use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;


/**
 * Syntaxic representation of a floating point number literal.
 *
 *
 */
@final
pub class FloatExpr over LiteralExpr {

    /*! Public fields */
    pub { 

        // The decimal part of the float
        let dec: &Word;

        // The expronent part (for Hex and Sci)
        let exp: &Word;

        // true iif exp part is positive
        let expSign: bool;

        // The suffix of the literal (f, l, d, r)
        let suffix: &Word;

        // True iif the float is written in hex notation
        let isHex: bool = false;
    }
    
    /**
     * @params:      
     *   - int:     the integral part of the literal
     *   - dec:     the decimal part of the literal
     *   - suffix:  the suffix of the literal (can be EOF)
     *   - exp:     the exponent part of the literal (can be EOF)
     *   - expSign: true iif the exponent part is positive
     *   - isHex:   true iif the float is written in Hex notation
     */
    pub self (int:     &Word,              
              dec:     &Word,
              suffix:  &Word,
              exp:     &Word,
              expSign: bool,
              isHex:   bool)
        
        with super (int)
        , dec = dec
        , suffix = suffix
        , exp = exp
        , expSign = expSign
        , isHex = isHex        

    {}

    /**
     * @returns: the integral part of the float
     * */
    @field
    pub fn int (self)-> &Word {
        self.loc
    }
        
    
    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the literal in the stream
         */
        pub over format (self, dmut stream: &Formatter) {
            stream:.write (self.int.str)
                :.write (Tokens::DOT)
                :.write (self.dec.str);
            
            if !self.exp.isEof {                
                if self.isHex
                    stream:.write (FloatExp::LP);
                else
                    stream:.write (FloatExp::LE);

                if self.expSign
                    stream:.write (Tokens::PLUS);
                else
                    stream:.write (Tokens::MINUS);

                stream:.write (self.exp.str);                    
            }

            if !self.suffix.isEof
                stream:.write (self.suffix.str);            
        }        
    }
    
}
