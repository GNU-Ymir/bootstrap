mod ymirc::syntax::expression::literal::char;

import ymirc::syntax::expression::base;
import ymirc::syntax::expression::literal::base;

import ymirc::syntax::keys;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;


import std::io;

/**
 * Syntaxic representation of a char literal
 * @verbatim: 
 * ================
 * char_lit := Tokens::SQUOTE any* Tokens::SQUOTE (Keys::C8 | Keys::C32)?
 * ================
 */
pub class @final CharExpr over LiteralExpr {

    /// The location of the end of the expression
    let _end : &Word;

    /// The content of the char
    let _sequence : [c8];

    /// The suffix
    let _suffix : &Word;

    /**
     * @params: 
     *   - loc: the location of the char (first quote)
     *   - end: the end of the char (last quote)
     *   - sequence: the chars forming the char literal
     *   - suffix: the suffix after the char literal (Keys::C8, Keys::C32, or eof ())
     */
    pub self (loc : &Word, end : &Word, sequence : [c8], suffix : &Word = EOF_WORD)
        with super (loc), _end = end, _sequence = sequence, _suffix = suffix
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */
    
    /**
     * @returns: the location of the last quote of the char literal
     */
    pub fn getEndLocation (self)-> &Word {
        self._end
    }

    /**
     * @returns: the sequence of the char literal
     */
    pub fn getSequence (self)-> [c8] {
        self._sequence
    }

    /**
     * @returns: the suffix of the char literal
     */
    pub fn getSuffix (self)-> &Word {
        self._suffix
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Expression)-> bool {
        match o {
            c : &CharExpr => {
                c._end == self._end && c._sequence == self._sequence && c._suffix == self._suffix
            }
            _ => { false }
        }
    }    
    
    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    
    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting the char into the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._loc.str (), self._sequence, self._end.str (), self._suffix.str ());
        }
    }
       
}
