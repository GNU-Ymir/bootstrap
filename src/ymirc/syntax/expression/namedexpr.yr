mod ymirc::syntax::expression::namedexpr;

import ymirc::syntax::declaration::_;
import ymirc::syntax::expression::_;

import ymirc::syntax::_;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;


import std::io;

/**
 * A named expression associate an Identifier to an expression, this is used in call arguments, matchers, etc.
 * @verbatim: 
 * ================
 * named_expr := Identifier Tokens::ARROW expression:(0)
 * ================
 */
pub class @final NamedExpr over Expression {

    /// The content of the expression
    let _content : &Expression;

    /**
     * @params: 
     *    - loc: the location and identifier of the named expression
     *    - content: the content of the expression
     */
    pub self (loc : &Word, content : &Expression)
        with super (loc), _content = content
    {}

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */
    
    /**
     * @returns: the content of the expression
     */
    pub fn getContent (self)-> &Expression {
        self._content
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Expression)-> bool {
        match o {
            n : &NamedExpr => {
                n._content == self._content
            }
            _ => {
                false
            }
        }
    }
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    
    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the expression inside the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._loc.str (), ' 'c8, Tokens::ARROW, ' 'c8);
            stream:.entabing ();
            self._content.format (alias stream);
            stream:.detabing ();
        }        
    }

}
