mod ymirc::syntax::expression::block;

import ymirc::syntax::declaration::_;
import ymirc::syntax::expression::_;

import ymirc::syntax::_;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;

import std::io;

/**
 * Syntaxic representation of a block expression
 * @verbatim: 
 * ===============
 * block_expr := '{' (expression ';')* (expression)? '}' (catcher_block)? (scope_guards)*
 * ===============
 */
pub class @final BlockExpr over Expression {

    /// The location of the end of the block
    let _end : &Word;

    /// The list of declaration inside the block
    let _declModule : &Declaration;

    /// THe content of the block
    let _content : [&Expression];

    /// The catcher of the block
    let _catcher : &Expression;
    
    /// The list of scope guards
    let _scopes : [&ScopeGuardExpr];

    /**
     * @params: 
     *    - loc: the location of the block
     *    - end: the location of the end of the block ('}')
     *    - module: the list of declaration made inside the block (imports, function, ...)
     *    - content: the list of expression contained in the block
     *    - guards: the list of scope guards of the block
     */
    pub self (loc : &Word, end : &Word, module : &Declaration = BlockDecl::empty (), content : [&Expression], catcher : &Expression = EmptyExpression::new (), guards : [&ScopeGuardExpr] = [])
        with super (loc), _end = end, _declModule = module, _content = content, _scopes = guards, _catcher = catcher
    {}

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */
    
    /**
     * @returns: the location of the end of the block
     */
    pub fn getEndLocation (self)-> &Word {
        self._end
    }

    /**
     * @returns: the internal module of the block containing declarations
     */
    pub fn getModule (self)-> &Declaration {
        self._declModule
    }

    /**
     * @returns: the list of expression contained in the block
     */
    pub fn getExpressions (self)-> [&Expression] {
        self._content
    }

    /**
     * @returns: the list of scope guards of the block
     */
    pub fn getGuards (self)-> [&ScopeGuardExpr] {
        self._scopes
    }

    /**
     * @returns: the catcher of the block
     */
    pub fn getCatcher (self)-> &Expression {
        self._catcher
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Expression)-> bool {
        match o {
            b : &BlockExpr => {
                b._end == self._end && b._declModule == self._declModule && b._content == self._content && b._catcher == self._catcher && b._scopes == self._scopes
            }
            _ => {
                false
            }
        }
    }
        
    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the block inside the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            stream:.write ("{\n"s8);
            stream:.entabing ();

            match  self._declModule {
                x : &BlockDecl => {
                    if (x.getDeclarations ().len != 0u64) {
                        x.format (alias stream);
                    }
                }
                _ => {
                    self._declModule.format (alias stream);
                }
            }

            let mut j = 0us;
            for i in self._content {
                if (j != 0us)
                    stream:.write (Tokens::SEMI_COLON, "\n");
                i.format (alias stream);
                j += 1us;
            }
            if (self._content.len != 0us) {
                stream:.write ('\n'c8);
            }
            stream:.detabing ();
            stream:.write ("}"s8);
            
            match self._catcher {
                EmptyExpression () => { stream:.write ("\n"s8); }
                _ => {
                    self._catcher.format (alias stream);
                }
            }
            
            for i in self._scopes {
                i.format (alias stream);
            }
        }        
    }
    
}
