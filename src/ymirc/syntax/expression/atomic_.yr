mod ymirc::syntax::expression::atomic_;

import ymirc::syntax::declaration::_;
import ymirc::syntax::expression::_;

import ymirc::syntax::_;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;

import ymirc::utils::serialize;
import std::io;

/**
 * Syntaxic representation of an atomic expression
 */
pub class @final AtomicExpr over Expression {

    /// The expression that is synchronized
    let _sync : &Expression;

    /// The content value
    let _value : &Expression;

    /**
     * @params:
     *   - loc: the location of the atomic
     *   - sync: the value used as a monitor
     *   - value: the value that must be accessed synchronisely
     */
    pub self (loc : &Word, monitor : &Expression = UnitExpr::new (), value : &Expression)
        with super (loc), _sync = monitor, _value = value
    {}


    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */


    /**
     * @returns: the inner value of the atomic expression
     */
    pub fn getValue (self)-> &Expression {
        self._value
    }

    /**
     * @returns: the value used as a monitor
     */
    pub fn getMonitor (self)-> &Expression {
        self._sync
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Expression)-> bool {
        match o {
            a : &AtomicExpr => {
                a._value == self._value && a._sync == self._sync
            }
            _ => { false }
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Serializable;
    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the atomic inside the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            stream:.write (Keys::ATOMIC);
            stream:.entabing ();
            match self._sync {
                UnitExpr () => {}
                _ => {
                    stream:.write (" ("s8);
                    self._sync.format (alias stream);
                    stream:.write (')'c8);
                }
            }
            stream:.write (" {\n"s8);
            self._value.format (alias stream);
            stream:.detabing ();
            stream:.write ("\n}"s8);
        }
    }

}
