/*
 * Declares the ConditionalLetExp class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::expression::control::conditional
 * @File: ymirc/syntax/expression/control/conditional.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-12
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in iflet;

use ymirc::syntax::expression::empty;

use std::stream;
use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;

/**
 * Syntaxic representation of a condition control structure, i.e. if else
 *
 */
@final
pub class ConditionalLetExpr over ControlExpr {

    /*! Public fields */
    pub {
        // The test of the conditional
        let test: &Expression;

        // Guard expression, applicable iif the condition is an ifLet
        let guard: &Expression;

        // The block of the conditional
        let block: &Expression;

        // The else part of the conditional
        let else_: &Expression;
    }
    
    /**
     * @params: 
     *   - loc:     the location of the expression
     *   - test:    the test of the expression
     *   - block:   the block of the expression
     *   - guard:   the guard expression (added to the test part)
     *   - else_:   the else part of the conditional
     */
    pub self (loc: &Word,
              test: &Expression,
              block: &Expression,              
              guard: &Expression,
              else_: &Expression = EMPTY_EXPR)              
        
        with super (loc)
        , test = test
        , block = block
        , else_ = else_        
        , guard = guard
    {}
    
    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream: &Formatter) {
            stream:.write (Keys::IF, ' ', Keys::LET, ' ');
            stream:.entabing ();
            self.test.format (alias stream);
            stream:.write (' ');
            if self.guard !of EmptyExpr {
                stream:.write (Tokens::DAND, " ");
                self.guard.format (alias stream);
            }
            
            stream:.detabing ();
            
            self.block.format (alias stream);
            if self.else_ !of EmptyExpr {                
                stream:.write (Keys::ELSE, ' ');
                self.else_.format (alias stream);                
            }            
        }
    }
    
}
