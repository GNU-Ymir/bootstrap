/*
 * Declares the ForLoopExpr class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::expression::control::for_
 * @File: ymirc/syntax/expression/control/for_.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-12
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in for_;

use std::stream;

use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;

/**
 * Syntaxic representation of a for loop
 *
 */
@final
pub class ForLoopExpr over ControlExpr {

    pub {
        // The list of var declarations
        let vars: [&Expression];

        // The iteration value
        let iter: &Expression;

        // The content of the loop
        let block: &Expression;

        // The location of the cte keyword if any
        let isCte: &Word;
    }
    
    /**
     * @params: 
     *    - loc:   the location of the loop
     *    - isCte: the location of the cte keyword (or EOF_WORD)
     *    - vars:  the list of variable used to iter over iter
     *    - iter:  the value iterate over
     *    - block: the block of expression value of each loop
     */
    pub self (loc: &Word, isCte: &Word, vars: [&Expression], iter: &Expression, block: &Expression)
        with super (loc)
        , isCte = isCte
        , vars = vars
        , iter = iter
        , block = block
    {}
    
    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the loop inside the stream
         */
        pub over format (self, dmut stream: &Formatter) {
            if !self.isCte.isEof
                stream:.write (Keys::CTE, ' ');
            
            stream:.write (Keys::FOR, ' ');            
            stream:.entabing ();
            
            for i, elem in self.vars {
                if (i != 0) stream:.write (", ");
                elem.format (alias stream);                
            }
            stream:.write (' ', Keys::IN, ' ');
            self.iter.format (alias stream);
            stream:.write (' ');
            stream:.detabing ();  
            self.block.format (alias stream);             
        }        
    }

}
