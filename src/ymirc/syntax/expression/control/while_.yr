/*
 * Declares the WhileLoopExpr class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::expression::control::while_
 * @File: ymirc/syntax/expression/control/while_.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-12
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in while_;

use ymirc::syntax::expression::empty;

use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;

use std::stream;

/**
 * Syntaxic representation of a while loop control structure
 *
 */
@final
pub class WhileLoopExpr over ControlExpr {

    pub {
        // The test of the while
        let test: &Expression;

        // The content of the loop
        let content: &Expression;

        // True if this is a do while loop
        let isDo: bool;
    }

    /**
     * @params: 
     *   - loc: the location of the expression
     *   - test: the test of the expression (can be EmptyExpr for infinite loops)
     *   - content: the content of the expression
     *   - isDo: the while loop is a do while
     */
    pub self (loc:     &Word,
              test:    &Expression,
              content: &Expression,
              isDo:    bool)
        
        with super (loc)
        , test = test
        , content = content
        , isDo = isDo
    {}
    
    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream: &Formatter) {
            if self.test of EmptyExpr {        
                stream:.write (Keys::LOOP, ' ');
                stream:.entabing ();
                self.content.format (alias stream);
                stream:.detabing ();
            }

            else if self.isDo {            
                stream:.write (Keys::DO, ' ');
                self.content.format (alias stream);
                stream:.write (' ', Keys::WHILE, ' ');
                stream:.entabing ();
                self.test.format (alias stream);
                stream:.detabing ();
            }
            
            else {
                stream:.write (Keys::WHILE, ' ');
                stream:.entabing ();
                self.test.format (alias stream);
                stream:.write (' ');
                stream:.detabing ();
                self.content.format (alias stream);
            }
        }
    }
    
}
