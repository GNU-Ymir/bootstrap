/*
 * Declares the MatcherExpr class, and MatchLine record.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::expression::control::match_
 * @File: ymirc/syntax/expression/control/match_.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-12
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in match_;

use ymirc::syntax::expression::empty;
use std::stream;

use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;


/**
 * Record holding the different values of a line in a matcher/catcher expression
 * */
pub record MatchLine {
    
    // The filter to apply
    pub let filter: &Expression;

    // The guard to test if the filter passed
    pub let guard: &Expression;

    // The value to execute if the filter and guard passed
    pub let value: &Expression;

    /**
     * @params:
     *    - filter: the filter to apply
     *    - guard:  the guard to test if the filter pass
     *    - value:  the value to execute if the filter and guard passed
     * */
    pub self (filter: &Expression, guard: &Expression, value: &Expression)
        with filter = filter
        , guard = guard
        , value = value
    {}
    
}

/**
 * syntaxic representation of a matcher 
 *
 */
@final
pub class MatcherExpr over ControlExpr {

    /*! Public fields */
    pub {
        // The value being matched
        let value: &Expression;

        // The list of matchers lines
        let matchers: [MatchLine];

        // Is this a catch
        let isCatcher: bool;
    }
    
    /**
     * @params:
     *  - loc:       the location of the matcher
     *  - value:     the value being pattern matched
     *  - matchers:  the list of matcher lines
     *  - isCatcher: true, iif the matcher is a catcher
     */
    pub self (loc: &Word, value: &Expression, matchers: [MatchLine], isCatcher: bool)
        with super (loc)
        , value = value
        , matchers = matchers
        , isCatcher = isCatcher
    {}

        
    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the matcher inside the stream
         */
        pub over format (self, dmut stream: &Formatter) {
            if (self.isCatcher) {                
                stream:.write (' ', Keys::CATCH, " ", Tokens::LACC);
            } else {
                stream:.write (Keys::MATCH, ' ');
                self.value.format (alias stream);
                stream:.write (" ", Tokens::LACC);
            }

            stream:.entabing ();
            stream:.write ("\n");
            for elem in self.matchers {
                elem.filter.format (alias stream);
                if elem.guard !of EmptyExpr {
                    stream:.write (' ', Keys::IF, ' ');
                    elem.guard.format (alias stream);
                }
                
                stream:.write (' ', Tokens::DARROW, ' ');
                elem.value.format (alias stream);
                stream:.write ("\n");
            }
            stream:.detabing ();
            stream:.write (Tokens::RACC);
        }

    }
    
}
