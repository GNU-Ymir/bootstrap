mod ymirc::syntax::expression::matcher;

import ymirc::syntax::declaration::_;
import ymirc::syntax::expression::_;

import ymirc::syntax::_;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;

import ymirc::utils::serialize;
import std::io;

/**
 * Syntaxic representation of a matcher 
 * @verbatim: 
 * ==============
 * catch_expr := Keys::CATCH Tokens::LACC (expression:(0) Tokens::DARROW expression:(0))* Tokens::RACC
 * match_expr := Keys::MATCH expression:(0) Tokens::LACC (expression:(0) Tokens::DARROW expression:(0))* Tokens::RACC
 * ==============
 */
pub class @final MatcherExpr over Expression {

    /// The content of the expression
    let _content : &Expression;

    /// The list of matchers
    let _matchers : [(&Expression, &Expression)];

    /// Is this a catch 
    let _isCatcher : bool;
    
    /**
     * @params:
     *  - loc: the location of the matcher
     *  - content: the expression on which to match
     *  - matchers: the list of matcher
     *  - isCatcher: true, if the matcher is a catcher, false otherwise
     */
    pub self (loc : &Word, content : &Expression, matchers : [(&Expression, &Expression)], isCatcher : bool = false)
        with super (loc), _content = content, _matchers = matchers, _isCatcher = isCatcher
    {}

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */    
    
    /**
     * @returns: the matchers (rule, content)
     */
    pub fn getMatchers (self)-> [(&Expression, &Expression)] {
        self._matchers
    }

    /**
     * @returns: true, if the matcher is a catcher
     */
    pub fn isCatcher (self)-> bool {
        self._isCatcher
    }

    /**
     * @returns: the content of the matcher
     * @info: if the matcher is a catcher, it will return the block
     */
    pub fn getContent (self)-> &Expression {
        self._content
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Expression)-> bool {
        match o {
            m : &MatcherExpr => {
                if (m._matchers.len != self._matchers.len) return false;
                for i in 0us .. m._matchers.len {
                    if (m._matchers[i]._0 != self._matchers[i]._0 ||
                        m._matchers[i]._1 != self._matchers[i]._1) return false;
                }
                
                m._isCatcher == self._isCatcher && m._content == self._content
            }
            _ => { false }
        }
    }
        
    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Serializable;
    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the matcher inside the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            if (self._isCatcher) {
                //self._content.format (alias stream);
                stream:.write (' 'c8, Keys::CATCH, " { "s8);
            } else {
                stream:.write (Keys::MATCH, ' 'c8);
                self._content.format (alias stream);
                stream:.write (" {"s8);
            }

            stream:.entabing ();
            stream:.write ("\n"s8);
            for i in self._matchers {
                (i._0).format (alias stream);
                stream:.write (' 'c8, Tokens::DARROW, ' 'c8);
                (i._1).format (alias stream);
                stream:.write ("\n"s8);
            }
            stream:.detabing ();
            stream:.write ("}");
        }

    }
    
}
