mod ymirc::syntax::expression::decoexpr;

import ymirc::syntax::declaration::_;
import ymirc::syntax::expression::_;

import ymirc::syntax::_;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;

import std::io;

/**
 * Syntaxic representation of a decorated expression
 * @verbatim: 
 * ================
 * deco_expr := (Keys::MUT | Keys::DMUT | Keys::STATIC | Keys::CTE) expression:(0)
 * ================
 */
pub class @final DecoratedExpr over Expression {

    /// The list of decorators 
    let _decorators : [DecoratorWord];

    /// The content of the expression
    let _content : &Expression;

    /**
     * @params: 
     *    - loc: the location of the expression
     *    - content: the 
     */
    pub self (loc : &Word, content : &Expression, decorators : [DecoratorWord])
        with super (loc), _content = content, _decorators = decorators
    {}

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    
    /**
     * @returns: the list of decorators 
     */
    pub fn getDecorators (self)-> [DecoratorWord] {
        self._decorators
    }

    /**
     * @returns: the content expression
     */
    pub fn getContent (self)-> &Expression {
        return self._content
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Expression)-> bool {
        match o {
            d : &DecoratedExpr => {
                if (d._decorators.len != self._decorators.len) return false;
                for i in 0us .. d._decorators.len {
                    if (d._decorators[i].deco != self._decorators[i].deco) return false;
                }
                
                self._content == d._content
            }
            _ => {
                false
            }
        }
    }   
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        /**
         * Pretty print of the expression into the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            let mut j = 0u64;
            for i in self._decorators {
                if (j != 0u64) stream:.write (' 'c8);
                stream:.write (i.deco);
                j += 1u64;
            }
            
            stream:.write (' 'c8);
            stream:.entabing ();
            self._content.format (alias stream);
            stream:.detabing ();
        }
    }
    
}

