in for_;

use ymirc::syntax::expression::base;

use ymirc::syntax::keys;
use ymirc::lexing::{word, tokens};
use ymirc::utils::format;

/**
 * Syntaxic representation of a for loop
 * @verbatim: 
 * ===============
 * for_loop := Keys::FOR var_decl*(',') Keys::IN expression:(0) expression:(0)
 * ===============
 */
@final
pub class ForLoopExpr over Expression {

    // The list of var declarations
    let _vars : [&Expression];

    // The iteration value
    let _iter : &Expression;

    // The content of the loop
    let _block : &Expression;

    /**
     * @params: 
     *    - loc: the location of the loop
     *    - vars: the list of variable used to iter over iter
     *    - iter: the value iterate over
     *    - block: the block of expression value of each loop
     */
    pub self (loc : &Word, vars : [&Expression], iter : &Expression, block : &Expression)
        with super (loc), _vars = vars, _iter = iter, _block = block
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */
    
    /**
     * @returns: the list of variable used as iterators
     */
    pub fn getVariables (self)-> [&Expression] {
        self._vars
    }

    /**
     * @returns: the expression content
     */
    pub fn getBlock (self)-> &Expression {
        self._block
    }

    /**
     * @returns: the value to iterate over
     */
    pub fn getValue (self)-> &Expression {
        self._iter
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Expression)-> bool {
        match o {
            f : &ForLoopExpr => {
                f._vars == self._vars && f._block == self._block && f._iter == self._iter
            }
            _ => {
                false
            }
        }
    }
    
    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    
    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the loop inside the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            stream:.write (Keys::FOR, ' ');
            let mut j = 0u64;
            stream:.entabing ();
            for i in self._vars {
                if (j != 0u64) stream:.write (", ");
                i.format (alias stream);
                j += 1u64;
            }
            stream:.write (' ', Keys::IN, ' ');
            self._iter.format (alias stream);
            stream:.write (' ');
            stream:.detabing ();  
            self._block.format (alias stream);             
        }        
    }

}
