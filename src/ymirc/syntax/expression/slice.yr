mod ymirc::syntax::expression::slice;

import ymirc::syntax::declaration::_;
import ymirc::syntax::expression::_;

import ymirc::syntax::_;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;

import std::net::packet;
import std::io;

/**
 * Syntaxic representation of a slice allocation 
 * This allocation can be either static of dynamic (resulting an array or a slice)
 * @verbatim: 
 * ===============
 * slice_alloc := Keys::LCRO expression:(0) Tokens::SEMI_COLON (Keys::NEW)? expression:(0) Tokens::RCRO
 * ===============
 */
pub class @final SliceAllocatorExpr over Expression {

    /// The left value
    let _value : &Expression;

    /// The size of the allocation
    let _size : &Expression;

    /// True iif this is a dynamic allocation
    let _isDynamic : bool ;

    /**
     * @params: 
     *    - loc: the location of the allocator
     *    - value: the value (to replicate at each index)
     *    - size: the size of the allocation
     *    - isDynamic: true, if the allocation must be dynamic, false otherwise
     */
    pub self (loc : &Word, value : &Expression, size : &Expression, isDynamic : bool = false)
        with super (loc), _value = value, _size = size, _isDynamic = isDynamic
    {}

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */
    
    /**
     * @returns: the value to replicate at each index
     */
    pub fn getValue (self)-> &Expression {
        self._value
    }

    /**
     * @returns: the size of the allocation
     */
    pub fn getSize (self)-> &Expression {
        self._size
    }

    /**
     * @returns: true, if the allocation is dynamic, false otherwise
     */
    pub fn isDynamic (self)-> bool {
        self._isDynamic
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Expression)-> bool {
        match o {
            s : &SliceAllocatorExpr => {
                s._value == self._value && s._size == self._size && s._isDynamic == self._isDynamic
            }
            _ => { false }                
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Packable;
    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the allocator inside the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            stream:.write (Tokens::LCRO, ' 'c8);
            stream:.entabing ();
            self._value.format (alias stream);
            stream:.write (' 'c8, Tokens::SEMI_COLON, ' 'c8);
            if (self._isDynamic) {
                stream:.write (Keys::NEW, ' 'c8);                
            }
            self._size.format (alias stream);
            stream:.detabing ();
            stream:.write (Tokens::RCRO);
        }        
    }
    
}
