/*
 * Declares the VarDeclExpr class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::expression::instruction::vardecl
 * @File: ymirc/syntax/expression/instruction/vardecl.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-12
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in vardecl;

use ymirc::syntax::expression::empty;

use std::stream;

use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;

/**
 * Syntaxic representation of a single var declaration
 *
 */
@final
pub class VarDeclExpr over InstructionExpr {

    pub {
        // The name of the variable to declare
        let name: &Word;

        // The type of the variable
        let type: &Expression;

        // The value of the variable
        let value: &Expression;

        // The location of the word mut or dmut
        let isMutOrDmut: &Word;

        // The location of the word if lazy or ref (EOF if none of those)
        let isLazyOrRef: &Word;
    }
        
    /**
     * @params: 
     *    - loc: the location of the variable declaration (let keyword)
     *    - name: the identifier of the variable
     *    - value: the value part of the vardecl
     *    - type: the type part of the vardecl
     *    - isMutOrDmut: the location of the 'mut' of 'dmut' decorator (if any)
     *    - isLazyOrRef: the location of the 'lazy' of 'ref' decorator (if any)
     */
    pub self (loc:         &Word,
              name:        &Word,
              value:       &Expression,
              type:        &Expression,
              isMutOrDmut: &Word,
              isLazyOrRef: &Word)
        
        with super (loc)
        , name = name
        , value = value
        , type = type
        , isMutOrDmut = isMutOrDmut
        , isLazyOrRef = isLazyOrRef
    {}

    /**
     * @returns: true if the variable is lazy
     * */
    @field
    pub fn isLazy (self)-> bool {
        self.isLazyOrRef == Keys::LAZY
    }

    /**
     * @returns: the location of the lazy keyword (if lazy)
     * */
    @field
    pub fn lazyLoc (self)-> &Word {
        if (self.isLazyOrRef == Keys::LAZY)
            return self.isLazyOrRef;
        
        EOF_WORD
    }

    /**
     * @returns: true if the variable is a reference
     * */
    @field
    pub fn isRef (self)-> bool {
        self.isLazyOrRef == Keys::REF
    }

    /**
     * @return: the location of the ref keyword if ref
     * */
    @field
    pub fn refLoc (self)-> &Word {
        if (self.isLazyOrRef == Keys::REF)
            return self.isLazyOrRef;
        
        EOF_WORD
    }

    /**
     * @returns: true if the var is deeply mutable
     * */
    @field
    pub fn isDmut (self)-> bool {
        self.isMutOrDmut == Keys::DMUTABLE
    }

    /**
     * @returns: the location of the  dmut keyword if deeply mutable
     * */
    @field
    pub fn dmutLoc (self)-> &Word {
        if (self.isMutOrDmut == Keys::DMUTABLE)
            return self.isMutOrDmut;
        
        EOF_WORD
    }

    /**
     * @returns: true if the var is mutable (but not deeply)
     * */
    @field
    pub fn isMut (self)-> bool {
        self.isMutOrDmut == Keys::MUTABLE
    }

    /**
     * @return: the location of the keyword mut if mutable (but not deeply)
     * */
    @field
    pub fn mutLoc (self)-> &Word {
        if (self.isMutOrDmut == Keys::MUTABLE)
            return self.isMutOrDmut;
        
        EOF_WORD
    }
        
    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream: &Formatter) {
            stream:.write (Keys::LET, ' ');

            if (!self.isLazyOrRef.isEof) stream:.write (self.isLazyOrRef.str, ' ');
            if (!self.isMutOrDmut.isEof) stream:.write (self.isMutOrDmut.str, ' ');

            stream:.write (self.name.str);
            
            stream:.entabing ();
            if self.type !of EmptyExpr {
                stream:.write (' ', Tokens::COLON, ' ');
                self.type.format (alias stream);
            }
            
            if self.value !of EmptyExpr {        
                stream:.write (' ', Tokens::EQUAL, ' ');
                self.value.format (alias stream);                
            }            
            stream:.detabing ();
        }
    }

}
