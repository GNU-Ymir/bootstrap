/*
 * Declares the ListExpr class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::expression::compose::list
 * @File: ymirc/syntax/expression/compose/list.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-12
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in list;

use std::stream;
use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;

/**
 * A list is a list of expression, surrounded by tokens, such as () or []
 *
 */
@final
pub class ListExpr over ComposeExpr {

    pub {
        // The location of the end of the expression
        let end: &Word;

        // The list of parameters
        let params: [&Expression];

        // True iif this is a list from an array initialization (between [])
        // Tuple otherwise
        let isArray: bool;
    }
    
    /**
     * @params: 
     *    - loc: the location (and type, depending on the token contained in the word), of the expression
     *    - end: the location of the closing token of the expression
     *    - params: the list of parameters inside the literal
     */
    pub self (loc: &Word, end: &Word, params: [&Expression], isArray: bool)
        with super (loc)
        , end = end
        , params = params
        , isArray = isArray
    {}

    /**
     * @returns: true iif the list is a tuple 
     * */
    @field
    pub fn isTuple (self)-> bool {
        !self.isArray
    }
        
    
    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream: &Formatter) {
            if self.isArray
                stream:.write (Tokens::LCRO);
            else
                stream:.write (Tokens::LPAR);
            
            stream:.entabing ();            
            for i, elem in self.params {
                if (i != 0) stream:.write (", ");
                elem.format (alias stream);                
            }
            
            if (self.params.len == 1us && !self.isArray) // (one,)
                stream:.write (',');
            
            stream:.detabing ();

            if self.isArray
                stream:.write (Tokens::RCRO);
            else
                stream:.write (Tokens::RPAR);            
        }
    }
    
}
