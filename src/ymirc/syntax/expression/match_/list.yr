/*
 * Declares the MatchListFilterExpr class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::expression::match_::list
 * @File: ymirc/syntax/expression/match_/list.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-12
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in list;

use std::stream;
use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;


/**
 * Syntaxic representation of a list matcher expression (A, B...)
 *
 */
@final
pub class MatchListFilterExpr over MatchFilterExpr {

    /*! Public fields */
    pub {
        // The closing token
        let end: &Word;

        // The inner operand
        let params: [&Expression];

        // True if this matcher is variadic
        let isVariadic: bool;

        // Matching an array or a tuple
        let isArray: bool;
    }

    /**
     * @params:
     *    - loc: the location and operator of the expression
     *    - end: the location of the closing token of the filter
     *    - params: the list of parameters in the matcher
     *    - isVariadic: true if the list is variadic (last filter taking everything else)
     *    - isArray: true if this is an array filter (tuple otherwise)
     */
    pub self (loc: &Word, end: &Word, params: [&Expression], isVariadic: bool, isArray: bool)
        with super (loc)
        , end = end
        , params = params
        , isVariadic = isVariadic
        , isArray = isArray 
    {}

    /**
     * @returns: true if this is a tuple filter
     * */
    @field
    pub fn isTuple (self)-> bool {
        !self.isArray
    }
        
    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream: &Formatter) {
            if (self.isTuple) 
                stream:.write (Tokens::LPAR);
            else
                stream:.write (Tokens::LCRO);            

            for i, elem in self.params {
                if (i != 0us) stream:.write (", ");
                elem.format (alias stream);
            }

            if (self.isTuple && self.params.len == 1us) stream:.write (',');
            if (self.isTuple) 
                stream:.write (Tokens::RPAR);
            else 
                stream:.write (Tokens::RCRO);            
        }
    }
}
