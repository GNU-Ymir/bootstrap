mod ymirc::syntax::expression::break_;

import ymirc::syntax::declaration::_;
import ymirc::syntax::expression::_;

import ymirc::syntax::_;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;

import ymirc::utils::serialize;
import std::io;

/**
 * Syntaxic node representation of a break expression
 * @verbatim: 
 * ==============
 * break_expr := Keys::BREAK expression:(0)
 * ==============
 */
pub class @final BreakExpr over Expression {

    /// The value of the break expression
    let _value : &Expression;

    /**
     * @params : 
     *    - loc: the location of the break
     *    - value: the value of the break
     */
    pub self (loc : &Word, value : &Expression)
        with super (loc), _value = value
    {}

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */
    
    /**
     * @returns: the value of the break
     */
    pub fn getValue (self)-> &Expression {
        self._value
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Expression)-> bool {
        match o {
            b : &BreakExpr => {
                self._value == b._value 
            }
            _ => { false }
        }
    }    
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Serializable;
    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the break
         */
        pub over format (self, dmut stream : &Formatter) {
            stream:.write (Keys::BREAK, ' 'c8);
            //stream:.entabing ();
            self._value.format (alias stream);
            //stream:.detabing ();
        }        
    }
    
}
