mod ymirc::syntax::expression::while_;

import ymirc::syntax::declaration::_;
import ymirc::syntax::expression::_;

import ymirc::syntax::_;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;

import std::io;


/**
 * Syntaxic representation of a while loop control structure
 * @verbatim: 
 * =============
 * while_expr :=   Keys::WHILE expression:(0) expression:(0)
 *               | Keys::DO expression:(0) Keys::WHILE expression:(0)
 * =============
 */
pub class @final WhileLoop over Expression {

    /// The test of the while
    let _test : &Expression;

    /// The content of the loop
    let _content : &Expression;

    /// True if this is a do while loop
    let _isDo : bool;

    /**
     * @params: 
     *   - loc: the location of the expression
     *   - test: the test of the expression, (if not specified the while loop is an infinite loop)
     *   - content: the content of the expression
     *   - isDo: the while loop is a do while
     */
    pub self (loc : &Word, test : &Expression = Unit::new (), content : &Expression, isDo : bool = false)
        with super (loc), _test = test, _content = content, _isDo = isDo
    {}

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */
    
    /**
     * @returns: the test to validate to enter the while loop
     */
    pub def getTest (self)-> &Expression {
        self._test
    }

    /**
     * @returns: the expression to evaluate if the condition is true
     */
    pub def getContent (self)-> &Expression {
        self._content
    }

    /**
     * @returns: true, if the loop is a do while loop
     */
    pub def isDo (self)-> bool {
        self._isDo
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */
    
    impl Streamable;
    impl Formattable {

        /**        
         * Pretty formatting of the while inside the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            match self._test {
                Unit () => {
                    stream:.write (Keys::LOOP, ' 'c8);
                    stream:.entabing ();
                    self._content.format (alias stream);
                    stream:.detabing ();
                }
                _ =>  {
                    if (self._isDo) {
                        stream:.write (Keys::DO, ' 'c8);
                        stream:.entabing ();
                        self._content.format (alias stream);
                        stream:.detabing ();
                        stream:.write (' 'c8, Keys::WHILE, ' 'c8);
                        stream:.entabing ();
                        self._test.format (alias stream);
                        stream:.detabing ();
                    } else {
                        stream:.write (Keys::WHILE, ' 'c8);
                        stream:.entabing ();
                        self._test.format (alias stream);
                        self._content.format (alias stream);
                        stream:.detabing ();
                    }
                }
            }
        }
    }
    
}
