/*
 * Declares the CtorDecl class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::declaration::function::ctor
 * @File: ymirc/syntax/declaration/function/ctor.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-12
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in ctor;

use ymirc::syntax::declaration::attributes;
use ymirc::syntax::{expression, expression::empty};

use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;
use std::stream;

/**
 * A FieldCtor defines a field assigned value at Class/Struct construction
 * */
pub record FieldCtor {

    // The location of the affectation
    pub let loc: &Word;

    // The name of the field to set
    pub let name: &Word;

    // The value to assign to the field
    pub let value: &Expression;

    /**
     * @params:
     *    - loc:   the location of the field ctor (the affectation operator)
     *    - name:  the name of the field to construct
     *    - value: the value to set in the field at construction
     * */
    pub self (loc: &Word, name: &Word, value: &Expression)
        with loc = loc
        , name = name
        , value = value
    {}

}

/**
 * A redirect ctor defines a call to another ctor inside the ctor (`self` or
 * `super`)
 * */
pub record RedirectCtor {

    // The location of the `super` or `self` keyword
    pub let loc: &Word;

    // The name of the redirection (or EOF if unamed)
    pub let name: &Word;

    // The list of parameters passed to the redirection
    pub let params: [&Expression];

    // The list of template parameters passed to the redirection
    pub let templateParams: [&Expression];

    /**
     * @params:
     *    - loc:            the location of the `super` or `self` keyword
     *    - name:           the name of the redirection (or EOF if unamed)
     *    - params:         the list of parameters passed to the redirection
     *    - templateParams: the list of template parameters passed to the redirection
     * */
    pub self (loc: &Word, name: &Word, params: [&Expression], templateParams: [&Expression])
        with loc = loc
        , name = name
        , params = params
        , templateParams = templateParams
    {}
}

/**
 * Syntaxic node representation of a constructor
 *
 */
@final
pub class CtorDecl over FunctionDecl {
    
    pub {
        // The call to a redirection ctor
        let redirect: (RedirectCtor)?;
        
        // The list of field construction
        let fields: [FieldCtor];
    }
    
    /**
     * @params:
     *   - loc:        the location of the ctor
     *   - comm:       the comment above the ctor
     *   - rename:     the renaming of the ctor
     *   - proto:      the prototype of the ctor     
     *   - redirect:   the call to a redirection ctor
     *   - fields:     the list of fields constructions
     *   - body:       the body of the ctor    
     *   - attrs:      the list of attributes above the ctor
     *   - throwers:   the list of exception thrown by the ctor
     *   - throwerLoc: the location of the 'throws' keyword
     *     
     */
    pub self (loc:        &Word,
              comm:      [c8],
              rename:     &Word,
              proto:      FunctionPrototype,
              redirect:   (RedirectCtor)?,              
              fields:     [FieldCtor],
              body:       &Expression,              
              attrs:      [AttributeWord],
              throwers:   [&Expression],
              throwerLoc: &Word)
        
        with super (loc, comm, rename, proto, body, attrs, throwers, throwerLoc, false)
        , redirect = redirect        
        , fields = fields        
        
    {}
  
    
    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream: &Formatter) {
            if (self.attrs.len != 0u64) { // write attributes
                stream:.write (Tokens::AT, Tokens::LACC);                
                for i, e in self.attrs {
                    if (i != 0) stream:.write (Tokens::COMA, " ");
                    stream:.write (e.attr);                    
                }
                stream:.write (Tokens::RACC, Tokens::RETURN);
            }
            
            stream:.write (Keys::SELF, ' ');
            if !self.name.isEof // Write ctor name 
                stream:.write (self.name.str, ' ');
            
            stream:.entabing ();
            self.proto.format (alias stream); // Prototype
            if (self.redirect.hasValue || self.fields.len != 0) {
                stream:.write (Tokens::RETURN, Keys::WITH);
            }
            
            if let Ok (red) = self.redirect { // Super/self call                        
                stream:.write (red.loc.str);
                if !red.name.isEof { // self::rename
                    stream:.write (Tokens::DCOLON, red.name.str);
                }
                
                if red.templateParams.len != 0 { // self::rename!{T}
                    stream:.write (Tokens::NOT, Tokens::LACC);
                    for i, e in self.red.templateParams {
                        if i != 0 stream:.write (Tokens::COMA, " ");
                        e.format (alias stream);
                    }
                    stream:.write (Tokens::RACC);
                }
                
                stream:.write (Tokens::LPAR);                                
                for i, e in self.red.params { // self::rename!{T} (P...)
                    if (i != 0) stream:.write (Tokens::COMA, " ");
                    e.format (alias stream);                    
                }
                stream:.write (Tokens::RPAR);                
            }
            
            for i, e in self.fields { // , field = X
                if (i != 0 || self.renameRedirectCall.isEof) // with field = X, or with super ()\n, field = X
                    stream:.write (Tokens::RETURN, Tokens::COMA, " ");
                
                stream:.write (e.name, " ", Tokens::EQUAL, " ");
                e.value.format (alias stream);                
            }
            
            if (self.throwers.len != 0) { // throws X, Y, Z
                stream:.write (Tokens::RETURN, Keys::THROWS);                
                for i, e in self.throwers {
                    if (i != 0u64) stream:.write (Tokens::COMA, " ");
                    e.format (alias stream);                
                }                
            }
            stream:.detabing ();
            
            if (!self.redirect.hasValue || self.fields.len != 0 || self.throwers.len != 0) {
                stream:.write (Tokens::RETURN);
            }

            // { ... }
            self.body.format (alias stream);
            stream:.write (Tokens::RETURN);
        }

    }

}
