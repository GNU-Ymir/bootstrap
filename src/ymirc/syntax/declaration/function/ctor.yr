/*
 * Declares the CtorDecl class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::declaration::function::ctor
 * @File: ymirc/syntax/declaration/function/ctor.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-12
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in ctor;

use ymirc::syntax::declaration::attributes;
use ymirc::syntax::{expression, expression::empty};

use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;
use std::stream;

/**
 * A FieldCtor defines a field assigned value at Class/Struct construction
 * */
pub record FieldCtor {

    // The location of the affectation
    pub let loc: &Word;

    // The name of the field to set
    pub let name: &Word;

    // The value to assign to the field
    pub let value: &Expression;

    /**
     * @params:
     *    - loc:   the location of the field ctor (the affectation operator)
     *    - name:  the name of the field to construct
     *    - value: the value to set in the field at construction
     * */
    pub self (loc: &Word, name: &Word, value: &Expression)
        with loc = loc
        , name = name
        , value = value
    {}

}

/**
 * Syntaxic node representation of a constructor
 *
 */
@final
pub class CtorDecl over FunctionDecl {
    
    pub {
        // The parameters passed to the super/self ctor 
        let redirectParams: [&Expression];

        // The template parameters used to call the super/self ctor
        let redirectTemplateParams: [&Expression];

        // Set to a valid location if the constructor is a redirection
        // This word give use information for calling super or self
        let explicitRedirectCall: &Word;

        // has a name if the ancestor call is renamed i.e. self::rename,
        // super::rename    
        let renameRedirectCall: &Word;

        // The list of field construction
        let fieldCtors: [FieldCtor];
    }
    
    /**
     * @params:
     *   - loc:                    the location of the ctor
     *   - comms:                  the comment above the ctor
     *   - rename:                 the renaming of the ctor
     *   - proto:                  the prototype of the ctor
     *   - redirectParams:         the list of parameters passed to the redirection ctor (self/super)
     *   - redirectTemplateParams: the list of template parameters passed to the redirection ctor
     *   - explicitRedirectCall:   the location of the redirection ctor (EOF is allowed iif redirectParams.len == 0)
     *   - renameRedirectCall:     the name of the redirection ctor called (self: : rename, EOF is allowed)
     *   - fieldCtors:             the list of fields constructions
     *   - body:                   the body of the ctor    
     *   - attrs:                  the list of attributes above the ctor
     *   - throwers:               the list of exception thrown by the ctor
     *   - throwerLoc:             the location of the 'throws' keyword
     *
     *      
     */
    pub self (loc:                    &Word,
              comms:                  [c8],
              rename:                 &Word,
              proto:                  FunctionPrototype,
              redirectParams:         [&Expression],
              redirectTemplateParams: [&Expression],
              explicitRedirectCall:   &Word,
              renameRedirectCall:     &Word,
              fieldCtors:             [FieldCtor],
              body:                   &Expression,              
              attrs:                  [AttributeWord],
              throwers:               [&Expression],
              throwerLoc:             &Word)
        
        with super (loc, comms, rename, proto, body, attrs, throwers, throwerLoc, false)                
        , redirectParams = redirectParams
        , redirectTemplateParams = redirectTemplateParams
        , renameRedirectCall = renameRedirectCall
        , explicitRedirectCall = explicitRedirectCall
        , fieldCtors = fieldCtors        
        
    {}


    
    
    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream: &Formatter) {
            if (self.attrs.len != 0u64) { // write attributes
                stream:.write (Tokens::AT, Tokens::LACC);                
                for i, e in self.attrs {
                    if (i != 0) stream:.write (Tokens::COMA, " ");
                    stream:.write (e.attr);                    
                }
                stream:.write (Tokens::RACC, Tokens::RETURN);
            }
            
            stream:.write (Keys::SELF, ' ');
            if !self.name.isEof // Write ctor name 
                stream:.write (self.name.str, ' ');
            
            stream:.entabing ();
            self.proto.format (alias stream); // Prototype
            if (!self.explicitRedirectCall.isEof || self.fieldCtors.len != 0) {
                stream:.write (Tokens::RETURN, Keys::WITH);
            }

            
            if (!self.explicitRedirectCall.isEof) { // Super/self call
                stream:.write (self.explicitRedirectCall.str);
                if !self.renameRedirectCall.isEof { // self::rename
                    stream:.write (Tokens::DCOLON, self.explicitRedirectCall.str);
                }
                
                if self.redirectTemplateParams.len != 0 { // self::rename!{T}
                    stream:.write (Tokens::NOT, Tokens::LACC);
                    for i, e in self.redirectTemplateParams {
                        if i != 0 stream:.write (Tokens::COMA, " ");
                        e.format (alias stream);
                    }
                    stream:.write (Tokens::RACC);
                }
                
                stream:.write (Tokens::LPAR);                                
                for i, e in self.redirectParams { // self::rename!{T} (P...)
                    if (i != 0) stream:.write (Tokens::COMA, " ");
                    e.format (alias stream);                    
                }
                stream:.write (Tokens::RPAR);                
            }
            
            for i, e in self.fieldCtors { // , field = X
                if (i != 0 || self.renameRedirectCall.isEof) // with field = X, or with super ()\n, field = X
                    stream:.write (Tokens::RETURN, Tokens::COMA, " ");
                
                stream:.write (e.name, " ", Tokens::EQUAL, " ");
                e.value.format (alias stream);                
            }
            
            if (self.throwers.len != 0) { // throws X, Y, Z
                stream:.write (Tokens::RETURN, Keys::THROWS);                
                for i, e in self.throwers {
                    if (i != 0u64) stream:.write (Tokens::COMA, " ");
                    e.format (alias stream);                
                }                
            }
            stream:.detabing ();
            
            if (!self.explicitRedirectCall.isEof || self.fieldCtors.len != 0 || self.throwers.len != 0) {
                stream:.write (Tokens::RETURN);
            }

            // { ... }
            self.body.format (alias stream);
            stream:.write (Tokens::RETURN);
        }

    }

}
