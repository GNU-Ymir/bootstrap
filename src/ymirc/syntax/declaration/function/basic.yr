/*
 * Declares the FunctionDecl class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::declaration::function::basic
 * @File: ymirc/syntax/declaration/function/basic.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-12
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in basic;

use ymirc::syntax::declaration::attributes;
use ymirc::syntax::expression;

use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;
use std::stream;

/**
 * Syntax node encoding a function declaration
 *
 */
pub class FunctionDecl over Declaration {

    pub {
        // The name of the function being declared
        let name: &Word;
        
        // The prototype of the function
        let proto: FunctionPrototype;

        // The body of the function
        let body: &Expression;

        // The custom attributes of the function
        let attrs: [AttributeWord];

        // The throwers of the function
        let throwers: [&Expression];

        // The location of the throws word
        let throwerLoc: &Word;

        // True iif the function is marked as override
        let isOver: bool;
    }
    
    /**
     * @params: 
     *    - loc:      the identifier of the function
     *    - comm:     the comment above the function declaration
     *    - name:     the name of the function being declared
     *    - proto:    the prototype of the function
     *    - body:     the body of the function
     *    - attrs:    the attributes of the function (final, ...)
     *    - throwers: the list of throwers
     *    - isOver:   the function is declared as override (for methods)
     */
    pub self (loc:        &Word,              
              comm:       [c8],
              name:       &Word,
              proto:      FunctionPrototype,
              body:       &Expression,
              attrs:      [AttributeWord],
              throwers:   [&Expression],
              throwerLoc: &Word,
              isOver:     bool)
        
        with super (loc, comm)
        , proto = proto
        , name = name
        , body = body
        , attrs = attrs
        , throwers = throwers
        , isOver = isOver
        , throwerLoc = throwerLoc
    {}
    
    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream: &Formatter) {
            if (self.attrs.len != 0u64) {
                stream:.write (Tokens::AT, Tokens::LACC);                
                for i, e in self.attrs {
                    if (i != 0u64) stream:.write (Tokens::COMA, " ");
                    stream:.write (e.attr);                    
                }
                stream:.write (Tokens::RACC, Tokens::RETURN);
            }
            
            if (self.isOver)
                stream:.write (Keys::OVER, " ");
            else
                stream:.write (Keys::FUNCTION, " "); 
            
            stream:.write (self.name.str, ' ');
            
            stream:.entabing ();
            self.proto.format (alias stream);
            if (self.throwers.len != 0u64) {
                stream:.write (Tokens::RETURN, Keys::THROWS, ' ');                
                for i, t in self.throwers {
                    if (i != 0u64) stream:.write (Tokens::COMA, " ");
                    t.format (alias stream);                    
                }
                stream:.write (Tokens::RETURN);
            }
            stream:.detabing ();
            
            self.body.format (alias stream);
            stream:.write (Tokens::RETURN);
        }        
    }
    
}
