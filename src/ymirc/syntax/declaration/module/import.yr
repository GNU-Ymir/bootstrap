/*
 * Declares the ImportDecl class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::declaration::module::import_
 * @File: ymirc/syntax/declaration/module/import_.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-12
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in import;

use ymirc::syntax::expression;

use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;
use std::stream;

/**
 * Syntaxic representation of an importation, declaration of a submodule defined in another file.
 *
 */
@final
pub class ImportDecl over Declaration {

    pub {
        // The name of the module that is imported
        let name: &Word;

        // The root of the module path to import
        // If EOF, either sibling or child, if == '.' sibling, else child
        let root: &Word;
    }

    /**
     * @params:
     *    - loc: the location of the importation
     *    - comm: the comment above the importation
     *    - name: the name of the module to import
     *    - root: the root token in front of the name path ('.', '::', or EOF)
     */
    pub self (loc: &Word, comm: [c8], name: &Word, root: &Word)
        with super (loc, comm)
        , name = name
        , root = root
    {}


    /**
     * @returns: true if this necessarily imports a child module
     * */
    @field
    pub fn isChild (self)-> bool {
        if (self.root.isEof)
            return false;
        
        self.root == Tokens::DCOLON
    }

    /**
     * @returns: true if this necessarily imports a sibling module
     * */
    @field
    pub fn isSibling (self)-> bool {
        if (self.root.isEof)
            return false;
        
        self.root == Tokens::DOT
    }
    
    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream: &Formatter) {
            stream:.write (Keys::MOD, ' ');
            if !self.root.isEof
                stream:.write (self.root.str);
            
            stream:.write (self.name, Tokens::SEMI_COLON, Tokens::RETURN);
        }
    }

}
