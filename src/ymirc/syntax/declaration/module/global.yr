/*
 * Declares the GlobalVarDecl class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::declaration::module::global
 * @File: ymirc/syntax/declaration/module/global.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-12
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in global;

use ymirc::syntax::declaration::attributes;
use ymirc::syntax::{expression, expression::{instruction, empty}};

use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;
use std::stream;

/**
 * Syntaxic node representation of a global declaration (or a field)
 *
 */
@final
pub class GlobalVarDecl over Declaration {

    pub {
        // The declaration of the global
        let var: &VarDeclExpr;

        // Was declared with the keyword static, otherwise lazy (or self.isField)
        let isStatic: bool;

        // Was declared inside a TypeDecl
        let isField: bool;
        
        // The list of attributes
        let attrs: [AttributeWord];
    }

    /**
     * @params: 
     *     - loc: the location of the declaration (not necessarily the name)
     *     - comm: the comments about the declaration
     *     - var: the var declaration
     *     - attrs: the list of attributes above the declaration
     *     - isField: true if declared within a TypeDecl
     *     - isStatic: true if declared as extern static 
     */
    pub self (loc:      &Word,
              comm:     [c8],
              var:      &VarDeclExpr,
              attrs:    [AttributeWord],
              isField:  bool,
              isStatic: bool)
        
        with super (loc, comm)
        , var = var
        , attrs = attrs
        , isField = isField
        , isStatic = isStatic
    {}

    /**
     * @returns: true iif the global decl is declared lazy
     * */
    @field
    pub fn isLazy (self)-> bool {
        !self.isField && !self.isStatic
    }
    
    impl Streamable;
    impl Formattable {
        
        pub over format (self, dmut stream: &Formatter) {
            if (self.attrs.len != 0u64) {
                stream:.write (Tokens::AT, Tokens::LACC);                
                for i, e in self.attrs {
                    if (i != 0u64) stream:.write (Tokens::COMA, " ");
                    stream:.write (e.attr);                    
                }
                stream:.write (Tokens::RACC, Tokens::RETURN);
            }
            
            if (self.isStatic) {
                stream:.write (Keys::STATIC, ' ');
            } else if (self.isField) {
                stream:.write (Keys::LET, ' ');
            } else {
                stream:.write (Keys::LAZY, ' ');
            }

            stream:.entabing ();
            stream:.write (self.var.name.str);            
            stream:.entabing ();
            if self.var.type !of EmptyExpr {
                stream:.write (' ', Tokens::COLON, ' ');
                self.var.type.format (alias stream);
            }
            
            if self.var.value !of EmptyExpr {        
                stream:.write (' ', Tokens::EQUAL, ' ');
                self.var.value.format (alias stream);                
            }            
            stream:.detabing ();            
            stream:.detabing ();            
        }
        
    }

}
