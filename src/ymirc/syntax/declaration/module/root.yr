/*
 * Declares the ModuleDecl class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::declaration::module::root
 * @File: ymirc/syntax/declaration/module/root.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-12
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */


in root;

use ymirc::syntax::declaration::block;
use ymirc::syntax::expression;

use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;
use std::stream;

/**
 * A module declaration 
 *
 */
@final
pub class ModuleDecl over Declaration {

    pub {
        // The path of the module
        let name: &Word;

        // The list of declaration inside the module
        let block: &BlockDecl;

        // True iif the module is global
        let isGlobal: bool;
    }

    /**
     * @params: 
     *   - loc:      the location of the module
     *   - comm:     the comments about the module
     *   - name:     the name of the module 
     *   - block:    the content of the module
     *   - isGlobal: true, if this module is global
     */
    pub self (loc: &Word, comm: [c8], name: &Word, block: &BlockDecl, isGlobal: bool)
        with super (loc, comm)
        , name = name
        , block = block
        , isGlobal = isGlobal
    {}
    
    /**
     * @returns: the list of declaration inside the module (unwrapping the block directly)
     */
    @field
    pub fn decls (self)-> [&Declaration] {
        self.block.decls
    }
    
    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream: &Formatter) {
            if (self.isGlobal) {
                if !self.name.isEof
                    stream:.write (Keys::IN, ' ', self.name, Tokens::SEMI_COLON, Tokens::RETURN);
                
                for e in self.decls {
                    e.format (alias stream);
                    stream:.write (Tokens::RETURN);
                }
            } else {                
                stream:.write (Keys::MOD, ' ', self.name.str);                
                stream:.write (Tokens::LACC, Tokens::RETURN);
                stream:.entabing ();
                for e in self.decls {
                    e.format (alias stream);
                    stream:.write (Tokens::RETURN);
                }
                stream:.detabing ();
                stream:.write (Tokens::RETURN, Tokens::RACC, Tokens::RETURN);
            }
        }
        
    }
    
}
