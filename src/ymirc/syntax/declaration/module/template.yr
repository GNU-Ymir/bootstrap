/*
 * Declares the TemplateDecl class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::declaration::module::template
 * @File: ymirc/syntax/declaration/module/template.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-12
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in template;

use ymirc::syntax::{expression, expression::empty};

use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;
use std::stream;

/**
 * Syntaxic representation of a template declaration
 *
 * @warning: the formatter does not generate valid Ymir source, it is only for debug purposes
 */
@final
pub class TemplateDecl over Declaration {

    pub {
        // The guard expression after specialization if any
        let guard: &Expression;

        // The list of template parameters
        let params: [&Expression];

        // The declaration of the template if the test and specialization succeeds
        let decl: &Declaration;
    }
    
    /**
     * @params: 
     *    - loc:    the location of the template
     *    - comm:   the comments about the template
     *    - params: the template parameters (to specialize)
     *    - guard:   the template guard after specialization (or EmptyExpr)
     *    - decl:   the declaration to create if the template specialization succeeds
     */
    pub self (loc: &Word, comm: [c8], params: [&Expression], guard: &Expression, decl: &Declaration)
        with super (loc, comm)
        , params = params
        , guard = guard
        , decl = decl
    {}
        
    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream: &Formatter) {
            stream:.write ("template ", self.loc.str, " ", Tokens::LACC);
            stream:.entabing ();            
            for i, e in self.params {
                if (i != 0u64) stream:.write (Tokens::COMA, " ");
                e.format (alias stream);                
            }
            
            stream:.write (Tokens::RACC, " ");
            if self.guard !of EmptyExpr {
                stream:.write (Keys::IF, ' ');
                self.guard.format (alias stream);
                stream:.write (" ");            
            }
            
            stream:.write (Tokens::LACC, Tokens::RETURN);
            self.decl.format (alias stream);
            stream:.detabing ();
            stream:.write (Tokens::RETURN, Tokens::RACC, Tokens::RETURN);
        }
    }
    
}
