/*
 * Declares the DefDecl class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::declaration::def_
 * @File: ymirc/syntax/declaration/def_.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-12
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in def_;

use ymirc::syntax::expression;

use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;

use std::stream;

/**
 * A global `def` declaration 
 *
 */
@final
pub class DefDecl over Declaration {

    pub {
        // The name of the decl to define
        let name: &Word;
        
        // The content of the def
        let content: &Expression;

        // True if this aka defines a value, false if it defines a type
        let isValue: bool;

        // The location of the def keyword
        let tokDecl: &Word;
    }
    
    /**
     * @params: 
     *    - loc: the identifier of the aka
     *    - comm: the comment above the decl
     *    - name: the name of the decl to define
     *    - tokDecl: the location of the ':' or '=' token used to define a value or a type decl
     *    - content: the content of the `def`     
     */
    pub self (loc: &Word, comm: [c8], name: &Word, tokDecl: &Word, content: &Expression)
        with super (loc, comm)
        , name = name
        , content = content
        , isValue = (tokDecl.str == Tokens::EQUAL)
        , tokDecl = tokDecl
    {}

    /**
     * @returns: true iif the declaration defines a type
     * */
    @field
    pub fn isType (self)-> bool {
        self.isValue
    }
    
    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream: &Formatter) {
            stream:.write (Keys::DEF, ' ', self.name.str, ' ');

            if (self.isValue)
                stream:.write (Tokens::EQUAL, ' ');
            else
                stream:.write (Tokens::COLON, ' ');
            
            self.content.format (alias stream);
            stream:.write (Tokens::SEMI_COLON, '\n');
        }
    }
}
