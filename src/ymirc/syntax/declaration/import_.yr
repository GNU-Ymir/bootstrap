mod ymirc::syntax::declaration::import_;

import ymirc::syntax::declaration::base;
import ymirc::syntax::expression::base;

import ymirc::syntax::keys;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;


import std::io;


/**
 * Syntaxic representation of an importation
 * @verbatim:
 * ===============
 * import_decl := Keys::IMPORT Path
 * ===============
 */
pub class @final ImportDecl over Declaration {

    /// The name of the module that is imported
    let _module : &Word;

    // If not eof, then the importation cannot be a sibling but necessarily a children
    let _isSub : &Word;

    /**
     * @params:
     *    - loc: the location of the importation
     *    - com: the comments about the importation
     *    - module: the path of the module to import
     */
    pub self (loc : &Word, com : [c8], module : &Word, isSub : &Word = EOF_WORD)
        with super (loc, com)
        , _module = module
        , _isSub = isSub
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */


    /**
     * @returns: the path of the module to import
     */
    pub fn getModuleName (self)-> &Word {
        self._module
    }

    /**
     * @returns: true if this imports a child module
     * */
    pub fn isChild (self)-> bool {
        if (self._isSub.isEof ()) return false;
        return self._isSub == Tokens::DCOLON
    }

    /**
     * @returns: true if this imports a child module
     * */
    pub fn isSibling (self)-> bool {
        if (self._isSub.isEof ()) return false;
        return self._isSub == Tokens::DOT
    }

    /**
     * @returns: the location of the sub specifier
     * */
    pub fn getSubLoc (self)-> &Word {
        self._isSub
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Declaration)-> bool {
        match o {
            i : &ImportDecl => {
                i._module == self._module
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    
    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the declaration inside the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            stream:.write (Keys::MOD, ' 'c8, self._module.str (), Tokens::SEMI_COLON, '\n'c8);
        }

    }

}
