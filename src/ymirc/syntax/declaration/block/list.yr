/*
 * Declares the BlockDecl class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::declaration::block::list
 * @File: ymirc/syntax/declaration/block/list.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-12
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in list;

use ymirc::syntax::expression;

use ymirc::lexing::{keys, word, tokens};
use ymirc::utils::format;

use std::stream;


/**
 * A block of declaration.
 * 
 */
@final
pub class BlockDecl over Declaration {
    
    pub {
        // The list of declaration inside the block
        let decls: [&Declaration];

        // The protection of the declaration inside the block
        let protection: Protection;

        // True iif the protection does not apply, and parent protection is used
        let isSet: bool;
    }
    
    /**
     * @params: 
     *   - loc:        the location of the block
     *   - comm:       the comments above the decl block
     *   - decls:      the list of declaration inside the decl block
     *   - protection: the protection to apply on the declarations
     *   - isSet:      just a set of declaration, meaning protection block has no meaning here
     */
    pub self (loc : &Word,
              comm : [c8],
              decls : [&Declaration],
              protection : Protection,
              isSet : bool)
        
        with super (loc, comm)
        , decls = decls
        , protection = protection
        , isSet = isSet
        
    {}

    /**
     * Create an empty declaration block containing no declaration
     */
    pub self empty ()        
        with super (EOF_WORD, "")
        , decls = []
        , protection = Protection::PRIVATE
        , isSet = false
    {}

    
    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the decl block, in the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            match self.protection {
                Protection::PUBLIC =>
                    stream:.write (Keys::PUBLIC, " ");
                Protection::PROTECTED =>
                    stream:.write (Keys::PROTECTED, " ");
                _ =>
                    stream:.write (Keys::PRIVATE, " ");
            }            
            
            if (self.decls.len != 1us) {
                stream:.entabing ();
                stream:.write (Tokens::LACC, Tokens::RETURN);
            } 

            for i in self.decls {
                i.format (alias stream);
            }
            
            if (self.decls.len != 1us) {
                stream:.detabing ();
                stream:.write (Tokens::RACC, Tokens::RETURN);                
            }
        }
        
    }

}
