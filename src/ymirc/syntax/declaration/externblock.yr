in externblock;

use ymirc::syntax::expression;

use ymirc::lexing::keys;
use ymirc::lexing::{word, tokens};
use ymirc::utils::format;
use std::stream;

/**
 * An external block declaration
 * @verbatim : 
 * ==================
 * extern_block := Keys::EXTERN '(' Language ')' (( '{' declaration '}' )* | declaration)
 * ==================
 */
@final
pub class ExternBlockDecl over Declaration {

    // The language of the extern
    let _from : &Word;

    // The declaration block
    let _decls : [&Declaration];

    /**
     * @params: 
     *   - loc: the location of the extern
     *   - comms: the comments about the extern
     *   - space: the namespace of the external declaration
     *   - decl: the declaration 
     */
    pub self (loc : &Word, comms : [c8], from : &Word, decls : [&Declaration])
        with super (loc, comms)
        , _from = from
        , _decls = decls        
    {}

    /***
     * ==========================================================================
     * ==========================================================================
     * =========================          GETTERS        ========================
     * ==========================================================================
     * ==========================================================================
     */
    
    /**
     * @returns: the language of the external
     */
    pub fn getLanguage (self)-> &Word {
        self._from
    }

    /**
     * @returns: the declaration inside the external block
     */
    pub fn getDeclarations (self)-> [&Declaration] {
        self._decls
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Declaration)-> bool {
        match o {
            e : &ExternBlockDecl => {
                e._from == self._from && e._decls == self._decls
            }
            _ => { false }
        }
    }
    
    /***
     * ================================================================================
     * ================================================================================
     * =========================          MISCELLANEOUS        ========================
     * ================================================================================
     * ================================================================================
     */
    
    
    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the extern block inside the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            stream:.write (Keys::EXTERN)
                :.write ("(")
                :.write (self._from.str)            
                :.write (") {\n");
            
            stream:.entabing ();
            for d in self._decls {
                d.format (alias stream);                
            }            
            stream:.detabing ();
            
            stream:.write ("}\n");
        }        
    }
    
}
