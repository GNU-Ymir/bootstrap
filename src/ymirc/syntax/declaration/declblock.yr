mod ymirc::syntax::declaration::declblock;

import ymirc::syntax::declaration::_;
import ymirc::syntax::expression::_;

import ymirc::syntax::_;
import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;

/**
 * A protection block declaration 
 * @verbatim: 
 * ====================
 * decl_block := (Keys::PRIVATE | Keys::PUBLIC | Keys::PROTECTED) (('{' declaration* '}') | declaration)
 * ====================
 */
pub class @final DeclBlock over Declaration {
    
    /// The list of declaration inside the block
    let _content : [&Declaration];

    /// The protection of the declaration inside the block
    let _prot : Protection;

    /**
     * @params: 
     *   - loc: the location of the block (basically the keyword Keys::PUBLIC, Keys::PRIVATE or Keys::PROTECTED)
     *   - comm: the comments above the decl block
     *   - decls: the list of declaration inside the decl block
     *   - protection: the protection to apply on the declarations
     */
    pub self (loc : &Word, comm : [c8], decls : [&Declaration], protection : Protection = Protection::PRIVATE)
        with super (loc, comm), _content = decls, _prot = protection
    {}

    /**
     * Create an empty declaration block containing no declaration
     */
    pub self empty ()
        with super (EOF_WORD, ""s8), _content = [], _prot = Protection::PRIVATE
    {}

    
    /***
     * ============================================================================
     * ============================================================================
     * =========================          GETTERS        ==========================
     * ============================================================================
     * ============================================================================
     */
    
    /**
     * @returns: the list of declaration inside the block
     */
    pub def getDeclarations (self)-> [&Declaration] {
        self._content
    }

    /**
     * @returns: the protection of the decl block
     */
    pub def getProtection (self)-> Protection {
        self._prot
    }

    /***
     * ==================================================================================
     * ==================================================================================
     * =========================          MISCELLANEOUS        ==========================
     * ==================================================================================
     * ==================================================================================
     */

    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the decl block, in the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            match self._prot {
                Protection::PUBLIC => {
                    stream:.write (Keys::PUBLIC);
                }
                Protection::PROTECTED => {
                    stream:.write (Keys::PROTECTED);
                }
                _ => {
                    stream:.write (Keys::PRIVATE);
                }
            }
            
            if (self._content.len != 1us) {
                stream:.entabing ();
                stream:.write (" {\n"s8);
            } else stream:.write (' 'c8);

            for i in self._content {
                i.format (alias stream);
            }
            
            if (self._content.len != 1us) {
                stream:.detabing ();
                stream:.write ("}\n"s8);
            }
        }
        
    }
   
}
