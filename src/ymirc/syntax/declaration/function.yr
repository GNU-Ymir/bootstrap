mod ymirc::syntax::declaration::function;

import ymirc::syntax::declaration::base;
import ymirc::syntax::expression::base;

import ymirc::syntax::_;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;


import std::io;

/**
 * Syntax node encoding a function declaration
 * @verbatim: 
 * ================
 * function_decl := (Keys::FN | Keys::OVER) Identifier prototype (Keys::THROWS expression:(0) (',' expression:(0))*)? expression:(0)
 * ================
 */
pub class FunctionDecl over Declaration {

    /// The prototype of the function
    let _proto : &FunctionProtoDecl;

    /// The body of the function
    let _body : &Expression;

    /// The custom attributes of the function
    let _attributes : [AttributeWord];

    /// The throwers of the function
    let _throwers : [&Expression];

    /// The location of the throws word
    let _throwerLoc : &Word;

    /// True iif the function is marked as override
    let _isOver : bool;

    /**
     * @params: 
     *    - loc: the identifier of the function
     *    - proto: the prototype of the function
     *    - body: the body of the function
     *    - attrs: the attributes of the function (final, ...)
     *    - throwers: the list of throwers
     *    - isOver: the function is override declaration
     */
    pub self (loc : &Word, comm : [c8], proto : &FunctionProtoDecl, body : &Expression, attrs : [AttributeWord] = [], throwers : [&Expression] = [], throwerLoc : &Word = EOF_WORD, isOver : bool = false)
        with super (loc, comm), _proto = proto, _body = body, _attributes = attrs, _throwers = throwers, _isOver = isOver, _throwerLoc = throwerLoc
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the prototype of the function
     */
    pub fn getFunctionProto (self)-> &FunctionProtoDecl {
        self._proto
    }

    /**
     * @returns: the body of the function
     */
    pub fn getBody (self)-> &Expression {
        self._body
    }

    /**
     * @returns: the list of attributes 
     */
    pub fn getAttributes (self)-> [AttributeWord] {
        self._attributes
    }

    /**
     * @returns: the list of throwers of the function
     */
    pub fn getThrowers (self)-> [&Expression] {
        self._throwers
    }

    /**
     * @returns: the location of the throws keyword
     */
    pub fn getThrowerLoc (self)-> &Word {
        self._throwerLoc
    }
    
    /**
     * @returns: true, if the function is declared as override, false otherwise
     */
    pub fn isOver (self)-> bool {
        self._isOver
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Declaration)-> bool {
        match o {
            f : &FunctionDecl => {
                if (f._attributes.len != self._attributes.len) return false;
                for i in 0us .. f._attributes.len {
                    if (f._attributes [i].attr != self._attributes[i].attr) return false;
                }
                
                f._loc == self._loc && f._proto == self._proto && f._body == self._body && f._throwers == self._throwers && f._isOver == self._isOver
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    
    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the function inside the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            if (self._isOver) { stream:.write (Keys::OVER, " "); }
            else { stream:.write (Keys::FUNCTION, " "); }
            if (self._attributes.len != 0u64) {
                stream:.write (Tokens::AT, '{'c8);
                let mut j = 0u64;
                for i in self._attributes {
                    if (j != 0u64) stream:.write (','c8);
                    stream:.write (i);
                    j += 1u64;
                }
                stream:.write ("} "s8);
            }
            stream:.write (self._loc.str (), ' 'c8);
            stream:.entabing ();
            self._proto.format (alias stream);
            if (self._throwers.len != 0u64) {
                stream:.write ('\n'c8, Keys::THROWS, ' 'c8);
                let mut j = 0u64;
                for i in self._throwers {
                    if (j != 0u64) stream:.write (", "s8);
                    i.format (alias stream);
                    j += 1u64;
                }
                stream:.write ('\n'c8);
            }
            stream:.detabing ();
            self._body.format (alias stream);
            stream:.write ('\n'c8);
        }
        
    }
    
}
