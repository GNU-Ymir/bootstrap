mod ymirc::syntax::declaration::module;

import ymirc::syntax::declaration::_;
import ymirc::syntax::expression::_;

import ymirc::syntax::_;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;


import std::io;

/**
 * A module declaration 
 * @verbatim: 
 * ============
 * global_mod := 'mod' Path (';')? declaration*
 * local_mod := 'mod' Identifier '{' declaration* '}'
 * ============
 */
pub class @final ModuleDecl over Declaration {


    /// The path of the module
    let _path : &Word;

    /// The list of declaration inside the module
    let _decls : &BlockDecl;

    /// True iif the module is global
    let _isGlobal : bool;

    /**
     * @params: 
     *   - loc: the location of the module
     *   - comm: the comments about the module
     *   - decls: the declarations inside the module
     *   - isGlobal: true, if this module is global
     */
    pub self (loc : &Word, path : &Word, comm : [c8], decls : &BlockDecl, isGlobal : bool = true)
        with super (loc, comm), _path = path, _decls = decls, _isGlobal = isGlobal
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */
    
    /**
     * @returns: true, if the module is global
     */
    pub fn isGlobal (self)-> bool {
        self._isGlobal
    }

    /**
     * @returns: the block containing the declaration of the module
     */
    pub fn getBlockDecl (self)-> &BlockDecl {
        self._decls
    }
    
    /**
     * @returns: the list of declaration inside the module (<=> self.getBlockDecl ().getDeclarations ())
     */
    pub fn getDeclarations (self)-> [&Declaration] {
        self._decls.getDeclarations ()
    }

    /**
     * @returns: the path of the module
     */
    pub fn getPath (self)-> &Word {
        self._path
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Declaration)-> bool {
        match o {
            m : &ModuleDecl => {
                m._isGlobal == self._isGlobal && m._path == self._path && m._decls == self._decls
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */    

    
    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the module inside the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            if (self._isGlobal) {
                if (!self._path.isEof ()) {
                    stream:.write (Keys::IN, ' 'c8, self._path.str ());
                }

                stream:.write (Tokens::SEMI_COLON, '\n'c8);
                for i in self._decls.getDeclarations () {
                    i.format (alias stream);
                    stream:.write ('\n'c8);
                }
            } else {
                if (!self._path.isEof ()) {
                    stream:.write (Keys::MOD, ' 'c8, self._path.str ());
                }

                stream:.write ("{\n"s8);
                stream:.entabing ();
                for i in self._decls.getDeclarations () {
                    i.format (alias stream);
                    stream:.write ('\n'c8);
                }
                stream:.detabing ();
            }
        }
        
    }
    
}
