mod ymirc::syntax::declaration::module;

import ymirc::syntax::declaration::_;
import ymirc::syntax::expression::_;

import ymirc::syntax::_;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;

import std::io;

/**
 * A module declaration 
 * @verbatim: 
 * ============
 * global_mod := 'mod' Path (';')? declaration*
 * local_mod := 'mod' Identifier '{' declaration* '}'
 * ============
 */
pub class @final Module over Declaration {

    /// The path of the module
    let _path : &Expression;

    /// The list of declaration inside the module
    let _decls : &DeclBlock;

    /// True iif the module is global
    let _isGlobal : bool;

    /**
     * @params: 
     *   - loc: the location of the module
     *   - comm: the comments about the module
     *   - decls: the declarations inside the module
     *   - isGlobal: true, if this module is global
     */
    pub self (loc : &Word, path : &Expression, comm : [c8], decls : &DeclBlock, isGlobal : bool = true)
        with super (loc, comm), _path = path, _decls = decls, _isGlobal = isGlobal
    {}

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */
    
    /**
     * @returns: true, if the module is global
     */
    pub def isGlobal (self)-> bool {
        self._isGlobal
    }

    /**
     * @returns: the list of declaration inside the module
     */
    pub def getDeclarations (self)-> [&Declaration] {
        self._decls.getDeclarations ()
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */    

    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the module inside the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            match self._path {
                Unit () => {}
                _ => {
                    stream:.write (Keys::MOD, ' 'c8);
                    stream:.entabing ();
                    self._path.format (alias stream);
                    stream:.write (' 'c8);
                    stream:.detabing ();
                }
            }
            
            if (self._isGlobal) {
                match self._path {
                    Unit () => {}
                    _ => {
                        stream:.write (Tokens::SEMI_COLON, '\n'c8);
                    }
                }
                
                for i in self._decls.getDeclarations () {
                    i.format (alias stream);
                    stream:.write ('\n'c8);
                }
            } else {
                stream:.write ("{\n"s8);
                stream:.entabing ();
                for i in self._decls.getDeclarations () {
                    i.format (alias stream);
                    stream:.write ('\n'c8);
                }
                stream:.detabing ();
            }
        }
        
    }
    
}
