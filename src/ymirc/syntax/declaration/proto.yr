mod ymirc::syntax::declaration::proto;

import ymirc::syntax::declaration::_;
import ymirc::syntax::expression::_;

import ymirc::syntax::_;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;

import std::io;

/**
 * A prototype is a list of parameters, and return type
 * They are used by functions, constructors, lambdas...
 * @verbatim: 
 * ===============
 * prototype :=   '(' (var_decl (',' var_decl)*)? ')' ('->' expression:(0))?
                | '|' (var_decl (',' var_decl)*)? '|' ('->' expression:(0))?
 * ===============
 */
pub class @final FunctionProtoDecl over Declaration {

    /// The list of parameters of the prototype
    let _parameters : [&Expression];

    /// The return type of the prototype
    let _retType : &Expression;

    /// Is this a variadic prototype (C-style)
    let _isVariadic : bool;

    /// True iif, this is a lambda prototype
    let _forLambda : bool;

    /**
     * @params: 
     *    - loc: the location of the prototype
     *    - comm: the comments about the prototype (generally empty)
     *    - params: the list of parameters of the prototype
     *    - retType: the return type of the prototype
     *    - isVariadic: for extern (C) functions, when they are variadic (such as printf)
     */
    pub self (loc : &Word, comm : [c8] = ""s8, params : [&Expression], retType : &Expression, isVariadic : bool = false, forLambda : bool = false)
        with super (loc, comm), _parameters = params, _retType = retType, _isVariadic = isVariadic, _forLambda = forLambda
    {}

    
    /***
     * ==========================================================================
     * ==========================================================================
     * =========================          GETTERS        ========================
     * ==========================================================================
     * ==========================================================================
     */


    /**
     * @returns: the list of parameters
     */
    pub def getParameters (self)-> [&Expression] {
        self._parameters
    }

    /**
     * @returns: the return type
     */
    pub def getRetType (self)-> &Expression {
        self._retType
    }

    /**
     * @returns: true, iif the prototype is an extern (C) variadic prototype (printf prototype for example)
     */
    pub def isVariadic (self)-> bool {
        self._isVariadic
    }

    /**
     * @returns: true, iif the prototype is a lambda prototype
     */
    pub def isForLambda (self)-> bool {
        self._forLambda
    }

    
    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the prototype inside the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            if (!self._forLambda) stream:.write (Tokens::LPAR);
            else stream:.write (Tokens::PIPE);
            
            stream:.entabing ();
            let mut j = 0u64;
            for i in self._parameters {
                if (j != 0u64) stream:.write (", "s8);
                i.format (alias stream);
                j += 1u64;
            }
            
            if (self._isVariadic) {
                if (j != 0u64) stream:.write (", "s8);
                stream:.write (Tokens::TDOT);
            }

            if (!self._forLambda) stream:.write (Tokens::RPAR);
            else stream:.write (Tokens::PIPE);

            match self._retType {
                EmptyExpression () => {}
                _ => {
                    stream:.write (Tokens::ARROW, " ");
                    self._retType.format (alias stream);
                }
            }
            stream:.write (" ");
            stream:.detabing ();
        }
        
    }
    
}
