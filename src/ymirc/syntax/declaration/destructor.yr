in destructor;

use ymirc::syntax::declaration::{function, proto};
use ymirc::syntax::{expression, expression::{vardecl, empty}};

use ymirc::syntax::keys;
use ymirc::lexing::{word, tokens};
use ymirc::utils::format;
use std::stream;

/**
 * Syntaxic node representation of a destructor
 * @verbatim: 
 * ==================
 * destructor := Keys::DTOR Tokens::LPAR Keys::MUT Keys::SELF Tokens::RPAR expression:(0)
 * ==================
 */
@final
pub class DestructorDecl over FunctionDecl {
    
    pub self (loc : &Word, comm : [c8], param : &VarDeclExpr, body : &Expression)
        with super (loc, comm, copy FunctionProtoDecl (loc, comm-> comm, [param], retType-> EMPTY_EXPR), body)
    {}

    pub self (loc : &Word, comm : [c8], proto : &FunctionProtoDecl, body : &Expression)
        with super (loc, comm, proto, body)
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Declaration)-> bool {
        match o {
            DestructorDecl () => {
                self.super.opEquals (o)
            }
            _ => { false }
        }
    }
    
    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    
    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (Keys::DTOR);
            self._proto.format (alias stream);
            stream:.write ("\n");
            self._body.format (alias stream);
            stream:.write ("\n");            
        }        
    }
    

}
