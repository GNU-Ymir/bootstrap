mod ymirc::syntax::declaration::use_;

import ymirc::syntax::declaration::_;
import ymirc::syntax::expression::_;

import ymirc::syntax::_;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;

import ymirc::utils::serialize;
import std::io;


/**
 * Syntaxic representation of an importation
 * @verbatim:
 * ===============
 * import_decl := Keys::IMPORT Path
 * ===============
 */
pub class @final UseDecl over Declaration {

    /// The name of the module that is imported
    let _module : &Expression;

    /**
     * @params:
     *    - loc: the location of the importation
     *    - com: the comments about the importation
     *    - module: the path of the module to import
     */
    pub self (loc : &Word, com : [c8], module : &Expression)
        with super (loc, com), _module = module
    {}

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */


    /**
     * @returns: the path of the module to import
     */
    pub fn getModulePath (self)-> &Expression {
        self._module
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Declaration)-> bool {
        match o {
            i : &UseDecl => {
                i._module == self._module
            }
            _ => { false }
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Serializable;
    impl Streamable;
    impl Formattable {

        /**
         * Pretty formatting of the declaration inside the stream
         */
        pub over format (self, dmut stream : &Formatter) {
            stream:.write (Keys::USE, ' 'c8);
            self._module.format (alias stream);
            stream:.write (Tokens::SEMI_COLON);
        }

    }

}
