/*
 * Declares the EnumDecl class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::declaration::custom::enum_
 * @File: ymirc/syntax/declaration/custom/enum_.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2026-02-12
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */


in enum_;

use ymirc::syntax::expression;
use ymirc::syntax::expression::{instruction, empty};

use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;
use std::stream;

/**
 * Syntaxic node representation of an enum declaration
 *
 */
@final
pub class  EnumDecl over Declaration {

    pub {
        // The name of the enum to declare
        let name: &Word;
        
        // The list of values defined in the enumeration
        let fields: [&VarDeclExpr];

        // The type of the enumeration
        let type: &Expression;

        // The list of comments of the values
        let fieldComms: [[c8]];
    }
    
    /**
     * @params: 
     *    - loc:        the location of the enumeration
     *    - comm:       the comments about the enum
     *    - name:       the name of the enum to declare
     *    - type:       the type of the enumeration
     *    - fields:     the list of fields contained in the enum
     *    - fieldComms: the comments about the field of the enum
     */
    pub self (loc:        &Word,
              comm:       [c8],
              name:       &Word,
              type:       &Expression,
              fields:     [&VarDeclExpr],
              fieldComms: [[c8]])
        
        with super (loc, comm)
        , name = name
        , type = type
        , fields = fields
        , fieldComms = fieldComms
    {}

    
    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream: &Formatter) {
            stream:.write (Keys::ENUM);
            if self.type !of EmptyExpr {        
                stream:.write (Tokens::COLON, " ");
                self.type.format (alias stream);                
            }
            
            stream:.write (Tokens::RETURN);
            
            for i in self.fields {
                stream:.write (Tokens::PIPE, ' ');
                stream:.entabing ();
                i.format (alias stream);
                stream:.detabing ();
                stream:.write (Tokens::RETURN);
            }
            
            stream:.write (' ', Tokens::ARROW, ' ', self.name.str, Tokens::SEMI_COLON, Tokens::RETURN);
        }

        
    }
    
}

