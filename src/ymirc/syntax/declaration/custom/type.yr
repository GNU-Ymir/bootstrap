/*
 * Declares the TypeDecl class.
 *
 * <hr>
 * 
 * @Package: ymirc        
 * @Module: ymirc::syntax::declaration::type_
 * @File: ymirc/syntax/declaration/type_.yr
 * @Author(s):
 *   - Emile Cadorel <ecadorel@gmail.com>
 *
 * @Created (YYYY-MM-DD): 2021-11-20
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 * @License
 *   ```text
 *   This file is part of the Ymir language project.
 *
 *   Ymir is free software: you can redistribute it and/or modify it
 *   under the terms of the GNU General Public License as published by
 *   the Free Software Foundation, either version 3 of the License, or
 *   (at your option) any later version.
 *
 *   Ymir is distributed in the hope that it will be useful,
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *   GNU General Public License for more details.
 *
 *   You should have received a copy of the GNU General Public License
 *   along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 *   ```
 */

in type;

use ymirc::syntax::{expression, expression::empty};
use ymirc::syntax::declaration::attributes;

use ymirc::lexing::{word, tokens, keys};
use ymirc::utils::format;
use std::stream;

/**
 * A syntax type declaration, type decl is the same for union, entity, record
 * and type. A field `.kind` defines the kind of Type that is actually declared
 *
 */
@final
pub class TypeDecl over Declaration {

    pub {
        // The name of the type
        let name: &Word;
        
        // The ancestor of the type (might be empty)
        let ancestor: &Expression;

        // The list of declaration inside the type
        let decls: [&Declaration];

        // The list of attributes
        let attrs: [AttributeWord];

        // The kind of type declared
        let kind: TypeDeclKind;
    }

    /**
     * @params: 
     *    - loc:      the identifier of the type 
     *    - comm:     the comments about the type
     *    - inner:    the inner declarations about the type
     *    - attrs:    the attributes of the type (final, abstract...)
     *    - ancestor: the expression that declare the ancestor of the type
     *    - kind:     the kind of type that is actually declared
     */
    pub self (loc: &Word,
              comm: [c8],
              name: &Word,
              decls: [&Declaration],
              attrs: [AttributeWord],
              ancestor: &Expression,
              kind: TypeDeclKind)
        
        with super (loc, comm)
        , name = name
        , decls = decls
        , attrs = attrs
        , ancestor = ancestor
        , kind = kind
        
    {}

    /**
     * @returns: true if the type is actually an entity
     * */
    @field
    pub fn isEntity (self)-> bool {
        self.kind == TypeDeclKind::ENTITY
    }

    /**
     * @returns: true if the type is a structure without ownership
     * */
    @field
    pub fn isRecord (self)-> bool {
        self.kind == TypeDeclKind::RECORD
    }

    /**
     * @returns: true iif the type is an union
     * */
    @field
    pub fn isUnion (self)-> bool {
        self.kind == TypeDeclKind::UNION
    }

    /**
     * @returns: true iif the type is a real type
     * */
    @field
    pub fn isClass (self)-> bool {
        self.kind == TypeDeclKind::CLASS
    }
    
    
    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream: &Formatter) {
            if (self.attrs.len != 0u64) {
                stream:.write (Tokens::AT, Tokens::LACC);                
                for i, e in self.attrs {
                    if (i != 0u64) stream:.write (Tokens::COMA, " ");
                    stream:.write (e.attr);                    
                }
                stream:.write (Tokens::RACC, Tokens::RETURN);
            }
            
            stream:.write (self.kind, ' ', self.name.str);
            
            if self.ancestor !of EmptyExpr {        
                stream:.entabing ();
                stream:.write (' ', Keys::OVER, ' ');
                self.ancestor.format (alias stream);
                stream:.detabing ();                
            }

            stream:.write (" ", Tokens::LACC, Tokens::RETURN);
            stream:.entabing ();
            for i in self.decls {
                i.format (alias stream);
            }
            stream:.detabing ();
            stream:.write (Tokens::RACC, Tokens::RETURN);
        }
        
    }

}
