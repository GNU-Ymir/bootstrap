mod ymirc::syntax::declaration::impl_;

import ymirc::syntax::declaration::_;
import ymirc::syntax::expression::_;

import ymirc::syntax::_;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;

import std::io;


/**
 * Syntaxic representation of an impl declaration
 * @verbatim:
 * ==============
 * impl_decl := Keys::IMPL expression:(0) ('{' declaration* '}')? (Tokens::SEMI_COLON)?
 * ==============
 */
pub class @final Impl over Declaration {

    /// The trait that is implemented
    let _trait : &Expression;

    /// The content of the implementation
    let _content : [&Declaration];

    /**
     * @params: 
     *   - loc: the location of the impl
     *   - comm: the comments about the impl
     *   - trait_: the trait that will be implemented
     *   - content: the list of declaration inside the impl
     */
    pub self (loc : &Word, comm : [c8], trait_ : &Expression, content : [&Declaration] = [])
        with super (loc, comm), _trait = trait_, _content = content
    {}

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */
    
    /**
     * @returns: the trait that is implemented
     */
    pub def getTrait (self)-> &Expression {
        self._trait
    }

    /**
     * @returns: the list of overriden declarations
     */
    pub def getContent (self)-> [&Declaration] {
        self._content
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (Keys::IMPL, ' 'c8);
            self._trait.format (alias stream);
            if (self._content.len != 0u64) {
                stream:.write (" {\n"s8);
                stream:.entabing ();
                for i in self._content {
                    i.format (alias stream);
                }
                stream:.detabing ();
                stream:.write ("}\n"s8);
            } else stream:.write (";\n"s8);
        }
    }
    
}
