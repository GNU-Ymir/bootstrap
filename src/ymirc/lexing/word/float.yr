/*
 * @Package: ymirc        
 * @Module: ymirc::lexing::word::float
 * @File: ymirc/lexing/word/float.yr
 *
 * @Purpose:
 *   Defines the FloatWord class, representing a word containing a float value.
 *   
 *
 * @Author(s):
 *   - Emile Cadorel ecadorel@gmail.com
 *
 * @Created (YYYY-MM-DD): 2026-02-08
 *
 * @Copyright (C) 2021â€“2026 GNU-Ymir
 *
 * ```text
 * This file is part of the Ymir language project.
 *
 * Ymir is free software: you can redistribute it and/or modify it
 * under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Ymir is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Ymir.  If not, see <https://www.gnu.org/licenses/>.
 * ```
 */

in float;

use std::{stream, io};
use ymirc::utils::colors;

pub enum
| DEC = 1
| SCI = 2
| HEX = 3
 -> FloatNotation; 


/**
 * A FloatWord models a lexical float value extracted from a file.
 *
 * It contains information about the used notation, and suffix at the end of the
 * token, and exponent part for sci and hex notations.
 * 
 * */
@final
pub class FloatWord over Word {

    /*! Public fields */
    pub {
        // The encoding used
        let notation : FloatNotation;

        // The location of the dot 
        let dot : &Word;
        
        // The decimal part
        let dec : &Word;
        
        // The suffix at the end of the float word
        let suffix : &Word;        
        
        // The exponent part (for sci and hex)
        let exp : &Word;

        // The sign (for sci and hex)
        let sign : &Word;
    }
    
    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =====================================          CTORS          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create a word from a file containing a token
     * 
     * @params: 
     *   - str:      the content of the word
     *   - filename: the name of the file that contains the word
     *   - notation: the kind of notation used to encode the float constant
     *   - suffix:   the suffix placed at the end of the constant
     *   - exp:      the exponent part (for sci or hex notation)
     *   - line:     the location line of the word
     *   - col:      the location column of the word
     *   - seek:     the location cursor of the word (number of c8 preceding it)
     *   - lineSeek: the location cursor of the beginning of the line containing the word     
     * */
    pub self (str:         [c8],
              dot:         &Word,
              dec:         &Word,
              suffix:      &Word,
              exp:         &Word,
              sign:        &Word,              
              notation:    FloatNotation,
              
              filename:    [c8],
              fileContent: [c8],
              line:        u64,
              col:         u64,
              seek:        usize,
              lineSeek:    usize)
        with super (str, filename, fileContent, line, col, seek, lineSeek)
        , notation = notation
        , dot = dot
        , dec = dec
        , suffix = suffix
        , exp = exp
        , sign = sign
    {}
        
    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          COMPARISON          ===================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * @returns: false
     *
     * @info
     *    Float token are not comparable to string values, since they are composed of multiple words
     * */
    pub over opEquals (self, _ : [c8])-> bool {
        false
    }
    
    /**         
     * @returns: true iif `o` is a float word and contain the same content,
     *           notation and suffix
     * */
    pub over opEquals (self, o : &Word)-> bool {        
        if let n: &FloatWord = o {    
            if self.str != n.str return false;
            if self.notation != n.notation return false;
            if self.dot != n.dot return false;
            if self.dec != n.dec return false;
            if self.suffix != n.suffix return false;
            if self.exp != n.exp return false;
            if self.sign != n.sign return false;                   
        }

        return false;
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =================================          MISCELLANEOUS          ==================================
     * ====================================================================================================
     * ====================================================================================================
     */
    
    impl Hashable;        
    impl Streamable {
        pub over toStream (self, dmut stream: &StringStream)-> void {
            stream:.write (Colors::PURPLE)                
                :.write (self.str)
                :.write (Colors::RESET, Colors::BOLD)
                :.write (" --> ")
                :.write (self.filename)
                :.write (":(")
                :.write (self.line)
                :.write (",")
                :.write (self.col)
                :.write (",")
                :.write (self.notation)
                :.write (",")
                :.write (self.exp)
                :.write (",")
                :.write (self.suffix)
                :.write (")")
                :.write (Colors::RESET);                                            
        }
    }
    
}
