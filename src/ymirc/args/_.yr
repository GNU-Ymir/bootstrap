mod ymirc::args::_;
import std::args, std::env;
import std::io;

pub struct
| includes : [[c32]] = []
| libs : [[c32]] = []
| files : [[c32]]
| help : YesNo = YesNo::NO
| fversion : [[c32]] = []
| debug : YesNo = YesNo::NO
 -> YmirArguments;

/**
 * Parse the command line options, to create the YmirArgument struct
 * @returns: the parsed arguments
 */
pub def parseArguments () -> YmirArguments? {
    let x = ArgumentParser!{YmirArguments}::new ()
        :.positional!"files" (comments-> "list of files")
        :.info!"help" (short-> "h", comments-> "print this help and exit", isHelp-> true)
        :.info!"includes" (short-> "I", comments-> "add include path")
        :.info!"fversion" (comments-> "add a version")
        :.info!"debug" (short-> "g", comments-> "produce debugging information")
        :.info!"libs" (short-> "l", comments-> "add library link")
        ;
    {        
        let args = x.parse (env::args ());
        return args?;
    } catch {
        err : &ArgumentError => {
            println (x.simpleUsage (cmd-> "ymirc"));
            println (x.diagnosis (err));
            return (YmirArguments?)::__err__;            
        }
        _ : &HelpRequired => {
            println (x.usage (cmd-> "ymirc"));
            return (YmirArguments?)::__err__;
        }   
    }
}
