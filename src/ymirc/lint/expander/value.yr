mod ymirc::lint::expander::value;

import ymirc::lint::expander::visitor;
import ymirc::lint::expander::type;
import ymirc::lint::expander::stmtlist;

import ymirc::lint::node::_;

import std::collection::map;
import std::collection::vec;
import std::io;

import ymirc::utils::format;
import ymirc::utils::bigint;
import ymirc::utils::bigfloat;

import ymirc::semantic::generator::_;
import ymirc::global::core_;
import ymirc::global::state;
import ymirc::syntax::keys;

import ymirc::lexing::word;
import std::fs::path;

/**
 * The value expander is used to expand values into a list of statements
 */
pub class ValueExpander {

    /**
     * Create an empty value expander
     * */
    pub self () {}

    /**
     * Finalize a value
     * @params:
     *    - context: the context of the expansion
     *    - val: the value to expand
     *    - list: the list of statement to fill
     * @returns: the final value
     * */
    pub fn finalize (self, dmut context : &Expander, val : &Value, dmut list : &StmtList, asLeft : bool = false)-> &YILValue {
        match val {
            b : &BreakValue        => { self.finalizeBreak (alias context, b, alias list) }
            n : &NamedValue        => { self.finalize (alias context, n.getValue (), alias list, asLeft-> asLeft) }
            r : &ReturnValue       => { self.finalizeReturn (alias context, r, alias list) }
            t : &ThrowValue        => { self.finalizeThrow (alias context, t, alias list) }
            p : &PanicValue        => { self.finalizePanic (alias context, p, alias list) }
            v : &VarDeclValue      => { self.finalizeVarDecl (alias context, v, alias list) }
            v : &VarRefValue       => { self.finalizeVarRef (alias context, v, alias list) }
            g : &GlobalVarRefValue => { self.finalizeGlobalVarRef (alias context, g, alias list) }
            s : &ScopeValue        => { self.finalizeScope (alias context, s, alias list, asLeft-> asLeft) }
            l : &LiteralValue      => { self.finalizeLiteral (alias context, l, alias list, asLeft-> asLeft) }
            o : &OperatorValue     => { self.finalizeOperator (alias context, o, alias list, asLeft-> asLeft) }
            m : &MemoryValue       => { self.finalizeMemory (alias context, m, alias list) }
            c : &ConstructValue    => { self.finalizeConstruct (alias context, c, alias list, asLeft-> asLeft) }
            c : _ => {
                eprintln (c::typeinfo.name);
                __pragma!panic ();
            }
        }
    }

    /**
     * Finalize a break statement
     * @params:
     *    - context: the context of the finalization
     *    -  b: the break statement to finalize
     * @returns: UNIT_VALUE, break values are never right operands
     * */
    fn finalizeBreak (self, dmut context : &Expander, b : &BreakValue, dmut list : &StmtList)-> &YILValue {
        let endLabel = context.getLoopExitLabel ();
        let decl = context.getLoopVarRef ();
        if (!decl.isOf!{&YILUnit} ()) {
            let inner = self.finalize (alias context, b.getValue (), alias list);
            self.finalizeAffect (alias context, b.getLoc (), decl, inner, alias list);
        }

        list:.append (YILGoto::new (b.getLoc (), endLabel));
        YIL_UNIT_VALUE
    }

    /**
     * Finalize a return statement
     * @params:
     *    - context: the context of the finalization
     *    - t: the return stmt to finalize
     * @returns: UNIT_VALUE, return values are never right operands
     * */
    fn finalizeReturn (self, dmut context : &Expander, r : &ReturnValue, dmut list : &StmtList)-> &YILValue {
        let inner = self.finalize (alias context, r.getValue (), alias list);
        self.removeDestroy (inner, alias list);
        list:.append (YILReturn::new (r.getLoc (), inner));

        YIL_UNIT_VALUE
    }

    /**
     * Finalize a throw statement
     * @params:
     *    - t: the throw stmt to finalize
     * @returns: UNIT_VALUE, throw values are never right operands
     * */
    fn finalizeThrow (self, dmut context : &Expander, t : &ThrowValue, dmut list : &StmtList)-> &YILValue {
        let inner = self.finalize (alias context, t.getValue (), alias list);
        let line = YILIntValue::new (t.getLoc (), YIL_UINT_32, cast!usize (t.getLoc ().line ()));
        let filename = state::relativePath (t.getLoc ().filename ());

        let fileStr = self.finalize (alias context, makeStringSliceValue (t.getLoc (), filename), alias list);
        let funcStr = self.finalize (alias context, makeStringSliceValue (t.getLoc (), context.getCurrentFrameName ()), alias list);

        let fileStrType = fileStr.getType ().asOf!{&YILTuple} ().getInners ()[1];
        let fileStrPtr = fileStr.asOf!{&YILTupleValue} ().getValue ()[1];

        let funcStrType = funcStr.getType ().asOf!{&YILTuple} ().getInners ()[1];
        let funcStrPtr = funcStr.asOf!{&YILTupleValue} ().getValue ()[1];

        let (fileStrPtrU, _) = self.createVarForBlock (alias context, t.getLoc (), NONE_TYPE, fileStrType, alias list, withValue-> fileStrPtr);
        let (funcStrPtrU, _) = self.createVarForBlock (alias context, t.getLoc (), NONE_TYPE, funcStrType, alias list, withValue-> funcStrPtr);

        let call = YILNameCallValue::new (t.getLoc (), YIL_VOID, YRuntimeFuncs::THROW_EXCEPT, [fileStrPtrU, funcStrPtrU, line, inner]);

        list:.append (YILCall::new (t.getLoc (), call));

        YIL_UNIT_VALUE
    }

    /**
     * Finalize a panice statement
     * @params :
     *    - p: the panic stmt to finalize
     * @returns: UNIT_VALUE
     * */
    fn finalizePanic (self, dmut context : &Expander, p : &PanicValue, dmut list : &StmtList)-> &YILValue {
        let line = YILIntValue::new (p.getLoc (), YIL_UINT_32, cast!usize (p.getLoc ().line ()));
        let filename = state::relativePath (p.getLoc ().filename ());

        let fileStr = self.finalize (alias context, makeStringSliceValue (p.getLoc (), filename), alias list);
        let funcStr = self.finalize (alias context, makeStringSliceValue (p.getLoc (), context.getCurrentFrameName ()), alias list);

        let fileStrType = fileStr.getType ().asOf!{&YILTuple} ().getInners ()[1];
        let fileStrPtr = fileStr.asOf!{&YILTupleValue} ().getValue ()[1];

        let funcStrType = funcStr.getType ().asOf!{&YILTuple} ().getInners ()[1];
        let funcStrPtr = funcStr.asOf!{&YILTupleValue} ().getValue ()[1];

        let (fileStrPtrU, _) = self.createVarForBlock (alias context, p.getLoc (), NONE_TYPE, fileStrType, alias list, withValue-> fileStrPtr);
        let (funcStrPtrU, _) = self.createVarForBlock (alias context, p.getLoc (), NONE_TYPE, funcStrType, alias list, withValue-> funcStrPtr);

        let call = YILNameCallValue::new (p.getLoc (), YIL_VOID, YRuntimeFuncs::PANIC, [fileStrPtrU, funcStrPtrU, line]);
        list:.append (YILCall::new (p.getLoc (), call));

        YIL_UNIT_VALUE
    }

    /**
     * Finalize the registration of a unit test
     * @params:
     *    - context: the context of the expansion
     *    - name: the name of the unit test function to register
     *    - loc: the location of the unittest
     *    - list: the list to fill
     * */
    pub fn finalizeRegisterUnitTest (self, dmut context : &Expander, name : [c8], mangledName : [c8], loc : &Word, dmut list : &StmtList) {
        let funcAddr = YILAddrValue::new (EOF_WORD, YIL_PTR_VOID, YILVar::new (EOF_WORD, mangledName, YIL_VOID, 0us));

        let funcStr = self.finalize (alias context, makeStringSliceValue (loc, name), alias list);
        let funcStrPtr = funcStr.asOf!{&YILTupleValue} ().getValue ()[1];
        let funcStrType = funcStr.getType ().asOf!{&YILTuple} ().getInners ()[1];
        let (funcStrPtrU, _) = self.createVarForBlock (alias context, loc, NONE_TYPE, funcStrType, alias list, withValue-> funcStrPtr);

        let call = YILNameCallValue::new (loc, YIL_VOID, YRuntimeFuncs::REGISTER_UNIT_TEST, [funcStrPtrU, funcAddr]);
        list:.append (YILCall::new (EOF_WORD, call));
    }
    
    /**
     * Finalize a variable declaration statement
     * @params:
     *    - v: the variable declaration to finalize
     * @returns: UNIT_VALUE, variable decl are never right operands
     * */
    fn finalizeVarDecl (self, dmut context : &Expander, v : &VarDeclValue, dmut list : &StmtList)-> &YILValue {
        // Lambda definition, is not generated
        if (v.getVarType ().isOf!{&LambdaType} ()) return YIL_UNIT_VALUE;

        let inner = self.finalize (alias context, v.getValue (), alias list);
        let mut type = context:.finalizeType (v.getVarType ());

        if (v.isLazy ()) {
            let lazyType = context.getTypeExpander ().finalizeLazy ();
            let varId = context:.generateVarId ();
            let decl = YILVarDecl::new (v.getLoc (), v.getLoc ().str (), lazyType, varId);
            let varref = YILVar::new (v.getLoc (), v.getLoc ().str (), lazyType, varId);

            list:.append (decl);
            if (!inner.isOf!{&YILUnit} ()) {
                self.finalizeAffect (alias context, v.getLoc (), varref, inner, alias list);
            }

            let addr = YILAddrValue::new (v.getLoc (), YILPointer::new (lazyType), varref);
            context:.registerLazyRef (v.getUniqId (), varref);
            context:.registerVariable (v.getUniqId (), YILNameCallValue::new (v.getLoc (), YILPointer::new (type), YRuntimeFuncs::CALL_LAZY, [addr]));
        } else {
            if (v.isReference ()) {
                type = YILPointer::new (type);
            }

            let varId = context:.generateVarId ();
            let decl = YILVarDecl::new (v.getLoc (), v.getLoc ().str (), type, varId);
            let mut varref = YILVar::new (v.getLoc (), v.getLoc ().str (), type, varId);

            list:.append (decl);
            if (!inner.isOf!{&YILUnit} ()) {
                self.finalizeAffect (alias context, v.getLoc (), varref, inner, alias list);
            }

            if (v.getVarType ().isMovable () && !v.isReference ()) {
                self.registerDestroy (alias context, varref, v.getVarType (), alias list, implicit-> false);
            }

            if (v.isReference ()) {
                context:.registerVariable (v.getUniqId (),
                                           YILUnrefValue::new (v.getLoc (), type.asOf!{&YILPointer} ().getInner (), varref));
            } else {
                context:.registerVariable (v.getUniqId (), varref);
            }
        }

        YIL_UNIT_VALUE
    }

    /**
     * Finalize a variable reference value
     * @params:
     *   - v: the variable to finalize
     * @returns: a variable reference
     * */
    fn finalizeVarRef (self, dmut context : &Expander, v : &VarRefValue, dmut list : &StmtList)-> &YILValue {
        if (v.isLazy ()) {
            let type = context:.finalizeType (v.getType ());
            match context.getVariable (v.getUniqId ()) {
                Ok (u : &YILValue) => { return YILUnrefValue::new (v.getLoc (), type, u); }
            }

            match context.getVariable (v.getRefId ()) {
                Ok (u : &YILValue) => {
                    return YILUnrefValue::new (v.getLoc (), type,
                                               self.registerUniqValue (alias context, v, type-> u.getType (), u, alias list));
                }
            }
        } else {
            match context.getVariable (v.getRefId ()) {
                Ok (vref : &YILValue) => { return vref; }
            };
        }

        let type = context:.finalizeType (v.getType ());
        YILVar::new (v.getLoc (), v.getDeclLoc ().str (), type, 0us)
    }

    /**
     * Finalize a global variable reference value
     * @params:
     *   - v: the value to finalize
     * @returns: a variable reference
     * */
    fn finalizeGlobalVarRef (self, dmut context : &Expander, v : &GlobalVarRefValue, dmut list : &StmtList)-> &YILValue {
        let type = context:.finalizeType (v.getType ());
        if (v.isLazy ()) {
            match context.getVariable (v.getUniqId ()) {
                Ok (u : &YILValue) => return YILUnrefValue::new (v.getLoc (), type, u);
            }
        }

        let vname = context:.getMangler ().mangleGlobalVar (v.getRefGenerator ());
        context:.registerTouchSym (vname);

        if (v.isLazy ()) {
            let lazyType = context.getTypeExpander ().finalizeLazy ();
            let mut vref = YILVar::new (v.getLoc (), vname, lazyType, 0us, isThreadLocal-> v.getRefSymbol ().isThreadLocal ());

            let addr = YILAddrValue::new (v.getLoc (), YILPointer::new (lazyType), vref);
            let ret = YILNameCallValue::new (v.getLoc (), YILPointer::new (type), YRuntimeFuncs::CALL_LAZY, [addr]);
            return YILUnrefValue::new (v.getLoc (), type, self.registerUniqValue (alias context, v, type-> ret.getType (), ret, alias list));
        } else {
            return YILVar::new (v.getLoc (), vname, type, 0us);
        }
    }

    /*!
     * =====================================================================================
     * =====================================================================================
     * =========================          SCOPE EXPANSION          =========================
     * =====================================================================================
     * =====================================================================================
     */

    /**
     * Finalize a scope value
     * @params:
     *    - sc: the scope value to finalize
     *    - list: the statement list to fill
     * @returns: the final value of the scope
     * */
    fn finalizeScope (self, dmut context : &Expander, sc : &ScopeValue, dmut list : &StmtList, asLeft : bool)-> &YILValue {
        match context.getVariable (sc.getUniqId ()) {
            Ok (u : &YILValue) => { // The call was already seen and validated
                return u;
            }
        };

        let retVal = match sc {
            b : &BlockValue => {
                if (b.isSet ()) {
                    return self.finalizeBlockSet (alias context, b, alias list, asLeft);
                }

                self.finalizeBlock (alias context, b, alias list)
            }
            c : &ConditionalValue => { self.finalizeCond (alias context, c, alias list) }
            l : &LoopValue => { self.finalizeLoop (alias context, l, alias list) }
            t : &TryFinallyValue => { self.finalizeTryFinally (alias context, t, alias list) }
            t : &TryCatchValue => { self.finalizeTryCatch (alias context, t, alias list) }
            l : &ListComprValue => { self.finalizeBlock (alias context, l.getContent (), alias list) }
            _ => {
                eprintln (sc::typeinfo.name);
                __pragma!panic ();
            }
        }

        retVal
    }

    /**
     * Finalize a set
     * @params:
     *     - context: the context of the expansion
     *     - b: the set to finalize
     *     - list: the list of values to fill
     *     - asLeft: true if used as lvalue
     * */
    fn finalizeBlockSet (self, dmut context : &Expander, b : &BlockValue, dmut list : &StmtList, asLeft : bool)-> &YILValue {
        let retVal = self.finalizeSet (alias context, b, alias list);
        if (!asLeft) {
            self.registerUniqValue (alias context, b, retVal.getType (), retVal, alias list)
        } else {
            retVal
        }
    }

    /**
     * Finalize a block of values
     * @params:
     *    - b: the block of values to finalize
     *    - list: the list of statement to fill
     * @returns: the final value of the block if any
     * */
    fn finalizeBlock (self, dmut context : &Expander, b : &BlockValue, dmut list : &StmtList)-> &YILValue {
        let dmut innerList = StmtList::new ();

        let retType = context:.finalizeType (b.getType ());
        let (resultVar, _) = self.createVarForBlock (alias context, b.getLoc (), b.getType (), retType, alias list);
        context:.enterBlock ();

        let retVal = self.finalizeSet (alias context, b, alias innerList);
        self.finalizeAffect (alias context, b.getLoc (), resultVar, retVal, alias innerList);

        context:.exitBlock ();

        if (innerList.hasDestroy ()) {
            if (b.isReturner () || b.isBreaker () || b.getThrowers ().len != 0us) {
                list:.append (YILTryFinally::new (b.getLoc (), innerList[], innerList.getToDestroy ()));
            } else {
                list:.append (innerList[]);
                list:.append (innerList.getToDestroy ());
            }
        } else {
            // Block without scope guards can be flattened, there is no variable shadowing anymore
            list:.append (innerList); // BlockValue::new (b.getLoc (), VOID_TYPE, innerList[], noValue-> true));
        }

        if (!resultVar.isOf!{&YILUnit} ()) {
            context:.registerVariable (b.getUniqId (), resultVar);
        }

        resultVar
    }

    /**
     * Finalize a try finally value
     * */
    fn finalizeTryFinally (self, dmut context : &Expander, t : &TryFinallyValue, dmut list : &StmtList)-> &YILValue {
        let dmut tryP = StmtList::new ();

        let vdecl = t.getFailVar ();
        if (!vdecl.isOf!{&UnitValue} ()) { // let #_failed = true;
            self.finalize (alias context, vdecl, alias list);
        }

        context:.enterBlock ();
        let (tryVal, isCatch) = match t.getTryPart () {
            c : &TryCatchValue => {
                let aff = if (vdecl.isOf!{&UnitValue} ()) {
                    vdecl
                } else {
                    let v = vdecl.asOf!{&VarDeclValue} ();
                    let vref = VarRefValue::new (v.getLoc (), v.getLoc (), v.getVarType (), v.getUniqId (), isSelf-> false, canCte-> false);
                    cast!{&Value} (AffectValue::new (t.getLoc (), v.getVarType (), vref, BoolValue::new (t.getLoc (), false)))
                };

                (self.finalizeTryCatch (alias context, c, alias tryP, aff-> aff), true)
            }
            _ => {
                (self.finalize (alias context, t.getTryPart (), alias tryP), false)
            }
        };

        let type = context:.finalizeType (t.getTryPart ().getType ());
        let (varTry, _) = self.createVarForBlock (alias context, t.getTryPart ().getLoc (), t.getTryPart ().getType (), type, alias list);
        if (!varTry.isOf!{&YILUnit} ()) {
            context:.registerVariable (t.getTryPart ().getUniqId (), varTry);
            self.finalizeAffect (alias context, t.getTryPart ().getLoc (), varTry, tryVal, alias tryP);
        }

        if (!isCatch && !vdecl.isOf!{&UnitValue} ()) { // #_failed = false;
            let v = vdecl.asOf!{&VarDeclValue} ();
            let vref = VarRefValue::new (v.getLoc (), v.getLoc (), v.getVarType (), v.getUniqId (), isSelf-> false, canCte-> false);
            let aff = AffectValue::new (t.getLoc (), v.getVarType (), vref, BoolValue::new (t.getLoc (), false));

            self.finalize (alias context, aff, alias tryP);
        }

        context:.exitBlock ();
        context:.enterBlock ();

        if (t.canTryThrow () || t.getTryPart ().isReturner () || t.getTryPart ().isBreaker ()) {
            // Need a try finally
            let dmut finP = StmtList::new ();
            self.finalize (alias context, t.getFinallyPart (), alias finP);

            let tryF = YILTryFinally::new (t.getLoc (), tryP[], finP[]);
            list:.append (tryF);
        } else {
            // only write elements after the end of the scope
            self.finalize (alias context, t.getFinallyPart (), alias tryP);
            list:.append (tryP);
        }

        context:.exitBlock ();
        if (!varTry.isOf!{&YILUnit} ()) {
            context:.registerVariable (t.getUniqId (), varTry);
        }

        varTry // return the value generated by the try part
    }

    /**
     * Finalize a try catch value
     * */
    fn finalizeTryCatch (self, dmut context : &Expander, t : &TryCatchValue, dmut list : &StmtList, aff : &Value = UNIT_VALUE)-> &YILValue {
        let dmut tryP = StmtList::new ();

        context:.enterBlock ();
        let tryVal = self.finalize (alias context, t.getTryPart (), alias tryP);

        let type = context:.finalizeType (t.getTryPart ().getType ());
        let (varTry, _) = self.createVarForBlock (alias context, t.getTryPart ().getLoc (), t.getTryPart ().getType (), type, alias list);
        if (!varTry.isOf!{&YILUnit} ()) {
            context:.registerVariable (t.getTryPart ().getUniqId (), varTry);
            self.finalizeAffect (alias context, t.getTryPart ().getLoc (), varTry, tryVal, alias tryP);
        }

        if (!aff.isOf!{&UnitValue} ()) {
            self.finalize (alias context, aff, alias tryP);
        }
        context:.exitBlock ();

        let dmut catchP = StmtList::new ();
        context:.enterBlock ();

        self.finalize (alias context, t.getCatchVar (), alias catchP);
        let ctVarValue = t.getCatchVar ().asOf!{&VarDeclValue} ();
        let varref = self.finalize (alias context, VarRefValue::new (ctVarValue.getLoc (), ctVarValue.getLoc (), ctVarValue.getVarType (), ctVarValue.getUniqId (), isSelf-> false, canCte-> false), alias catchP);

        catchP:.append (YILAffect::new (t.getCatchVar ().getLoc (), varref, YILBeginCatch::new (t.getLoc (), YRuntimeFuncs::BEGIN_CATCH)));

        let catchVal = self.finalize (alias context, t.getCatchPart (), alias catchP);
        self.finalizeAffect (alias context, t.getCatchPart ().getLoc (), varTry, catchVal, alias catchP);

        context:.exitBlock ();

        let ctVarType = context:.finalizeType (ctVarValue.getVarType ());
        let tryCatch = YILTryCatch::new (t.getLoc (), ctVarType, tryP[], catchP[]);
        list:.append (tryCatch);

        if (!varTry.isOf!{&YILUnit} ()) {
            context:.registerVariable (t.getUniqId (), varTry);
        }

        varTry
    }

    /**
     * Finalize a set of value
     * @params:
     *    - s: the set of values to finalize
     *    - list: the list of statement to fill
     * @returns: the final value of the set if any
     * */
    fn finalizeSet (self, dmut context : &Expander, s : &BlockValue, dmut list : &StmtList)-> &YILValue {
        let mut finVal : &YILValue = YIL_UNIT_VALUE;
        for i in 0us .. s.getValues ().len {
            let inner = s.getValues ()[i];
            finVal = self.finalize (alias context, inner, alias list);
        }

        finVal
    }

    /**
     * Finalize a branch statement
     * @params:
     *    - c: the conditional statement to finalize
     *    - list: the list of statement to fill
     * @returns: the final value of the condition if any
     * */
    fn finalizeCond (self, dmut context : &Expander, c : &ConditionalValue, dmut list : &StmtList)-> &YILValue {
        let type = context:.finalizeType (c.getType ());
        let (var, _) = self.createVarForBlock (alias context, c.getLoc (), c.getType (), type, alias list);

        let dmut ifList = StmtList::new ();
        let dmut elseList = StmtList::new ();

        // Maybe calls from this test are evaluated in the else cond
        // If there are calls in there, we need to validate them first
        let test = self.finalize (alias context, c.getTest (), alias list);
        let testVar = if (!test.isOf!{&YILVar} () && !test.isOf!{&YILIntValue} ()) {
            let (testVar, _) = self.createVarForBlock (alias context,
                                                       c.getTest ().getLoc (),
                                                       c.getTest ().getType (),
                                                       test.getType (),
                                                       alias list,
                                                       withValue-> test);
            testVar
        } else {
            test
        };

        context:.enterBlock ();
        let ifVal = self.finalize (alias context, c.getValue (), alias ifList);
        context:.exitBlock ();

        context:.enterBlock ();
        let elseVal = self.finalize (alias context, c.getElse (), alias elseList);
        context:.exitBlock ();

        if (!var.isOf!{&YILUnit} ()) {
            if (!ifVal.isOf!{&YILUnit} ()) { // can happen if the if val is returner/breaker
                self.finalizeAffect (alias context, c.getLoc (), var, ifVal, alias ifList);
            }
            if (!elseVal.isOf!{&YILUnit} ()) { // if the else val is a returner/breaker or just does not have a value
                self.finalizeAffect (alias context, c.getLoc (), var, elseVal, alias elseList);
            }
        }

        self.finalizeCond (c.getLoc (), alias context,  alias list, testVar, ifList, elseList);
        if (!var.isOf!{&YILUnit} ()) {
            context:.registerVariable (c.getUniqId (), var);
        }

        var
    }

    /**
     * Create conditional jump structure using the list of instruction of each blocks
     * @params:
     *    - loc: the location of the condition
     *    - list: the list of statement to fill
     *    - test: the test of the conditional
     *    - thenList: the list of statement to execute if the condition is met
     *    - elseList: the list of statement to execute if the condition is not met
     * */
    fn finalizeCond (self, loc : &Word, dmut context : &Expander, dmut list : &StmtList, test : &YILValue, thenList : &StmtList, elseList : &StmtList) {
        let thenLabel = YILLabel::new (loc, "then"s8, context:.generateLblId ());
        let endLabel = YILLabel::new (loc, "end"s8, context:.generateLblId ());
        let elseLabel = if (elseList.len () != 0us) {
            YILLabel::new (loc, "else"s8, context:.generateLblId ())
        } else {
            endLabel
        };

        list:.append (YILCondJmp::new (loc, test, thenLabel, elseLabel));
        list:.append (thenLabel);
        list:.append (thenList);

        if (elseList.len () != 0us) {
            list:.append (YILGoto::new (loc, endLabel));
            list:.append (elseLabel);
            list:.append (elseList);
            // list:.append (YILGoto::new (loc, endLabel));
        }

        list:.append (endLabel);
    }

    /**
     * Finalize a loop statement
     * @params:
     *    - l: the loop statement to finalize
     *    - list: the list of statement to fill
     * @returns: the final value of the loop if any
     * */
    fn finalizeLoop (self, dmut context : &Expander, l : &LoopValue, dmut list : &StmtList)-> &YILValue {
        if (l.isCteFor ()) {
            self.finalizeCteForLoop (alias context, l, alias list)
        } else {
            self.finalizeClassicLoop (alias context, l, alias list)
        }
    }

    /**
     * Finalize a cte for loop statement
     * @params:
     *    - l: the for loop statement to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeCteForLoop (self, dmut context : &Expander, l : &LoopValue, dmut list : &StmtList)-> &YILValue {
        let type = context:.finalizeType (l.getType ());
        let (var, _) = self.createVarForBlock (alias context, l.getLoc (), l.getType (), type, alias list);

        context:.enterBlock ();
        let endLabel = YILLabel::new (l.getLoc (), "end"s8, context:.generateLblId ());
        context:.enterLoop (var, endLabel);

        let dmut innerList = StmtList::new ();
        let innerValue = self.finalize (alias context, l.getValue (), alias innerList);
        if (!var.isOf!{&YILUnit} () && !l.getValue ().isBreaker ()) {
            self.finalizeAffect (alias context, l.getLoc (), var, innerValue, alias innerList);
        }

        context:.exitLoop ();
        context:.exitBlock ();

        list:.append (innerList);
        list:.append (endLabel);
        var
    }

    /**
     * Finalize a loop with (or without) a test, that is execute at runtime
     * @params:
     *    - l: the loop to finalize
     *    - list: the statement list to fill
     * @returns: the value of the loop if any
     * */
    fn finalizeClassicLoop (self, dmut context : &Expander, l : &LoopValue, dmut list : &StmtList)-> &YILValue {
        let type = context:.finalizeType (l.getType ());
        let (varref, _) = self.createVarForBlock (alias context, l.getLoc (), l.getType (), type, alias list);

        let dmut testList = StmtList::new ();
        // test is in its own list, because we need to validate it right away
        // But it has to be placed after the test label
        let test = self.finalize (alias context, l.getTest (), alias testList);

        context:.enterBlock ();
        let dmut loopList = StmtList::new ();
        let endLabel = YILLabel::new (l.getLoc (), "end"s8, context:.generateLblId ());
        context:.enterLoop (varref, endLabel);

        let contentVal = self.finalize (alias context, l.getValue (), alias loopList);
        if (!varref.isOf!{&YILUnit} () && !l.getValue ().isBreaker () && !test.isOf!{&YILUnit} ()) {
            self.finalizeAffect (alias context, l.getLoc (), varref, contentVal, alias loopList);
        }

        context:.exitLoop ();
        context:.exitBlock ();

        let begLabel = YILLabel::new (l.getLoc (), "beg"s8, context:.generateLblId ());
        let testLabel = YILLabel::new (l.getLoc (), "test"s8, context:.generateLblId ());

        if (!l.isDo () && !test.isOf!{&YILUnit} ()) {
            list:.append (YILGoto::new (l.getLoc (), testLabel));
        }

        list:.append (begLabel);
        list:.append (loopList);

        if (!test.isOf!{&YILUnit} ()) {
            list:.append (testLabel);
            list:.append (testList);
            list:.append (YILCondJmp::new (l.getLoc (), test, begLabel, endLabel));
        } else {
            list:.append (YILGoto::new (l.getLoc (), begLabel));
        }

        list:.append (endLabel);
        if (!varref.isOf!{&YILUnit} ()) {
            context:.registerVariable (l.getUniqId (), varref);
        }

        varref
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ==============================          TEMPORARY VARIABLES          ===============================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create a variable to store a block value
     * @params:
     *    - loc: the location of the creation
     *    - type: the type of the block
     *    - list: the list of stmt to fill
     *    - name: the name of the variable (with a possible formatter, its uniq Id)
     * @returns:
     *    - a varref or unit value, if the type is empty
     *    - the address of the var ref (if requested)
     * */
    fn createVarForBlock (self, dmut context : &Expander, loc : &Word, genType : &Type, type : &YILType, dmut list : &StmtList, name : [c8] = "YI_%"s8, withValue : &YILValue = YIL_UNIT_VALUE, getAddr : bool = false)-> (&YILValue, &YILValue) {
        match type {
            YILVoid () => { return (YIL_UNIT_VALUE, YIL_UNIT_VALUE); }
            _ => {
                let varId = context:.generateVarId ();
                let fullName = format (name, varId);
                let blckVar = YILVarDecl::new (loc, fullName, type, varId, isTemp-> (name == "YI_%"s8));
                list:.append (blckVar);

                let var = YILVar::new (loc, fullName, type, varId, isTemp-> (name == "YI_%"s8));
                if (!withValue.isOf!{&YILUnit} ()) self.finalizeAffect (alias context, loc, var, withValue, alias list);

                if genType.isMovable () {
                    self.registerDestroy (alias context, var, genType, alias list, implicit-> true);
                }

                return if (getAddr) {
                    let addr = self.createAddress (alias context, var, type-> YILPointer::new (type), alias list);
                    (var, addr)
                } else {
                    (var, YIL_UNIT_VALUE)
                };
            }
        };
    }

    /**
     * Register a uniq value in a variable
     * @returns: the uniq var ref, or YIL_UNIT_VALUE depending on the type
     * * */
    fn registerUniqValue (self, dmut context : &Expander, gen : &Value, value : &YILValue, dmut list : &StmtList, name : [c8] = "YI_%"s8) -> &YILValue {
        if (!context.isInAFrame ()) return value;

        let type = context:.finalizeType (gen.getType ());
        let (varref, _) = self.createVarForBlock (alias context, gen.getLoc (), gen.getType (), type, alias list, name-> name);
        if (!varref.isOf!{&YILUnit} ()) {
            context:.registerVariable (gen.getUniqId (), varref);
            self.finalizeAffect (alias context, gen.getLoc (), varref, value, alias list);
        }

        varref
    }

    /**
     * Register a uniq value in a variable
     * @returns: the uniq var ref, or YIL_UNIT_VALUE depending on the type
     * * */
    fn registerUniqValue (self, dmut context : &Expander, gen : &Value, type : &YILType, value : &YILValue, dmut list : &StmtList, name : [c8] = "YI_%"s8) -> &YILValue {
        if (!context.isInAFrame ()) return value;

        let (varref, _) = self.createVarForBlock (alias context, gen.getLoc (), gen.getType (), type, alias list, name-> name);
        if (!varref.isOf!{&YILUnit} ()) {
            context:.registerVariable (gen.getUniqId (), varref);
            self.finalizeAffect (alias context, gen.getLoc (), varref, value, alias list);
        }

        varref
    }

    /**
     * Register a variable to destroy in the current block list
     * */
    pub fn registerDestroy (self, dmut context : &Expander, gen : &YILVar, type : &Type, dmut list : &StmtList, implicit : bool) {
        {
            match type {
                c : &ClassPtrType => {
                    if (!c.getSymbol ().isMovable ()) return {};
                    match c.getInner ().getDestructor () {
                        UnitValue () => return {}
                        p : &PrototypeValue => {
                            if (!list.hasToDestroy (gen.getVarId ())) {
                                let thenLabel = YILLabel::new (gen.getLoc (), "then"s8, context:.generateLblId ());
                                let endLabel = YILLabel::new (gen.getLoc (), "end"s8, context:.generateLblId ());
                                let test = YILFieldValue::new (gen.getLoc (), YIL_UINT_8, gen, name-> ClassKeys::SET);

                                let name = context:.getMangler ().mangle (p);
                                context:.registerTouchSym (name);

                                let call = YILNameCallValue::new (gen.getLoc (), YIL_VOID, name, [YILAddrValue::new (gen.getLoc (), YIL_PTR_VOID, gen)]);
                                let instr = YILCall::new (gen.getLoc (), call);
                                let dmut condList = StmtList::new ();

                                condList:.append (YILCondJmp::new (gen.getLoc (), test, thenLabel, endLabel));
                                condList:.append (thenLabel);
                                condList:.append (instr);
                                condList:.append (YILAffect::new (gen.getLoc (), test, YILIntValue::new (gen.getLoc (), YIL_UINT_8, 0us)));
                                condList:.append (endLabel);

                                list:.registerDestroy (gen.getVarId (), YILBlock::new (gen.getLoc (), condList[]), implicit);
                            }
                        }
                    }
                }
            }
        } catch {
            _ => {}
        }
    }

    /**
     * Remove a destroyer (if it was implicit - ctor, or function call)
     * */
    pub fn removeDestroy (self, p : &YILValue, dmut list : &StmtList) {
        match p {
            v : &YILVar => {
                list:.removeDestroyNotImplicit (v.getVarId ());
            }
        }
    }

    /*!
     * =====================================================================================
     * =====================================================================================
     * ========================          LITERAL EXPANSION          ========================
     * =====================================================================================
     * =====================================================================================
     */

    /**
     * Finalize a literal value
     * @params:
     *    - l: the literal value to finalize
     *    - list: the scope list of statement to fill
     * @returns: the finalized value
     * */
    fn finalizeLiteral (self, dmut context : &Expander, l : &LiteralValue, dmut list : &StmtList, asLeft : bool)-> &YILValue {
        match l { // simple literals
            s : &StringValue => { return self.finalizeStr (alias context, s); }
            b : &BoolValue => { return self.finalizeBool (alias context, b); }
            c : &CharValue => { return self.finalizeChar (alias context, c); }
            f : &FloatValue => { return self.finalizeFloat (alias context, f); }
            i : &IntValue => { return self.finalizeInt (alias context, i); }
            NullValue () => { return YIL_NULL_VALUE; }
            UnitValue () => { return YIL_UNIT_VALUE; }
        }

        match context.getVariable (l.getUniqId ()) {
            Ok (u : &YILValue) => { // The call was already seen and validated
                return u;
            }
        };

        let val = match l {
            a : &ArrayValue => { self.finalizeArray (alias context, a, alias list) }
            a : &ArrayAllocValue => { self.finalizeArrayAlloc (alias context, a, alias list) }
            t : &TupleValue => { self.finalizeTuple (alias context, t, alias list) }
            s : &StructInitValue => { self.finalizeStructInit (alias context, s, alias list) }
            m : &MapValue => { self.finalizeMap (alias context, m, alias list) }
            _ => {
                eprintln (l::typeinfo.name);
                __pragma!panic ();
            }
        }

        if (!asLeft) {
            return self.registerUniqValue (alias context, l, val, alias list);
        } else return val;
    }

    /**
     * Finalize a string literal
     * */
    fn finalizeStr (self, dmut context : &Expander, str : &StringValue)-> &YILValue {
        let slc = str.getType ().asOf!{&SliceType} ();
        let type = context:.finalizeType (slc);

        if (str.getLen () == 0us) {
            let len = YILIntValue::new (str.getLoc (), YIL_UINT_MAX, str.getLen ());
            return YILTupleValue::new (str.getLoc (), type, [len, YIL_NULL_VALUE, YIL_NULL_VALUE],
                                       fieldNames-> [SliceKeys::LEN, SliceKeys::PTR, SliceKeys::BLK_INFO]);
        }

        let dmut res = Vec!{u8}::new ();
        let content = str.getValue ();
        let innerType = match str.getEncodingSize () {
            8u16 => {
                res:.reserve (str.getLen () + 1us);
                for i in 0us .. str.getLen () {
                    res:.push (content.utf8 [i]);
                }
                res:.push (0u8);
                YIL_UINT_8
            }
            16u16 => {
                res:.reserve (str.getLen () * 2us + 1us);
                for i in 0us .. str.getLen () {
                    let x : &u8 = cast!{&u8} (cast!{&void} (content.utf16.ptr + (i * sizeof (u16))));
                    __pragma!trusted ({
                        res:.push (*x);
                        res:.push (*(x + 1us));
                    });
                }
                res:.push (0u8);
                YIL_UINT_16
            }
            _ => {
                res:.reserve (str.getLen () * 4us + 1us);
                for i in 0us .. str.getLen () {
                    let x : &u8 = cast!{&u8} (cast!{&void} (content.utf32.ptr + (i * sizeof (u32))));
                    __pragma!trusted ({
                        res:.push (*x);
                        res:.push (*(x + 1us));
                        res:.push (*(x + 2us));
                        res:.push (*(x + 3us));
                    });
                }
                res:.push (0u8);
                YIL_UINT_32
            }
        };

        let array = YILStringValue::new (str.getLoc (), YILArray::new (innerType, res.len ()), res[]);
        let len = YILIntValue::new (str.getLoc (), YIL_UINT_MAX, str.getLen ());
        YILTupleValue::new (str.getLoc (), type, [len, array, YIL_NULL_VALUE], fieldNames-> [SliceKeys::LEN, SliceKeys::PTR, SliceKeys::BLK_INFO])
    }

    /**
     * Finalize a bool literal
     * */
    fn finalizeBool (self, dmut _ : &Expander, b : &BoolValue)-> &YILValue {
        YILIntValue::new (b.getLoc (), YIL_UINT_8, if b.isTrue () { 1us } else { 0us })
    }

    /**
     * Finalize a char literal
     * */
    fn finalizeChar (self, dmut context : &Expander, c : &CharValue)-> &YILValue {
        let type = context:.finalizeType (c.getType ());
        YILIntValue::new (c.getLoc (), type, c.getValueAsInt ())
    }

    /**
     * Finalize a float literal
     * */
    fn finalizeFloat (self, dmut context : &Expander, f : &FloatValue)-> &YILValue {
        let type = context:.finalizeType (f.getType ());
        YILFloatValue::new (f.getLoc (), type, f.getValue ())
    }

    /**
     * Finalize an int literal value
     * */
    fn finalizeInt (self, dmut context : &Expander, i : &IntValue)-> &YILValue {
        let type = context:.finalizeType (i.getType ());
        YILIntValue::new (i.getLoc (), type, i.getValue ())
    }

    /**
     * Finalize an array value
     * @params:
     *    - a: the array value to finalize
     *    - list: the list of statement to fill
     * @returns: the value
     * */
    fn finalizeArray (self, dmut context : &Expander, a : &ArrayValue, dmut list : &StmtList)-> &YILValue {
        let dmut res = Vec!{&YILValue}::new ();
        for i in a.getValues () {
            res:.push (self.finalize (alias context, i, alias list));
        }

        let atype = context:.finalizeType (a.getType ());
        YILArrayValue::new (a.getLoc (), atype, res[])
    }

    /**
     * Finalize an array allocation value
     * @params:
     *     - a: the array allocation to finalize
     *     - list: the list of statement to fill
     * @returns: the value
     * */
    fn finalizeArrayAlloc (self, dmut context : &Expander, a : &ArrayAllocValue, dmut list : &StmtList)-> &YILValue {
        let type = context:.finalizeType (a.getType ());

        if (a.isDynamic ()) {
            let len = self.finalize (alias context, a.getLen (), alias list);
            let (resultVar, addrResult) = self.createVarForBlock (alias context, a.getLoc (), a.getType (), type, alias list, getAddr-> true);
            let innerType = context:.finalizeType (a.getType ().asOf!{&SliceType} ().getInners ()[0]);
            let size = YILIntValue::new (a.getLoc (), YIL_UINT_MAX, innerType.getSize ());

            if (a.getValue ().isOf!{&UnitValue} ()) {
                list:.append (YILCall::new (a.getLoc (),
                                            YILNameCallValue::new (a.getLoc (), YIL_VOID, YRuntimeFuncs::ALLOC_NO_SET_SLICE, [addrResult, len, size])));
            } else {
                let valPtr = self.finalizeAddressFromAny (alias context, a.getValue (), alias list);
                list:.append (YILCall::new (a.getLoc (),
                                            YILNameCallValue::new (a.getLoc (), YIL_VOID, YRuntimeFuncs::ALLOC_SET_SLICE, [addrResult, valPtr, len, size])));
            }

            resultVar
        } else {
            let inner = if (a.getValue ().isOf!{&UnitValue} ()) {
                YIL_UNIT_VALUE
            } else {
                self.finalize (alias context, a.getValue (), alias list)
            };

            let dmut params = Vec!{&YILValue}::new ();
            let len = a.getLen ().asOf!{&IntValue} ().getValue ().toT!{usize} ();
            for _ in 0us .. len {
                params:.push (inner);
            }

            cast!{&YILValue} (YILArrayValue::new (a.getLoc (), type, params[]))
        }
    }

    /**
     * Finalize a tuple value
     * @params:
     *    - t: the tuple value to finalize
     *    - list: the list of statement to fill
     * @returns: the value
     * */
    fn finalizeTuple (self, dmut context : &Expander, t : &TupleValue, dmut list : &StmtList)-> &YILValue {
        let dmut res = Vec!{&YILValue}::new ();
        for i in t.getValues () {
            res:.push (self.finalize (alias context, i, alias list));
        }

        let ttype = context:.finalizeType (t.getType ());
        YILTupleValue::new (t.getLoc (), ttype, res[])
    }

    /**
     * Finalize a struct init value
     * @params:
     *   - t: the struct init value to finalize
     *   - list: th elist of statement to fill
     * @returns: the value
     * */
    fn finalizeStructInit (self, dmut context : &Expander, s : &StructInitValue, dmut list : &StmtList)-> &YILValue {
        let dmut res = Vec!{&YILValue}::new ();
        for i in s.getValues () {
            res:.push (self.finalize (alias context, i, alias list));
        }

        let ttype = context:.finalizeType (s.getType ());
        YILTupleValue::new (s.getLoc (), ttype, res[], fieldNames-> s.getFieldNames ())
    }

    /**
     * Finalize a map literal value
     * @params:
     *    - m: the map to finalize
     *    - list: the list of stmtlist to fill
     * */
    fn finalizeMap (self, dmut context : &Expander, m : &MapValue, dmut list : &StmtList)-> &YILValue {
        let mapType = m.getType ().asOf!{&MapType} ();
        let (mapInst, mpAddr) = self.finalizeMapInstance (alias context, m.getLoc (), mapType, alias list);

        if (m.getLen () == 0us) return mapInst;

        for i in 0us .. m.getLen () {
            let keyAddr = self.finalizeAddressFromAny (alias context, m.getKeys ()[i], alias list);
            let valueAddr = self.finalizeAddressFromAny (alias context, m.getValues ()[i], alias list);

            list:.append (YILCall::new (m.getLoc (),
                                        YILNameCallValue::new (m.getLoc (), YIL_VOID, YRuntimeFuncs::MAP_INSERT, [mpAddr, keyAddr, valueAddr])));
        }

        mapInst
    }

    /**
     * Finalize the allocation of a new map instance
     * @params:
     *    - loc: the location of the creation
     *    - m: the map instance to allocate
     *    - list: the stmt list to fill
     * @returns:
     *    - .0: the map instance by value
     *    - .1: the address of the created map instance
     * */
    fn finalizeMapInstance (self, dmut context : &Expander, loc : &Word, m : &MapType, dmut list : &StmtList)-> (&YILValue, &YILValue) {
        let mtype = context:.finalizeType (m);
        let vtableName = context:.getMangler ().mangleMapInfo (m);
        context:.registerTouchSym (vtableName);

        let vtableAddr = YILAddrValue::new (loc, YIL_PTR_2_VOID, YILVar::new (loc, vtableName, YIL_PTR_VOID, 0us));

        let (res, resAddr) = self.createVarForBlock (alias context, loc, NONE_TYPE, mtype, alias list, name-> "__map_%"s8, getAddr-> true);
        list:.append (YILCall::new (loc,
                                    YILNameCallValue::new (loc, YIL_VOID, YRuntimeFuncs::MAP_EMPTY, [resAddr, vtableAddr])));

        (res, resAddr)
    }

    /*!
     * =====================================================================================
     * =====================================================================================
     * ======================            OPERATOR EXPANSION           ======================
     * =====================================================================================
     * =====================================================================================
     */

    /**
     * Finalize an operator value
     * @params:
     *    - op: the operator to finalize
     *    - list: the list of stmtlist to fill
     * */
    fn finalizeOperator (self, dmut context : &Expander, op : &OperatorValue, dmut list : &StmtList, asLeft : bool)-> &YILValue {
        match op {
            a : &AffectValue => { self.finalizeAffect (alias context, a, alias list) }
            c : &CallOperatorValue => { self.finalizeCall (alias context, c, alias list) }
            b : &BinaryOperatorValue => { self.finalizeBinary (alias context, b, alias list, asLeft-> asLeft) }
            f : &FieldOperatorValue => { self.finalizeField (alias context, f, alias list) }
            s : &SliceOrArrayOperatorValue => { self.finalizeSliceOrArrayOp (alias context, s, alias list) }
            m : &MapOperatorValue => { self.finalizeMapOp (alias context, m, alias list) }
            u : &UnaryOperatorValue => { self.finalizeUnary (alias context, u, alias list) }
            _ => {
                eprintln (op::typeinfo.name);
                __pragma!panic ();
            }
        }
    }

    /**
     * Finalize an affectation operation
     * @params:
     *     - a: the affectation operation to validate
     *     - list: the list of statement to fill
     * @returns: the left operand
     * */
    fn finalizeAffect (self, dmut context : &Expander, a : &AffectValue, dmut list : &StmtList)-> &YILValue {
        let left = self.finalize (alias context, a.getLeft (), alias list, asLeft-> true);
        let right = self.finalize (alias context, a.getRight (), alias list);

        self.finalizeAffect (alias context, a.getLoc (), left, right, alias list);

        left
    }

    /**
     * Finalize an affection moving value from left to right
     * */
    fn finalizeAffect (self, dmut context : &Expander, loc : &Word, left : &YILValue, right : &YILValue, dmut list : &StmtList) {
        context;
        if (!left.isOf!{&YILUnit} () && !right.isOf!{&YILUnit} ()) {
            self.removeDestroy (right, alias list);
            list:.append (YILAffect::new (loc, left, right));
        }
    }

    /**
     * Finalize a binary operator value
     * @params:
     *    - b: the binary operator to finalize
     *    - list: the list of statement to fill
     * @returns: the finalized binary
     * */
    fn finalizeBinary (self, dmut context : &Expander, b : &BinaryOperatorValue, dmut list : &StmtList, asLeft : bool)-> &YILValue {
        match b {
            bl : &LogicalBinBoolOperatorValue => { return self.finalizeLogicalBool (alias context, bl, alias list); }
            r : &RangeContainValue => { return self.finalize (alias context, r.getCall (), alias list); }
            s : &SpecialMathIntOperatorValue => { return self.finalize (alias context, s.getCall (), alias list); }
            f : &SpecialMathFloatOperatorValue => { return self.finalize (alias context, f.getCall (), alias list); }
            a : &SpecialArrayOperatorValue => { return self.finalize (alias context, a.getCall (), alias list); }
            c : &CmpClassTypeOperatorValue => { return self.finalizeClassTypeCmp (alias context, c, alias list); }
        }

        match context.getVariable (b.getUniqId ()) {
            Ok (u : &YILValue) => { // The call was already seen and validated
                return u;
            }
        };

        let left = self.finalize (alias context, b.getLeft (), alias list);
        let right = self.finalize (alias context, b.getRight (), alias list);

        let type = context:.finalizeType (b.getType ());
        let ret = YILBinaryValue::new (b.getLoc (), type, b.getOperator (), left, right);

        if (!asLeft) {
            return self.registerUniqValue (alias context, b, ret, alias list);
        } else return ret;
    }

    /**
     * Fianlize a logical bool operator
     * @params:
     *    - b: the bool operator to finalize
     *    - list: the list of stmt to fill
     * @returns: the value of the expression
     * */
    fn finalizeLogicalBool (self, dmut context : &Expander, b : &LogicalBinBoolOperatorValue, dmut list : &StmtList)-> &YILValue {
        let dmut rightList = StmtList::new ();
        let left = self.finalize (alias context, b.getLeft (), alias list);
        let right = self.finalize (alias context, b.getRight (), alias rightList);

        let leftVar = if !left.isOf!{&YILVar} () && !left.isOf!{&YILIntValue} () {
            let (leftVar, _) = self.createVarForBlock (alias context,
                                                       b.getLoc (), NONE_TYPE, left.getType (), alias list, withValue-> left);
            leftVar
        } else {
            left
        };

        let type = context:.finalizeType (b.getType ());
        let (var, _) = self.createVarForBlock (alias context, b.getLoc (), b.getType (), type, alias list);
        let dmut ifL = StmtList::new (), dmut ifNL = StmtList::new ();

        if (b.getOperator () == BinaryOperators::DAND) { // let leftVar = left; if (leftVar) { right } else { leftVar }
            ifL:.append (rightList);
            ifL:.append (YILAffect::new (b.getLoc (), var, right));
            ifNL:.append (YILAffect::new (b.getLoc (), var, leftVar));
        } else { // operator == ||, let leftVar = left; if (leftVar) { leftVar } else { right }
            ifL:.append (YILAffect::new (b.getLoc (), var, leftVar));

            ifNL:.append (rightList);
            ifNL:.append (YILAffect::new (b.getLoc (), var, right));
        };

        self.finalizeCond (b.getLoc (), alias context, alias list, leftVar, ifL, ifNL);
        var
    }

    /**
     * Finalize a class type comparison operator
     * @params:
     *    - c: the class cmp operator to finalize
     *    - list: the list of stmt to fill
     * @returns: the value of the expression
     * */
    fn finalizeClassTypeCmp (self, dmut context : &Expander, c : &CmpClassTypeOperatorValue, dmut list : &StmtList)-> &YILValue {
        let left = if (c.getLeft ().isOf!{&ReferencerValue} ()) { // can be a reference value if this test is inside a pattern matching
            // i don't really know if it should be tested there (binary operator validator) or here
            self.finalize (alias context, c.getLeft ().asOf!{&ReferencerValue} ().getValue (), alias list)
        } else {
            self.finalize (alias context, c.getLeft (), alias list)
        };

        let derefInst = YILUnrefValue::new (c.getLoc (), left.getType ().asOf!{&YILPointer} ().getInner (), left);

        let rightTypeInfoName = context:.getMangler ().mangleTypeInfo (c.getRType ());
        context:.registerTouchSym (rightTypeInfoName);

        let rTypeInfo = YILAddrValue::new (c.getLoc (), YIL_PTR_2_VOID, YILVar::new (c.getLoc (), rightTypeInfoName, YIL_PTR_2_VOID, 0us));

        let lvtable = YILFieldValue::new (c.getLoc (), YIL_PTR_2_VOID, derefInst, name-> "#_vtable"s8);
        let (vtableAddrU, _) = self.createVarForBlock (alias context, c.getLoc (), NONE_TYPE, YIL_PTR_2_VOID, alias list, withValue-> lvtable);
        let call = YILNameCallValue::new (c.getLoc (), YIL_UINT_8, YRuntimeFuncs::CMP_CLASS_OF_TYPE, [rTypeInfo, vtableAddrU]);

        if (c.getOperator () == BinaryOperators::NOT_OF) {
            let (resCall, _) = self.createVarForBlock (alias context, c.getLoc (), NONE_TYPE, YIL_UINT_8, alias list, withValue-> call);
            YILUnaryValue::new (c.getLoc (), YIL_UINT_8, UnaryOperators::NOT, resCall)
        } else {
            call
        }
    }

    /**
     * Finalize an unary operator
     * @params:
     *    - context: the context of the expansion
     *    - u: the unary operator to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeUnary (self, dmut context : &Expander, u : &UnaryOperatorValue, dmut list : &StmtList)-> &YILValue {
        let value = self.finalize (alias context, u.getOperand (), alias list);
        let type = context:.finalizeType (u.getType ());

        YILUnaryValue::new (u.getLoc (), type, u.getOperator (), value)
    }

    /*!
     * =====================================================================================
     * =====================================================================================
     * ========================            FIELD EXPANSION           =======================
     * =====================================================================================
     * =====================================================================================
     */

    /**
     * Finalize a field access value
     * @params:
     *    - f : the field access to finalize
     *    - list: the list of statement to fill
     * @returns: the value of the access
     * */
    fn finalizeField (self, dmut context : &Expander, f : &FieldOperatorValue, dmut list : &StmtList)-> &YILValue {
        match f {
            c : &ClassFieldAccessValue => { self.finalizeClassFieldAccess (alias context, c, alias list) }
            c : &ClosureFieldAccessValue => { self.finalizeClosureFieldAccess (alias context, c, alias list) }
            s : &StructFieldAccessValue => { self.finalizeStructFieldAccess (alias context, s, alias list) }
            t : &TupleFieldAccessValue => { self.finalizeTupleFieldAccess (alias context, t, alias list) }
            o : &OptionFieldAccessValue => { self.finalizeOptionFieldAccess (alias context, o, alias list) }
            t : &FutureFieldAccessValue => { self.finalizeFutureFieldAccess (alias context, t, alias list) }
            c : &ClassTypeInfoAccessValue => { self.finalizeClassTypeInfoAccess (alias context, c, alias list) }
            e : &EnumFieldAccessValue => { self.finalize (alias context, e.getContent (), alias list) }
            _ => {
                eprintln (f::typeinfo.name);
                __pragma!panic ();
            }
        }
    }

    /**
     * Finalize the access of a field in a class value
     * @params:
     *    - f: the field access to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeClassFieldAccess (self, dmut context : &Expander, f : &ClassFieldAccessValue, dmut list : &StmtList)-> &YILValue {
        let cl = self.finalize (alias context, f.getClassInstance (), alias list);
        let type = cl.getType ();
        let retType = context:.finalizeType (f.getType ());

        let clR = YILUnrefValue::new (f.getLoc (), type.asOf!{&YILPointer} ().getInner (), cl);
        YILFieldValue::new (f.getLoc (), retType, clR, name-> f.getFieldName ())
    }

    /**
     * Finalize the access of a field in a closure value
     * @params:
     *    - f: the field access to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeClosureFieldAccess (self, dmut context : &Expander, f : &ClosureFieldAccessValue, dmut list : &StmtList)-> &YILValue {
        let cl = self.finalize (alias context, f.getClosure (), alias list);
        let retType = context:.finalizeType (f.getType ());
        let clR = YILUnrefValue::new (f.getLoc (), cl.getType ().asOf!{&YILPointer} ().getInner (), cl);

        YILFieldValue::new (f.getLoc (), retType, clR, index-> f.getFieldIndex ())
    }

    /**
     * Finalize the access of a field in a struct value
     * @params:
     *    - f: the field access to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeStructFieldAccess (self, dmut context : &Expander, f : &StructFieldAccessValue, dmut list : &StmtList)-> &YILValue {
        let retType = context:.finalizeType (f.getType ());
        let str = self.finalize (alias context, f.getStruct (), alias list);

        YILFieldValue::new (f.getLoc (), retType, str, name-> f.getFieldName ())
    }

    /**
     * Finalize the access of a field in an option value
     * @params:
     *    - o: the field access to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeOptionFieldAccess (self, dmut context : &Expander, o : &OptionFieldAccessValue, dmut list : &StmtList)-> &YILValue {
        let retType = context:.finalizeType (o.getType ());
        let opt = self.finalize (alias context, o.getOption (), alias list);

        let optContentType = opt.getType ().asOf!{&YILTuple} ().getInners ()[1];

        // a.error => a.content.error, a.value => a.content.value
        let content = YILFieldValue::new (o.getLoc (), optContentType, opt, name-> OptionKeys::CONTENT);
        YILFieldValue::new (o.getLoc (), retType, content, name-> o.getFieldName ())
    }

    /**
     * Finalize the access of a field in a future value
     * @params:
     *    - f: the field access to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeFutureFieldAccess (self, dmut context : &Expander, f : &FutureFieldAccessValue, dmut list : &StmtList)-> &YILValue {
        match f.getFieldName () {
            FutureKeys::VALUE => {
                self.finalizeFutureWait (alias context, f, alias list)
            }
            FutureKeys::FINISHED => {
                self.finalizeFutureCheck (alias context, f, alias list)
            }
            _ => __pragma!panic ();
        }
    }

    /**
     * Finalize the wait of a future value
     * @params:
     *    - context: the context of the expansion
     *    - f: the field access to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeFutureWait (self, dmut context : &Expander, f : &FutureFieldAccessValue, dmut list : &StmtList)-> &YILValue {
        let retType = context:.finalizeType (f.getType ());
        match context.getVariable (f.getUniqId ()) {
            Ok (u : &YILValue) => { // The call was already seen and validated
                return YILUnrefValue::new (f.getLoc (), retType, u);
            }
        };


        let futureValue = self.finalize (alias context, f.getFuture (), alias list);
        let call = YILNameCallValue::new (f.getLoc (), YILPointer::new (retType), YRuntimeFuncs::WAIT_FUTURE, [futureValue]);
        let varRes = self.registerUniqValue (alias context, f, type-> call.getType (), call, alias list);

        return YILUnrefValue::new (f.getLoc (), retType, varRes);
    }

    /**
     * Finalize the checking of a future value
     * @params:
     *    - context: the context of the expansion
     *    - f: the field access to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeFutureCheck (self, dmut context : &Expander, f : &FutureFieldAccessValue, dmut list : &StmtList)-> &YILValue {
        let retType = context:.finalizeType (f.getType ());
        match context.getVariable (f.getUniqId ()) {
            Ok (u : &YILValue) => { // The call was already seen and validated
                return u;
            }
        };


        let futureValue = self.finalize (alias context, f.getFuture (), alias list);
        let call = YILNameCallValue::new (f.getLoc (), retType, YRuntimeFuncs::CHECK_FUTURE, [futureValue]);

        self.registerUniqValue (alias context, f, type-> call.getType (), call, alias list)
    }

    /**
     * Finalize the access of a field in a tuple value
     * @params:
     *    - f: the field access to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeTupleFieldAccess (self, dmut context : &Expander, f : &TupleFieldAccessValue, dmut list : &StmtList)-> &YILValue {
        let retType = context:.finalizeType (f.getType ());
        let str = self.finalize (alias context, f.getTuple (), alias list);

        YILFieldValue::new (f.getLoc (), retType, str, index-> f.getFieldIndex ())
    }

    /**
     * Finalize the access of the typeinfo of an object instance
     * @params:
     *    - ti:  the access to finalize
     *    - list: the list of stmt to fill
     * */
    fn finalizeClassTypeInfoAccess (self, dmut context : &Expander, ti : &ClassTypeInfoAccessValue, dmut list : &StmtList)-> &YILValue {
        let loc = ti.getLoc ();
        let inst = self.finalize (alias context, ti.getClassInstance (), alias list);

        let (instU, _) = self.createVarForBlock (alias context, loc, NONE_TYPE, inst.getType (), alias list, withValue-> inst);
        let derefInst = YILUnrefValue::new (loc, inst.getType ().asOf!{&YILPointer} ().getInner (), instU);

        let vtableAddr = YILFieldValue::new (loc, YIL_PTR_2_VOID, derefInst, name-> "#_vtable"s8);
        let typeInfoT = context:.finalizeType (ti.getType ());
        let (vtableAddrU, _) = self.createVarForBlock (alias context, loc, NONE_TYPE, YIL_PTR_2_VOID, alias list, withValue-> vtableAddr); // vtable => [*typeinfo, *dtor, *method1, ..., *methodN]

        let tiPtr = YILCastValue::new (loc, YILPointer::new (typeInfoT), YILUnrefValue::new (loc, YIL_PTR_VOID, vtableAddrU));
        YILUnrefValue::new (loc, typeInfoT, tiPtr)
    }

    /*!
     * =====================================================================================
     * =====================================================================================
     * ========================            CALL EXPANSION           ========================
     * =====================================================================================
     * =====================================================================================
     */

    /**
     * Finalize a call operator value
     * @params:
     *    - c: the call operator to validate
     *    - list: the list of statement to fill
     * @returns: the value of the call if any
     * */
    fn finalizeCall (self, dmut context : &Expander, c : &CallOperatorValue, dmut list : &StmtList)-> &YILValue {

        // Call of function performs heavy computation with possible side effects
        // Function call must appear only once in the finalized code
        match context.getVariable (c.getUniqId ()) {
            Ok (u : &YILValue) => { // The call was already seen and validated
                return u;
            }
        };

        let call = match c {
            ca : &CallValue => { self.finalizeSimpleCall (alias context, ca, alias list) }
            ctor : &CtorCallValue => { self.finalizeCtorCall (alias context, ctor, alias list) }
            fptr : &FuncPtrCallValue => { self.finalizeFuncPtrCall (alias context, fptr, alias list) }
            meth : &MethodCallValue => { self.finalizeMethodCall (alias context, meth, alias list) }
            _ => {
                eprintln (c::typeinfo.name);
                __pragma!panic ();
            }
        };

        let result = self.registerUniqValue (alias context, c, call, alias list);
        if (result.isOf!{&YILUnit} ()) {
            list:.append (YILCall::new (c.getLoc (), call));
        } else if (c.getType ().isMovable ()) {
            self.registerDestroy (alias context, result.asOf!{&YILVar} (), c.getType (), alias list, implicit-> true);
        }

        result
    }

    /**
     * Finalize a simple call value
     * @params:
     *   - c: the call value to finalize
     *   - list: the statement list to fill
     * @returns: the value of the call
     * */
    fn finalizeSimpleCall (self, dmut context : &Expander, c : &CallValue, dmut list : &StmtList)-> &YILValue {
        let retType = context:.finalizeType (c.getType ());

        let dmut params = Vec!{&YILValue}::new ();
        for i in c.getParameters () {
            let p = self.finalize (alias context, i, alias list);
            self.removeDestroy (p, alias list);
            params:.push (p);
        }

        for i in c.getAddParameters () {
            let p = self.finalize (alias context, i, alias list);
            self.removeDestroy (p, alias list);
            params:.push (p);
        }

        let name = context.getMangler ().mangle (c.getPrototype ());
        context:.registerTouchSym (name);

        YILNameCallValue::new (c.getLoc (), retType, name, params[])
    }

    /**
     * Finalize a class ctor call value
     * @params:
     *     -
     * */
    fn finalizeCtorCall (self, dmut context : &Expander, c : &CtorCallValue, dmut list : &StmtList)-> &YILValue {
        let retType = context:.finalizeType (c.getType ());
        let (inst, value) = self.finalizeCtorSelf (alias context, c.getLoc (), retType, c.getType (), c.getCtor ().getInstance (), alias list);

        let dmut params = Vec!{&YILValue}::new ();
        params:.push (inst);
        for pi in c.getParameters () {
            let p = self.finalize (alias context, pi, alias list);
            self.removeDestroy (p, alias list);
            params:.push (p);
        }
        for pi in c.getAddParameters () {
            let p = self.finalize (alias context, pi, alias list);
            self.removeDestroy (p, alias list);
            params:.push (p);
        }

        let name = context.getMangler ().mangle (c.getCtor ().getPrototype ());
        context:.registerTouchSym (name);

        list:.append (YILCall::new (c.getLoc (), YILNameCallValue::new (c.getLoc (), YIL_VOID, name, params[])));
        value
    }

    /**
     * Finalize the creation of a new class instance, that is not constructed but only allocated
     * @params:
     *     - loc: the location of the ctor construction
     *     - clType: the ClassPtr version of the type of the class to construct
     *     - inst: the instance value generator (if nonevalue, then call class allocation)
     *     - list: the list to fill
     * @returns:
     *    - .0: the value to pass to the ctor
     *    - .1: the instance of the class
     * */
    fn finalizeCtorSelf (self, dmut context : &Expander, loc : &Word, retType : &YILType, clType : &Type, inst : &Value, dmut list : &StmtList)-> (&YILValue, &YILValue) {
        // We gave a value to the ctor
        if (!inst.isOf!{&UnitValue} ())  {
            let inner = self.finalize (alias context, inst, alias list);
            return (inner, inner);
        }

        let (varref, _) = self.createVarForBlock (alias context, loc, NONE_TYPE, retType, alias list, name-> "__self_%"s8);
        match clType {
            cptr : &ClassPtrType => {
                if (cptr.isStruct ()) {
                    if (cptr.getSymbol ().isMovable ()) {
                        let field = YILFieldValue::new (loc, YIL_UINT_8, varref, name-> ClassKeys::SET);
                        list:.append (YILAffect::new (loc, field, YILIntValue::new (loc, YIL_UINT_8, 1us)));
                    }

                    return (YILAddrValue::new (varref.getLoc (), YILPointer::new (retType), varref), varref);
                } else {
                    let clRef = {
                        cptr.getInner ()
                    } catch {
                        _ => __pragma!panic (); // invalid class
                    };

                    let vtableName = context:.getMangler ().mangleVtable (clRef);
                    context:.registerTouchSym (vtableName);

                    let vtableAddr = YILAddrValue::new (loc, YIL_PTR_2_VOID,
                                                        YILVar::new (loc, vtableName, YIL_PTR_2_VOID, 0us));


                    let callVal = YILNameCallValue::new (loc, retType, YRuntimeFuncs::ALLOC_CLASS, [vtableAddr]);
                    list:.append (YILAffect::new (loc, varref, callVal));

                    return (varref, varref);
                }
            }
            _ => __pragma!panic ();
        }
    }

    /**
     * Finalize a call to a function pointer
     * @params:
     *    - f: the function pointer to finalize
     *    - list: the list of statement to fill
     * @returns: the call
     * */
    fn finalizeFuncPtrCall (self, dmut context : &Expander, f : &FuncPtrCallValue, dmut list : &StmtList)-> &YILValue {
        let retType = context:.finalizeType (f.getType ());

        let dmut params = Vec!{&YILValue}::new ();
        for pi in f.getParameters () {
            let p = self.finalize (alias context, pi, alias list);
            self.removeDestroy (p, alias list);
            params:.push (p);
        }

        match f.getFuncPtr () {
            n : &ProtoFuncPtrValue => {
                let proto = context.getMangler ().mangle (n.getPrototype ());
                context:.registerTouchSym (proto);

                YILNameCallValue::new (f.getLoc (), retType, proto, params [])
            }
            _ => {
                let fptr = self.finalize (alias context, f.getFuncPtr (), alias list);
                YILPtrCallValue::new (f.getLoc (), retType, fptr, params[])
            }
        }
    }

    /**
     * Finalize a call to a method value
     * @params:
     *     - f: the method call to finalize
     *     - list: the list of statement to fill
     * @returns: the result of the call
     * */
    fn finalizeMethodCall (self, dmut context : &Expander,  f : &MethodCallValue, dmut list : &StmtList)-> &YILValue {
        let retType = context:.finalizeType (f.getType ());

        let dmut params = Vec!{&YILValue}::new ();
        let methProto = f.getMethod ();
        let mut inst = self.finalize (alias context, methProto.getClosure (), alias list);

        match methProto.getClosure ().getType () {
            c : &ClassPtrType => {
                if (c.isStruct ()) inst = self.createAddress (alias context, inst, YILPointer::new (inst.getType ()), alias list);
            }
        }

        params:.push (inst);
        self.removeDestroy (inst, alias list);
        for pi in f.getParameters () {
            let p = self.finalize (alias context, pi, alias list);
            self.removeDestroy (p, alias list);
            params:.push (p);
        }

        for pi in f.getAddParameters () {
            let p = self.finalize (alias context, pi, alias list);
            self.removeDestroy (p, alias list);
            params:.push (p);
        }

        if (methProto.isDirect ()) {
            let name = context.getMangler ().mangle (methProto.getPrototype ());
            context:.registerTouchSym (name);

            YILNameCallValue::new (f.getLoc (), retType, name, params[])
        } else {
            let fptr = self.createVtableAccess (alias context, f.getLoc (), inst, methProto.getVtableIndex (), alias list);
            YILPtrCallValue::new (f.getLoc (), retType, fptr, params[])
        }
    }

    /**
     * Access a method from the vtable of a class instance
     * @params:
     *    - loc: the location of the access
     *    - proto: the proto value, defining the type of the final value
     *    - index: the index of the function in the vtable
     * @returns: the address of the method to call
     * */
    fn createVtableAccess (self, dmut context : &Expander, loc : &Word, inst : &YILValue, index : usize, dmut list : &StmtList)-> &YILValue {
        let ptrSize = global::state::instance ().getSizeTypeSize () / 8us;

        let (instU, _) = self.createVarForBlock (alias context, loc, NONE_TYPE, inst.getType (), alias list, withValue-> inst);
        let derefInst = YILUnrefValue::new (loc, inst.getType ().asOf!{&YILPointer} ().getInner (), instU);


        let vtableAddr = YILFieldValue::new (loc, YIL_PTR_2_VOID, derefInst, name-> "#_vtable"s8);
        let (vtableAddrU, _) = self.createVarForBlock (alias context, loc, NONE_TYPE, YIL_PTR_2_VOID, alias list, withValue-> vtableAddr);

        // +1us because 0 is the dtor
        let funcInVtableAddr = YILBinaryValue::new (loc,
                                                    YIL_PTR_2_VOID,
                                                    BinaryOperators::PLUS,
                                                    vtableAddrU,
                                                    YILIntValue::new (loc, YIL_UINT_MAX, (index + 2us) * cast!{usize} (ptrSize))); // [TYPEINFO, DTOR, funcs*]

        let (funcInVtableAddrU, _) = self.createVarForBlock (alias context, loc, NONE_TYPE, YIL_PTR_2_VOID, alias list, withValue-> funcInVtableAddr);
        YILUnrefValue::new (loc, YIL_PTR_VOID, funcInVtableAddrU)
    }

    /*!
     * =====================================================================================
     * =====================================================================================
     * =====================            SLICE/ARRAY EXPANSION           ====================
     * =====================================================================================
     * =====================================================================================
     */

    /**
     * Finalize an operator on slice or array operands
     * @params:
     *    - sl: the operator to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeSliceOrArrayOp (self, dmut context : &Expander, sl : &SliceOrArrayOperatorValue, dmut list : &StmtList)-> &YILValue {
        match sl {
            a : &ArrayAccessValue => { self.finalizeArrayAccess (alias context, a, alias list) }
            a : &SliceAppendValue => { self.finalizeSliceAppend (alias context, a, alias list) }
            c : &SliceConcatValue => { self.finalizeSliceConcat (alias context, c, alias list) }
            s : &SliceAccessValue => { self.finalizeSliceAccess (alias context, s, alias list) }
            s : &SliceCompareValue => { self.finalizeSliceCompare (alias context, s, alias list) }
            _ => {
                eprintln (sl::typeinfo.name);
                __pragma!panic ();
            }
        }
    }

    /**
     * Finalize an array access value
     * @params:
     *    - context: the context of the finalization
     *    - a: the array access to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeArrayAccess (self, dmut context : &Expander, a : &ArrayAccessValue, dmut list : &StmtList)-> &YILValue {
        let arr = self.finalize (alias context, a.getArray (), alias list, asLeft-> true);
        let index = self.finalize (alias context, a.getIndex (), alias list);
        let type = context:.finalizeType (a.getType ());

        YILArrayAccessValue::new (a.getLoc (), type, arr, index)
    }

    /**
     * Finalize a slice concatenation operation
     * @params:
     *    - context: the context of the finalization
     *    - a: the slice concat to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeSliceConcat (self, dmut context : &Expander, s : &SliceConcatValue, dmut list : &StmtList)-> &YILValue {
        // Slice concat allocates values, thus must be performed only once
        match context.getVariable (s.getUniqId ()) {
            Ok (u : &YILValue) => { // The call was already seen and validated
                return u;
            }
        };

        let leftAddr = self.finalizeAddressFromAny (alias context, s.getLeft (), alias list);
        let rightAddr = self.finalizeAddressFromAny (alias context, s.getRight (), alias list);
        let type = context:.finalizeType (s.getType ());
        let inner = context:.finalizeType (s.getType ().asOf!{&SliceType} ().getInners ()[0]);
        let size = YILIntValue::new (s.getLoc (), YIL_UINT_MAX, inner.getSize ());

        let (resultVar, addrResult) = self.createVarForBlock (alias context, s.getLoc (), s.getType (), type, alias list, getAddr-> true);
        list:.append (YILCall::new (s.getLoc (),
                                    YILNameCallValue::new (s.getLoc (), YIL_VOID, YRuntimeFuncs::CONCAT_SLICE, [addrResult, leftAddr, rightAddr, size])));

        return self.registerUniqValue (alias context, s, resultVar, alias list);
    }

    /**
     * Finalize a slice append operation
     * @params:
     *    - context: the context of the finalization
     *    - a: the slice append to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeSliceAppend (self, dmut context : &Expander, a : &SliceAppendValue, dmut list : &StmtList)-> &YILValue {
        let leftAddr = self.finalizeAddressFromAny (alias context, a.getLeft (), alias list);
        let right = self.finalize (alias context, a.getRight (), alias list);

        let inner = context:.finalizeType (a.getLeft ().getType ().asOf!{&SliceType} ().getInners ()[0]);
        let innerSize = YILIntValue::new (a.getLoc (), YIL_UINT_MAX, inner.getSize ());

        let (_, ptrU) = self.createVarForBlock (alias context, a.getLoc (), a.getRight ().getType (), right.getType (), alias list, withValue-> right, getAddr-> true);
        list:.append (YILCall::new (a.getLoc (),
                                    YILNameCallValue::new (a.getLoc (), YIL_VOID, YRuntimeFuncs::APPEND_SLICE, [leftAddr, ptrU, innerSize])));

        YIL_UNIT_VALUE
    }

    /**
     * Finalize a slice access value
     * @params:
     *    - context: the context of the finalization
     *    - s: the slice access to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeSliceAccess (self, dmut context : &Expander, s : &SliceAccessValue, dmut list : &StmtList)-> &YILValue {
        let slc = self.finalize (alias context, s.getSlice (), alias list);
        let index = self.finalize (alias context, s.getIndex (), alias list);

        let resType = context:.finalizeType (s.getType ());
        let size = YILIntValue::new (s.getLoc (), YIL_UINT_MAX, resType.getSize ());

        let indexV = YILBinaryValue::new (s.getLoc (), YIL_UINT_MAX, BinaryOperators::STAR, size, index);
        let (indexVU, _) = self.createVarForBlock (alias context, s.getLoc (), NONE_TYPE, YIL_UINT_MAX, alias list, withValue-> indexV);
        let ptrT = YILPointer::new (resType);

        let dataField = YILBinaryValue::new (s.getLoc (),
                                             YIL_PTR_VOID,
                                             BinaryOperators::PLUS,
                                             YILFieldValue::new (s.getLoc (), YIL_PTR_VOID, slc, name-> SliceKeys::PTR),
                                             indexVU);

        let (dataFieldU, _) = self.createVarForBlock (alias context, s.getLoc (), NONE_TYPE, YIL_UINT_MAX, alias list, withValue-> dataField);
        let finalPtrV = YILCastValue::new (s.getLoc (), ptrT, dataFieldU);
        YILUnrefValue::new (s.getLoc (),
                            resType,
                            self.registerUniqValue (alias context, s, type-> ptrT, finalPtrV, alias list))
    }

    /**
     * Finalize the comparison of two slices
     * @params:
     *    - context: the context of the finalization
     *    - s: the slice compare to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeSliceCompare (self, dmut context : &Expander, s : &SliceCompareValue, dmut list : &StmtList)-> &YILValue {
        context;
        s;
        list;
        __pragma!panic ();
    }


    /*!
     * ====================================================================================================
     * ====================================================================================================
     * =================================          MAP EXPANSION          ==================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Finalize a map operation
     * @params:
     *    - context: the context of the finalization
     *    - m: the map value to validate
     *    - list: the list of statement to fill
     * */
    fn finalizeMapOp (self, dmut context : &Expander, m : &MapOperatorValue, dmut list : &StmtList)-> &YILValue {
        match m {
            a : &MapAccessValue => { self.finalizeMapAccess (alias context, a, alias list) }
            c : &MapContainValue => { self.finalizeMapContain (alias context, c, alias list) }
            i : &MapInsertValue => { self.finalizeMapInsert (alias context, i, alias list) }
            b : &MapIteratorBeginValue => { self.finalizeMapIterBegin (alias context, b, alias list) }
            e : &MapIteratorEndValue => { self.finalizeMapIterEnd (alias context, e, alias list) }
            d : &MapIteratorDelValue => { self.finalizeMapIterDel (alias context, d, alias list) }
            n : &MapIteratorNextValue => { self.finalizeMapIterNext (alias context, n, alias list) }
            k : &MapIteratorKeyValue => { self.finalizeMapIterKey (alias context, k, alias list) }
            v : &MapIteratorValueValue => { self.finalizeMapIterValue (alias context, v, alias list) }
            _ => {
                eprintln (m::typeinfo.name);
                __pragma!panic ();
            }
        }
    }

    /**
     * Finalize a map access value
     * @params:
     *    - context: the context of the finalization
     *    - m: the map access value to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeMapAccess (self, dmut context : &Expander, m : &MapAccessValue, dmut list : &StmtList)-> &YILValue {
        let valueType = context:.finalizeType (m.getType ());
        match context.getVariable (m.getUniqId ()) {
            Ok (u : &YILValue) => {
                return YILUnrefValue::new (m.getLoc (), valueType, u);
            }
        }

        let mpAddr = self.finalizeAddressFromAny (alias context, m.getMap (), alias list);
        let keyAddr = self.finalizeAddressFromAny (alias context, m.getKey (), alias list);

        let (value, _) = self.createVarForBlock (alias context, m.getLoc (), NONE_TYPE, YILPointer::new (valueType), alias list);

        list:.append (YILAffect::new (m.getLoc (),
                                      value,
                                      YILNameCallValue::new (m.getLoc (), value.getType (), YRuntimeFuncs::MAP_FIND, [mpAddr, keyAddr])));


        // let result : *V = {
        //    let _value = _yrt_map_find (&mp, &key);
        //    if (_value == null) throw copy KeyErrMap ();
        //    _value
        // };

        let dmut thenList = StmtList::new ();
        self.finalize (alias context, m.getKeyError (), alias thenList);
        self.finalizeCond (m.getLoc (), alias context, alias list,
                           YILBinaryValue::new (m.getLoc (), YIL_UINT_8, BinaryOperators::DEQUAL, value, YIL_NULL_VALUE),
                           thenList, StmtList::new ());

        context:.registerVariable (m.getUniqId (), value);
        YILUnrefValue::new (m.getLoc (), valueType, value) // the returned value is a reference to the actual value
    }


    /**
     * Finalize a map containing value
     * @params:
     *    - context: the context of the expansion
     *    - m: the the map contain to validate
     *    - list: the list of statement to fil
     * */
    fn finalizeMapContain (self, dmut context : &Expander, m : &MapContainValue, dmut list : &StmtList)-> &YILValue {
        let valueType = context:.finalizeType (m.getType ());
        match context.getVariable (m.getUniqId ()) {
            Ok (u : &YILValue) => {
                if (m.isNot ()) {
                    return YILBinaryValue::new (m.getLoc (), YIL_UINT_8, BinaryOperators::NOT_EQUAL, u, YIL_NULL_VALUE);
                } else {
                    return YILBinaryValue::new (m.getLoc (), YIL_UINT_8, BinaryOperators::DEQUAL, u, YIL_NULL_VALUE);
                }
            }
        }

        let mpAddr = self.finalizeAddressFromAny (alias context, m.getMap (), alias list);
        let keyAddr = self.finalizeAddressFromAny (alias context, m.getKey (), alias list);

        let (value, _) = self.createVarForBlock (alias context, m.getLoc (), NONE_TYPE, YILPointer::new (valueType), alias list);
        list:.append (YILAffect::new (m.getLoc (),
                                      value,
                                      YILNameCallValue::new (m.getLoc (), value.getType (), YRuntimeFuncs::MAP_FIND, [mpAddr, keyAddr])));


        context:.registerVariable (m.getUniqId (), value);

        if (m.isNot ()) {
            YILBinaryValue::new (m.getLoc (), YIL_UINT_8, BinaryOperators::DEQUAL, value, YIL_NULL_VALUE)
        } else {
            YILBinaryValue::new (m.getLoc (), YIL_UINT_8, BinaryOperators::NOT_EQUAL, value, YIL_NULL_VALUE)
        }
    }

    /**
     * Finalize an insertion in a map
     * @params:
     *    - context: the context of the finalization
     *    - m: the map insert to validate
     *    - list: the list of statement to fill
     * */
    fn finalizeMapInsert (self, dmut context : &Expander, m : &MapInsertValue, dmut list : &StmtList)-> &YILValue {
        let mpAddr = self.finalizeAddressFromAny (alias context, m.getMap (), alias list);
        let keyAddr = self.finalizeAddressFromAny (alias context, m.getKey (), alias list);
        let valueAddr = self.finalizeAddressFromAny (alias context, m.getValue (), alias list);

        list:.append (YILCall::new (m.getLoc (),
                                    YILNameCallValue::new (m.getLoc (), YIL_VOID, YRuntimeFuncs::MAP_INSERT, [mpAddr, keyAddr, valueAddr])));

        YIL_UNIT_VALUE
    }

    /**
     * Finalize the creation of an iterator from a map value
     * @params:
     *    - context: the context of the validation
     *    - b: the iterator creation
     *    - list: the list of statement to fill
     * */
    fn finalizeMapIterBegin (self, dmut context : &Expander, b : &MapIteratorBeginValue, dmut list : &StmtList)-> &YILValue {
        let mpAddr = self.finalizeAddressFromAny (alias context, b.getMap (), alias list);
        let iterType = context:.finalizeType (b.getType ());

        YILNameCallValue::new (b.getLoc (), iterType, YRuntimeFuncs::MAP_ITER_BEGIN, [mpAddr])
    }

    /**
     * Finalize the deletion of an iterator of a map
     * @params:
     *    - context: the context of the validation
     *    - d: the iterator deletion
     *    - list: the list of statement to fill
     * */
    fn finalizeMapIterDel (self, dmut context : &Expander, d : &MapIteratorDelValue, dmut list : &StmtList)-> &YILValue {
        let iterValue = self.finalize (alias context, d.getIterator (), alias list);
        list:.append (YILCall::new (d.getLoc (),
                                    YILNameCallValue::new (d.getLoc (), YIL_VOID, YRuntimeFuncs::MAP_ITER_DEL, [iterValue])));

        YIL_UNIT_VALUE
    }

    /**
     * Finalize the test of end reached of an iterator of a map
     * @params:
     *    - context: the context of the validation
     *    - e: the iterator checker
     *    - list: the list of statement to fill
     * */
    fn finalizeMapIterEnd (self, dmut context : &Expander, e : &MapIteratorEndValue, dmut list : &StmtList)-> &YILValue {
        let iterValue = self.finalize (alias context, e.getIterator (), alias list);
        YILNameCallValue::new (e.getLoc (), YIL_UINT_8, YRuntimeFuncs::MAP_ITER_END, [iterValue])
    }

    /**
     * Finalize the increment of a map iterator
     * @params:
     *    - context: the context of the validation
     *    - n: the iterator incrementer
     *    - list: the list of statement to fill
     * */
    fn finalizeMapIterNext (self, dmut context : &Expander, n : &MapIteratorNextValue, dmut list : &StmtList)-> &YILValue {
        let iterValue = self.finalize (alias context, n.getIterator (), alias list);
        list:.append (YILCall::new (n.getLoc (),
                                    YILNameCallValue::new (n.getLoc (), YIL_VOID, YRuntimeFuncs::MAP_ITER_NEXT, [iterValue])));

        YIL_UNIT_VALUE
    }

    /**
     * Finalize the extraction of the key from a map iterator
     * @params:
     *    - context: the context of the validation
     *    - k: the iterator key extractor
     *    - list: the list of statement to fill
     * */
    fn finalizeMapIterKey (self, dmut context : &Expander, k : &MapIteratorKeyValue, dmut list : &StmtList)-> &YILValue {
        let iterValue = self.finalize (alias context, k.getIterator (), alias list);
        let retType = context:.finalizeType (k.getType ());
        YILUnrefValue::new (k.getLoc (),
                            retType,
                            YILNameCallValue::new (k.getLoc (), YILPointer::new (retType), YRuntimeFuncs::MAP_ITER_KEY, [iterValue]))
    }

    /**
     * Finalize the extraction of a value from a map iterator
     * @params:
     *    - context: the context of the validation
     *    - v: the iterator value extractor
     *    - list: the list of statement to fill
     * */
    fn finalizeMapIterValue (self, dmut context : &Expander, v : &MapIteratorValueValue, dmut list : &StmtList)-> &YILValue {
        let iterValue = self.finalize (alias context, v.getIterator (), alias list);
        let retType = context:.finalizeType (v.getType ());
        YILUnrefValue::new (v.getLoc (),
                            retType,
                            YILNameCallValue::new (v.getLoc (), YILPointer::new (retType), YRuntimeFuncs::MAP_ITER_VAL, [iterValue]))
    }

    /*!
     * =====================================================================================
     * =====================================================================================
     * ========================            MEMORY EXPANSION           ======================
     * =====================================================================================
     * =====================================================================================
     */

    /**
     * Finalize a memory operation value
     * @params:
     *    - context: the context of the finalization
     *    - mem: the memory value to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeMemory (self, dmut context : &Expander, mem : &MemoryValue, dmut list : &StmtList)-> &YILValue {
        match mem {
            a : &AddressValue      => { self.finalizeAddress (alias context, a, alias list) }
            a : &AliaserValue      => { self.finalize (alias context, a.getValue (), alias list) }
            c : &ClassAliaserValue => { self.finalize (alias context, c.getValue (), alias list) }
            c : &CopierValue       => { self.finalizeCopier (alias context, c, alias list) }
            d : &DereferencerValue => { self.finalizeDeref (alias context, d, alias list) }
            r : &ReferencerValue   => { self.finalizeRef (alias context, r, alias list) }
            l : &LazyValue         => { self.finalizeLazy (alias context, l, alias list) }
            f : &FutureValue       => { self.finalizeFuture (alias context, f, alias list) }
            m : &MoverValue        => { self.finalizeMover (alias context, m, alias list) }
            _ => {
                eprintln (mem::typeinfo.name);
                __pragma!panic ();
            }
        }
    }

    /**
     * Finalize an address value
     * @params:
     *    - context: the context of the finalization
     *    - add: the address value to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeAddress (self, dmut context : &Expander, add : &AddressValue, dmut list : &StmtList)-> &YILValue {
        let type = context:.finalizeType (add.getType ());
        let value = self.finalize (alias context, add.getValue (), alias list, asLeft-> true);

        let depthRet = context.getTypeExpander ().getPointerDepth (type);
        let depthVal = context.getTypeExpander ().getPointerDepth (value.getType ());

        // sometimes AddressValue are used on already constructed addresses (references, string literals, ...)
        // So if we get a pointer value that has the same depth, meaning pointer of pointer of ...
        // Then we can assume it is already constructed, and can be returned as it is, with a cast though
        if (depthVal == depthRet) {
            YILCastValue::new (add.getLoc (), type, value)
        } else {
            self.createAddress (alias context, value, type-> type.asOf!{&YILPointer} (), alias list)
        }
    }

    /**
     * Finalize the creation of an address from a value
     * @params:
     *    - context: the context of the finalization
     *    - val: value whose address we want to create
     *    - list: the list of statement to fill
     * */
    fn finalizeAddressFromAny (self, dmut context : &Expander, val : &Value, dmut list : &StmtList)-> &YILValue {
        let type = YILPointer::new (context:.finalizeType (val.getType ()));
        let value = self.finalize (alias context, val, alias list, asLeft-> true);

        let depthRet = context.getTypeExpander ().getPointerDepth (type);
        let depthVal = context.getTypeExpander ().getPointerDepth (value.getType ());

        // sometimes AddressValue are used on already constructed addresses (references, string literals, ...)
        // So if we get a pointer value that has the same depth, meaning pointer of pointer of ...
        // Then we can assume it is already constructed, and can be returned as it is, with a cast though
        if (depthVal == depthRet) {
            YILCastValue::new (val.getLoc (), type, value)
        } else {
            self.createAddress (alias context, value, type-> type, alias list)
        }
    }

    /**
     * Create an address from a value
     * If the value is not addressable, then put it in a variable and return the address of the variable
     * */
    fn createAddress (self, dmut context : &Expander, value : &YILValue, type : &YILPointer, dmut list : &StmtList)-> &YILValue {
        match value {
            v : &YILVar => {
                return YILAddrValue::new (value.getLoc (), type, v);
            }
            u : &YILUnrefValue => {
                return YILCastValue::new (value.getLoc (), type, u.getValue ());
            }
            f : &YILFieldValue => {
                return YILAddrValue::new (value.getLoc (), type, f);
            }
            _ => {
                let (var, _) = self.createVarForBlock (alias context, value.getLoc (), NONE_TYPE, value.getType (), alias list);
                list:.append (YILAffect::new (value.getLoc (), var, value));

                return YILAddrValue::new (var.getLoc (), type, var);
            }
        }
    }

    /**
     * Finalize a copier value
     * @params:
     *    - context: the context of the finalization
     *    - c: the copier value to copy
     *    - list: the list of statement to fill
     * */
    fn finalizeCopier (self, dmut context : &Expander, c : &CopierValue, dmut list : &StmtList)-> &YILValue {
        // If the copy is a done copy, then we only need to validate its inner value
        if (c.isDone ()) {
            // assuming that it is a call, so no need to save the uniqness
            return self.finalize (alias context, c.getValue (), alias list);
        }

        // otherwise, we need to call builtin functions
        // Copy performs heavy computation with possible side effects
        // Copy values must appear only once in the finalized code
        match context.getVariable (c.getUniqId ()) {
            Ok (u : &YILValue) => { // The copy was already seen and validated
                return u;
            }
        };

        let call = match c.getType () {
            slc : &SliceType => { self.finalizeCopierSlice (alias context, c, slc, alias list) }
            m : &MapType => { self.finalizeCopierMap (alias context, c, m, alias list) }
            p : &PointerType => {
                match p.getInners ()[0] {
                    t : &TupleType => { self.finalizeCopierTuple (alias context, c, t, alias list) }
                    t : _ => {
                        eprintln (t::typeinfo.name);
                        __pragma!panic ();
                    }
                }
            }
            t : _ => {
                eprintln (t::typeinfo.name);
                __pragma!panic ();
            }
        };

        return self.registerUniqValue (alias context, c, call, alias list);
    }

    /**
     * Finalize a copier value that returns a slice
     * */
    fn finalizeCopierSlice (self, dmut context : &Expander, c : &CopierValue, slc : &SliceType, dmut list : &StmtList)-> &YILValue {
        let value = self.finalize (alias context, c.getValue (), alias list);
        if (slc.getInners ()[0].isOf!{&VoidType} ()) { return value; } // copy []

        let (_, ptrU) = self.createVarForBlock (alias context, c.getLoc (), c.getValue ().getType (), value.getType (), alias list, withValue-> value, getAddr-> true);

        let retType = context:.finalizeType (c.getType ());
        let innerType = context:.finalizeType (slc.getInners ()[0]);
        let innerSize = YILIntValue::new (c.getLoc (), YIL_UINT_32, innerType.getSize ());

        let (resultVar, addrResult) = self.createVarForBlock (alias context, c.getLoc (), c.getType (), retType, alias list, getAddr-> true);
        list:.append (YILCall::new (c.getLoc (),
                                    YILNameCallValue::new (c.getLoc (), YIL_PTR_VOID, YRuntimeFuncs::DUPLICATE_SLICE, [addrResult, ptrU, innerSize])));

        resultVar
    }

    /**
     * Finalize a copier value that copies a map value
     *
     * */
    fn finalizeCopierMap (self, dmut context : &Expander, c : &CopierValue, m : &MapType, dmut list : &StmtList)-> &YILValue {
        let loc = c.getLoc ();
        let mpAddr = self.finalizeAddressFromAny (alias context, c.getValue (), alias list);

        let mtype = context:.finalizeType (m);
        let vtableName = context:.getMangler ().mangleMapInfo (m);
        context:.registerTouchSym (vtableName);

        let vtableAddr = YILAddrValue::new (loc, YIL_PTR_2_VOID, YILVar::new (loc, vtableName, YIL_PTR_VOID, 0us));
        let (res, resultAddr) = self.createVarForBlock (alias context, loc, NONE_TYPE, mtype, alias list, name-> "__map_%"s8, getAddr-> true);

        list:.append (YILCall::new (loc,
                                    YILNameCallValue::new (loc, YIL_VOID, YRuntimeFuncs::MAP_COPY, [resultAddr, vtableAddr, mpAddr])));

        res
    }

    /**
     * Finalize the copy of a tuple value
     * */
    fn finalizeCopierTuple (self, dmut context : &Expander, c : &CopierValue, t : &TupleType, dmut list : &StmtList)-> &YILValue {
        if (t.getInners ().len == 0us) return YIL_NULL_VALUE;

        let value = self.finalize (alias context, c.getValue (), alias list);
        let retType = context:.finalizeType (c.getType ());

        let innerType = context:.finalizeType (t);
        let innerSize = YILIntValue::new (c.getLoc (), YIL_UINT_32, innerType.getSize ());

        let tuAddr = YILAddrValue::new (c.getLoc (), retType, value);

        let (resultVar, addrResult) = self.createVarForBlock (alias context, c.getLoc (), c.getType (), retType, alias list, getAddr-> true);

        list:.append (YILCall::new (c.getLoc (),
                                    YILNameCallValue::new (c.getLoc (), YIL_VOID, YRuntimeFuncs::DUPLICATE_TUPLE, [addrResult, tuAddr, innerSize]))
            );

        resultVar
    }


    /**
     * Finalize a dereferencer value
     * */
    fn finalizeDeref (self, dmut context : &Expander, d : &DereferencerValue, dmut list : &StmtList)-> &YILValue {
        let value = self.finalize (alias context, d.getValue (), alias list);
        let type = context:.finalizeType (d.getType ());

        YILUnrefValue::new (d.getLoc (), type, value)
    }

    /**
     * Finalize a referencer value
     * */
    fn finalizeRef (self, dmut context : &Expander, r : &ReferencerValue, dmut list : &StmtList)-> &YILValue {
        // same as an addresser but storing the inner type only
        let type = YILPointer::new (context:.finalizeType (r.getType ()));
        let value = self.finalize (alias context, r.getValue (), alias list, asLeft-> true);

        let depthRet = context.getTypeExpander ().getPointerDepth (type);
        let depthVal = context.getTypeExpander ().getPointerDepth (value.getType ());

        // sometimes references are used on already constructed addresses (references, string literals, ...)
        // So if we get a pointer value that has the same depth, meaning pointer of pointer of ...
        // Then we can assume it is already constructed, and can be returned as it is, with a cast though
        if (depthVal == depthRet) {
            YILCastValue::new (r.getLoc (), type, value)
        } else {
            self.createAddress (alias context, value, type-> type.asOf!{&YILPointer} (), alias list)
        }
    }

    /**
     * Finalize a lazy value construction
     * */
    pub fn finalizeLazy (self, dmut context : &Expander, l : &LazyValue, dmut list : &StmtList)-> &YILValue {
        if l.isDone () {
            match context:.getLazyRef (l.getValue ().asOf!{&VarRefValue} ().getRefId ()) {
                Ok (y : &YILVar) => return y;
                _ => __pragma!panic ();
            }
        }

        let type = context.getTypeExpander ().finalizeLazy ();
        let delegate = self.finalize (alias context, l.getLambda (), alias list);
        let set = YILIntValue::new (l.getLoc (), YIL_UINT_8, 0us);

        let innerType = context:.finalizeType (l.getType ());
        let size = YILIntValue::new (l.getLoc (), YIL_UINT_32, innerType.getSize ());

        YILTupleValue::new (l.getLoc (), type, [set, YIL_NULL_VALUE, size, delegate],
                            fieldNames-> [LazyKeys::IS_SET, LazyKeys::VALUE, LazyKeys::SIZE, LazyKeys::FUNC])
    }

    /**
     * Finalize a memory movement (where right is reset after affectation)
     * */
    fn finalizeMover (self, dmut context : &Expander, m : &MoverValue, dmut list : &StmtList)-> &YILValue {
        let what = self.finalize (alias context, m.getValue (), alias list);
        let field = YILFieldValue::new (m.getLoc (), YIL_UINT_8, what, name-> ClassKeys::SET);

        let result = self.registerUniqValue (alias context, m.getValue (), what, alias list);
        match m.getReset () {
            c : &CtorCallValue => {
                self.finalizeCtorCall (alias context, c, alias list);
            }
            _ => __pragma!panic ();
        }

        list:.append (YILAffect::new (m.getLoc (), field, YILIntValue::new (m.getLoc (), YIL_UINT_8, 0us)));

        self.registerDestroy (alias context, result.asOf!{&YILVar} (), m.getType (), alias list, implicit-> true);

        result
    }

    /**
     * Finalize a future value (value created by the spawning of a thread)
     * */
    fn finalizeFuture (self, dmut context : &Expander, f : &FutureValue, dmut list : &StmtList)-> &YILValue {
        match context.getVariable (f.getUniqId ()) {
            Ok (u : &YILValue) => { // The spawn was already seen and validated
                return u;
            }
        };

        let futureType = context:.finalizeType (f.getType ());
        let delegate = self.finalize (alias context, f.getLambda (), alias list);

        let innerType = context:.finalizeType (f.getType ().asOf!{&FutureType} ().getInners ()[0]);
        let size = YILIntValue::new (f.getLoc (), YIL_UINT_32, innerType.getSize ());

        let call = YILNameCallValue::new (f.getLoc (), futureType, YRuntimeFuncs::SPAWN_FUTURE, [delegate, size]);

        // We want to spawn only once, so we register it for other validations
        return self.registerUniqValue (alias context, f, call, alias list);
    }

    /*!
     * =====================================================================================
     * =====================================================================================
     * =======================          CONSTRUCT EXPANSION          =======================
     * =====================================================================================
     * =====================================================================================
     */

    /**
     * Finalize a construct value
     * @params:
     *    - context: the context of the finalization
     *    - ctor: the construct value to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeConstruct (self, dmut context : &Expander, ctor : &ConstructValue, dmut list : &StmtList, asLeft : bool)-> &YILValue {
        match context.getVariable (ctor.getUniqId ()) {
            Ok (u : &YILValue) => { // The call was already seen and validated
                return u;
            }
        };

        let val = match ctor {
            c : &CastValue => { self.finalizeCast (alias context, c, alias list) }
            c : &CtorDelegateValue => { self.finalizeCtorDG (alias context, c, alias list) }
            d : &NameDelegateValue => { self.finalizeNameDG (alias context, d, alias list) }
            f : &FakeMethodDelegateValue => { self.finalizeFakeMethodDG (alias context, f, alias list) }
            n : &ProtoFuncPtrValue => { self.finalizeNameFptr (alias context, n, alias list) }
            m : &MethodDelegateValue => { self.finalizeMethodDG (alias context, m, alias list) }
            r : &RangeValue => { self.finalizeRange (alias context, r, alias list) }
            s : &SliceCtorValue => { self.finalizeSliceCtor (alias context, s, alias list) }
            t : &TypeInfoValue => { self.finalizeTypeInfo (alias context, t, alias list) }
            n : &ErrOptionValue => { return self.finalizeErrOpt (alias context, n, alias list); }
            o : &OptionValue => { return self.finalizeOption (alias context, o, alias list); }
            d : &FptrCastDelegateValue => { return self.finalizeCastFptrToDel (alias context, d, alias list) }
            _ => {
                eprintln (ctor::typeinfo.name);
                __pragma!panic ();
            }
        }

        if (!asLeft && context.isInAFrame ()) {
            return self.registerUniqValue (alias context, ctor, val, alias list);
        } else return val;
    }

    /**
     * Finalize a cast value
     * @params:
     *    - context: the context of the finalization
     *    - ct: the cast value to finalize
     *    - list: the list of statement to fill
     * */
    fn finalizeCast (self, dmut context : &Expander, ct : &CastValue, dmut list : &StmtList)-> &YILValue {
        let value = self.finalize (alias context, ct.getValue (), alias list);
        let type = context:.finalizeType (ct.getType ());
        match (type, value) {
            (ti : &YILInt, vi : &YILIntValue) => {
                return YILIntValue::new (ct.getLoc (), ti, vi.getValue ());
            } 
        }

        if type == value.getType () {
            return value;
        }

        YILCastValue::new (ct.getLoc (), type, value)
    }

    /**
     * Finalize the construction of a delegate from a simple function pointer
     * */
    fn finalizeCastFptrToDel (self, dmut context : &Expander, ct : &FptrCastDelegateValue, dmut list : &StmtList)-> &YILValue {
        let fptr = self.finalize (alias context, ct.getFuncPtrValue (), alias list);
        let protoRef = YILAddrValue::new (ct.getLoc (), YIL_PTR_VOID, YILVar::new (ct.getLoc (), YRuntimeFuncs::CALL_FPTR_AS_DG, YIL_VOID, 0us));

        let type = context:.finalizeType (ct.getType ());
        YILTupleValue::new (ct.getLoc (), type, [fptr, protoRef], fieldNames-> [DelegateKeys::CLOSURE, DelegateKeys::FUNC])
    }

    /**
     * Finalize a delegate to a constructor prototype
     * */
    fn finalizeCtorDG (self, dmut context : &Expander, ctor : &CtorDelegateValue, dmut list : &StmtList)-> &YILValue {
        let name = context.getMangler ().mangle (ctor.getPrototype ());
        context:.registerTouchSym (name);

        let protoRef = YILAddrValue::new (ctor.getLoc (), YIL_PTR_VOID, YILVar::new (ctor.getLoc (), name, YIL_VOID, 0us));
        let instance = self.finalize (alias context, ctor.getInstance (), alias list);
        let type = context:.finalizeType (ctor.getType ());

        YILTupleValue::new (ctor.getLoc (), type, [instance, protoRef], fieldNames-> [DelegateKeys::CLOSURE, DelegateKeys::FUNC])
    }

    /**
     * Finalize a delegate referenced by a function name
     * */
    fn finalizeNameDG (self, dmut context : &Expander, name : &NameDelegateValue, dmut list : &StmtList)-> &YILValue {
        let pname = context.getMangler ().mangle (name.getPrototype ());
        context:.registerTouchSym (pname);

        let protoRef = YILAddrValue::new (name.getLoc (), YIL_PTR_VOID, YILVar::new (name.getLoc (), pname, YIL_VOID, 0us));
        let instance = self.finalize (alias context, name.getClosure (), alias list);
        let type = context:.finalizeType (name.getType ());

        YILTupleValue::new (name.getLoc (), type, [instance, protoRef], fieldNames-> [DelegateKeys::CLOSURE, DelegateKeys::FUNC])
    }

    /**
     * Finalize a fake method delegate value
     * */
    fn finalizeFakeMethodDG (self, dmut _ : &Expander, _ : &FakeMethodDelegateValue, dmut _ : &StmtList)-> &YILValue {
        YIL_UNIT_VALUE
    }

    /**
     * Finalize a function pointer referencing a function name (i.e. lambdas)
     * */
    fn finalizeNameFptr (self, dmut context : &Expander, name : &ProtoFuncPtrValue, dmut _ : &StmtList)-> &YILValue {
        let pname = context.getMangler ().mangle (name.getPrototype ());
        context:.registerTouchSym (pname);

        YILAddrValue::new (name.getLoc (), YIL_PTR_VOID, YILVar::new (name.getLoc (), pname, YIL_VOID, 0us))
    }

    /**
     * Finalize the creation of a method delegate value
     * */
    fn finalizeMethodDG (self, dmut context : &Expander, meth : &MethodDelegateValue, dmut list : &StmtList)-> &YILValue {
        let mut inst = self.finalize (alias context, meth.getClosure (), alias list);
        let type = YILTuple::new ([YIL_PTR_VOID, YIL_PTR_VOID], fieldNames-> [DelegateKeys::CLOSURE, DelegateKeys::FUNC]);

        match meth.getClosure ().getType () {
            c : &ClassPtrType => {
                if (c.isStruct ()) {
                    if (meth.isAddressed ()) { // if addressed we make a copy of the tuple
                        let retType = YILPointer::new (inst.getType ());
                        let instAddr = self.createAddress (alias context, inst, retType, alias list);
                        let innerSize = YILIntValue::new (meth.getLoc (), YIL_UINT_32, inst.getType ().getSize ());

                        let (resultVar, addrResult) = self.createVarForBlock (alias context, c.getLoc (), meth.getClosure ().getType (), retType, alias list, getAddr-> true);
                        list:.append (YILCall::new (c.getLoc (),
                                                    YILNameCallValue::new (c.getLoc (), YIL_VOID, YRuntimeFuncs::DUPLICATE_TUPLE, [addrResult, instAddr, innerSize])));

                        inst = resultVar;
                    } else { // otherwise we simply make a
                        let retType = YILPointer::new (inst.getType ());
                        inst = self.createAddress (alias context, inst, retType, alias list);
                    }
                }
            }
        }

        let fptr = if (meth.isDirect ()) {
            let name = context.getMangler ().mangle (meth.getPrototype ());
            context:.registerTouchSym (name);

            YILAddrValue::new (meth.getLoc (), YIL_PTR_VOID, YILVar::new (meth.getLoc (), name, YIL_VOID, 0us))
        } else {
            self.createVtableAccess (alias context, meth.getLoc (), inst, meth.getVtableIndex (), alias list)
        }

        YILTupleValue::new (meth.getLoc (), type, [inst, fptr], fieldNames-> [DelegateKeys::CLOSURE, DelegateKeys::FUNC])
    }

    /**
     * Finalize the creation of a range value
     * */
    fn finalizeRange (self, dmut context : &Expander, rng : &RangeValue, dmut list : &StmtList)-> &YILValue {
        let left = self.finalize (alias context, rng.getLeft (), alias list);
        let right = self.finalize (alias context, rng.getRight (), alias list);
        let step = self.finalize (alias context, rng.getStep (), alias list);
        let contain = self.finalize (alias context, rng.getIsFull (), alias list);
        let type = context:.finalizeType (rng.getType ());

        YILTupleValue::new (rng.getLoc (), type, [left, right, step, contain], fieldNames-> [RangeKeys::FST, RangeKeys::SCD, RangeKeys::STEP, RangeKeys::CONTAINS])
    }

    /**
     * Finalize a slice ctor value
     * */
    fn finalizeSliceCtor (self, dmut context : &Expander, ctor : &SliceCtorValue, dmut list : &StmtList)-> &YILValue {
        let type = context:.finalizeType (ctor.getType ());
        let len = self.finalize (alias context, ctor.getLen (), alias list);
        let ptr = self.finalize (alias context, ctor.getPointer (), alias list);
        let blk = self.finalize (alias context, ctor.getBlk (), alias list);

        YILTupleValue::new (ctor.getLoc (), type, [len, ptr, blk], fieldNames-> [SliceKeys::LEN, SliceKeys::PTR, SliceKeys::BLK_INFO])
    }

    /**
     * Finalize the creation of a typeinfo
     * */
    fn finalizeTypeInfo (self, dmut context : &Expander, ti : &TypeInfoValue, dmut list : &StmtList)-> &YILValue {
        let id = self.finalize (alias context, ti.getId (), alias list);
        let size = self.finalize (alias context, ti.getSize (), alias list);
        let inner = self.finalize (alias context, ti.getInners (), alias list);
        let name = self.finalize (alias context, ti.getName (), alias list);

        let type = context:.finalizeType (ti.getType ());
        let fields = type.asOf!{&YILTuple} ().getFieldNames ();

        YILTupleValue::new (ti.getLoc (), type, [id, size, inner, name], fieldNames-> fields)
    }

    /**
     * Finalize a none value literal
     * */
    fn finalizeErrOpt (self, dmut context : &Expander, n : &ErrOptionValue, dmut list : &StmtList)-> &YILValue {
        let type = context:.finalizeType (n.getType ());
        let contentType = type.asOf!{&YILTuple} ().getInners ()[1]; // {.ok, .content}
        let errValue = self.finalize (alias context, n.getValue (), alias list);

        let content = YILTupleValue::new (n.getLoc (), contentType, fieldNames-> [OptionKeys::ERROR], [errValue]);

        YILTupleValue::new (n.getLoc (), type, fieldNames-> [OptionKeys::SET, OptionKeys::CONTENT],
                            [YILIntValue::new (n.getLoc (), YIL_UINT_8, 0us), content]) // {.set = 0, .content = {.error = null}}
    }

    /**
     * Finalize an option value construction
     * */
    fn finalizeOption (self, dmut context : &Expander, o : &OptionValue, dmut list : &StmtList)-> &YILValue {
        let innerType = o.getType ().asOf!{&OptionType} ().getInners ()[0];
        let innerValue = if (innerType.isOf!{&VoidType} () || innerType.isOf!{&NoneType} ()) {
            self.finalize (alias context, o.getValue (), alias list);
            YILIntValue::new (o.getLoc (), YIL_UINT_8, 0us)
        } else {
            self.finalize (alias context, o.getValue (), alias list)
        };

        let type = context:.finalizeType (o.getType ());
        let contentType = type.asOf!{&YILTuple} ().getInners ()[1]; // {.ok, .content}

        let content = YILTupleValue::new (o.getLoc (), contentType, fieldNames-> [OptionKeys::VALUE], [innerValue]);

        YILTupleValue::new (o.getLoc (), type, fieldNames-> [OptionKeys::SET, OptionKeys::CONTENT],
                            [YILIntValue::new (o.getLoc (), YIL_UINT_8, 1us), content]) // {.set = 1, .content = {.value = value}}
    }

}
