in stmtlist;


use ymirc::lint::node::instr;
use ymirc::lint::node::instr::vardecl;

use ymirc::utils::format;



/**
 * A statement list is a vector of instruction
 * It sorts the instruction per types, putting the var declaration at the beginning
 * And obviously conserving the order of al the other instructions
 * */
pub record StmtList {

    let mut _list : [mut &YILInstr] = [];

    let mut _decls : [mut &YILInstr] = [];

    let mut _toDestroy : [usize => mut (&YILInstr, bool)] = [];

    /**
     * Empty statement list
     * */
    pub self () {}

    /**
     * Append all the instruction in a list of statement to the current list
     * */
    pub fn append (mut self, v : StmtList) {
        for il in v[] {
            self:.append (il);
        }
    }

    /**
     * Append an instruction to the list of statement
     * */
    pub fn append (mut self, v : &YILInstr) {
        match v {
            var : &YILVarDecl => {
                self._decls ~= [var];
            }
            _ => {
                self._list ~= [v];
            }
        }
    }

    /**
     * Append a list of instruction to the list
     * */
    pub fn append (mut self, v : [&YILInstr]) {
        for il in v {
            self:.append (il);
        }
    }

    /**
     * Insert a variable containing an entity value that needs to be destroyed at the end of the block
     * */
    pub fn registerDestroy (mut self, id : usize, v : &YILInstr, implicit : bool) {
        self._toDestroy [id] = (v, implicit);
    }

    /**
     * Remove a var that was inserted to be destroyed
     * */
    pub fn removeDestroyNotImplicit (mut self, id : usize) {
        if let Ok ((_, _ = true)) = self._toDestroy [id]  {
            self._toDestroy:.remove (id);
        }
    }

    /**
     * @returns: true if the list is already destroying the value uid
     * */
    pub fn hasToDestroy (self, uid : usize)-> bool {
        uid in self._toDestroy
    }

    /**
     * @returns: true if values need to be destroyed
     * */
    pub fn hasDestroy (self)-> bool {
        self._toDestroy.len != 0us
    }

    /**
     * @returns: the list of instruction to destroy variable
     * */
    pub fn getToDestroy (self)-> [&YILInstr] {
        return copy [v.0 for _, v in self._toDestroy];
    }

    /**
     * @returns: the list of ids of the variables to destroy
     * */
    pub fn getToDestroyVars (self)-> [usize] {
        return copy [i for i, _ in self._toDestroy];
    }

    /**
     * @returns: all the instruction in the list (placing vardecls on top)
     * */
    pub fn opIndex (self)-> [&YILInstr] {
        self._decls ~ self._list
    }

    /**
     * @returns: the length of the statement list
     * */
    @field
    pub fn len (self)-> usize {
        self._list.len  + self._decls.len
    }

}
