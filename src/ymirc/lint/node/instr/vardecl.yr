mod ymirc::lint::node::instr::vardecl;

import ymirc::lint::node::base;
import ymirc::lint::node::type::base;
import ymirc::lint::node::instr::base;

import ymirc::lexing::word;
import ymirc::utils::format;
import std::conv, std::io;

/**
 * A Ymir Intermediate Language var declaration
 * */
pub class @final YILVarDecl over YILInstr {

    // The name of the vardecl
    let _name : [c8];

    // The uniq id used to refer to the variable
    let _varId : usize;

    // The type of the variable
    let _type : &YILType;

    let _isTemp : bool;

    pub self (loc : &Word, name : [c8], type : &YILType, varId : usize, isTemp : bool = false)
        with super (loc)
        , _name = name
        , _varId = varId
        , _type = type
        , _isTemp = isTemp
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the name of the variable
     * */
    pub fn getName (self)-> [c8] {
        self._name
    }

    /**
     * @returns: the type of the variable
     * */
    pub fn getType (self)-> &YILType {
        self._type
    }

    /**
     * @returns: the var id
     * */
    pub fn getVarId (self)-> usize {
        self._varId
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Compare two nodes
     * @returns: true, iif the two nodes are strictly identical
     */
    pub over opEquals (self, o : &YILNode)-> bool {
        match o {
            v : &YILVarDecl => {
                self._varId == v._varId
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            if (self._isTemp && self._varId != 0us) {
                let id = stream:.getYilId (self._varId);
                stream:.write ("let YI_"s8, id);
                stream:.write (" : ", self._type);
            } else {
                stream:.write ("let "s8, self._name);
                if (stream.withVarIds ()) {
                    stream:.write ("(#"s8, self._varId.to!{[c8], 36us} (), ')'c8);
                }
                stream:.write (" : ", self._type);
            }
        }
    }

}
