in trycatch;

use ymirc::lint::node::{instr::vardecl, type};

use ymirc::lexing::word;
use ymirc::utils::{format, packet};
use std::stream;


/**
 * Ymir Intermediate Language representation of a try catch
 * */
@final
pub class YILTryCatch over YILInstr {

    // The type of the catcher var
    let _ctType : &YILType;

    // List of instruction to execute inside the try
    let _tryP : [&YILInstr];

    // List of instruction to execute when exiting
    let _catchP : [&YILInstr];

    pub self (loc : &Word, ctType : &YILType, tryP : [&YILInstr], catchP : [&YILInstr])
        with super (loc)
        , _ctType = ctType
        , _tryP = tryP
        , _catchP = catchP
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    pub fn getTryPart (self)-> [&YILInstr] {
        self._tryP
    }

    pub fn getCatchPart (self)-> [&YILInstr] {
        self._catchP
    }

    pub fn getCatcherType (self)-> &YILType {
        self._ctType
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Compare two nodes
     * @returns: true, iif the two nodes are strictly identical
     */
    pub over opEquals (self, o : &YILNode)-> bool {
        match o {
            v : &YILTryCatch => {
                if self._tryP != v._tryP return false;
                if self._catchP != v._catchP return false;
                if self._ctType != v._ctType return false;
                true
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Packable {
        pub over packName (self, ref dmut out : [u8]) {
            self.super.packName (ref out);
        }
    }

    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            stream:.write ("try {\n");
            stream:.entabing ();
            for i in self._tryP match i {
                YILVarDecl () => {
                    if stream.tags.withVars {
                        stream:.write (i, ";\n");
                    }
                }
                _ => {
                    stream:.write (i, ";\n");
                }
            }

            stream:.detabing ();
            stream:.write ("}\ncatch{\n");
            stream:.entabing ();
            for i in self._catchP match i {
                YILVarDecl () => {
                    if stream.tags.withVars {
                        stream:.write (i, ";\n");
                    }
                }
                _ => {
                    stream:.write (i, ";\n");
                }
            }
            stream:.detabing ();
            stream:.write ("}");
        }
    }


}
