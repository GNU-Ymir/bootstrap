mod ymirc::lint::node::instr::tryfin;

import ymirc::lint::node::base;
import ymirc::lint::node::instr::base;
import ymirc::lint::node::instr::vardecl;

import ymirc::lexing::word;
import ymirc::utils::format;
import std::conv, std::io;


/**
 * Ymir Intermediate Language representation of a try finally
 * */
pub class @final YILTryFinally over YILInstr {

    /// List of instruction to execute inside the try
    let _tryP : [&YILInstr];


    /// List of instruction to execute when exiting
    let _finP : [&YILInstr];


    pub self (loc : &Word, tryP : [&YILInstr], finP : [&YILInstr])
        with super (loc)
        , _tryP = tryP
        , _finP = finP
    {}


    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    pub fn getTryPart (self)-> [&YILInstr] {
        self._tryP
    }

    pub fn getFinallyPart (self)-> [&YILInstr] {
        self._finP
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Compare two nodes
     * @returns: true, iif the two nodes are strictly identical
     */
    pub over opEquals (self, o : &YILNode)-> bool {
        match o {
            v : &YILTryFinally => {
                if self._tryP != v._tryP return false;
                if self._finP != v._finP return false;
                true
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            stream:.write ("try {\n"s8);
            stream:.entabing ();
            for i in self._tryP match i {
                YILVarDecl () => {
                    if stream.hasCustomFormatTag ("withVars"s8) {
                        stream:.write (i, ";\n");
                    }
                }
                _ => {
                    stream:.write (i, ";\n");
                }
            }

            stream:.detabing ();
            stream:.write ("}\nfinally{\n"s8);
            stream:.entabing ();
            for i in self._finP match i {
                YILVarDecl () => {
                    if stream.hasCustomFormatTag ("withVars"s8) {
                        stream:.write (i, ";\n");
                    }
                }
                _ => {
                    stream:.write (i, ";\n");
                }
            }

            stream:.detabing ();
            stream:.write ("}"s8);
        }
    }


}
