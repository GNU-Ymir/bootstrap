in condjmp;

use ymirc::lint::node::{instr::label, value};

use ymirc::lexing::word;
use ymirc::utils::{format, packet};
use std::stream;

/**
 * A Ymir Intermediate Language Conditional Jump
 * */
@final
pub class YILCondJmp over YILInstr {

    // The condition value to test
    let _cond : &YILValue;

    // The label to goto if condition is met
    let _then : &YILLabel;

    // The label to goto if condition is not met
    let _else : &YILLabel;

    pub self (loc : &Word, cond : &YILValue, then : &YILLabel, else_ : &YILLabel)
        with super (loc)
        , _cond = cond
        , _then = then
        , _else = else_
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */


    pub fn getCond (self)-> &YILValue {
        self._cond
    }

    pub fn getThenLabel (self)-> &YILLabel {
        self._then
    }

    pub fn getElseLabel (self)-> &YILLabel {
        self._else
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Compare two nodes
     * @returns: true, iif the two nodes are strictly identical
     */
    pub over opEquals (self, o : &YILNode)-> bool {
        match o {
            v : &YILCondJmp => {
                if self._cond != v._cond { return false; }
                if self._then != v._then { return false; }
                if self._else != v._else { return false; }
                true
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Packable {
        pub over packName (self, ref dmut out : [u8]) {
            self.super.packName (ref out);
        }
    }

    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            stream:.detabing ();
            stream:.write ("#IF ", self._cond, "\n");
            stream:.write ("#THEN_GOTO ", self._then.getName (), "(#", std::conv::to!{[c8], 36} (self._then.getLabelId ()), ")\n");
            stream:.write ("#ELSE_GOTO ", self._else.getName (), "(#", std::conv::to!{[c8], 36} (self._else.getLabelId ()), ')');
            stream:.entabing ();
        }
    }

}
