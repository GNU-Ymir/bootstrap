mod ymirc::lint::node::const_;

import ymirc::lint::node::base;
import ymirc::lint::node::value::base;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;

/**
 * A constant global value in Ymir Intermediate Language
 * */
pub class @final YILConstant over YILNode {

    // The name of the constant
    let _name : [c8];

    // The value of the constant
    let _value : &YILValue;

    // True iif the constant is a weak symbol
    let _isWeak : bool;

    // True if the symbol is a local symbol
    let _isLocal : bool;

    pub self (loc : &Word, name : [c8], value : &YILValue, isWeak : bool = false, isLocal : bool = false)
        with super (loc)
        , _name = name
        , _value = value
        , _isWeak = isWeak
        , _isLocal = isLocal
    {}

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the name of the constant
     * */
    pub fn getName (self)-> [c8] {
        self._name
    }

    /**
     * @returns: the value of the constant
     * */
    pub fn getValue (self)-> &YILValue {
        self._value
    }

    /**
     * @returns: true iif the constant is weak
     * */
    pub fn isWeak (self)-> bool {
        self._isWeak
    }

    /**
     * @returns: true if this is a local constant (without complex name, and stuff)
     * */
    pub fn isLocal (self)-> bool {
        self._isLocal
    }


    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Compare two nodes
     * @returns: true, iif the two nodes are strictly identical
     */
    pub over opEquals (self, o : &YILNode)-> bool {
        match o {
            c : &YILConstant => {
                if self._name != c._name { return false; }
                if self._value != c._value { return false; }
                if self._isWeak != c._isWeak { return false; }

                return true;
            }
        }

        false
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;

    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            stream:.write ("const : "s8);
            if (self._isLocal || self._isWeak) {
                stream:.write ("[");
                if (self._isWeak) {
                    stream:.write ("weak"s8);
                    if (self._isLocal) stream:.write (","s8);
                }
                if (self._isLocal) { stream:.write ("local"s8); }
                stream:.write ("]"s8);
            }

            stream:.write (' 'c8, self._name, ' 'c8);
            self._value.format (alias stream);
        }
    }



}
