in float;


use ymirc::lexing::word;
use ymirc::utils::format;
use std::{io, conv, stream};

use ymirc::global::state;

pub lazy YIL_FLOAT_32  = copy YILFloat (32u32);
pub lazy YIL_FLOAT_64  = copy YILFloat (64u32);
pub lazy YIL_FLOAT_80  = copy YILFloat (80u32);
pub lazy YIL_FLOAT_MAX = copy YILFloat (0u32);

/**
 * A Ymir Floatermediate Language float reference
 * */
@final
pub class YILFloat over YILType {

    // The size of the float in bit
    let _size : u32;

    pub self (size : u32)
        with _size = size
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    pub over getSize (self)-> u32 {
        if (self._size == 0u32) {
            state::instance ().getSizeTypeSize () / 8u32
        } else {
            self._size / 8u32
        }
    }

    pub over getAlign (self)-> u32 {
        if self._size == 0u32 {
            state::instance ().getSizeTypeSize () / 8u32
        } else {
            self._size / 8u32
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Compare two nodes
     * @returns: true, iif the two nodes are strictly identical
     */
    pub over opEquals (self, o : &YILNode)-> bool {
        match o {
            f : &YILFloat => {
                self._size == f._size
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Hashable;
    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            stream:.write ("f", self.getSize ());
        }
    }

}
