mod ymirc::lint::node::type::tuple;

import ymirc::lint::node::base;
import ymirc::lint::node::type::base;

import ymirc::lexing::word;
import ymirc::utils::format;
import std::conv, std::io;

/**
 * A Ymir Intermediate Language pointer reference
 * */
pub class @final YILTuple over YILType {

    // The type inside the pointer
    let _inners : [&YILType];

    // The name of the fiels (@warning: .len <= _inners.len)
    let _fieldNames : [[c8]];

    // True if the type is an union type
    let _isUnion : bool;

    // True if the type is packed
    let _isPacked : bool;

    pub self (inners : [&YILType], fieldNames : [[c8]] = [], isUnion : bool = false, isPacked : bool = false)
        with _inners = inners
        , _fieldNames = fieldNames
        , _isUnion = isUnion
        , _isPacked = isPacked
    {}

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the inner types of the tuple
     * */
    pub fn getInners (self)-> [&YILType] {
        self._inners
    }

    /**
     * @returns: the name of the fields
     * @warning: .len <= self.getInners ().len
     * */
    pub fn getFieldNames (self)-> [[c8]] {
        self._fieldNames
    }

    /**
     * @returns: the size in bytes of the type
     * */
    pub over getSize (self)-> u32 {
        0u32
    }

    pub fn isUnion (self)-> bool {
        self._isUnion
    }

    pub fn isPacked (self)-> bool {
        self._isPacked
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Compare two nodes
     * @returns: true, iif the two nodes are strictly identical
     */
    pub over opEquals (self, o : &YILNode)-> bool {
        match o {
            v : &YILTuple => {
                if self._fieldNames != v._fieldNames { return false; }
                if self._inners != v._inners { return false; }
                true
            }
            _ => { false }
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            stream:.write ('('c8);
            if (self._isUnion) stream:.write ("@union");
            else if (self._isPacked) stream:.write ("@packed");

            for i, ti in self._inners {
                if (i != 0us || self._isUnion || self._isPacked) stream:.write (", "s8);
                if (self._fieldNames.len > i) {
                    stream:.write (self._fieldNames [i], "-> "s8);
                }
                stream:.write (ti);
            }
            stream:.write (')'c8);
        }
    }

}
