in pointer;

use ymirc::lint::node::type::void_;

use ymirc::lexing::word;
use ymirc::utils::format;
use std::{io, conv, stream};

use ymirc::global::state;

pub lazy YIL_PTR_VOID = copy YILPointer (YIL_VOID, uid-> 1us);
pub lazy YIL_PTR_2_VOID = copy YILPointer (YIL_PTR_VOID, uid-> 2us);

/**
 * A Ymir Intermediate Language pointer reference
 * */
@final
pub class YILPointer over YILType {

    // The type inside the pointer
    let _inner : &YILType;

    pub self (inner : &YILType, uid : usize = 0us)
        with super (uid-> uid)
        , _inner = inner
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the name of the pointeriable
     * */
    pub fn getInner (self)-> &YILType {
        self._inner
    }

    /**
     * @returns: the size in bytes of the type
     * */
    pub over getSize (self)-> u32 {
        state::instance ().getSizeTypeSize () / 8u32
    }

    pub over getAlign (self)-> u32 {
        state::instance ().getSizeTypeSize () / 8u32
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Compare two nodes
     * @returns: true, iif the two nodes are strictly identical
     */
    pub over opEquals (self, o : &YILNode)-> bool {
        match o {
            v : &YILPointer => {
                self._inner == v._inner
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Hashable {
        pub over toHash (self)-> u64 {
            core::types::hash::__hash (self._inner)
        }
    }

    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            if stream.tags.withParams {
                stream:.write ("pointer P_", self._uid);
                // Don't print parameters of inner type
                stream:.write (" *(", format ("%", self._inner), ')');
            } else {
                stream:.write ("P_", self._uid);
            }
        }
    }

}
