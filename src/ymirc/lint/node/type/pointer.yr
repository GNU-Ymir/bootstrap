in pointer;

use ymirc::lint::node::{base, type::{base, void_}};

use ymirc::lexing::word;
use ymirc::utils::format;
use std::{io, conv};

use ymirc::global::state;

pub lazy YIL_PTR_VOID   = copy YILPointer (copy YILVoid ());
pub lazy YIL_PTR_2_VOID = copy YILPointer (copy YILPointer (copy YILVoid ()));

/**
 * A Ymir Intermediate Language pointer reference
 * */
@final
pub class YILPointer over YILType {

    // The type inside the pointer
    let _inner : &YILType;

    pub self (inner : &YILType)
        with _inner = inner
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the name of the pointeriable
     * */
    pub fn getInner (self)-> &YILType {
        self._inner
    }

    /**
     * @returns: the size in bytes of the type
     * */
    pub over getSize (self)-> u32 {
        state::instance ().getSizeTypeSize () / 8u32
    }

    pub over getAlign (self)-> u32 {
        state::instance ().getSizeTypeSize () / 8u32
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Compare two nodes
     * @returns: true, iif the two nodes are strictly identical
     */
    pub over opEquals (self, o : &YILNode)-> bool {
        match o {
            v : &YILPointer => {
                self._inner == v._inner
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            stream:.write ("*(", self._inner, ')');
        }
    }

}
