mod ymirc::lint::node::value::string;

import ymirc::lint::node::base;
import ymirc::lint::node::value::base;
import ymirc::lint::node::type::base;
import ymirc::lint::node::type::array;
import ymirc::lint::node::type::int;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::conv, std::io;


/**
 * A Ymir Intermediate Language unit reference
 * */
pub class @final YILStringValue over YILValue {

    // The content of the literal
    let _value : [u8];

    pub self (loc : &Word, type : &YILType, value : [u8])
        with super (loc, type)
        , _value = value
    {}

    /**
     * ================================================================================
     * ================================================================================
     * ==========================          GETTERS          ===========================
     * ================================================================================
     * ================================================================================
     */

    pub fn getValue (self)-> [u8] {
        self._value
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Compare two nodes
     * @returns: true, iif the two nodes are strictly identical
     */
    pub over opEquals (self, o : &YILNode)-> bool {
        match o {
            i : &YILStringValue => {
                if self._type != i._type { return false };
                self._value == i._value
            }
            _ => { false }
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            let FST = 5us, END = 4us;
            let z = self._type.asOf!{&YILArray} ().getInner ().asOf!{&YILInt} ();
            if (z.getSize () == 1u32) {
                stream:.write ('\"');
                for i, vi in self._value {
                    if (stream.hasCustomFormatTag ("short"s8) && self._value.len > FST + END + 5us) {
                        if (i == FST) {
                            stream:.write ("[...]");
                        } else if (i >= self._value.len - END || i < FST && i != self._value.len - 1us) {
                            stream:.write (cast!c8 (vi));
                        }
                    } else {
                        if (i != self._value.len - 1us) {
                            stream:.write (cast!c8 (vi));
                        }
                    }
                }
                stream:.write ('\"');
            } else {
                stream:.write ('[');
                for i, vi in self._value {
                    if (stream.hasCustomFormatTag ("short"s8) && self._value.len > FST + END + 3us) {
                        if (i == FST) {
                            stream:.write ("..., ");
                        } else if (i >= self._value.len - END || i < FST) {
                            stream:.write (vi);
                            if (i != self._value.len - 1us) {
                                if (i != 0us) stream:.write (", "s8);
                            }
                        }
                    } else {
                        stream:.write (vi);
                    }
                }
                stream:.write (']'c8);
            }
        }
    }
}
