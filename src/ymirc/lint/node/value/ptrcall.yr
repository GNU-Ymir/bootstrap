in ptrcall;

use ymirc::lint::node::type;

use ymirc::lexing::word;
use ymirc::utils::{format, packet};
use std::{io, conv, stream};

/**
 * A Ymir Intermediate Language pointer reference
 * */
@final
pub class YILPtrCallValue over YILValue {

    let _params : [&YILValue];

    // The ptr of the function being called
    let _funcPtr : &YILValue;

    pub self (loc : &Word, type : &YILType, funcPtr : &YILValue, params : [&YILValue])
        with super (loc, type)
        , _params = params
        , _funcPtr = funcPtr
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    pub fn getParameters (self)-> [&YILValue] {
        self._params
    }

    pub fn getFuncPtr (self)-> &YILValue {
        self._funcPtr
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Compare two nodes
     * @returns: true, iif the two nodes are strictly identical
     */
    pub over opEquals (self, o : &YILNode)-> bool {
        match o {
            v : &YILPtrCallValue => {
                if self._funcPtr != v._funcPtr { return false; }
                if self._params != v._params { return false; }
                true
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Packable {
           pub over packName (self, ref dmut out : [u8]) {
               self.super.packName (ref out);
           }
    }

    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            stream:.write ("(*", self._funcPtr, ")(");
            for i, ti in self._params {
                if (i != 0us) stream:.write (", ");
                stream:.write (ti);
            }
            stream:.write (')');
        }
    }

}
