mod ymirc::lint::node::value::binary;

import ymirc::lint::node::base;
import ymirc::lint::node::value::base;
import ymirc::lint::node::type::base;

import ymirc::lexing::word;
import ymirc::syntax::keys;

import ymirc::utils::format;
import std::conv, std::io;

/**
 * A Ymir Intermediate Language pointer reference
 * */
pub class @final YILBinaryValue over YILValue {

    let _left : &YILValue;

    let _right : &YILValue;

    // The name of the function being called
    let _op : BinaryOperators;

    pub self (loc : &Word, type : &YILType, op : BinaryOperators, left : &YILValue, right : &YILValue)
        with super (loc, type)
        , _op = op
        , _left = left
        , _right = right
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    pub fn getLeft (self)-> &YILValue {
        self._left
    }

    pub fn getRight (self)-> &YILValue {
        self._right
    }

    pub fn getOperator (self)-> BinaryOperators {
        self._op
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Compare two nodes
     * @returns: true, iif the two nodes are strictly identical
     */
    pub over opEquals (self, o : &YILNode)-> bool {
        match o {
            v : &YILBinaryValue => {
                if self._op != v._op { return false; }
                if self._left != v._left { return false; }
                if self._right != v._right { return false; }
                true
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._left, ' 'c8, self._op, ' 'c8, self._right);
        }
    }

}
