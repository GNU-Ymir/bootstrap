mod ymirc::lint::node::frame;

import ymirc::lint::node::base;
import ymirc::lint::node::instr::base;
import ymirc::lint::node::instr::vardecl;
import ymirc::lint::node::type::base;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;

/**
 * A frame in Ymir Intermediate Language
 * */
pub class @final YILFrame over YILNode {

    // The name of the frame (mangled)
    let _name : [c8];

    // The types of the parameters
    let _params : [&YILVarDecl];

    // The return type of the frame
    let _retType : &YILType;

    // The body of the frame
    let _body : &YILInstr;

    // True if the generated symbol is weak
    let _isWeak : bool;

    pub self (loc : &Word, name : [c8], params : [&YILVarDecl], retType : &YILType, body : &YILInstr, isWeak : bool = false)
        with super (loc)
        , _name = name
        , _params = params
        , _retType = retType
        , _body = body
        , _isWeak = isWeak
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the name of the frame
     * */
    pub fn getName (self)-> [c8] {
        self._name
    }

    /**
     * @returns: the name of the parameters of the frame
     * */
    pub fn getParameters (self)-> [&YILVarDecl] {
        self._params
    }

    /**
     * @returns: the type of the function
     * */
    pub fn getReturnType (self)-> &YILType {
        self._retType
    }

    /**
     * @returns: the body of the function
     * */
    pub fn getBody (self)-> &YILInstr {
        self._body
    }

    /**
     * @returns: true iif the function is weak
     * */
    pub fn isWeak (self)-> bool {
        self._isWeak
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Compare two nodes
     * @returns: true, iif the two nodes are strictly identical
     */
    pub over opEquals (self, o : &YILNode)-> bool {
        match o {
            f : &YILFrame => {
                if self._name != f._name { return false; }
                if self._params != f._params { return false; }
                if self._retType != f._retType { return false; }
                if self._body != f._body { return false; }
                if self._isWeak != f._isWeak { return false; }

                return true;
            }
        }

        false
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;

    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            stream:.write ("frame : "s8);
            if (self._isWeak) { stream:.write (" [weak]"s8); }
            stream:.write (' 'c8, self._name, ' 'c8);
            stream:.write ('('c8);

            stream:.entabing ();
            for i, tp in self._params {
                if (i != 0us) stream:.write (", "s8);
                stream:.write (tp);
            }

            stream:.write (")-> "s8);
            self._retType.format (alias stream);

            stream:.write (' 'c8);
            stream:.detabing ();

            self._body.format (alias stream);
        }
    }




}
