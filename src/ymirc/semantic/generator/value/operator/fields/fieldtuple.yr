mod ymirc::semantic::generator::value::operator::fields::fieldtuple;

import ymirc::lexing::word;
import ymirc::syntax::keys;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::semantic::generator::value::operator::fields::base;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::value::base;


/**
 */
pub class TupleFieldAccessValue over FieldOperatorValue {

    /// The name of the field to access 
    let _field : usize;

    /**
     * @params: 
     *  - loc: the location of the access operation
     *  - type: the type of the access
     *  - str: the struct operand of the operation
     *  - name: the name of the field to access
     */
    pub self (loc : &Word, type : &Type, str : &Value, field : usize)
        with super (loc, type, str), _field = field
    {
        self._throwers = str.getThrowers ();
        self._isLvalue = true;
    }


    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the struct operand of the access operation
     */
    pub fn getTuple (self)-> &Value {
        self._elem
    }

    /**
     * @returns: the name of the field to access
     */
    pub fn getFieldIndex (self)-> usize {
        self._field
    }
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &TupleFieldAccessValue => {
                self._elem == p._elem &&
                    self._field == p._field
            }
            _ => {
                false
            }                
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._elem);
            stream:.write ('.'c8);
            stream:.write (self._field);
        }
    }
    
}
