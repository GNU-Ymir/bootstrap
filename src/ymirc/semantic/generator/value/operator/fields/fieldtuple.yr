in fieldtuple;

use ymirc::lexing::word;
use ymirc::syntax::keys;
use ymirc::utils::format;

use ymirc::semantic::generator::{type,
                                 value::memory::dereferencer};
use std::stream;

@final
pub class TupleFieldAccessValue over FieldOperatorValue {

    // The name of the field to access
    let _field : usize;

    /**
     * @params: 
     *  - loc: the location of the access operation
     *  - type: the type of the access
     *  - str: the struct operand of the operation
     *  - name: the name of the field to access
     */
    pub self (loc : &Word, type : &Type, str : &Value, field : usize, isReduced : bool = false)
        with super (loc, type, str, isReduced-> isReduced)
        , _field = field
    {
        self._throwers = str.getThrowers ();
        self._isLvalue = true;
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the struct operand of the access operation
     */
    pub fn getTuple (self)-> &Value {
        self._elem
    }

    /**
     * @returns: the name of the field to access
     */
    pub fn getFieldIndex (self)-> usize {
        self._field
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            p : &TupleFieldAccessValue => {
                if (self._field != p._field) return false;

                return true;
            }
            _ => {
                false
            }                
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create an exact copy of the value with a different uniqId
     * */
    pub over clone (self)-> &Value {
        copy TupleFieldAccessValue (self._loc, self._type,
                                    self._elem.clone (),
                                    self._field,
                                    isReduced-> self._isReduced)
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            match self._elem {
                d : &DereferencerValue => {
                    stream:.write (d.getValue (), "-> ", self._field);
                }
                _ => {
                    stream:.write (self._elem, ".", self._field);
                }
            }
        }
    }
    
}
