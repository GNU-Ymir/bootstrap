in fieldclosure;

use ymirc::lexing::word;
use ymirc::syntax::keys;
use ymirc::utils::format;
use std::stream;
use ymirc::semantic::generator::type;

@final
pub class ClosureFieldAccessValue over FieldOperatorValue {

    // The name of the field to access
    let _field : usize;

    // The name of the variable being enclosed
    let _name : [c8];

    // True iif the access returns a reference
    let _byRef : bool;

    /**
     * @params: 
     *  - loc: the location of the access operation
     *  - type: the type of the access
     *  - str: the closure operand of the operation
     *  - name: the name of the field to access
     */
    pub self (loc : &Word, type : &Type, str : &Value, field : usize, name : [c8], byRef : bool, isReduced : bool = false)
        with super (loc, type, str, isReduced-> isReduced)
        , _field = field
        , _name = name
        , _byRef = byRef
    {
        self._throwers = str.getThrowers ();
        self._isLvalue = true;
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the closure operand of the access operation
     */
    pub fn getClosure (self)-> &Value {
        self._elem
    }

    /**
     * @returns: the index of the field within the closure
     */
    pub fn getFieldIndex (self)-> usize {
        self._field
    }

    /**
     * @returns: the name of the field
     * */
    pub fn getName (self)-> [c8] {
        self._name
    }

    /**
     * @returns: true if the returned value is a reference
     * */
    pub fn byRef (self)-> bool {
        self._byRef
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            p : &ClosureFieldAccessValue => {
                if (self._field != p._field) return false;
                if (self._name != p._name) return false;

                return true;
            }
            _ => {
                false
            }                
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create an exact copy of the value with a different uniqId
     * */
    pub over clone (self)-> &Value {
        copy ClosureFieldAccessValue (self._loc, self._type,
                                      self._elem.clone (),
                                      self._field,
                                      self._name,
                                      byRef-> self._byRef,
                                      isReduced-> self._isReduced)
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            if self._byRef {
                stream:.write ("*(", self._name, ")");
            } else {
                stream:.write (self._name);
            }
        }
    }
    
}
