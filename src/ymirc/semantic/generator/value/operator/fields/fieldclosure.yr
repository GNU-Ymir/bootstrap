mod ymirc::semantic::generator::value::operator::fields::fieldclosure;

import ymirc::lexing::word;
import ymirc::syntax::keys;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::semantic::generator::value::operator::fields::base;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::value::base;


/**
 */
pub class ClosureFieldAccessValue over FieldOperatorValue {

    /// The name of the field to access 
    let _field : usize;

    /// The name of the variable being enclosed
    let _name : [c8];

    /**
     * @params: 
     *  - loc: the location of the access operation
     *  - type: the type of the access
     *  - str: the closure operand of the operation
     *  - name: the name of the field to access
     */
    pub self (loc : &Word, type : &Type, str : &Value, field : usize, name : [c8], isReduced : bool = false)
        with super (loc, type, str, isReduced-> isReduced)
        , _field = field
        , _name = name
    {
        self._throwers = str.getThrowers ();
        self._isLvalue = true;
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the closure operand of the access operation
     */
    pub fn getClosure (self)-> &Value {
        self._elem
    }

    /**
     * @returns: the index of the field within the closure
     */
    pub fn getFieldIndex (self)-> usize {
        self._field
    }

    /**
     * @returns: the name of the field
     * */
    pub fn getName (self)-> [c8] {
        self._name
    }

    /*!
     * ================================================================================
     * ================================================================================
     * ==========================           CLONE            ==========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self)-> &Value {
        ClosureFieldAccessValue::new (self._loc, self._type, self._elem.clone (), self._field, self._name, isReduced-> self._isReduced)
    }
    
    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &ClosureFieldAccessValue => {
                self._elem == p._elem
                    && self._field == p._field
                    && self._name == p._name
            }
            _ => {
                false
            }                
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._name);
        }
    }
    
}
