in fieldenum;

use ymirc::lexing::word;
use ymirc::syntax::keys;
use ymirc::utils::format;
use ymirc::semantic::generator::type;

@final
pub class EnumFieldAccessValue over FieldOperatorValue {

    // The name of the field to access
    let _content : &Value;

    /**
     * @params:
     *   - loc: the location of the access
     *   - type: the type of the value
     *   - en: the enumeration being accessed
     *   - value: the value created by the access
     */
    pub self (loc : &Word, en : &EnumType, value : &Value, isReduced : bool = false)
        with super (loc, en, UNIT_VALUE, isReduced-> isReduced)
        , _content = value
    {
        self._isLvalue = false;
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the name of the field to access
     */
    pub fn getContent (self)-> &Value {
        self._content
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self::super.opEquals (o)) return false;

        match o {
            p : &EnumFieldAccessValue => {
                if (self._content != p._content) return false;

                return true;
            }
            _ => {
                false
            }
        }
    }

    pub over isFullLiteral (self)-> bool {
        self._content.isFullLiteral ()
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create an exact copy of the value with a different uniqId
     * */
    pub over clone (self)-> &Value {
        copy EnumFieldAccessValue (self._loc,
                                   self._type.asOf!{&EnumType} (),
                                   self._content.clone (),
                                   isReduced-> self._isReduced)
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._type, '(', self._content, ')');
        }
    }

}
