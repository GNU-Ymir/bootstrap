mod ymirc::semantic::generator::value::operator::fields::fieldclass;

import ymirc::lexing::word;
import ymirc::syntax::keys;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::semantic::generator::value::operator::fields::base;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::value::base;


/**
 */
pub class ClassFieldAccessValue over FieldOperatorValue {
    
    /// The class operand of the access
    let _instance : &Value;

    /// The name of the field to access 
    let _name : [c8];

    /**
     * @params: 
     *  - loc: the location of the access operation
     *  - type: the type of the access
     *  - str: the class operand of the operation
     *  - name: the name of the field to access
     */
    pub self (loc : &Word, type : &Type, str : &Value, name : [c8])
        with super (loc, type), _instance = str, _name = name
    {
        self._throwers = str.getThrowers ();
        self._isLvalue = true;
    }


    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the class operand of the access operation
     */
    pub fn getClassInstance (self)-> &Value {
        self._instance
    }

    /**
     * @returns: the name of the field to access
     */
    pub fn getFieldName (self)-> [c8] {
        self._name
    }
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &ClassFieldAccessValue => {
                self._instance == p._instance &&
                    self._name == p._name
            }
            _ => {
                false
            }                
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._instance);
            stream:.write ('.'c8);
            stream:.write (self._name);
        }
    }
    
}
