in fieldclass;

use ymirc::lexing::word;
use ymirc::lexing::keys;
use ymirc::utils::format;
use std::stream;
use ymirc::semantic::generator::type;

@final
pub class ClassFieldAccessValue over FieldOperatorValue {

    // The name of the field to access
    let _name : [c8];

    /**
     * @params: 
     *  - loc: the location of the access operation
     *  - type: the type of the access
     *  - str: the class operand of the operation
     *  - name: the name of the field to access
     */
    pub self (loc : &Word, type : &Type, str : &Value, name : [c8], isReduced : bool = false)
        with super (loc, type, str, isReduced-> isReduced)
        , _name = name
    {
        self._throwers = str.getThrowers ();
        self._isLvalue = true;
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the class operand of the access operation
     */
    pub fn getClassInstance (self)-> &Value {
        self._elem
    }

    /**
     * @returns: the name of the field to access
     */
    pub fn getFieldName (self)-> [c8] {
        self._name
    }
    
    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            p : &ClassFieldAccessValue => {
                if (self._name != p._name) return false;

                return true;
            }
            _ => {
                false
            }                
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create an exact copy of the value with a different uniqId
     * */
    pub over clone (self)-> &Value {
        copy ClassFieldAccessValue (self._loc, self._type,
                                    self._elem.clone (),
                                    self._name,
                                    isReduced-> self._isReduced)
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._elem);
            stream:.write ('.');
            stream:.write (self._name);
        }
    }
    
}
