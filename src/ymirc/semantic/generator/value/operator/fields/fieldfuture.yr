in fieldfuture;

use ymirc::lexing::word;
use ymirc::syntax::keys;
use ymirc::utils::format;
use ymirc::semantic::generator::{type,
                                 value::memory::dereferencer};
use std::stream;

@final
pub class FutureFieldAccessValue over FieldOperatorValue {

    // The name of the field to access
    let _name : [c8];

    /**
     * @params:
     *  - loc: the location of the access operation
     *  - type: the type of the access
     *  - str: the option operand of the operation
     *  - name: the name of the field to access
     */
    pub self (loc : &Word, type : &Type, str : &Value, name : [c8], isReduced : bool = false)
        with super (loc, type, str, isReduced-> isReduced)
        , _name = name
    {
        self._throwers = str.getThrowers ();
        self._isLvalue = false;
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the option operand of the access operation
     */
    pub fn getFuture (self)-> &Value {
        self._elem
    }

    /**
     * @returns: the name of the field to access
     */
    pub fn getFieldName (self)-> [c8] {
        self._name
    }

    /**
     * Future value is not a full literal
     * */
    pub over isFullLiteral (self)-> bool {
        if (self._name == FutureKeys::VALUE) {
            false
        } else {
            self.__super__.isFullLiteral ()
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            p : &FutureFieldAccessValue => {
                if (self._name != p._name) return false;

                return true;
            }
            _ => {
                false
            }
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create an exact copy of the value with a different uniqId
     * */
    pub over clone (self)-> &Value {
        copy FutureFieldAccessValue (self._loc, self._type,
                                     self._elem.clone (),
                                     self._name,
                                     isReduced-> self._isReduced)
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            match self._elem {
                d : &DereferencerValue => {
                    stream:.write (d.getValue (), "-> ", self._name);
                }
                _ => {
                    stream:.write (self._elem, ".", self._name);
                }
            }
        }
    }

}
