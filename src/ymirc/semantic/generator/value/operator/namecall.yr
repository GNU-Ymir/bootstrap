mod ymirc::semantic::generator::value::operator::namecall;

import ymirc::lexing::word;
import ymirc::syntax::keys;
import ymirc::utils::format;

import std::io;
import std::fs::path;
import std::collection::vec;

import ymirc::semantic::generator::value::operator::base;
import ymirc::semantic::generator::type::native::compound::pointer;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::prototypes::proto;



/**
 * A call value is a call operator of a prototype
 */
pub class NameCallValue over OperatorValue {

    /// The parameters to pass to the prototype
    let _params : [&Value];

    /// The name of function to call
    let _name : &Path;
    
    /**
     * Create a new call value
     * @params: 
     *   - type: the type returned by the calling
     *   - proto: the prototype to call
     *   - params: the list of parameters
     */
    pub self (loc : &Word, type : &Type, name : &Path, params : [&Value])
        with super (loc, type), _params = params, _name = name
    {
        let dmut thrs = Vec!{&Type}::new ();
        for i in self._params {
            for j in i.getThrowers () {
                thrs:.push (j);
            }
        }
                
        self._throwers = thrs[];
        self._isLvalue = true;
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the parameters passed to the prototype
     */
    pub def getParameters (self)-> [&Value] {
        self._params
    }

    /**
     * @returns: the prototype to call
     */
    pub def getFunctionName (self)-> &Path {
        self._name
    }
                
    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            s : &NameCallValue => {
                if (s._params.len != self._params.len) return false;
                
                if (s._type != self._type)  return false;
                if (s._name != self._name)  return false;                
                for i in 0us .. self._params.len {
                    if (s._params [i] != self._params [i]) return false;
                }
                
                true
            }
            _ => { false }
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write ('(', self._name.toStr (sep-> "::"s8), ')', '(');
            stream:.entabing ();
            let mut i = 0us;
            for j in self._params {
                if (i != 0us) stream:.write (", "s8);
                j.format (alias stream);
                i += 1us;
            }
                        
            stream:.detabing ();
            stream:.write (')'c8);            
        }        
    }        
}

