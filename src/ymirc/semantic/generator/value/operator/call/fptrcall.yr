in fptrcall;

use ymirc::lexing::word;
use ymirc::syntax::keys;
use ymirc::utils::format;
use ymirc::semantic::generator::type;
use std::stream;

/**
 * A call value is a call operator of a prototype
 */
@final
pub class FuncPtrCallValue over CallOperatorValue {

    // The parameters to pass to the prototype
    let _params : [&Value];

    // The pointer of function to call
    let _fptr : &Value;
    
    /**
     * Create a new call value
     * @params: 
     *   - type: the type returned by the calling
     *   - proto: the prototype to call
     *   - params: the list of parameters
     */
    pub self (loc : &Word, type : &Type, fptr : &Value, params : [&Value], isReduced : bool = false)
        with super (loc, type, isReduced-> isReduced)
        , _params = params
        , _fptr = fptr
    {
        let mut thrs : [mut (&Word, &Type)] = [];
        for i in self._params {
            thrs ~= i.getThrowers ();
        }

        thrs ~= copy [(loc, j.1) for j in self._fptr.getThrowers ()];

        self._throwers = thrs;
        self._isLvalue = false;
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the parameters passed to the prototype
     */
    pub fn getParameters (self)-> [&Value] {
        self._params
    }

    /**
     * @returns: the prototype to call
     */
    pub fn getFuncPtr (self)-> &Value {
        self._fptr
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            s : &FuncPtrCallValue => {
                if (s._params.len != self._params.len) return false;
                if (s._fptr != self._fptr)  return false;

                for i in 0us .. self._params.len {
                    if (s._params [i] != self._params [i]) return false;
                }
                
                true
            }
            _ => { false }
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create an exact copy of the value with a different uniqId
     * */
    pub over clone (self)-> &Value {
        let params = copy [i.clone () for i in self._params];

        copy FuncPtrCallValue (self._loc, self._type,
                               self._fptr.clone (),
                               params,
                               isReduced-> self._isReduced)
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            if (stream.forParams ()) {
                stream:.write ("_");
            } else {
                stream:.write ('(', self._fptr, ')', '(');
                stream:.entabing ();
                let mut i = 0us;
                for j in self._params {
                    if (i != 0us) stream:.write (", ");
                    j.format (alias stream);
                    i += 1us;
                }

                stream:.detabing ();
                stream:.write (')');
            }
        }        
    }        
}

