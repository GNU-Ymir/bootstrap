in contain;

use ymirc::lexing::word;
use ymirc::syntax::keys;
use ymirc::utils::format;
use std::stream;
use ymirc::semantic::generator::type;

@final
pub class MapContainValue over MapOperatorValue {

    // The map value being contained
    let _mp : &Value;

    // The key used to contain
    let _key : &Value;

    // If true then it is a not contain operation
    let _not : bool;

    pub self (loc : &Word, type : &Type, mp : &Value, key : &Value, not : bool, isReduced : bool = false)
        with super (loc, type, isReduced-> isReduced)
        , _mp = mp
        , _key = key
        , _not = not
    {
        self._throwers = mp.getThrowers () ~ key.getThrowers ();
        self._isLvalue = false;
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          GETTERS          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * @returns: the map value being contained
     * */
    pub fn getMap (self)-> &Value {
        self._mp
    }

    /**
     * @returns: the key used in the contain
     * */
    pub fn getKey (self)-> &Value {
        self._key
    }

    /**
     * @returns: true iif it is a not contain
     * */
    pub fn isNot (self)-> bool {
        self._not
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          COMPARISON          ===================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            p : &MapContainValue => {
                if (self._mp != p._mp) return false;
                if (self._key != p._key) return false;
                if (self._not != p._not) return false;

                return true;
            }
            _ => {
                false
            }
        }
    }

    pub over isFullLiteral (self)-> bool {
        if (!self._mp.isFullLiteral ()) return false;
        if (!self._key.isFullLiteral ()) return false;

        true
    }


    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create an exact copy of the value with a different uniqId
     * */
    pub over clone (self)-> &Value {
        copy MapContainValue (self._loc, self._type,
                              self._mp.clone (),
                              self._key.clone (),
                              not-> self._not,
                              isReduced-> self._isReduced)
    }


    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ======================================          MISC          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._key);
            if (self._not) {
                stream:.write (' ', BinaryOperators::NOT_IN, ' ');
            } else {
                stream:.write (' ', BinaryOperators::IN, ' ');
            }
            stream:.write (self._mp);
        }
    }

}
