mod ymirc::semantic::generator::value::operator::access;

import ymirc::lexing::word;
import ymirc::syntax::keys;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::semantic::generator::value::operator::base;
import ymirc::semantic::generator::type::native::compound::pointer;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::proto;


/**
 */
pub class SliceAccessValue over OperatorValue {
    
    /// The left operand of the access
    let _left : &Value;

    /// The right operand of the access
    let _index : &Value;

    /**
     * @params: 
     *  - loc: the location of the access operation
     *  - type: the type of the access
     *  - left: the left operand of the operation
     *  - index: the index operand of the operation
     */
    pub self (loc : &Word, type : &Type, left : &Value, index : &Value)
        with super (loc, type), _left = left, _index = index
    {
        self._throwers = left.getThrowers () ~ index.getThrowers ();
    }


    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the left operand of the access operation
     */
    pub def getLeft (self)-> &Value {
        self._left
    }

    /**
     * @returns: the index operand of the access operation
     */
    pub def getIndex (self)-> &Value {
        self._index
    }
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &SliceAccessValue => {
                self._left == p._left &&
                    self._index == p._index
            }
            _ => {
                false
            }                
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._left);
            stream:.write (" ["s8);
            stream:.write (self._index);
            stream:.write (']'c8);
        }
    }
    
}
