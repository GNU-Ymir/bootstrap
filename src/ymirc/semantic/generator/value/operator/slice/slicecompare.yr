in slicecompare;

use ymirc::lexing::word;
use ymirc::syntax::keys;
use ymirc::utils::format;
use std::stream;
use ymirc::semantic::generator::type;

@final
pub class SliceCompareValue over SliceOrArrayOperatorValue {

    // The left operand of the compare
    let _left : &Value;

    // The right operand of the compare
    let _right : &Value;


    /**
     * @params: 
     *  - loc: the location of the comparison
     *  - type: the type of the compare
     *  - left: the left operand of the operation
     *  - right: the right operand of the operation
     */
    pub self (loc : &Word, type : &Type, left : &Value, right : &Value, isReduced : bool = false)
        with super (loc, type, isReduced-> isReduced)
        , _left = left
        , _right = right
    {
        self._throwers = left.getThrowers () ~ right.getThrowers ();
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the left operand of the comparison
     */
    pub fn getLeft (self)-> &Value {
        self._left
    }

    /**
     * @returns: the right operand of the comparison
     */
    pub fn getRight (self)-> &Value {
        self._right
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            p : &SliceCompareValue => {
                if (self._left != p._left) return false;
                if (self._right != p._right) return false;

                return true;
            }
            _ => {
                false
            }                
        }
    }

    pub over isFullLiteral (self)-> bool {
        if (!self._left.isFullLiteral ()) return false;
        if (!self._right.isFullLiteral ()) return false;

        return true;
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create an exact copy of the value with a different uniqId
     * */
    pub over clone (self)-> &Value {
        copy SliceCompareValue (self._loc,
                                self._type,
                                self._left.clone (),
                                self._right.clone (),
                                isReduced-> self._isReduced)
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._left);
            stream:.write (" == ");
            stream:.write (self._right);
        }
    }
    
}
