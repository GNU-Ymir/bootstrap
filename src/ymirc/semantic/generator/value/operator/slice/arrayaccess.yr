in arrayaccess;

use ymirc::lexing::word;
use ymirc::syntax::keys;
use ymirc::utils::format;
use std::stream;
use ymirc::semantic::generator::type;

@final
pub class ArrayAccessValue over SliceOrArrayOperatorValue {

    // The array operand of the access
    let _array : &Value;

    // The right operand of the access
    let _index : &Value;

    /**
     * @params:
     *  - loc: the location of the access operation
     *  - type: the type of the access
     *  - array: the array operand of the operation
     *  - index: the index operand of the operation
     */
    pub self (loc : &Word, type : &Type, array : &Value, index : &Value, isReduced : bool = false)
        with super (loc, type, isReduced-> isReduced)
        , _array = array
        , _index = index
    {
        self._throwers = array.getThrowers () ~ index.getThrowers ();
        self._isLvalue = true;
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the array operand of the access operation
     */
    pub fn getArray (self)-> &Value {
        self._array
    }

    /**
     * @returns: the index operand of the access operation
     */
    pub fn getIndex (self)-> &Value {
        self._index
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            p : &ArrayAccessValue => {
                if (self._array != p._array) return false;
                if (self._index != p._index) return false;

                return true;
            }
            _ => {
                false
            }
        }
    }

    pub over isFullLiteral (self)-> bool {
        self._array.isFullLiteral () && self._index.isFullLiteral ()
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create an exact copy of the value with a different uniqId
     * */
    pub over clone (self)-> &Value {
        copy ArrayAccessValue (self._loc,
                               self._type,
                               self._array.clone (),
                               self._index.clone (),
                               isReduced-> self._isReduced)
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._array);
            stream:.write (" [");
            stream:.write (self._index);
            stream:.write (']');
        }
    }

}
