in sliceaccess;

use ymirc::lexing::word;
use ymirc::syntax::keys;
use ymirc::utils::format;
use std::stream;
use ymirc::semantic::generator::type;

@final
pub class SliceAccessValue over SliceOrArrayOperatorValue {

    // The slice operand of the access
    let _slice : &Value;

    // The right operand of the access
    let _index : &Value;

    /**
     * @params:
     *  - loc: the location of the access operation
     *  - type: the type of the access
     *  - slice: the slice operand of the operation
     *  - index: the index operand of the operation
     */
    pub self (loc : &Word, type : &Type, slice : &Value, index : &Value, isReduced : bool = false)
        with super (loc, type, isReduced-> isReduced)
        , _slice = slice
        , _index = index
    {
        self._throwers = slice.getThrowers () ~ index.getThrowers ();
        self._isLvalue = self._slice._isLvalue;
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the slice operand of the access operation
     */
    pub fn getSlice (self)-> &Value {
        self._slice
    }

    /**
     * @returns: the index operand of the access operation
     */
    pub fn getIndex (self)-> &Value {
        self._index
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            p : &SliceAccessValue => {
                if (self._slice != p._slice) return false;
                if (self._index != p._index) return false;

                return true;
            }
            _ => {
                false
            }
        }
    }

    pub over isFullLiteral (self)-> bool {
        self._slice.isFullLiteral () && self._index.isFullLiteral ()
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create an exact copy of the value with a different uniqId
     * */
    pub over clone (self)-> &Value {
        copy SliceAccessValue (self._loc,
                               self._type,
                               self._slice.clone (),
                               self._index.clone (),
                               isReduced-> self._isReduced)
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._slice);
            stream:.write (" [");
            stream:.write (self._index);
            stream:.write (']');
        }
    }

}
