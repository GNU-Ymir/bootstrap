mod ymirc::semantic::generator::value::templateref;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;

import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::literal::none;
import ymirc::semantic::generator::type::native::none;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::symbol::template;


/**
 * A module reference is a variable refering to a module symbol
 */
pub class TemplateRefValue over Value {

    /// The location of the module
    let _declLoc : &Word;

    /// The symbol
    let _sym : &TemplateSymbol;
    
    
    /**
     * @params: 
     *   - loc: the location of the reference
     *   - sym: the module symbol
     */
    pub self (loc : &Word, sym : &TemplateSymbol)
        with super (loc, NONE_TYPE), _declLoc = sym.getLoc (), _sym = sym
    {}

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &TemplateRefValue => {
                self._sym == p._sym
            }
            _ => { false }
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the module symbol that is refered to
     */
    pub def getSymbol (self)-> &TemplateSymbol {
        self._sym
    }
    
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            let content = self._sym.getContent ();
            stream:.write (self._sym.getPath ().toStr (sep-> "::"s8), content.getParameters ());
        }        
    }
    
}



