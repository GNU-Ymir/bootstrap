mod ymirc::semantic::generator::value::return_;


import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::lexing::tokens;
import ymirc::syntax::keys;

import ymirc::semantic::generator::type::native::scalar::none;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::scope::base;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::type::native::scalar::void_;
import ymirc::semantic::generator::value::literal::unit;


/**
 * Return statement
 */
pub class ReturnValue over Value {

    /// The value returned by the return
    let _value : &Value;

    /**
     * @params: 
     *   - value: the value returned by the return
     */
    pub self (loc : &Word, value : &Value, isReduced : bool = false)
        with super (loc, VoidType::new (loc), isReduced-> isReduced)
        , _value = value
    {
        self._returnLoc = loc;
        self._throwers = self._value.getThrowers ();
    }    

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the value returned by the return
     */
    pub fn getValue (self)-> &Value {
        self._value
    }

    /*!
     * ================================================================================
     * ================================================================================
     * ==========================           CLONE            ==========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self)-> &Value {
        ReturnValue::new (self._loc, self._value.clone (), isReduced-> self._isReduced)
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            b : &ReturnValue => {
                self._value == b._value
            }
            _ => false
        }
    }

    pub over needUniqness (self)-> bool {
        true
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (Keys::RETURN, " ");
            match self._value {
                UnitValue () => { stream:.write ("{}"s8); }
                _ => {
                    self._value.format (alias stream);
                }
            }
        }        

    }   
    
    
}

