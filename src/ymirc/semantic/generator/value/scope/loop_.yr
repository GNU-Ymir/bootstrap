mod ymirc::semantic::generator::value::scope::loop_;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::lexing::tokens;
import ymirc::syntax::keys;

import ymirc::semantic::generator::type::native::scalar::none;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::scope::base;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::value::literal::unit;


/**
 */
pub class LoopValue over ScopeValue {

    /// The test of the condition
    let _test : &Value;

    /// The content if the test is respected
    let _content : &Value;

    /// True if it is a do while loop
    let _isDo : bool;

    /// True if it is a cte for loop (already unwinded)
    let _isCteFor : bool;
    
    /**
     * @params: 
     *   - loc: the location of the variable
     *   - type: the type of the loop
     *   - test: the test of the loop
     *   - content: the content of the loop if the test pass
     *   - isDo: true iif the loop is a do while
     */
    pub self (loc : &Word, type : &Type, test : &Value, content : &Value, isDo : bool, isCteFor : bool = false, isReduced : bool = false)
        with super (loc, type, isReduced-> isReduced)
        , _test = test
        , _content = content
        , _isDo = isDo
        , _isCteFor = isCteFor
    {
        self._throwers = self._test.getThrowers () ~ self._content.getThrowers ();
        if (!content._returnLoc.isEof ()) {
            self._returnLoc = content._returnLoc;
        }
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the test of the condition
     */    
    pub fn getTest (self)-> &Value {
        self._test
    }

    /**
     * @returns: the value to evaluate if the test is true
     */
    pub fn getValue (self)-> &Value {
        self._content
    }

    /**
     * @returns: true iif the loop is a do while
     */
    pub fn isDo (self)-> bool {
        self._isDo
    }

    /**
     * @returns: true iif the loop is a cte for
     */
    pub fn isCteFor (self)-> bool {
        self._isCteFor
    }
    
    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &LoopValue => {
                if (p._type != self._type) return false;
                if (self._test != p._test) return false;
                if (self._content != p._content) return false;
                
                true
            }
            _ => { false }
        }
    }

    pub over needUniqness (self)-> bool {
        true
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (stream.forParams ()) {
                stream:.write ("_");
            } else {
                if (self._isDo) {
                    stream:.write ("do "s8);
                    self._content.format (alias stream);
                    stream:.write (" while "s8, self._test, ';'c8);
                } else {
                    if (!self._isCteFor) {
                        match self._test {
                            UnitValue () => {
                                stream:.write ("loop "s8);
                            }
                            _ => {
                                stream:.write ("while "s8, self._test, ' 'c8);
                            }
                        }
                    } else { stream:.write ("cte for "s8); }
                    self._content.format (alias stream);
                }
            }
        }        
    }
    
}



