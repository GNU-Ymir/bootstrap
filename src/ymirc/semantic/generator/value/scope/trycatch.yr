mod ymirc::semantic::generator::value::scope::trycatch;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::lexing::tokens;
import ymirc::syntax::keys;

import ymirc::semantic::generator::type::native::scalar::none;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::scope::base;
import ymirc::semantic::generator::value::literal::unit;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;

pub class TryCatchValue over ScopeValue {

    // The content of the try part
    let _tryBl : &Value;

    // The content of the catcher part
    let _catchP : &Value;

    // true iif the try part can throw an exception
    let _canThrow : bool;

    // The declaration of the variable that will catchP the thrown error
    let _catchVar : &Value;

    pub self (loc : &Word, type : &Type, tryP : &Value, catchP : &Value, catchVar : &Value, isReduced : bool = false)
        with super (loc, type, isReduced-> isReduced)
        , _catchVar = catchVar
        , _tryBl = tryP
        , _catchP = catchP
        , _canThrow = (tryP.getThrowers ().len != 0us)
    {
        self._breakLoc = self._tryBl._breakLoc;
        self._returnLoc = self._tryBl._returnLoc;
        self._throwers = self._catchP.getThrowers ();

        // throwers of the try catch are from the catching part, every throwers from the try part are caught

        // catchP part cannot break, or return, nor can it throw exceptions
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the try part of the try catchP block
     * */
    pub fn getTryPart (self)-> &Value {
        self._tryBl
    }

    /**
     * @returns: the catchP part of the try catchP block
     * */
    pub fn getCatchPart (self)-> &Value {
        self._catchP
    }

    /**
     * @returns: the variable that is check in case of catchure/success scope guards
     * */
    pub fn getCatchVar (self)-> &Value {
        self._catchVar
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &TryCatchValue => {
                if (p._type != self._type) return false;
                if (p._tryBl != self._tryBl) return false;
                if (p._catchP != self._catchP) return false;
                if (p._catchVar != self._catchVar) return false;

                true
            }
            _ => { false }
        }
    }

    pub over needUniqness (self)-> bool {
        true
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (stream.forParams ()) {
                stream:.write ("_");
            } else {
                stream:.write ("\ntry ");
                self._tryBl.format (alias stream);
                stream:.write ("\ncatch ");
                self._catchVar.format (alias stream);
                stream:.write ("\n");
                self._catchP.format (alias stream);
            }
        }
    }


}
