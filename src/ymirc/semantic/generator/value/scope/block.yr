mod ymirc::semantic::generator::value::scope::block;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::lexing::tokens;
import ymirc::syntax::keys;

import ymirc::semantic::generator::type::native::none;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::scope::base;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;


/**
 * A block is a list of values
 */
pub class BlockValue over ScopeValue {

    /// The content of th block
    let _values : [&Value];

    /// True iif the block is not a new scope
    let _isSet : bool;
    
    /**
     * @params: 
     *    - loc: the location of the variable
     *    - type: the type of the variable
     *    - value: the default value of the variable
     *    - isMutable: true iif the variable is declared mutable
     *    - isSelf: true iif the variable is self
     */
    pub self (loc : &Word, type : &Type, values : [&Value], breakLoc : &Word = EOF_WORD, returnLoc : &Word = EOF_WORD, isSet : bool = false)
        with super (loc, type), _values = values, _isSet = isSet
    {
        self._breakLoc = breakLoc;
        self._returnLoc = returnLoc;
        
        let dmut th = Vec!{(&Word, &Type)}::new ();
        for i in values {
            for j in i.getThrowers () {
                th:.push (j);
            }
        }
        
        self._throwers = th[];
    }


    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the list of values contained in the block
     */
    pub def getValues (self)-> [&Value] {
        self._values
    }

    /**
     * @returns: true iif the block is just a set (does not enter a new scope)
     */
    pub def isSet (self)-> bool {
        self._isSet
    }
    
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &BlockValue => {
                if (p._type != self._type) return false;
                if (p._values.len != self._values.len) return false;
                for i in 0us .. self._values.len {
                    if (self._values [i] != p._values [i]) return false;
                }

                true
            }
            _ => { false }
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (!self._isSet) {
                stream:.write (Tokens::LACC, '\n'c8);
                stream:.entabing ();
            }
            for i in 0us .. self._values.len {
                if (i != 0us) stream:.write (Tokens::SEMI_COLON, '\n'c8);
                self._values [i].format (alias stream);
            }
            
            if (!self._isSet) {
                stream:.detabing ();
                stream:.write ('\n'c8, Tokens::RACC);
            }
        }        
    }
    
}



