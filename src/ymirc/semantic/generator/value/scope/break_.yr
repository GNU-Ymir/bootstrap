in break_;

use ymirc::lexing::{word, tokens};
use ymirc::utils::format;
use ymirc::syntax::keys;
use std::stream;
use ymirc::semantic::generator::type;
use ymirc::semantic::generator::value::literal::unit;

/**
 * Break statement
 */
@final
pub class BreakValue over Value {

    // The value returned by the break
    let _value : &Value;

    /**
     * @params: 
     *   - value: the value returned by the break
     */
    pub self (loc : &Word, value : &Value, isReduced : bool = false)
        with super (loc, VoidType::new (loc), isReduced-> isReduced)
        , _value = value
    {
        self._breakLoc = loc;
        self._throwers = self._value.getThrowers ();
    }    

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the value returned by the break
     */
    pub fn getValue (self)-> &Value {
        self._value
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            b : &BreakValue => {
                self._value == b._value
            }
            _ => false
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create an exact copy of the value with a different uniqId
     * */
    pub over clone (self)-> &Value {
        BreakValue::new (self._loc, self._value.clone (), isReduced-> self._isReduced)
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (Keys::BREAK, " ");
            match self._value {
                UnitValue () => { stream:.write ("{}"); }
                _ => {
                    stream:.entabing ();
                    self._value.format (alias stream);
                    stream:.detabing ();
                }
            }
        }        

    }   
    
    
}

