mod ymirc::semantic::generator::value::macro_::base;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;

import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::type::native::scalar::none;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::symbol::macro_::_;
import ymirc::semantic::symbol::class_;

import ymirc::syntax::declaration::function;
import ymirc::syntax::expression::vardecl;
import ymirc::syntax::expression::empty;

/**
 * A module reference is a variable refering to a module symbol
 */
pub class MacroRefValue over Value {

    /// The location of the module
    let _declLoc : &Word;

    /// The symbol
    let _sym : &MacroSymbol;


    /**
     * @params:
     *   - loc: the location of the reference
     *   - sym: the module symbol
     */
    pub self (loc : &Word, sym : &MacroSymbol)
        with super (loc, NONE_TYPE, isReduced-> true)
        , _declLoc = sym.getLoc ()
        , _sym = sym
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &MacroRefValue => {
                self._sym == p._sym
            }
            _ => { false }
        }
    }

    pub over needUniqness (self)-> bool {
        false
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the module symbol that is refered to
     */
    pub fn getSymbol (self)-> &MacroSymbol {
        self._sym
    }


    /*!
     * ================================================================================
     * ================================================================================
     * ==========================           CLONE            ==========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self)-> &Value {
        self
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._sym.getPath ().toStr ());
        }
    }
}
