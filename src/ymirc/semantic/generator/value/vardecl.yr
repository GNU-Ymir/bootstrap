mod ymirc::semantic::generator::value::vardecl;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io, std::conv;

import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::literal::none;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::type::native::void_;



/**
 * A var decl is a variable declared in a function
 */
pub class VarDeclValue over Value {

    // The type of the variable declared (self.getType () always return Void)
    let _varType : &Type;

    /// The value of the variable
    let _value : &Value;

    /// True if the variable is declared mutable
    let _isMutable : bool;
    
    /// True iif the variable is declare pure
    let _isPure : bool;

    /// True iif it can create cte var references
    let _isCte : bool;
    
    /**
     * @params: 
     *    - loc: the location of the variable
     *    - type: the type of the variable
     *    - value: the default value of the variable
     *    - isMutable: true iif the variable is declared mutable
     */
    pub self (loc : &Word, type : &Type, value : &Value, isMutable : bool, isPure : bool, isCte : bool = true)
        with super (loc, VoidType::new (loc)), _value = value, _isMutable = isMutable, _isPure = isPure, _varType = type, _isCte = isCte
    {
        __version DEBUG {
            if !(!isMutable || !isPure) {
                __pragma!panic ();
            }
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &VarDeclValue => {
                self._loc.isSame (p._loc)
            }
            _ => { false }
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the value of the var decl
     */
    pub fn getValue (self)-> &Value {
        self._value
    }

    /**
     * @returns: the type of the variable that was declared
     * */
    pub fn getVarType (self)-> &Type {
        self._varType
    }

    /**
     * @returns: true if the var is declared mutable
     */
    pub fn isMutable (self)-> bool {
        self._isMutable
    }

    /**
     * @returns: true if the var is declared pure
     */
    pub fn isPure (self)-> bool {
        self._isPure
    }

    /**
     * @returns: true iif the var can be used to create cte var references
     * */
    pub fn isCte (self)-> bool {
        self._isCte
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write ("let "s8);
            if (self._isMutable) stream:.write ("mut "s8);
            else if (self._isPure) stream:.write ("pure "s8);

            stream:.write (self._loc.str ());
            if (stream.withVarIds ()) {
                stream:.write ("(#"s8, self._uniqId.to!{[c8], 36us} (), ')'c8);
            }
            
            stream:.entabing ();
            stream:.write (" : "s8, self._varType);
            match self._value {
                NoneValue () => {}
                _ => {
                    stream:.write (" = "s8);
                    stream:.write (self._value);
                }
            }
            
            stream:.detabing ();
        }        
    }
    
}



