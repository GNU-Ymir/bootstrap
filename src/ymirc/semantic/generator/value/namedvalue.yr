in namedvalue;

use ymirc::lexing::{word, tokens};
use ymirc::utils::format;
use ymirc::syntax::keys;

use std::stream;

use ymirc::semantic::generator::type;

/**
 * A named value is a value with a named attached to it
 */
@final
pub class NamedValue over Value {

    // The name of the named value
    let _name : [c8];
    
    // The value of the variable
    let _value : &Value;
        
    /**
     * @params: 
     *    - loc: the location of the variable
     *    - type: the type of the variable
     *    - value: the default value of the variable
     *    - isMutable: true iif the variable is declared mutable
     *    - isSelf: true iif the variable is self
     */
    pub self (loc : &Word, name : [c8], value : &Value, isReduced : bool = false)
        with super (loc, value.getType (), isReduced-> isReduced)
        , _name = name
        , _value = value
    {
        self._returnLoc = self._value.getReturnerLoc ();
        self._breakLoc = self._value.getBreakerLoc ();
        self._throwers = self._value.getThrowers ();
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the content of the named value
     */
    pub fn getValue (self)-> &Value {
        self._value
    }

    /**
     * @returns: the name of the value
     */
    pub fn getName (self)-> [c8] {
        self._name
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            p : &NamedValue => {
                if (self._name != p._name) return false;
                if (self._value != p._value) return false;

                return true;
            }
            _ => { false }
        }
    }

    pub over isFullLiteral (self)-> bool {
        self._value.isFullLiteral ()
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create an exact copy of the value with a different uniqId
     * */
    pub over clone (self)-> &Value {
        copy NamedValue (self._loc, self._name, self._value.clone (), isReduced-> self._isReduced)
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._name, Tokens::ARROW, ' ');
            stream:.entabing ();
            self._value.format (alias stream);
            stream:.detabing ();
        }        
    }
    
}



