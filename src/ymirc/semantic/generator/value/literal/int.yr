in int;

use ymirc::lexing::word;
use ymirc::lexing::keys;
use ymirc::utils::{format, bigint};
use std::stream;

use ymirc::semantic::generator::type::native::scalar::int;

@final
pub class IntValue over LiteralValue {

    // The value
    let _value : &BigInt;
    
    /**
     * Create a new value
     */
    pub self (loc : &Word, type : &IntType, val : usize)
        with super (loc, type)
        , _value = copy BigInt (val)
    {}


    pub self (loc : &Word, type : &IntType, val : &BigInt)
        with super (loc, type)
        , _value = val
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the content of the value
     */
    pub fn getValue (self)-> &BigInt {
        self._value
    }
    
    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            s : &IntValue => { self._value == s._value }
            _ => { false }
        }
    }

    pub over isFullLiteral (self)-> bool {
        true
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub over clone (self)-> &Value {
        self
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if let f : &IntType = self._type {
                match (f.isSigned (), f.getSize ()) {
                    (true, 8u16) => { stream:.write (self._value, "i8"s8); }
                    (true, 16u16) => { stream:.write (self._value, "i16"s8); }
                    (true, 64u16) => { stream:.write (self._value, "i64"s8); }
                    (true, 0u16) => { stream:.write (self._value, "is"s8); }
                    (false, 8u16) => { stream:.write (self._value, "u8"s8); }
                    (false, 16u16) => { stream:.write (self._value, "u16"s8); }
                    (false, 32u16) => { stream:.write (self._value, "u32"s8); }
                    (false, 64u16) => { stream:.write (self._value, "u64"s8); }
                    (false, 0u16) => { stream:.write (self._value, "us"s8); }
                    _ => { stream:.write (self._value); }
                }
            } else panic;
        }
    }
}


/**
 * Create a IntValue from a usize
 */
pub fn makeIntValue (loc : &Word, value : usize, size : u16 = 0u16, signed : bool = false)-> &IntValue {
    copy IntValue (loc, copy IntType (loc, size-> size, signed-> signed), copy BigInt (value))
}

/**
 * Create a IntValue from a isize
 */
pub fn makeIntValue (loc : &Word, value : isize, size : u16 = 0u16, signed : bool = true)-> &IntValue {
    copy IntValue (loc, copy IntType (loc, size-> size, signed-> signed), copy BigInt (value))
}

/**
 * Create a IntValue from a usize
 */
pub fn makeIntValue (value : usize, size : u16 = 0u16, signed : bool = false)-> &IntValue {
    copy IntValue (EOF_WORD, copy IntType (EOF_WORD, size-> size, signed-> signed), copy BigInt (value))
}


/**
 * Create a IntValue from a isize
 */
pub fn makeIntValue (value : isize, size : u16 = 0u16, signed : bool = true)-> &IntValue {
    copy IntValue (EOF_WORD, copy IntType (EOF_WORD, size-> size, signed-> signed), copy BigInt (value))
}

/**
 * Create a IntValue from another one, forcing the type casting
 */
pub fn makeIntCastValue (loc : &Word, nType : &IntType, value : &IntValue)-> &IntValue {
    if let oldT : &IntType = value.getType () {
        let old = match (oldT.isSigned (), oldT.getSize ()) {
            (true, 8u16) => { cast!usize (value.getValue ().toT!{i8} ()) }
            (false, 8u16) => { cast!usize (value.getValue ().toT!{u8} ()) }
            (true, 16u16) => { cast!usize (value.getValue ().toT!{i16} ()) }
            (false, 16u16) => { cast!usize (value.getValue ().toT!{u16} ()) }
            (true, 32u16) => { cast!usize (value.getValue ().toT!{i32} ()) }
            (false, 32u16) => { cast!usize (value.getValue ().toT!{u32} ()) }
            (true, 64u16) => { cast!usize (value.getValue ().toT!{i64} ()) }
            (false, 64u16) => { cast!usize (value.getValue ().toT!{u64} ()) }
            (true, _) => { cast!usize (value.getValue ().toT!{isize} ()) }
            _ => { cast!usize (value.getValue ().toT!{usize} ()) }
        };

        if (nType.isSigned ()) {
            makeIntValue (loc, size-> nType.getSize (), signed-> true, cast!isize (old))
        } else {
            makeIntValue (loc, size-> nType.getSize (), signed-> false, old)
        }
    } else panic;
}
