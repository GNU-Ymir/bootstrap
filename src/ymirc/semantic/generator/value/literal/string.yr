mod ymirc::semantic::generator::value::literal::string;

import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::syntax::keys;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::literal::base;
import ymirc::semantic::generator::value::literal::int;
import ymirc::semantic::generator::value::literal::char;
import ymirc::semantic::generator::value::literal::array;

import ymirc::semantic::generator::type::native::compound::pointer;
import ymirc::semantic::generator::type::native::scalar::char;
import ymirc::semantic::generator::type::native::scalar::int;
import ymirc::semantic::generator::type::native::compound::array;
import ymirc::semantic::generator::type::native::compound::slice;

import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::validator::utf;


pub struct @union
| utf8 : [u8]
| utf16 : [u16]
| utf32 : [u32]
 -> StringValueContent;

/**
 */
pub class StringValue over LiteralValue {

    // The content of the string
    let _content : StringValueContent;

    pub self (loc : &Word, type : &SliceType, utf8 : [u8])
        with super (loc, type), _content = StringValueContent (utf8-> utf8)
    {}

    pub self (loc : &Word, type : &SliceType, utf16 : [u16])
        with super (loc, type), _content = StringValueContent (utf16-> utf16)
    {}

    pub self (loc : &Word, type : &SliceType, utf32 : [u32])
        with super (loc, type), _content = StringValueContent (utf32-> utf32)
    {}

    pub self (loc : &Word, type : &SliceType, value : StringValueContent)
        with super (loc, type), _content = value
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the content of the string, if the string is utf8 encoded
     */
    pub fn getValue (self)-> StringValueContent {
        self._content
    }


    /**
     * @returns: the size of the encoding used by the string value
     * */
    pub fn getEncodingSize (self)-> u16 {
        self._type.asOf!{&SliceType} ().getInners ()[0].asOf!{&CharType}().getSize ()
    }

    /**
     * @returns: the lenght of the string value
     * */
    pub fn getLen (self)-> usize {
        match self.getEncodingSize () {
            8u16 => { self._content.utf8.len }
            16u16 => { self._content.utf16.len }
            _ => { self._content.utf32.len }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            s : &StringValue => {                
                if (s._type != self._type || self.getEncodingSize () != s.getEncodingSize ())
                    return false;
                match self.getEncodingSize () {
                    8u16 => {
                        self._content.utf8 == s._content.utf8
                    }
                    16u16 => {
                        self._content.utf16 == s._content.utf16
                    }
                    _ => {
                        self._content.utf32 == s._content.utf32
                    }
                }

            }
            _ => { false }
        }
    }


    pub over isFullLiteral (self)-> bool {
        true
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            let utfV = utf::instance ();
            match self.getEncodingSize () {
                8u16 => {
                    stream:.write (Tokens::DQUOTE, utfV.unescape (utfV.decode (self._content.utf8), squote-> false), Tokens::DQUOTE, StringSuffixes::UTF8);
                }
                16u16 => {
                    stream:.write (Tokens::DQUOTE, utfV.unescape (utfV.decode (self._content.utf16), squote-> false), Tokens::DQUOTE, StringSuffixes::UTF16);
                }
                _ => {
                    stream:.write (Tokens::DQUOTE, utfV.unescape (utfV.decode (self._content.utf32), squote-> false), Tokens::DQUOTE, StringSuffixes::UTF32);
                }
            }
        }
    }        
}


/**
 * Construct a string value from and store it in a slice
 * */
pub fn makeStringSliceValue (loc : &Word, value : [c8]) -> &StringValue {
    let dmut res = [0u8 ; new value.len];
    for i in 0us .. value.len {
        res [i] = cast!u8 (value [i]);
    }

    {
        StringValue::new (loc, SliceType::new (loc, CharType::new (loc)), utf8-> res)
    } catch {
        _ => __pragma!panic ();
    }
}

/**
 * Convert a string value to another encoding size
 * @params:
 *   - loc: the location of the new value
 *   - value: the old value to recode
 *   - resultSize: the size of the new encoding
 * @returns: a slice value containing the coded string
 * */
pub fn convertStringSliceValue (loc : &Word, value : &StringValue, resultSize : u16 = 8u16)-> &StringValue {
    let decoded : [c8] = match value.getEncodingSize () {
        8u16 => { utf::instance ().decode (value.getValue ().utf8) }
        16u16 => { utf::instance ().decode (value.getValue ().utf16) }
        _ => { utf::instance ().decode (value.getValue ().utf32) }
    }

    {
        match resultSize {
            8u16 => {
                let res = utf::instance ().encodeUtf8 (decoded);
                let slc = SliceType::new (loc, CharType::new (loc, size-> resultSize), withSize-> makeIntValue (loc, res.len));
                StringValue::new (loc, slc, utf8-> res)
            }
            16u16 => {
                let res = utf::instance ().encodeUtf16 (decoded);
                let slc = SliceType::new (loc, CharType::new (loc, size-> resultSize), withSize-> makeIntValue (loc, res.len));
                StringValue::new (loc, slc, utf16-> res)
            }
            _ => {
                let res = utf::instance ().encodeUtf32 (decoded);
                let slc = SliceType::new (loc, CharType::new (loc, size-> resultSize), withSize-> makeIntValue (loc, res.len));
                StringValue::new (loc, slc, utf32-> res)
            }
        }
    } catch { // slice type ctor can throw if it contains movable types
        _ => __pragma!panic ();
    }
}


/**
 * Convert a string value to another encoding size and return an arrayvalue instead of a string
 * @params:
 *   - loc: the location of the new value
 *   - value: the old value to recode
 *   - resultSize: the size of the new encoding
 * @returns: a slice value containing the coded string
 * */
pub fn convertStringArrayValue (loc : &Word, value : &StringValue, resultSize : u16 = 8u16)-> &ArrayValue  {
    let decoded : [c8] = match value.getEncodingSize () {
        8u16 => { utf::instance ().decode (value.getValue ().utf8) }
        16u16 => { utf::instance ().decode (value.getValue ().utf16) }
        _ => { utf::instance ().decode (value.getValue ().utf32) }
    }

    let dmut res = Vec!{&Value}::new ();
    match resultSize {
        8u16 => {
            let encode = utf::instance ().encodeUtf8 (decoded);
            for i in 0us .. encode.len {
                res:.push (makeCharValue (loc, cast!{u32} (encode [i]), size-> 8u16));
            }
        }
        16u16 => {
            let encode = utf::instance ().encodeUtf16 (decoded);
            for i in 0us .. encode.len {
                res:.push (makeCharValue (loc, cast!{u32} (encode [i]), size-> 16u16));
            }
        }
        _ => {
            let encode = utf::instance ().encodeUtf32 (decoded);
            for i in 0us .. encode.len {
                res:.push (makeCharValue (loc, cast!{u32} (encode [i]), size-> 32u16));
            }
        }
    }

    {
        let type = ArrayType::new (loc, CharType::new (loc, size-> resultSize), res.len ());
        ArrayValue::new (loc, type, res[])
    } catch { // array type can throw if it contains movable types
        _ => __pragma!panic ();
    }
}
