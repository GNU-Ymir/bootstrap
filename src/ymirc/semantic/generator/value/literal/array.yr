mod ymirc::semantic::generator::value::literal::array;

import ymirc::lexing::word;
import ymirc::syntax::keys;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::semantic::generator::value::literal::base;
import ymirc::semantic::generator::type::native::compound::pointer;
import ymirc::semantic::generator::type::native::compound::array;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::value::base;

/**
 * Array value that encode literal arrays (or slice, depending on the type)
 */
pub class ArrayValue over LiteralValue {

    /// The inner values
    let _inners : [&Value];

    /**
     * Create a new value
     */
    pub self (loc : &Word, type : &ArrayType, inners : [&Value], isReduced : bool = false)
        with super (loc, type, isReduced-> isReduced)
        , _inners = inners
    {
        let dmut throwers = Vec!{(&Word, &Type)}::new ();
        for i in self._inners {
            for j in i.getThrowers () { throwers:.push (j); }
        }

        self._throwers = throwers [];
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the content of the array
     */
    pub fn getValues (self)-> [&Value] {
        self._inners
    }

    /**
     * @returns: the size of the array
     */
    pub fn getLen (self)-> usize {
        self._inners.len
    }

    pub over isFullLiteral (self)-> bool {
        for i in self._inners {
            if !i.isFullLiteral () return false;
        }

        true
    }

    /*!
     * ================================================================================
     * ================================================================================
     * ==========================           CLONE            ==========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self)-> &Value {
        let dmut vals = Vec!{&Value}::new ();
        for i in self._inners {
            vals:.push (i.clone ());
        }

        ArrayValue::new (self._loc, self._type.asOf!{&ArrayType} (), vals[], isReduced-> self._isReduced)
    }

    
    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            s : &ArrayValue => {
                if (s._type != self._type)  return false;
                if (s._inners.len != self._inners.len) return false;
                for i in 0us .. self._inners.len {
                    if (s._inners [i] != self._inners [i]) return false;
                }

                true
            }
            _ => { false }
        }
    }

    pub over needUniqness (self)-> bool {
        for i in self._inners {
            if i.needUniqness () { return true; }
        }
        false
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write ('['c8);
            stream:.entabing ();
            for j, i in self._inners {
                if (j != 0us) stream:.write (", "s8);
                i.format (alias stream);
            }
            stream:.detabing ();
            stream:.write (']'c8);
        }        
    }        
}
