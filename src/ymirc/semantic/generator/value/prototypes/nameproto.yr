in nameproto;

use ymirc::lexing::word;
use ymirc::utils::format;
use ymirc::syntax::keys;
use std::stream;

use ymirc::syntax::expression;
use ymirc::semantic::generator::value::prototypes::{protovar, proto};
use ymirc::semantic::generator::type;
use ymirc::semantic::generator::type::native::scalar::none_;
use ymirc::semantic::symbol::{function, path, empty};


/**
 * A name prototype value is a prototype value, to which is attach a finalized name
 */
pub class NamePrototypeValue over PrototypeValue {

    // The name of the fn
    let _name : &SymbolPath;

    // True iif the prototype comes from a lambda
    let _isLambda : bool;

    /**
     * @params:
     *   - loc: the location of the prototype
     *   - name: the mangled name of function
     *   - parameters: the list of parameters of the prototype
     *   - retType: the return type of the function
     */
    pub self (loc : &Word, name : &SymbolPath, parameters : [&ProtoVarValue], retType : &Type, isLambda : bool = false)
        with super (loc, EMPTY_SYMBOL, parameters, retType, [], [])
        , _name = name
        , _isLambda = isLambda
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            p : &NamePrototypeValue => {
                if (self._name != p._name) return false;

                return true;
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the rename of the construname
     */
    pub fn getName (self)-> &SymbolPath {
        self._name
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (!self._isLambda) {
                stream:.write (self._name.toStr (), " ");
            }

            stream:.write ("(");

            stream:.entabing ();
            for i, j in self._parameters {
                if (i != 0us) stream:.write (", ");
                j.format (alias stream);
            }
            stream:.write (")-> ");
            stream:.write (self._retType);
            stream:.detabing ();
        }

    }

}
