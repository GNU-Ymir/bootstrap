mod ymirc::semantic::generator::value::prototypes::nameproto;

import ymirc::syntax::keys;
import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;

import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::prototypes::protovar;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::type::temporary::proto;
import ymirc::semantic::symbol::empty;
import ymirc::semantic::symbol::path;
import ymirc::semantic::generator::value::prototypes::proto;


/**
 * A name prototype value is a prototype value, to which is attach a finalized name
 */
pub class NamePrototypeValue over PrototypeValue {

    /// The name of the fn
    let _name : &SymbolPath;

    /**
     * @params:
     *   - loc: the location of the prototype
     *   - name: the mangled name of function
     *   - parameters: the list of parameters of the prototype
     *   - retType: the return type of the function
     */
    pub self (loc : &Word, name : &SymbolPath, parameters : [&ProtoVarValue], retType : &Type)
        with super (loc, EMPTY_SYMBOL, parameters, retType, [], [])
        , _name = name
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &NamePrototypeValue => {
                self._name == p._name
            }
            _ => { false }
        }
    }

    /**
     * @returns: true if the prototype is similar to 'o', but not necessarily the with the same clRef, it is only prototype comparison
     */
    pub over isSimilar (self, o : &Value)-> bool {
        match o {
            p : &NamePrototypeValue => {
                if (p._parameters.len != self._parameters.len) return false;
                if (self._retType != p._retType) return false;
                if (self._name != p._name) return false;
                for i in 0us .. self._parameters.len {
                    if (self._parameters [i] != p._parameters[i]) return false;
                }

                true
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the rename of the construname
     */
    pub fn getName (self)-> &SymbolPath {
        self._name
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._name.toStr ());

            if (stream.hasCustomFormatTag ("withParams"s8)) {
                stream:.write (" ("s8);

                stream:.entabing ();
                for i, j in self._parameters {
                    if (i != 0us) stream:.write (", "s8);
                    j.format (alias stream);
                }
                stream:.write (")-> "s8);
                stream:.write (self._retType);
                stream:.detabing ();
            }
        }

    }

}
