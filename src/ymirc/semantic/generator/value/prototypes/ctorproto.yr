mod ymirc::semantic::generator::value::prototypes::ctorproto;

import ymirc::syntax::keys;
import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;

import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::prototypes::protovar;

import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::type::native::scalar::void_;
import ymirc::semantic::generator::type::temporary::classref;
import ymirc::semantic::symbol::_;
import ymirc::semantic::generator::value::prototypes::methodproto;
import ymirc::semantic::validator::template::rewrite;

/**
 * A method prototype value is a prototype value, to which is attach a class symbol
 * It may also have a trait symbol, when the prototype was defined by a trait implementation
 */
pub class CtorPrototypeValue over MethodPrototypeValue {

    /// The rename of the ctor fn
    let _rename : [c8] = ""s8;

    /**
     * @params: 
     *   - loc: the location of the prototype
     *   - sym: the function that defines the prototype
     *   - parameters: the list of parameters of the prototype
     *   - throwers: the list of types that are thrown by the prototype
     *   - clRef: the class defining the method
     */
    pub self (loc : &Word, sym : &Symbol, rename : [c8] = ""s8, parameters : [&ProtoVarValue], throwers : [(&Word, &Type)], attributes : [AttributeWord], clRef : &ClassRefType, fromTemplate : (&TemplateRewriter)? = ((&TemplateRewriter)?)::__err__)
        with super (loc, sym, parameters, VoidType::new (loc), throwers, clRef-> clRef, trRef-> EMPTY_SYMBOL, isEmpty-> false, isOver-> false, isMutable-> true, attributes-> attributes, fromTemplate-> fromTemplate)
        , _rename = rename
    {}    


    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self::super.opEquals (o)) return false;

        match o {
            p : &CtorPrototypeValue => {
                if (self._rename != p._rename) return false;

                return true;
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the rename of the constructor
     */
    pub fn getRename (self)-> [c8] {
        self._rename
    }
    
    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    
    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            let path = match self.getTemplateMapper () {
                Ok (x : _) => { self._sym.getPath ().withTemplates (x?) }
                _ => { self._sym.getPath () }
            };

            stream:.write (path);
            if (stream.hasCustomFormatTag ("withParams"s8)) {
                stream:.write (" ("s8);

                stream:.entabing ();
                for i, j in self._parameters {
                    if (i != 0us) stream:.write (", "s8);
                    j.format (alias stream);
                }
                stream:.write (")-> mut &("s8);
                self._clRef.format (alias stream);
                stream:.write (")");
                stream:.detabing ();
            }
        }
        
    }
    
}


