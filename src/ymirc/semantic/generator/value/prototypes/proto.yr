in proto;


use ymirc::lexing::word;
use ymirc::utils::format;
use ymirc::syntax::keys;
use std::stream;

use ymirc::syntax::expression;
use ymirc::semantic::generator::value::prototypes::protovar;
use ymirc::semantic::generator::type;
use ymirc::semantic::generator::type::native::scalar::none_;
use ymirc::semantic::symbol::{function, path, empty};
use ymirc::semantic::validator::template::{rewrite, mapper};

/**
 * A prototype is the prototype of function that can be called
 */
pub class PrototypeValue over Value {

    // The location of the module that implement the function
    let _implLoc : &Word;
    
    // The symbol of the function
    let _sym : &Symbol;    
    
    // the location of the unsafe keyword or EOF
    let _attributes : [AttributeWord];

    // The list of parameters of the prototype
    let _parameters : [&ProtoVarValue];

    // The return type of the function
    let _retType : &Type;

    // The template rewriter used to generate this prototype
    let _templateRewriter : (TemplateRewriter)? = none;

    /**
     * @params: 
     *   - loc: the location of the prototype
     *   - sym: the function that defines the prototype
     *   - parameters: the list of parameters of the prototype
     *   - retType: the return type of the prototype
     *   - throwers: the list of types that are thrown by the prototype
     *   - fromTemplate: the template rewriter used to create this prototype
     */
    pub self (loc : &Word, implLoc : &Word, sym : &Symbol, parameters : [&ProtoVarValue], retType : &Type, throwers : [(&Word, &Type)], attributes : [AttributeWord], fromTemplate : (TemplateRewriter)? = none)
        with super (loc, NONE_TYPE)
        , _implLoc = if (implLoc.isEof ()) { loc } else { implLoc }
        , _sym = sym
        , _parameters = parameters
        , _retType = retType
        , _templateRewriter = fromTemplate
        , _attributes = attributes
    {
        self._throwers = throwers;
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the parameters of the prototype
     */
    pub fn getParameters (self)-> [&ProtoVarValue] {
        self._parameters
    }

    /**
     * @returns: the return type of the prototype
     */
    pub fn getRetType (self)-> &Type {
        self._retType
    }

    /**
     * @returns: true iif the prototype refers to a cvariadic function    
     */
    pub fn isCVariadic (self)-> bool {
        match self._sym {
            f : &FunctionSymbol => {
                f.isCVariadic ()
            }
            _ => { false }
        }
    }

    /**
     * @returns: true iif the prototype refers to an unsafe function
     * */
    pub fn isUnsafe (self)-> bool {
        for i in self._attributes match i.attr {
            Attributes::UNSAFE => { return true; }
        }

        false
    }

    /**
     * @returns: the location of the unsafe keyword or EOF
     * */
    pub fn getUnsafeLoc (self)-> &Word {

        for i in self._attributes match i.attr {
            Attributes::UNSAFE => { return i.loc; }
        }

        EOF_WORD
    }

    /**
     * @returns: true iif the prototype refers to an inline function
     * */
    pub fn isInline (self)-> bool {
        for i in self._attributes match i.attr {
            Attributes::INLINE => { return true; }
        }

        false
    }

    /**
     * @returns: the location of the inline keyword or EOF
     * */
    pub fn getInlineLoc (self)-> &Word {

        for i in self._attributes match i.attr {
            Attributes::INLINE => { return i.loc; }
        }

        EOF_WORD
    }


    /**
     * @returns: the symbol refered by the prototype
     */
    pub fn getSymbol (self)-> &Symbol {
        self._sym
    }

    pub fn getTemplateMapper (self)-> TemplateMapper? {
        match self._templateRewriter {
            Ok (t : _) => {
                t.getTemplateMapper ()?
            }
            _ => {
                none
            }
        }
    }

    /*
     * @returns: the score of the template rewriter (if any, 0 otherwise)
     * */
    pub fn getTemplateScore (self)-> u32 {
        match self._templateRewriter {
            Ok (t : _) => {
                t.getScore ()
            }
            _ => { 0u32 }
        }
    }

    /**
     * @returns: the location of the module that implement the function
     * */
    pub fn getImplLoc (self)-> &Word {
        self._implLoc
    }
    
    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            p : &PrototypeValue => {
                if (self._sym != p._sym) return false;
                if (self._parameters.len != p._parameters.len) return false;
                if (self._attributes != p._attributes) return false;
                for i in 0us .. self._parameters.len {
                    if (self._parameters [i] != p._parameters [i]) return false;
                }
                if (self._retType != p._retType) return false;

                return true;
            }
            _ => { false }
        }
    }

    pub over isFullLiteral (self)-> bool {
        true
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create an exact copy of the value with a different uniqId
     * */
    pub over clone (self)-> &Value {
        self
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            let path = match self.getTemplateMapper () {
                Ok (x : _) => { self._sym.getPath ().withTemplates (x?) }
                _ => { self._sym.getPath () }
            };

            stream:.write (path);
            if (stream.tags.withParams) {
                stream:.entabing ();
                stream:.write (" (");
                for i, j in self._parameters {
                    if (i != 0us) stream:.write (", ");
                    j.format (alias stream);
                }
                stream:.write (")-> ");
                self._retType.format (alias stream);
                stream:.detabing ();
            }
        }        
    }
    
}



