mod ymirc::semantic::generator::value::throw_;


import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::lexing::tokens;
import ymirc::syntax::keys;

import ymirc::semantic::generator::type::native::none;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::scope::base;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::type::native::void_;
import ymirc::semantic::generator::value::literal::none;


/**
 * Throw statement
 */
pub class ThrowValue over Value {

    /// The value thrown
    let _value : &Value;

    // The typeinfo access
    let _typeinfo : &Value;
    
    /**
     * @params: 
     *   - value: the value returned by the throw
     */
    pub self (loc : &Word, value : &Value, typeinfo : &Value)
        with super (loc, VoidType::new (loc)), _value = value, _typeinfo = typeinfo
    {
        self._throwers = [(loc, value.getType ())];
    }    

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the value returned by the throw
     */
    pub def getValue (self)-> &Value {
        self._value
    }

    /**
     * @returns: the typeinfo access of the value being thrown
     */
    pub def getTypeInfo (self)-> &Value {
        self._typeinfo
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            b : &ThrowValue => {
                self._value == b._value &&
                    self._typeinfo == b._typeinfo
            }
            _ => false
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (Keys::THROW, " ");
            stream:.entabing ();
            self._value.format (alias stream);
            stream:.detabing ();                
        }        

    }   
    
    
}

