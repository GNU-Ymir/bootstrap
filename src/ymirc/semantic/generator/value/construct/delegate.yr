mod ymirc::semantic::generator::value::construct::delegate;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;
import std::fs::path;

import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::type::native::scalar::delegate;
import ymirc::semantic::generator::value::construct::base;
import ymirc::semantic::generator::value::prototypes::nameproto;

pub class NameDelegateValue over ConstructValue {

    // The name of the function being pointed
    let _name : &NamePrototypeValue;

    // The closure value
    let _closure : &Value;

    /**
     * @params:
     *    - name: the name of the function pointed
     *    - closure: the closure content
     *    - references: for each parameter, true iif the param is a reference
     *    - parameters: the list of parameters of the function pointer
     *    - retType: the return type of the function pointer
     */
    pub self (loc : &Word, name : &NamePrototypeValue, closure : &Value, references : [bool], parameters : [&Type], retType : &Type)
        with super (loc, DelegateType::new (loc, references, parameters, retType), isReduced-> true)
        , _name = name
        , _closure = closure
    {
        self._throwers = self._closure.getThrowers ();
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the name of the function being pointed
     */
    pub fn getPrototype (self)-> &NamePrototypeValue {
        self._name
    }

    /**
     * @returns: the closure value
     */
    pub fn getClosure (self)-> &Value {
        self._closure
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &NameDelegateValue => {
                p._name == self._name &&
                    p._type == self._type
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (stream.forParams ()) {
                stream:.write ("_");
            } else {
                stream:.write ('{'c8, self._closure, ';'c8, self._name, '}'c8);
            }
        }

    }


}
