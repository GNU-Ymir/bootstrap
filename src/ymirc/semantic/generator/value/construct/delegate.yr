mod ymirc::semantic::generator::value::construct::delegate;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;
import std::fs::path;

import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::type::native::delegate;
import ymirc::semantic::generator::value::construct::base;
import ymirc::semantic::generator::value::prototypes::ctorproto;

pub class DelegateValue over ConstructValue {

    // The name of the function being pointed
    let _name : &Path;

    // The closure value
    let _closure : &Value;
    
    /**
     * @params:
     *    - name: the name of the function pointed
     *    - closure: the closure content
     *    - references: for each parameter, true iif the param is a reference
     *    - parameters: the list of parameters of the function pointer
     *    - retType: the return type of the function pointer
     */
    pub self (loc : &Word, name : &Path, closure : &Value, references : [bool], parameters : [&Type], retType : &Type)
        with super (loc, DelegateType::new (loc, references, parameters, retType)), _name = name, _closure = closure
    {}   



    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the name of the function being pointed
     */
    pub def getName (self)-> &Path {
        self._name
    }    

    /**
     * @returns: the closure value
     */
    pub def getClosure (self)-> &Value {
        self._closure
    }
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */


    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &DelegateValue => {
                p._name.toStr () == self._name.toStr () &&
                    p._type == self._type
            }
            _ => { false }
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._name.toStr (sep-> "::"s8));            
        }        

    }
    

}
