mod ymirc::semantic::generator::value::construct::ctordg;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::semantic::generator::_;
import ymirc::semantic::generator::value::base;


/**
 * Constructor of class value, associate an instance of classptr to a ctorprototypevalue
 * CtorPrototypeValue are not callable without an instance, that is why we have this delegate that encapsulate the instance
 */
pub class CtorDelegateValue over ConstructValue {

    // The value of the instance (of type ClassPtrType, if NoneValue, then it is constructed by this delegate before the call, with a runtime call)
    let _instance : &Value;

    /// The prototype of the constructor to call
    let _proto : &CtorPrototypeValue;
   
    /**
     * @params: 
     *    - loc: the location of the construction
     *    - type: the type returned by the construction
     *    - instance: the instance that is constructed (if not specified create one)
     *    - proto: the ctor that is called
     */
    pub self (loc : &Word, type : &Type, instance : &Value = NoneValue::new (), proto : &CtorPrototypeValue)
        with super (loc, type), _instance = instance, _proto = proto
    {}



    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the instance that is used (can be NoneValue)
     */
    pub def getInstance (self)-> &Value {
        return self._instance
    }
    
    /**
     * @returns: the prototype that is called
     */
    pub def getPrototype (self)-> &CtorPrototypeValue {
        self._proto
    }


    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */


    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &CtorDelegateValue => {
                p._instance == self._instance &&
                    p._proto == self._proto
            }
            _ => { false }
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */



    impl Streamable;
    impl Formattable {


        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._proto);
            stream:.entabing ();
            match self._instance {
                NoneValue () => {}
                _ => {
                    stream:.write ('('c8);
                    stream:.write (self._instance);
                    stream:.write (')'c8);
                }                
            }

            stream:.detabing ();
        }        
    }
    
}
