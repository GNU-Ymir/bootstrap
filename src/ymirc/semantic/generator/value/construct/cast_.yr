in cast_;

use ymirc::lexing::word;
use ymirc::utils::format;
use std::stream;
use ymirc::semantic::generator::type;

/**
 * Constructor of a value from a casted value
 * A cast value is a simple transformation of type (at low level) without data conversion
 */
pub class CastValue over ConstructValue {

    // The value that is casted
    prot let _content : &Value;

    /**
     * @params:
     *    - loc: the location of the cast
     *    - type: the type of the value (the type of the destination)
     *    - content: the value that is casted
     */
    pub self (loc : &Word, type : &Type, content : &Value, isLvalue : bool = false, isReduced : bool = false)
        with super (loc, type, isReduced-> isReduced)
        , _content = content
    {
        self._throwers = content.getThrowers ();
        self._isLvalue = isLvalue;
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the value that is casted
     */
    pub fn getValue (self)-> &Value {
        self._content
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;
        match o {
            c : &CastValue => {
                if (c._content != self._content) return false;

                return true;
            }
            _ => { false }
        }
    }

    pub over isFullLiteral (self)-> bool {
        self._content.isFullLiteral ()
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub over clone (self)-> &Value {
        copy CastValue (self._loc, self._type,
                        self._content.clone (),
                        isLvalue-> self._isLvalue,
                        isReduced-> self._isReduced)
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            stream:.writef ("cast!{%}(%)", self._type, self._content);
        }
    }
}
