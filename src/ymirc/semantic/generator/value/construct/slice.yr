mod ymirc::semantic::generator::value::construct::slice;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::semantic::generator::_;
import ymirc::semantic::generator::type::base;

/**
 * Constructor of a slice value from ptr and len fields
 */
pub class SliceCtorValue over ConstructValue {

    /// The left value of the range
    let _ptr : &Value;

    /// The right value of the range
    let _len : &Value;

    // true iif the ctor is a implicit alias
    let _implicitAlias : bool;

    /**
     * @params:
     *   - loc: the location of the range value
     *   - type: the type of the range value
     *   - ptr: the pointer operand of the value
     *   - len: the len operand of the value
     */
    pub self (loc : &Word, type : &Type, ptr : &Value, len : &Value, isImplicitAlias : bool, isReduced : bool = false)
        with super (loc, type, isReduced-> isReduced)
        , _ptr = ptr
        , _len = len
        , _implicitAlias = isImplicitAlias
    {
        self._throwers = ptr.getThrowers () ~ len.getThrowers ();
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the pointer value of the slice constructor
     */
    pub fn getPointer (self)-> &Value {
        self._ptr
    }

    /**
     * @returns: the len value of the slice constructor
     */
    pub fn getLen (self)-> &Value {
        self._len
    }

    /**
     * @returns: true iif the slice ctor can be implicitly aliased
     * */
    pub fn isImplicitAlias (self)-> bool {
        self._implicitAlias
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &SliceCtorValue => {
                p._ptr == self._ptr &&
                    p._len == self._len
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {


        pub over format (self, dmut stream : &Formatter) {
            if (stream.forParams ()) {
                stream:.write ("_");
            } else {
                stream:.write ("{.len = ", self._len, ", .ptr = ", self._ptr, "}");
            }
        }
    }

}
