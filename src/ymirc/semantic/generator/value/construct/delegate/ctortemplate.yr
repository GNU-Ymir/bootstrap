in ctortemplate;

use ymirc::lexing::word;
use ymirc::utils::format;
use std::stream;

use ymirc::semantic::generator::type::native::scalar::none_;

use ymirc::semantic::generator::value::{literal::unit,
                                        template::_};

@final
pub class CtorDelegateTemplateValue over DelegateValue {

    // The name of the function being pointed
    let _proto : &TemplateRefCtorValue;

    // The closure value
    let _instance : &Value;

    // The type of the instance that will be generated once the ctor is finished
    let _instanceType : &Type;

    let _nextCtor : &Value;

    /**
     * @params:
     *    - name: the name of the function pointed
     *    - closure: the closure content
     *    - references: for each parameter, true iif the param is a reference
     *    - parameters: the list of parameters of the function pointer
     *    - retType: the return type of the function pointer
     */
    pub self (loc : &Word,
              type : &Type,
              meth : &TemplateRefCtorValue,
              instance : &Value = UNIT_VALUE,
              nextCtor : &Value = UNIT_VALUE)
        with super (loc, NONE_TYPE, isReduced-> true)
        , _proto = meth
        , _instance = instance
        , _instanceType = type
        , _nextCtor = nextCtor
    {
        self._throwers = self._instance.getThrowers () ~ self._nextCtor.getThrowers ();
    }



    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the prototype of the method being accessed
     */
    pub fn getPrototype (self)-> &TemplateRefCtorValue {
        self._proto
    }

    /**
     * @returns: the closure value
     */
    pub fn getInstance (self)-> &Value {
        self._instance
    }

    /**
     * @returns: the type of the instance that the ctor can generate once template specialization is done
     * */
    pub fn getInstanceType (self)-> &Type {
        self._instanceType
    }

    /**
     * @returns: the ctor to call to init the fields of child classes
     * */
    pub fn getNextCtor (self)-> &Value {
        self._nextCtor
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */


    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            p : &CtorDelegateTemplateValue => {
                if (self._instance != p._instance) return false;
                if (self._nextCtor != p._nextCtor) return false;

                if (p._proto != self._proto) return false;

                return true;
            }
            _ => { false }
        }
    }

    pub over isFullLiteral (self)-> bool {
        self._instance.isFullLiteral () && self._nextCtor of UnitValue
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub over clone (self)-> &Value {
        copy CtorDelegateTemplateValue (self._loc,
                                        self._instanceType,
                                        self._proto,
                                        instance-> self._instance.clone (),
                                        nextCtor-> self._nextCtor.clone ())
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            if (stream.tags.forParams) {
                stream:.write ("_");
            } else {
                stream:.write (self._proto);
                match self._instance {
                    UnitValue () => {}
                    _ => {
                        stream:.write ('(');
                        stream:.write (self._instance);
                        if self._nextCtor !of UnitValue {
                            stream:.write (", ");
                            stream:.write (self._nextCtor);
                        }
                        stream:.write (')');
                    }
                }
            }
        }
    }
}
