in name;

use ymirc::lexing::word;
use ymirc::utils::format;
use std::stream;

use ymirc::semantic::generator::type::native::scalar::delegate;
use ymirc::semantic::generator::value::prototypes::nameproto;
use ymirc::syntax::keys;

@final
pub class NameDelegateValue over DelegateValue {

    // The proto of the function being pointed
    let _proto : &NamePrototypeValue;

    // The closure value
    let _closure : &Value;

    /**
     * @params:
     *    - proto: the proto of the function pointed
     *    - closure: the closure content
     *    - references: for each parameter, true iif the param is a reference
     *    - parameters: the list of parameters of the function pointer
     *    - retType: the return type of the function pointer
     */
    pub self (loc : &Word,
              proto : &NamePrototypeValue,
              closure : &Value,
              references : [bool],
              lazys : [bool],
              parameters : [&Type],
              retType : &Type,
              fromFptr : bool = false)

        with super (loc, copy DelegateType (loc, references, lazys, parameters, retType, fromFptr-> fromFptr), isReduced-> true)
        , _proto = proto
        , _closure = closure
    {
        self._throwers = self._closure.getThrowers ();
    }

    self (loc : &Word, proto : &NamePrototypeValue, closure : &Value, type : &Type)
        with super (loc, type, isReduced-> true)
        , _proto = proto
        , _closure = closure
    {
        self._throwers = self._proto.getThrowers () ~ self._closure.getThrowers ();
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the proto of the function being pointed
     */
    pub fn getPrototype (self)-> &NamePrototypeValue {
        self._proto
    }

    /**
     * @returns: the closure value
     */
    pub fn getClosure (self)-> &Value {
        self._closure
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            p : &NameDelegateValue => {
                if (p._proto != self._proto) return false;
                if (p._closure != self._closure) return false;

                return true;
            }
            _ => { false }
        }
    }

    pub over isFullLiteral (self)-> bool {
        self._closure.isFullLiteral ()
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub over clone (self)-> &Value {
        copy NameDelegateValue (self._loc, self._proto, self._closure.clone (), self._type)
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (stream.tags.forParams) {
                stream:.write ("_");
            } else {
                if (stream.tags.withParams) {
                    stream:.write (self._type);
                } else {
                    stream:.write ('{', self._closure, ';', self._proto, '}');
                }
            }
        }

    }


}
