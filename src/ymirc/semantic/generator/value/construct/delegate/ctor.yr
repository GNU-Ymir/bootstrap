in ctor;

use ymirc::lexing::word;
use ymirc::utils::format;
use std::stream;


use ymirc::semantic::generator::{value,
                                 value::literal::unit,
                                 value::construct::delegate,
                                 value::prototypes::_};

use ymirc::semantic::generator::type::native::scalar::none_;

/**
 * Constructor of class value, associate an instance of classptr to a ctorprototypevalue
 * CtorPrototypeValue are not callable without an instance, that is why we have this delegate that encapsulate the instance
 */
@final
pub class CtorDelegateValue over DelegateValue {

    // The value of the instance (of type ClassPtrType, if UnitValue, then it is
    // constructed by this delegate before the call, with a runtime call)
    let _instance : &Value;

    // The prototype of the constructor to call
    let _proto : &CtorPrototypeValue;

    let _instanceType : &Type;

    let _nextCtor : &Value;

    /**
     * @params: 
     *    - loc: the location of the construction
     *    - type: the type returned by the construction
     *    - instance: the instance that is constructed (if not specified create one)
     *    - proto: the ctor that is called
     */
    pub self (loc : &Word,
              type : &Type,
              proto : &CtorPrototypeValue,
              instance : &Value = UNIT_VALUE,
              nextCtor : &Value = UNIT_VALUE,
              isReduced : bool = false)

        with super (loc, NONE_TYPE, isReduced-> isReduced)
        , _instance = instance
        , _proto = proto
        , _instanceType = type
        , _nextCtor = nextCtor
    {
        self._throwers = self._instance.getThrowers ();
        self._throwers ~= nextCtor.getThrowers ();
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the instance that is used (can be UnitValue)
     */
    pub fn getInstance (self)-> &Value {
        return self._instance
    }
    
    /**
     * @returns: the prototype that is called
     */
    pub fn getPrototype (self)-> &CtorPrototypeValue {
        self._proto
    }

    /**
     * @returns: the type of the instance created by calling the ctor
     * */
    pub fn getInstanceType (self)-> &Type {
        self._instanceType
    }

    /**
     * The ctor delegate to call after the initialization of fields
     * */
    pub fn getNextCtor (self)-> &Value {
        self._nextCtor
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            p : &CtorDelegateValue => {
                if (p._instance != self._instance) return false;
                if (p._nextCtor != self._nextCtor) return false;
                if (p._proto != self._proto) return false;

                return true;
            }
            _ => { false }
        }
    }

    pub over isFullLiteral (self)-> bool {
        self._instance.isFullLiteral () && self._nextCtor of UnitValue
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub over clone (self)-> &Value {
        copy CtorDelegateValue (self._loc,
                                self._type,
                                self._proto,
                                instance-> self._instance.clone (),
                                nextCtor-> self._nextCtor.clone (),
                                isReduced-> self._isReduced)
   }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            if (stream.tags.forParams) {
                stream:.write ("_");
            } else {
                stream:.write (self._proto);
                stream:.entabing ();
                match self._instance {
                    UnitValue () => {}
                    _ => {
                        stream:.write ('(');
                        stream:.write (self._instance);
                        if self._nextCtor !of UnitValue {
                            stream:.write (", ");
                            stream:.write (self._nextCtor);
                        }
                        stream:.write (')');
                    }
                }

                stream:.detabing ();
            }
        }        
    }
}
