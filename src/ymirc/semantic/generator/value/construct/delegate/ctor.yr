in ctor;

use ymirc::lexing::word;
use ymirc::utils::format;
use std::stream;


use ymirc::semantic::generator::{value,
                                 value::literal::unit,
                                 value::construct::delegate,
                                 value::prototypes::_};

use ymirc::semantic::generator::type::native::scalar::none_;

/**
 * Constructor of class value, associate an instance of classptr to a ctorprototypevalue
 * CtorPrototypeValue are not callable without an instance, that is why we have this delegate that encapsulate the instance
 */
@final
pub class CtorDelegateValue over DelegateValue {

    // The value of the instance (of type ClassPtrType, if UnitValue, then it is
    // constructed by this delegate before the call, with a runtime call)
    let _instance : &Value;

    // The prototype of the constructor to call
    let _proto : &CtorPrototypeValue;

    let _instanceType : &Type;

    /**
     * @params: 
     *    - loc: the location of the construction
     *    - type: the type returned by the construction
     *    - instance: the instance that is constructed (if not specified create one)
     *    - proto: the ctor that is called
     */
    pub self (loc : &Word, type : &Type, instance : &Value = UNIT_VALUE, proto : &CtorPrototypeValue, isReduced : bool = false)
        with super (loc, NONE_TYPE, isReduced-> isReduced)
        , _instance = instance
        , _proto = proto
        , _instanceType = type
    {
        self._throwers = self._instance.getThrowers ();
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the instance that is used (can be UnitValue)
     */
    pub fn getInstance (self)-> &Value {
        return self._instance
    }
    
    /**
     * @returns: the prototype that is called
     */
    pub fn getPrototype (self)-> &CtorPrototypeValue {
        self._proto
    }

    /**
     * @returns: the type of the instance created by calling the ctor
     * */
    pub fn getInstanceType (self)-> &Type {
        self._instanceType
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            p : &CtorDelegateValue => {
                if (p._instance != self._instance) return false;
                if (p._proto != self._proto) return false;

                return true;
            }
            _ => { false }
        }
    }

    pub over isFullLiteral (self)-> bool {
        self._instance.isFullLiteral ()
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub over clone (self)-> &Value {
        copy CtorDelegateValue (self._loc,
                                self._type,
                                instance-> self._instance.clone (),
                                self._proto,
                                isReduced-> self._isReduced)
   }

    /**
     * Clone the delegate to transform it into a call to only the pre part of the ctor
     * */
    pub fn cloneForPre (self)-> &CtorDelegateValue {
        copy CtorDelegateValue (self._loc,
                                self._type,
                                instance-> self._instance.clone (),
                                self._proto.cloneForPre (),
                                isReduced-> self._isReduced)
    }

    /**
     * Clone the delegate to transform it into a call to only the post part of the ctor
     * */
    pub fn cloneForPost (self)-> &CtorDelegateValue {
        copy CtorDelegateValue (self._loc,
                                self._type,
                                instance-> self._instance.clone (),
                                self._proto.cloneForPost (),
                                isReduced-> self._isReduced)
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            if (stream.tags.forParams) {
                stream:.write ("_");
            } else {
                stream:.write (self._proto);
                stream:.entabing ();
                match self._instance {
                    UnitValue () => {}
                    _ => {
                        stream:.write ('(');
                        stream:.write (self._instance);
                        stream:.write (')');
                    }
                }

                stream:.detabing ();
            }
        }        
    }
}
