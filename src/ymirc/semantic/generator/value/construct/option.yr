in option;

use ymirc::lexing::word;
use ymirc::utils::format;
use std::stream;
use ymirc::semantic::generator::type;

/**
 * Constructor of a value from a optioned value
 * A option value is a simple transformation of type (at low level) without data conversion
 */
@final
pub class OptionValue over ConstructValue {

    // The value that is optioned
    let _content : &Value;

    /**
     * @params:
     *    - loc: the location of the option
     *    - type: the type of the value (the type of the destination)
     *    - content: the value that is optioned
     */
    pub self (loc : &Word, type : &Type, content : &Value, isReduced : bool = false)
        with super (loc, type, isReduced-> isReduced)
        , _content = content
    {
        self._throwers = self._content.getThrowers ();
        self._isLvalue = false;
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the value that is optioned
     */
    pub fn getValue (self)-> &Value {
        self._content
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */


    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            c : &OptionValue => {
                if (c._content != self._content) return false;

                return true;
            }
            _ => { false }
        }
    }

    pub over isFullLiteral (self)-> bool {
        self._content.isFullLiteral ()
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub over clone (self)-> &Value {
        copy OptionValue (self._loc, self._type,
                          self._content.clone (),
                          isReduced-> self._isReduced)
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            stream:.writef ("(%)?", self._content);
        }
    }
}
