mod ymirc::semantic::generator::value::construct::option;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::semantic::generator::_;
import ymirc::semantic::generator::value::base;


/**
 * Constructor of a value from a optioned value
 * A option value is a simple transformation of type (at low level) without data conversion
 */
pub class OptionValue over ConstructValue {

    /// The value that is optioned
    let _content : &Value;

    /**
     * @params:
     *    - loc: the location of the option
     *    - type: the type of the value (the type of the destination)
     *    - content: the value that is optioned
     */
    pub self (loc : &Word, type : &Type, content : &Value, isReduced : bool = false)
        with super (loc, type, isReduced-> isReduced)
        , _content = content
    {
        self._throwers = self._content.getThrowers ();
        self._isLvalue = false;
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the value that is optioned
     */
    pub fn getValue (self)-> &Value {
        self._content
    }

    /*!
     * ================================================================================
     * ================================================================================
     * ==========================           CLONE            ==========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self)-> &Value {
        OptionValue::new (self._loc, self._type, self._content.clone (), isReduced-> self._isReduced)
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */


    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            c : &OptionValue => {
                c._content == self._content &&
                    self._type == c._type
            }
            _ => { false }
        }
    }

    pub over needUniqness (self)-> bool {
        self._content.needUniqness ()
    }

    pub over isFullLiteral (self)-> bool {
        self._content.isFullLiteral ()
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.writef ("(%)?", self._content);
        }
    }

}
