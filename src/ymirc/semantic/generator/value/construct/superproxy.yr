mod ymirc::semantic::generator::value::construct::superproxy;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::semantic::generator::_;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::construct::cast_;

/**
 * Constructor of a value from a SuperProxyed value
 * A SuperProxy value is a simple transformation of type (at low level) without data conversion
 */
pub class SuperProxyValue over CastValue {

    /**
     * @params:
     *    - loc: the location of the SuperProxy
     *    - type: the type of the value (the type of the destination)
     *    - content: the value that is SuperProxyed
     */
    pub self (loc : &Word, type : &Type, content : &Value)
        with super (loc, type, content, isLvalue-> false, isReduced-> true)
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * ==========================           CLONE            ==========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self)-> &Value {
        SuperProxyValue::new (self._loc, self._type, self._content.clone ())
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */


    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            c : &SuperProxyValue => {
                c._content == self._content &&
                    self._type == c._type
            }
            _ => { false }
        }
    }

    pub over needUniqness (self)-> bool {
        self._content.needUniqness ()
    }

    pub over isFullLiteral (self)-> bool {
        false
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.writef ("{%}.super", self._content);
        }
    }

}
