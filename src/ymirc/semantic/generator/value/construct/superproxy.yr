in superproxy;

use ymirc::lexing::word;
use ymirc::utils::format;
use std::stream;
use ymirc::semantic::generator::type;

use ymirc::lexing::keys;

/**
 * Constructor of a value from a SuperProxyed value
 * A SuperProxy value is a simple transformation of type (at low level) without data conversion
 */
@final
pub class SuperProxyValue over cast_::CastValue {

    let _isSelf : bool;

    /**
     * @params:
     *    - loc: the location of the SuperProxy
     *    - type: the type of the value (the type of the destination)
     *    - content: the value that is SuperProxyed
     */
    pub self (loc : &Word, type : &Type, content : &Value, isSelf : bool = false)
        with super (loc, type, content, isLvalue-> false, isReduced-> true)
        , _isSelf = isSelf
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */


    pub over opEquals (self, o : &Generator)-> bool {
        if (!self.super.opEquals (o)) return false;

        match o {
            SuperProxyValue () => {
                true
            }
            _ => { false }
        }
    }

    pub over isFullLiteral (self)-> bool {
        false
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub over clone (self)-> &Value {
        copy SuperProxyValue (self._loc, self._type,
                              self._content.clone ())
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if self._isSelf {
                stream:.write (Keys::SELF, " ", Keys::OF, " ", self.getType ());
            } else {
                stream:.write (Keys::SUPER, " ", Keys::OF, " ", self.getType ());
            }
        }
    }

}
