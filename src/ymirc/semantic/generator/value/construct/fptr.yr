mod ymirc::semantic::generator::value::construct::fptr;

import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;
import ymirc::syntax::keys

import std::io;
import std::fs::path;

import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::type::native::scalar::fptr;
import ymirc::semantic::generator::value::construct::base;
import ymirc::semantic::generator::value::prototypes::proto;

pub class ProtoFuncPtrValue over ConstructValue {

    // The proto of the function being pointed
    let _proto : &PrototypeValue;

    /**
     * @params:
     *    - name: the name of the function pointed
     *    - closure: the closure content
     *    - references: for each parameter, true iif the param is a reference
     *    - parameters: the list of parameters of the function pointer
     *    - retType: the return type of the function pointer
     */
    pub self (loc : &Word, proto : &PrototypeValue, references : [bool], lazys : [bool], parameters : [&Type], retType : &Type)
        with super (loc, FuncPtrType::new (loc, references, lazys, parameters, retType, isMutable-> false), isReduced-> true)
        , _proto = proto
    {
        self._throwers = self._proto.getThrowers ();
    }

    self (loc : &Word, proto : &PrototypeValue, type : &Type)
        with super (loc, type, isReduced-> true)
        , _proto = proto
    {
        self._throwers = self._proto.getThrowers ();
    }
    
    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the name of the function being pointed
     */
    pub fn getPrototype (self)-> &PrototypeValue {
        self._proto
    }

    /*!
     * ================================================================================
     * ================================================================================
     * ==========================           CLONE            ==========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self)-> &Value {
        ProtoFuncPtrValue::new (self._loc, self._proto, self._type)
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &ProtoFuncPtrValue => {
                p._proto == self._proto &&
                    p._type == self._type
            }
            _ => { false }
        }
    }

    pub over isFullLiteral (self)-> bool {
        true
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (stream.forParams ()) {
                stream:.write ("_");
            } else {
                stream:.write (Keys::FUNCTION, ' ', self._proto);
            }
        }

    }


}
