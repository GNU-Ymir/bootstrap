in moduleref;

use ymirc::lexing::word;
use ymirc::utils::format;

use std::stream;

use ymirc::semantic::symbol::module;
use ymirc::semantic::generator::type::native::scalar::none_;

/**
 * A module reference is a variable refering to a module symbol
 */
@final
pub class ModuleRefValue over Value {

    // The location of the module
    let _declLoc : &Word;

    // The symbol
    let _sym : &ModuleSymbol;

    /**
     * @params: 
     *   - loc: the location of the reference
     *   - sym: the module symbol
     */
    pub self (loc : &Word, sym : &ModuleSymbol)
        with super (loc, NONE_TYPE, isReduced-> true)
        , _declLoc = sym.getLoc ()
        , _sym = sym
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self::super.opEquals (o)) return false;

        match o {
            p : &ModuleRefValue => {
                self._sym == p._sym
            }
            _ => { false }
        }
    }

    pub over isFullLiteral (self)-> bool {
        true
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the module symbol that is refered to
     */
    pub fn getSymbol (self)-> &ModuleSymbol {
        self._sym
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create an exact copy of the value with a different uniqId
     * */
    pub over clone (self)-> &Value {
        self
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._sym.getPath ().toStr ());
        }        
    }
    
}



