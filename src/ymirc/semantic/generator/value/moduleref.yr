mod ymirc::semantic::generator::value::moduleref;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;

import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::literal::none;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::type::temporary::moduleref;
import ymirc::semantic::symbol::module;



/**
 * A module reference is a variable refering to a module symbol
 */
pub class ModuleRefValue over Value {

    /// The location of the module
    let _declLoc : &Word;

    /// The symbol
    let _sym : &ModuleSymbol;
    
    
    /**
     * @params: 
     *   - loc: the location of the reference
     *   - sym: the module symbol
     */
    pub self (loc : &Word, sym : &ModuleSymbol)
        with super (loc, MODULE_REF_TYPE), _declLoc = sym.getLoc (), _sym = sym
    {}

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &ModuleRefValue => {
                self._sym == p._sym
            }
            _ => { false }
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the module symbol that is refered to
     */
    pub fn getSymbol (self)-> &ModuleSymbol {
        self._sym
    }
    
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (self._sym.getPath ().toStr (sep-> "::"s8));
        }        
    }
    
}



