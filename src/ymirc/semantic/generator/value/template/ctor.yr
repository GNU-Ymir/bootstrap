mod ymirc::semantic::generator::value::template::ctor;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;

import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::type::native::scalar::none;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::symbol::template;
import ymirc::semantic::symbol::class_;

import ymirc::syntax::declaration::function;
import ymirc::syntax::declaration::constructor;
import ymirc::syntax::expression::vardecl;
import ymirc::syntax::expression::empty;

import ymirc::semantic::generator::value::template::base;

pub class TemplateRefCtorValue over TemplateRefValue {

    let _clRef : &ClassSymbol;

    pub self (loc : &Word, cl : &ClassSymbol, tmp : &TemplateSymbol)
        with super (loc, tmp)
        , _clRef = cl
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * ==========================           CLONE            ==========================
     * ================================================================================
     * ================================================================================
     */


    pub over opEquals (self, o : &Generator)-> bool {
        if (!self::super.opEquals (o)) return false;

        match o {
            p : &TemplateRefCtorValue => {
                if (self._clRef != p._clRef) return false;

                return true;
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the class symbol that contains the template ctor
     * */
    pub fn getClassSymbol (self)-> &ClassSymbol {
        self._clRef
    }

    /**
     * @returns: the name of the ctor
     * */
    pub fn getRename (self)-> [c8] {
        match self._sym.getContent ().getContent () {
            c : &ConstructorDecl => { c.getRename ().str () }
            _ => __pragma!panic ();
        }
    }

    /**
     * @returns: the syntax declaration of the ctor
     * */
    pub fn getCtor (self)-> &ConstructorDecl {
        match self._sym.getContent ().getContent () {
            c : &ConstructorDecl => { c }
            _ => __pragma!panic ();
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (stream.hasCustomFormatTag ("withParams"s8)) {
                stream:.write (self._sym.getPath ().toStr (), " {");
                for i, z in self._sym.getContent ().getParameters () {
                    if i != 0us stream:.write (", ");
                    stream:.write (z);
                }
                stream:.write ("}");
                match self._sym.getContent ().getContent () {
                    c : &ConstructorDecl => {
                        stream:.write ("(");
                        for i, p in c.getFunctionProtoDecl ().getParameters () {
                            if i != 0us stream:.write (", ");
                            stream:.write (p);
                        }
                        stream:.write (")");
                    }
                }
            } else {
                stream:.write (self._sym.getPath ().toStr (), " {");
                for i, z in self._sym.getContent ().getParameters () {
                    if i != 0us stream:.write (", ");
                    stream:.write (z);
                }
                stream:.write ("}");
            }
        }
    }

}
