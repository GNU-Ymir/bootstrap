mod ymirc::semantic::generator::value::template::method;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;

import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::type::native::scalar::none;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::symbol::template;
import ymirc::semantic::symbol::class_;

import ymirc::syntax::declaration::function;
import ymirc::syntax::declaration::constructor;
import ymirc::syntax::expression::vardecl;
import ymirc::syntax::expression::empty;

import ymirc::semantic::generator::value::template::base;

pub class TemplateRefMethodValue over TemplateRefValue {

    let _clRef : &ClassSymbol;

    pub self (loc : &Word, cl : &ClassSymbol, tmp : &TemplateSymbol)
        with super (loc, tmp)
        , _clRef = cl
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * ==========================           CLONE            ==========================
     * ================================================================================
     * ================================================================================
     */


    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &TemplateRefMethodValue => {
                self._sym == p._sym
                    && self._clRef == p._clRef
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the class symbol that contains the template method
     * */
    pub fn getClassSymbol (self)-> &ClassSymbol {
        self._clRef
    }

    /**
     * @returns: the name of the method
     * */
    pub fn getFunctionName (self)-> [c8] {
        match self._sym.getContent ().getContent () {
            f : &FunctionDecl => { f.getLoc ().str () }
            _ => __pragma!panic ();
        }
    }

    /**
     * @returns: the syntax declaration of the method
     * */
    pub fn getFunction (self)-> &FunctionDecl {
        match self._sym.getContent ().getContent () {
            f : &FunctionDecl => { f }
            _ => __pragma!panic ();
        }
    }

    /**
     * @returns: true if the function declaration declares self as mutable
     * */
    pub fn isMutable (self)-> bool {
        match self._sym.getContent ().getContent () {
            f : &FunctionDecl => {
                let v = f.getFunctionProto ().getParameters ()[0];
                return (v.isMutable () || v.isDeeplyMutable ());
            }
        }

        __pragma!panic ();
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (stream.hasCustomFormatTag ("withParams"s8)) {
                stream:.write (self._sym.getPath ().toStr (), " {");
                for i, z in self._sym.getContent ().getParameters () {
                    if i != 0us stream:.write (", ");
                    stream:.write (z);
                }
                stream:.write ("}");
                match self._sym.getContent ().getContent () {
                    f : &FunctionDecl => {
                        stream:.write ("(");
                        for i, p in f.getFunctionProto ().getParameters ()[1us .. $] {
                            if i != 0us stream:.write (", ");
                            stream:.write (p);
                        }
                        stream:.write (")");
                        match f.getFunctionProto ().getRetType () {
                            EmptyExpression () => {}
                            x : _ => {
                                stream:.write ("-> ", x);
                            }
                        }
                    }
                }
            } else {
                stream:.write (self._sym.getPath ().toStr (), " {");
                for i, z in self._sym.getContent ().getParameters () {
                    if i != 0us stream:.write (", ");
                    stream:.write (z);
                }
                stream:.write ("}");
            }
        }
    }

}
