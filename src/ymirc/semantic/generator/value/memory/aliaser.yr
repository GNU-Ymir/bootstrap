in aliaser;

use ymirc::lexing::{word, tokens};
use ymirc::utils::format;
use ymirc::syntax::keys;
use std::stream;
use ymirc::semantic::generator::type;

pub class AliaserValue over MemoryValue {

    // The content of the aliaser
    let _value : &Value;

    // True if the alias was created inside an expansion
    let _fromExpansion : bool;

    /**
     * @params:
     * @params:
     *   - loc: the location of the aliaser
     *   - type: the type of the aliaser
     *   - values: the value contained in the aliaser
     */
    pub self (loc : &Word, type : &Type, value : &Value, fromExpansion : bool = false, isReduced : bool = false)
        with super (loc, type, isReduced-> isReduced)
        , _value = value
        , _fromExpansion = fromExpansion
    {
        self._throwers = value._throwers;
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self::super.opEquals (o)) return false;

        match o {
            p : &AliaserValue => {
                if (self._value != p._value) return false;
                if (self._fromExpansion != p._fromExpansion) return false;

                true
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the content of the aliaser
     */
    pub fn getValue (self)-> &Value {
        self._value
    }

    /**
     * @returns: true if the alias was declared inside an expansion
     * */
    pub fn isFromExpansion (self)-> bool {
        self._fromExpansion
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub over clone (self)-> &Value {
        copy AliaserValue (self._loc,
                           self._type,
                           self._value.clone (),
                           fromExpansion-> self._fromExpansion,
                           isReduced-> self._isReduced)
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            stream:.write (Keys::ALIAS, ' ', Tokens::LPAR);
            stream:.entabing ();
            self._value.format (alias stream);
            stream:.detabing ();
            stream:.write (Tokens::RPAR);
        }
    }

}
