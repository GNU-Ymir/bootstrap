mod ymirc::semantic::generator::value::memory::copier;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::lexing::tokens;
import ymirc::syntax::keys;

import ymirc::semantic::generator::type::native::none;
import ymirc::semantic::generator::value::memory::base;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::literal::unit;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;

/**
 * A copier is a value that is copied
 */
pub class CopierValue over MemoryValue {

    /// The content of the copier
    let _value : &Value;   

    /// True if the copy is deep (does not affect generation)
    let _isDeep = false;

    /// True if the copy is done by the value (dor example by calling a function from core::duplication, or a calss methods, ...)
    /// It is used during the expansion, to tell that we only need to expand the inner value, and do nothing special with it
    /// We still keep that in a copy for semantic analyses, and keep traces that the specific call is a copy call
    let _isDone : bool;

    /**
     * @params: 
     * @params: 
     *   - loc: the location of the copier
     *   - type: the type of the copier
     *   - values: the value contained in the copier
     */
    pub self (loc : &Word, type : &Type, value : &Value, isDeep : bool = false, isDone : bool = false)
        with super (loc, type)
        , _value = value
        , _isDeep = isDeep
        , _isDone = isDone
    {        
        self._throwers = value._throwers;
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &CopierValue => {
                if (self._type != p._type) return false;
                if (self._value != p._value) return false;
                true
            }
            _ => { false }
        }
    }

    pub over needUniqness (self)-> bool {
        true
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the content of the copier
     */
    pub fn getValue (self)-> &Value {
        self._value
    }

    /**
     * @returns: true if the copy is deep
     * @info: this should not affect generation, it only informs that inner values within self.getValue () are also copied
     * */
    pub fn isDeep (self)-> bool {
        self._isDeep
    }


    /**
     * @returns: true if the value inside the copy should be used directly, without any more transformations
     * @info:
     * ========
     * True if the copy is done by the value (dor example by calling a function from core::duplication, or a calss methods, ...)
     * It is used during the expansion, to tell that we only need to expand the inner value, and do nothing special with it
     * We still keep that in a copy for semantic analyses, and keep traces that the specific call is a copy call
     * ========
     * */
    pub fn isDone (self)-> bool {
        self._isDone
    }
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (self._isDone) {
                self._value.format (alias stream);
            } else {
                stream:.write (Keys::COPY, ' 'c8, Tokens::LPAR);
                stream:.entabing ();
                self._value.format (alias stream);
                stream:.detabing ();
                stream:.write (Tokens::RPAR);
            }
        }        
    }
    
}



