mod ymirc::semantic::generator::value::memory::class_aliaser;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::lexing::tokens;
import ymirc::syntax::keys;

import ymirc::semantic::generator::type::native::scalar::none;
import ymirc::semantic::generator::value::memory::base;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::literal::unit;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::type::custom::classptr;

/**
 * A aliaser is a mutable value (lvalue) that is aliased
 */
pub class ClassAliaserValue over MemoryValue {

    /// The content of the aliaser
    let _value : &Value;

    /// True if the aliased class is a struct
    let _isStruct : bool;

    /**
     * @params:
     * @params:
     *   - loc: the location of the aliaser
     *   - type: the type of the aliaser
     *   - values: the value contained in the aliaser
     */
    pub self (loc : &Word, type : &ClassPtrType, value : &Value, isReduced : bool = false)
        with super (loc, type, isReduced-> isReduced)
        , _value = value
        , _isStruct = type.isStruct ()
    {
        self._throwers = value._throwers;
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        if (!self::super.opEquals (o)) return false;

        match o {
            p : &ClassAliaserValue => {
                if (self._value != p._value) return false;
                if (self._isStruct != p._isStruct) return false;

                return true;
            }
            _ => { false }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the content of the aliaser
     */
    pub fn getValue (self)-> &Value {
        self._value
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub over clone (self)-> &Value {
        ClassAliaserValue::new (self._loc,
                                self._type.asOf!{&ClassPtrType} (),
                                self._value.clone (),
                                isReduced-> self._isReduced)
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            self._value.format (alias stream);
            stream:.write (Tokens::COLON);
        }
    }

}
