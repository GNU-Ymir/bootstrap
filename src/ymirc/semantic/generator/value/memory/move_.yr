mod ymirc::semantic::generator::value::memory::move_;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::lexing::tokens;
import ymirc::syntax::keys;

import ymirc::semantic::generator::type::native::scalar::none;
import ymirc::semantic::generator::value::memory::base;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::literal::unit;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;

/**
 * A Mover is a value that is copied
 */
pub class MoverValue over MemoryValue {

    /// The content of the Mover
    let _value : &Value;

    /// The value that is moved
    let _what : &Value;

    /**
     * @params:
     * @params:
     *   - loc: the location of the Mover
     *   - type: the type of the Mover
     *   - values: the block of instruction defining the move operations
     *   - what: the value that is moved
     */
    pub self (loc : &Word, type : &Type, value : &Value, what : &Value, isReduced : bool = false)
        with super (loc, type, isReduced-> isReduced)
        , _value = value
        , _what = what
    {
        self._throwers = value._throwers;
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &MoverValue => {
                if (self._type != p._type) return false;
                if (self._value != p._value) return false;
                true
            }
            _ => { false }
        }
    }

    pub over needUniqness (self)-> bool {
        self._value.needUniqness ()
    }

    /*!
     * ================================================================================
     * ================================================================================
     * ==========================           CLONE            ==========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self)-> &Value {
        MoverValue::new (self._loc, self._type, self._value.clone (), self._what.clone (), isReduced-> self._isReduced)
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the content of the Mover
     * @info: block of operations defining the instruction to do to move the value
     */
    pub fn getValue (self)-> &Value {
        self._value
    }

    /**
     * @returns: The value that is moved
     * */
    pub fn getWhat (self)-> &Value {
        self._what
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (Keys::MOVE, ' 'c8, Tokens::LPAR);
            stream:.entabing ();
            self._what.format (alias stream);
            stream:.detabing ();
            stream:.write (Tokens::RPAR);
        }
    }

}
