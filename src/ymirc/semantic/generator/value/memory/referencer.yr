mod ymirc::semantic::generator::value::memory::referencer;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;
import std::collection::vec;

import ymirc::lexing::tokens;
import ymirc::syntax::keys;

import ymirc::semantic::generator::type::native::scalar::none;
import ymirc::semantic::generator::value::memory::base;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::literal::unit;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;

/**
 * A referencer is a value that is copied
 */
pub class ReferencerValue over MemoryValue {

    /// The content of the referencer
    let _value : &Value;   

    // True if the reference is mutable
    let _isMutable : bool = false;

    /**
     * @params: 
     * @params: 
     *   - loc: the location of the referencer
     *   - type: the type of the referencer
     *   - values: the value contained in the referencer
     */
    pub self (loc : &Word, type : &Type, value : &Value, isMutable : bool = false, isReduced : bool = false)
        with super (loc, type, isReduced-> isReduced)
        , _isMutable = isMutable
        , _value = value
    {        
        self._throwers = value._throwers;
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &ReferencerValue => {
                if (self._type != p._type) return false;
                if (self._value != p._value) return false;
                true
            }
            _ => { false }
        }
    }

    pub over needUniqness (self)-> bool {
        self._value.needUniqness ()
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the content of the referencer
     */
    pub fn getValue (self)-> &Value {
        self._value
    }

    /**
     * @returns: true if the reference has mutable privileges
     * */
    pub fn isMutable (self)-> bool {
        self._isMutable
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (Keys::REF, ' 'c8, Tokens::LPAR);
            stream:.entabing ();
            self._value.format (alias stream);
            stream:.detabing ();
            stream:.write (Tokens::RPAR);
        }        
    }
    
}



