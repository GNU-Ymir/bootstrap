mod ymirc::semantic::generator::value::multsym;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;

import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::type::temporary::multsym;

import std::collection::vec;


/**
 * A mult sym value, is a value that contains references to multiple symbols
 * It can happen when multiple symbols have the same name for example
 */
pub class MultSymValue over Value {

    /// The content of the multiple sym
    let _values : [&Value];

    /// The string to print for pretty error formatting
    let _prettyStr : [c8];
    
    /**
     * @params: 
     *   - loc: the location of the reference
     * 
     */
    pub self (loc : &Word, content : [&Value], prettyStr : [c8] = ""s8)
        with super (loc, MULT_SYM_TYPE, isReduced-> true)
        , _prettyStr = prettyStr
        , _values = {
            // Flattening mult symbols
            let dmut res = Vec!{&Value}::new ();
            for c in content {
                match c {
                    m : &MultSymValue => {
                        for j in m._values { res:.push (j); }
                    }
                    _ => res:.push (c);
                }
            }
            res[]
        }
    {        
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the list of values contained in the mult sym
     */
    pub fn getValues (self)-> [&Value] {
        self._values
    }

    /*!
     * ================================================================================
     * ================================================================================
     * ==========================           CLONE            ==========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self)-> &Value {
        let dmut vals = Vec!{&Value}::new ();
        for i in self._values {
            vals:.push (i.clone ());
        }

        MultSymValue::new (self._loc, vals[], prettyStr-> self._prettyStr)
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &MultSymValue => {
                if (p._values.len != self._values.len) return false;
                for i in 0us .. self._values.len {
                    if (p._values [i] != self._values [i]) return false;
                }

                true
            }
            _ => { false }
        }
    }

    pub over needUniqness (self)-> bool {
        for v in self._values {
            if v.needUniqness () { return true }
        }
        false
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            match self._prettyStr {
                ""s8 => 
                    stream:.write ("<mutlsym-value>");
                _ =>
                    stream:.write (self._prettyStr);
            };                    
        }        
    }
    
}



