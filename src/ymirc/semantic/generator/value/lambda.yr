mod ymirc::semantic::generator::value::lambda;

import ymirc::lexing::word;
import ymirc::utils::format;

import std::io;

import ymirc::syntax::expression::_;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::protovar;
import ymirc::semantic::generator::value::literal::none;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::type::temporary::proto;
import ymirc::semantic::symbol::function;
import ymirc::syntax::keys;


/**
 * The lambda value unlike PrototypeValue stores a uncomplete prototype to a lambda 
 * It stores a syntaxic element that will be validated only when it will be called
 * Its validation (done in the will create a prototypevalue and the frame that is associated
 */
pub class LambdaValue over Value {

    /// The list of parameters of the Lambda
    let _parameters : [&Value];

    /// The return type of the function
    let _retType : &Type;

    /// The content of the lambda value
    let _content : &Expression;

    /// The name of the lambda (only for compiler, lambda are anonymus variable)
    let _name : [c8];

    /// True if the lambda enclose the declaring frame
    let _closure : bool;

    /// The index of the proto
    let _index : u32;
      
    /**
     * @params: 
     *   - loc: the location of the reference
     *   - type: a semi validated type
     *   - parameters: the list of parameters
     *   - retType: the return type of the lambda
     *   - name: the name of the prototype
     *   - closure: true if this is a closure (a simple fn otherwise)
     *   - index: the index of the proto
     */
    pub self (loc : &Word, type : &Type, name : [c8], parameters : [&Value], retType : &Type, content : &Expression, closure : bool, index : u32)
        with super (loc, type), _parameters = parameters, _name = name, _retType = retType, _closure = closure, _index = index, _content = content
    {}
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the parameters of the Lambda
     */
    pub def getParameters (self)-> [&Value] {
        self._parameters
    }

    /**
     * @returns: the return type of the Lambda
     */
    pub def getRetType (self)-> &Type {
        self._retType
    }

    /**
     * @returns: the content of the lambda
     */
    pub def getContent (self)-> &Expression {
        self._content
    }
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &LambdaValue => {
                if (p._index != self._index) return false;
                if (p._closure != self._closure) return false;
                if (p._name != self._name) return false;
                // if (p._content != self._content) return false;
                // if (p._retType != self._retType) return false;
                // if (p._parameters.len != self._parameters.len) return false;
                // for i in 0us .. self._parameters.len {
                //     if (self._parameters [i] != p._parameters [i]) return false;
                // }                    

                true
            }
            _ => { false }
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (self._closure) stream:.write (Keys::COPY);
            stream:.write ('|'c8);
            stream:.entabing ();
            for i, j in self._parameters {
                if (i != 0us) stream:.write (", "s8);
                j.format (alias stream);
            }
            stream:.write ("|-> "s8);
            self._retType.format (alias stream);
            stream:.write (" => "s8);
            self._content.format (alias stream);
            stream:.detabing ();
        }        
    }
    
}



