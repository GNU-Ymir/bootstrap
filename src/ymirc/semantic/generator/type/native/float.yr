mod ymirc::semantic::generator::type::native::float;

import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::type::native::base;
import ymirc::semantic::generator::base;
import ymirc::lexing::word;
import ymirc::utils::format;
import ymirc::syntax::keys;

import std::io;

/**
 * Float type
 */
pub class FloatType over NativeType {

    /// The size of the float in bits
    let _size : u16;
    
    /**
     * @params: 
     *    - loc: the location of the type
     */
    pub self (loc : &Word, size : u16 = 32u16, isMutable : bool = false) with super (loc, isMutable-> isMutable), _size = size {}

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            x : &FloatType => { x._size == self._size }
            _ => { false }
        }
    }
    
    pub over isCompatible (self, o : &Type)-> bool {
        self == o
    }


    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the size of the float in bits
     */
    pub def getSize (self)-> u16 {
        self._size
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================           CLONING            =========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self, isMutable : bool)-> &Type {
        FloatType::new (self._loc, size-> self._size, isMutable-> isMutable)
    }

    
    pub over toDeeplyMutable (self)-> &Type {
        if (self._isMutable) {
            self
        } else {
            self.clone (isMutable-> true)
        }
    }

    
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (self._isMutable) stream:.write (Decorators::MUT, ' 'c8);
            if self._size == 32u16 {
                stream:.write (NativeTypes::F32);
            } else
                stream:.write (NativeTypes::F64);
        }                
    }
    
}
