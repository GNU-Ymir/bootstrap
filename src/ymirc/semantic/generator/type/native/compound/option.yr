in option;

use ymirc::lexing::{word, tokens};
use ymirc::utils::format;
use ymirc::syntax::keys;
use std::stream;

use ymirc::errors::message;
use ymirc::semantic::validator::errors;
use ymirc::semantic::generator::type::native::scalar::none_;

pub lazy NONE_OPTION_TYPE = {
    copy OptionType (EOF_WORD, scalar::none_::NONE_TYPE)
} catch {
    _ => panic;
};

/**
 * Option type
 */
@final
pub class OptionType over CompoundType {

    /**
     * @params:
     *    - loc: the location of the type
     *    - inner: the inner type in the option
     * */
    pub self (loc : &Word, inner : &Type, isMutable : bool = false)
        with super (loc, inners-> copy [inner], isMutable-> isMutable)
        throws ErrorMsg
    {
        if inner.isMovable () {
            throw copy ErrorMsg::fatal (loc, end-> inner.getLoc (), format (ValidateErrorMessage::CONTAIN_MOVABLE_TYPE, inner),
                                        notes-> copy [copy ErrorMsg::note (loc, format (ValidateErrorMessage::CONTAIN_MOVABLE_TYPE_NOTE), oneLine-> true)]);
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */


    pub over isDeeplyMutable (self)-> bool {
        if (!self._isMutable) {
            false
        } else {
            self._inners [0us].isDeeplyMutable ()
        }
    }

    pub over needExplicitAlias (self)-> bool {
        if (!self._isMutable) {
            false
        } else {
            self._inners [0us].needExplicitAlias ()
        }
    }

    pub over borrowDatas (self)-> bool {
        self._inners [0us].borrowDatas ()
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            s : &OptionType => { self._inners [0us] == s._inners [0us] }
            _ => { false }
        }
    }

    pub over isCompatible (self, o : &Type)-> bool {
        match o {
            s : &OptionType => { self._inners [0us].isCompatible (s._inners [0us]) }
            _ => { false }
        }
    }


    pub over mutabilityFit (self, o : &Type, borrowed : bool)-> bool {
        match o {
            s : &OptionType => {
                if (!s._isMutable) return true;
                match (s._inners [0], self._inners [0]) { // none => anything, anything => none is always ok
                    (_, NoneType ()) => { return true; }
                    (NoneType (), _) => { return true; }
                }

                if (borrowed && !self._isMutable) return false;
                return self._inners [0us].mutabilityFit (s._inners [0us], borrowed-> borrowed);
            }
            _ => { panic; }
        }
    }

    pub over mutabilityDistance (self, o : &Type, borrowed : bool)-> u32 {
        match o {
            a : &OptionType => {
                if (!self._isMutable) return 0u32;

                let inner = self._inners [0us].mutabilityDistance (a._inners [0us], borrowed-> borrowed);
                if (borrowed && !a._isMutable) {
                    1u32 + inner
                } else { inner }
            }
            _ => panic;
        }
    }

    pub over maxMutabilityDistance (self, borrowed : bool)-> u32 {
        let inner = self._inners [0us].maxMutabilityDistance (borrowed-> borrowed);
        if (borrowed) {
            1u32 + inner
        } else {
            inner
        }
    }

    pub over sameMutability (self, o : &Type)-> bool {
        match o {
            p : &OptionType => {
                if (self._isMutable != p._isMutable) return false;
                return self._inners [0].sameMutability (p._inners [0us]);
            }
        }

        panic;
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           CLONING            =========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self, mutLevel : u32)-> &Type {
        let innerLevel = if (mutLevel >= 1u32) {
            mutLevel - 1u32
        } else { 0u32 };

        {
            copy OptionType (self._loc, self._inners [0us].clone (innerLevel), isMutable-> (mutLevel != 0u32))
        } catch {
            _ => panic;
        }
    }

    pub over clone (self, pattern : &Type)-> &Type {
        match pattern {
            a : &OptionType => {
                let inner = self._inners [0us].clone (a._inners [0us]);

                {
                    copy OptionType (self._loc, inner, isMutable-> pattern._isMutable)
                } catch {
                    _ => panic;
                }
            }
            _ => panic;
        }
    }

    pub over cloneMutableTilBorrow (self)-> &Type {
        {
            let inner = self._inners [0].cloneMutableTilBorrow ();
            copy OptionType (self._loc, inner, isMutable-> true)
        } catch {
            _ => panic;
        }
    }


    pub over toDeeplyMutable (self)-> &Type {
        {
            copy OptionType (self._loc, self._inners [0us].toDeeplyMutable (), isMutable-> true)
        } catch {
            _ => panic;
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (self._isMutable) stream:.write (Decorators::MUT, ' ');
            stream:.write (Tokens::LPAR);
            stream:.entabing ();
            self._inners [0us].format (alias stream);
            stream:.detabing ();
            stream:.write (Tokens::RPAR, Tokens::INTEG);
        }
    }


}
