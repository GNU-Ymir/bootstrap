mod ymirc::semantic::generator::type::native::compound::range;

import ymirc::semantic::generator::type::native::compound::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::value::literal::int;
import ymirc::semantic::generator::type::native::_;
import ymirc::semantic::generator::base;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;
import ymirc::syntax::keys;

import std::io;

/**
 * Range type
 */
pub class RangeType over CompoundType {
    
    /**
     * @params: 
     *    - loc: the location of the type
     *    - inner: the inner type of the array
     */
    pub self (loc : &Word, inner : &IntType, stepType : &IntType, isMutable : bool)
        with super (loc, inners-> [inner.clone (mutLevel-> (if (isMutable) { 1u32 } else { 0u32 })),
                                   stepType.clone (mutLevel-> (if (isMutable) { 1u32 } else { 0u32 }))],
                    isMutable-> isMutable)
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    pub over isDeeplyMutable (self)-> bool {
        self._isMutable && self._inners [0]._isMutable
    }

    pub over needExplicitAlias (self)-> bool {
        false
    }

    pub over borrowDatas (self)-> bool {
        false
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            s : &RangeType => { self._inners [0us] == s._inners [0us] }
            _ => { false }
        }
    }

    pub over isCompatible (self, o : &Type)-> bool {
        match o {
            s : &RangeType => { self._inners [0us].isCompatible (s._inners [0us]) }
            _ => { false }
        }
    }

    pub over mutabilityFit (self, o : &Type, borrowed : bool)-> bool {
        if (borrowed) {
            if (!self._isMutable && o._isMutable) return false;
        }

        true
    }

    pub over mutabilityDistance (self, o : &Type, borrowed : bool)-> u32 {
        if (borrowed) {
            if (self._isMutable && !o._isMutable) return 1u32;
        }

        0u32
    }

    pub over maxMutabilityDistance (self, borrowed : bool)-> u32 {
        if (borrowed) {
            return 1u32;
        }

        0u32
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           CLONING            =========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self, mutLevel : u32)-> &Type {
        let innerLevel = if (mutLevel >= 1u32) {
            mutLevel - 1u32
        } else { 0u32 };

        let inner = self._inners [0].clone (innerLevel).asOf!{&IntType} ();
        let step = self._inners [1].clone (innerLevel).asOf!{&IntType} ();

        RangeType::new (self.getLoc (), inner, step, isMutable-> (mutLevel != 0u32))
    }

    pub over clone (self, pattern : &Type)-> &Type {
        match pattern {
            r : &RangeType => {
                let inner = self._inners [0].clone (r._inners [0]).asOf!{&IntType} ();
                let step = self._inners [1].clone (r._inners [1]).asOf!{&IntType} ();

                RangeType::new (self._loc, inner, step, isMutable-> r._isMutable)
            }
            _ => __pragma!panic ();
        }
    }

    pub over toDeeplyMutable (self)-> &Type {
        let inner = self.getInners ()[0].toDeeplyMutable ().asOf!{&IntType} ();
        let step = self.getInners ()[1].toDeeplyMutable ().asOf!{&IntType} ();
        RangeType::new (self.getLoc (), inner, step, isMutable-> true)
    }
    
    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (self._isMutable) stream:.write (Decorators::MUT, ' 'c8);
            stream:.write ("(.."s8);
            self._inners [0us].format (alias stream);
            stream:.write (")"s8);
        }                
    }
    
}
