mod ymirc::semantic::generator::type::native::compound::range;

import ymirc::semantic::generator::type::native::compound::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::value::literal::int;
import ymirc::semantic::generator::type::native::_;
import ymirc::semantic::generator::base;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;
import ymirc::syntax::keys;

import std::io;

/**
 * Range type
 */
pub class RangeType over CompoundType {
    
    /**
     * @params: 
     *    - loc: the location of the type
     *    - inner: the inner type of the array
     */
    pub self (loc : &Word, inner : &IntType, stepType : &IntType, isMutable : bool)
        with super (loc, inners-> [inner.clone (mutLevel-> (if (isMutable) { 1u32 } else { 0u32 })),
                                   stepType.clone (mutLevel-> (if (isMutable) { 1u32 } else { 0u32 }))],
                    isMutable-> isMutable)
    {
        self._needExplicitAlias = false;
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            s : &RangeType => { self._inners [0us] == s._inners [0us] }
            _ => { false }
        }
    }

    pub over isCompatible (self, o : &Type)-> bool {
        match o {
            s : &RangeType => { self._inners [0us].isCompatible (s._inners [0us]) }
            _ => { false }
        }
    }    

    pub over borrowDatas (self)-> bool {
        false
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================           CLONING            =========================
     * ================================================================================
     * ================================================================================
     */

    prot over clone (self, isMutable : bool, inners : [&Type])-> &Type {
        RangeType::new (self._loc, inners [0us].asOf!{&IntType} (), inners [1us].asOf!{&IntType} (), isMutable-> isMutable)
    }

    pub over clone (self, mutLevel : u32)-> &Type {
        RangeType::new (self.getLoc (), self.getInners ()[0].clone (mutLevel).asOf!{&IntType} (), self.getInners ()[1].clone (mutLevel).asOf!{&IntType} (), isMutable-> (mutLevel != 0u32))
    }

    pub over toDeeplyMutable (self)-> &Type {
        RangeType::new (self.getLoc (), self.getInners ()[0].toDeeplyMutable ().asOf!{&IntType} (), self.getInners()[1].toDeeplyMutable ().asOf!{&IntType} (), isMutable-> true)
    }
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (self._isMutable) stream:.write (Decorators::MUT, ' 'c8);
            stream:.write ("(.."s8);
            self._inners [0us].format (alias stream);
            stream:.write (")"s8);
        }                
    }
    
}
