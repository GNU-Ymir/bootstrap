mod ymirc::semantic::generator::type::native::compound::future_;

import ymirc::semantic::generator::type::native::compound::base;
import ymirc::semantic::generator::type::native::scalar::none;
import ymirc::semantic::generator::type::native::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::base;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;
import ymirc::syntax::keys;

import std::io;

import ymirc::errors::message;
import ymirc::semantic::validator::errors;

/**
 * Future type
 */
pub class FutureType over CompoundType {

    /**
     * @params:
     *    - loc: the location of the type
     *    - inner: the inner type in the future
     * */
    pub self (loc : &Word, inner : &Type, isMutable : bool = false)
        with super (loc, inners-> [inner], isMutable-> isMutable)
        throws &ErrorMsg
    {
        if inner.isMovable () {
            throw ErrorMsg::fatal (loc, end-> inner.getLoc (), format (ValidateErrorMessage::CONTAIN_MOVABLE_TYPE, inner),
                                   notes-> [ErrorMsg::note (loc, format (ValidateErrorMessage::CONTAIN_MOVABLE_TYPE_NOTE), oneLine-> true)]);
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */


    pub over isDeeplyMutable (self)-> bool {
        if (!self._isMutable) {
            false
        } else {
            self._inners [0us].isDeeplyMutable ()
        }
    }

    pub over needExplicitAlias (self)-> bool {
        if (!self._isMutable) {
            false
        } else {
            self._inners [0us].needExplicitAlias ()
        }
    }

    pub over borrowDatas (self)-> bool {
        self._inners [0us].borrowDatas ()
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            s : &FutureType => { self._inners [0us] == s._inners [0us] }
            _ => { false }
        }
    }

    pub over isCompatible (self, o : &Type)-> bool {
        match o {
            s : &FutureType => { self._inners [0us].isCompatible (s._inners [0us]) }
            _ => { false }
        }
    }


    pub over mutabilityFit (self, o : &Type, borrowed : bool)-> bool {
        match o {
            s : &FutureType => {
                if (!s._isMutable) return true;

                if (borrowed && !self._isMutable) return false;
                return self._inners [0us].mutabilityFit (s._inners [0us], borrowed-> borrowed);
            }
            _ => { __pragma!panic (); }
        }
    }

    pub over mutabilityDistance (self, o : &Type, borrowed : bool)-> u32 {
        match o {
            a : &FutureType => {
                if (!self._isMutable) return 0u32;

                let inner = self._inners [0us].mutabilityDistance (a._inners [0us], borrowed-> borrowed);
                if (borrowed && !a._isMutable) {
                    1u32 + inner
                } else { inner }
            }
            _ => __pragma!panic ();
        }
    }

    pub over maxMutabilityDistance (self, borrowed : bool)-> u32 {
        let inner = self._inners [0us].maxMutabilityDistance (borrowed-> borrowed);
        if (borrowed) {
            1u32 + inner
        } else {
            inner
        }
    }

    pub over sameMutability (self, o : &Type)-> bool {
        match o {
            p : &FutureType => {
                if (self._isMutable != p._isMutable) return false;
                return self._inners [0].sameMutability (p._inners [0us]);
            }
        }

        __pragma!panic ();
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           CLONING            =========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self, mutLevel : u32)-> &Type {
        let innerLevel = if (mutLevel >= 1u32) {
            mutLevel - 1u32
        } else { 0u32 };

        {
            FutureType::new (self._loc, self._inners [0us].clone (innerLevel), isMutable-> (mutLevel != 0u32))
        } catch {
            _ => __pragma!panic ();
        }
    }

    pub over clone (self, pattern : &Type)-> &Type {
        match pattern {
            a : &FutureType => {
                let inner = self._inners [0us].clone (a._inners [0us]);
                {
                    FutureType::new (self._loc, inner, isMutable-> pattern._isMutable)
                } catch {
                    _ => __pragma!panic ();
                }
            }
            _ => {
                let inner = self._inners [0us].clone (0);
                {
                    FutureType::new (self._loc, inner, isMutable-> pattern._isMutable)
                } catch {
                    _ => __pragma!panic ();
                }
            }
        }
    }

    pub over cloneMutableTilBorrow (self)-> &Type {
        {
            let inner = self._inners [0].cloneMutableTilBorrow ();
            FutureType::new (self._loc, inner, isMutable-> true)
        } catch {
            _ => __pragma!panic ();
        }
    }


    pub over toDeeplyMutable (self)-> &Type {
        {
            FutureType::new (self._loc, self._inners [0us].toDeeplyMutable (), isMutable-> true)
        } catch {
            _ => __pragma!panic ();
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (self._isMutable) stream:.write (Decorators::MUT, ' 'c8);
            stream:.write ('('c8, Keys::FUTURE, Tokens::ARROW, ' 'c8);
            stream:.entabing ();
            self._inners [0us].format (alias stream);
            stream:.detabing ();
            stream:.write (')'c8);
        }
    }


}
