mod ymirc::semantic::generator::type::native::compound::tuple;

import ymirc::semantic::generator::type::native::compound::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::base;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;

import std::collection::vec;
import std::io;


import ymirc::errors::message;
import ymirc::semantic::validator::errors;


/**
 * Tuple type
 */
pub class TupleType over CompoundType {

    /**
     * @params: 
     *    - loc: the location of the type
     *    - inner: the inner type of the tuple
     */
    pub self (loc : &Word, inners : [&Type], isMutable : bool = false)
        with super (loc, inners-> inners, isMutable-> isMutable)
        throws &ErrorMsg
    {
        for inner in inners {
            if inner.isMovable () {
                throw ErrorMsg::fatal (loc, end-> inner.getLoc (), format (ValidateErrorMessage::CONTAIN_MOVABLE_TYPE, inner),
                                       notes-> [ErrorMsg::note (loc, format (ValidateErrorMessage::CONTAIN_MOVABLE_TYPE_NOTE), oneLine-> true)]);
            }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * ===========================          GETTERS          ==========================
     * ================================================================================
     * ================================================================================
     */

    pub over isDeeplyMutable (self)-> bool {
        if (!self._isMutable) return false;
        for i in self._inners {
            if (!i.isDeeplyMutable ()) return false;
        }

        true
    }

    pub over needExplicitAlias (self)-> bool {
        if (!self._isMutable) return false;

        for i in self._inners {
            if (i.needExplicitAlias ()) return true;
        }

        false
    }

    pub over borrowDatas (self)-> bool {
        for s in self._inners {
            if s.borrowDatas () { return true; }
        }

        false
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            s : &TupleType => { self._inners == s._inners }
            _ => { false }
        }
    }

    pub over isCompatible (self, o : &Type)-> bool {
        match o {
            s : &TupleType => {
                if (self._inners.len != s._inners.len) return false;
                for i in 0us .. self._inners.len {
                    if (!self._inners [i].isCompatible (s._inners [i])) return false;
                }

                true
            }
            _ => {
                false
            }
        }
    }

    pub over mutabilityFit (self, o : &Type, borrowed : bool)-> bool {
        match o {
            t : &TupleType => {
                if (self._inners.len != t._inners.len) __pragma!panic ();
                if (!t._isMutable) return true; // other is const, even if it borrows it is always ok
                if (borrowed && !self._isMutable) return false;

                for i in 0us .. self._inners.len {
                    if (!self._inners [i].mutabilityFit (t._inners [i], borrowed-> borrowed)) return false;
                }

                true
            }
            _ => __pragma!panic ();
        }
    }

    pub over mutabilityDistance (self, o : &Type, borrowed : bool)-> u32 {
        match o {
            t : &TupleType => {
                if (self._inners.len != t._inners.len) __pragma!panic ();
                let mut sum = 0u32;
                if (borrowed && !t._isMutable) sum = 1u32;

                for i in 0us .. self._inners.len {
                    sum += self._inners [i].mutabilityDistance (t._inners [i], borrowed-> borrowed);
                }

                sum
            }
            _ => __pragma!panic ();
        }
    }


    pub over maxMutabilityDistance (self, borrowed : bool)-> u32 {
        let mut sum = 0u32;
        if (borrowed) sum += 1u32;
        for i in self._inners {
            sum += i.maxMutabilityDistance (borrowed-> borrowed);
        }

        sum
    }

    pub over sameMutability (self, o : &Type)-> bool {
        match o {
            t : &TupleType => {
                if (self._isMutable != o._isMutable) return false;
                for i in 0us .. self._inners.len {
                    if (!self._inners [i].sameMutability (t._inners [i])) return false;
                }

                return true;
            }
        }

        __pragma!panic ();
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           CLONING            =========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self, mutLevel : u32)-> &Type {
        let innerLevel = if (mutLevel >= 1u32) { mutLevel - 1u32 } else { 0u32 };

        let dmut res = Vec!{&Type}::new ();
        for i in self._inners {
            res:.push (i.clone (innerLevel));
        }

        {
            TupleType::new (self._loc, res[], isMutable-> (mutLevel != 0u32))
        } catch {
            _ => __pragma!panic ();
        }
    }

    pub over clone (self, pattern : &Type)-> &Type {
        match pattern {
            t : &TupleType => {
                if (self._inners.len != t._inners.len) __pragma!panic ();

                let dmut res = Vec!{&Type}::new ();
                for i in 0us .. self._inners.len {
                    res:.push (self._inners [i].clone (t._inners [i]));
                }

                {
                    TupleType::new (self._loc, res[], isMutable-> t._isMutable)
                } catch {
                    _ => __pragma!panic ();
                }
            }
            _ => __pragma!panic ();
        }
    }

    pub over toDeeplyMutable (self)-> &Type {
        let dmut res = Vec!{&Type}::new ();
        for i in self._inners {
            res:.push (i.toDeeplyMutable ());
        }

        {
            TupleType::new (self._loc, res[], isMutable-> true)
        } catch {
            _ => __pragma!panic ();
        }
    }
    
    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write (Tokens::LPAR);
            for i, j in self._inners {
                if (i != 0us) stream:.write (", "s8);
                j.format (alias stream);
            }
            if (self._inners.len == 1us) stream:.write (","s8);
            stream:.write (Tokens::RPAR);
        }                
    }
    
}
