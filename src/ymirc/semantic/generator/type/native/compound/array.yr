mod ymirc::semantic::generator::type::native::compound::array;

import ymirc::semantic::generator::type::native::compound::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::value::literal::int;
import ymirc::semantic::generator::base;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;
import ymirc::syntax::keys;

import std::io;


/**
 * Array type, unlike slice arrays have a cte size
 */
pub class ArrayType over CompoundType {

    /// The size of the array
    let _size : &IntValue;
    
    /**
     * @params: 
     *    - loc: the location of the type
     *    - inner: the inner type of the array
     *    - size: the size of the array
     */
    pub self (loc : &Word, inner : &Type, size : &IntValue, isMutable : bool = false) with super (loc, inners-> [inner], isMutable-> isMutable), _size = size {
        self._mutabilityLevel = if (isMutable) {
            1u32 + inner._mutabilityLevel
        } else { 0u32 };
        
        self._needExplicitAlias = inner._needExplicitAlias;
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */
    
    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            s : &ArrayType => {
                self._size.getValue () == s._size.getValue () &&
                    self._inners [0us] == s._inners [0us]                    
            }
            _ => { false }
        }
    }

    pub over isCompatible (self, o : &Type)-> bool {
        match o {
            s : &ArrayType => {
                self._size.getValue () == s._size.getValue () &&
                    self._inners [0us].isCompatible (s._inners [0us])
            }
            _ => {
                false
            }
        }
    }
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the size of the array
     */
    pub fn getSize (self)-> &IntValue {
        self._size
    }

    /**
     * @returns: the size of the array as a cte value
     * */
    pub fn getSizeCte (self)-> usize {
        self._size.getValue ().to!{usize} ()
    } catch {
        _ => __pragma!panic ();
    }
    
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================           CLONING            =========================
     * ================================================================================
     * ================================================================================
     */

    prot over clone (self, isMutable : bool, inners : [&Type])-> &Type {
        ArrayType::new (self._loc, inners [0us], self._size, isMutable-> isMutable)
    }

    pub over clone (self, mutLevel : u32)-> &Type {
        let innerLevel = if (mutLevel >= 1u32) { mutLevel - 1u32 } else { 0u32 };
        ArrayType::new (self._loc, self._inners [0us].clone (innerLevel), self._size, isMutable-> (mutLevel != 0u32))
    }

    pub over toDeeplyMutable (self)-> &Type {
        ArrayType::new (self._loc, self._inners [0us].toDeeplyMutable (), self._size, isMutable-> true)
    }
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (self._isMutable) stream:.write (Decorators::MUT, ' 'c8);
            stream:.write (Tokens::LCRO);
            stream:.entabing ();
            self._inners [0us].format (alias stream);
            stream:.write (" ; "s8);
            self._size.format (alias stream);
            stream:.detabing ();
            stream:.write (Tokens::RCRO);
        }                
    }
    
}
