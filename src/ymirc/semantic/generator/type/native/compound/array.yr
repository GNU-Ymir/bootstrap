in array;
use ymirc::semantic::generator::value::literal::int;

use ymirc::lexing::{word, tokens};
use ymirc::utils::format;
use ymirc::syntax::keys;

use std::stream;
use ymirc::errors::message;
use ymirc::semantic::validator::errors;

/**
 * Array type, unlike slice arrays have a cte size
 */
@final
pub class ArrayType over CompoundType {

    // The size of the array
    let _len : usize;
    
    /**
     * @params: 
     *    - loc: the location of the type
     *    - inner: the inner type of the array
     *    - size: the size of the array
     */
    pub self (loc : &Word, inner : &Type, len : usize, isMutable : bool = false)
        with super (loc, inners-> copy [inner], isMutable-> isMutable)
        , _len = len

        throws ErrorMsg
    {
        if inner.isMovable () {
            throw copy ErrorMsg::fatal (loc, end-> inner.getLoc (), format (ValidateErrorMessage::CONTAIN_MOVABLE_TYPE, inner),
                                   notes-> copy [copy ErrorMsg::note (loc, format (ValidateErrorMessage::CONTAIN_MOVABLE_TYPE_NOTE), oneLine-> true)]);
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
 */
    
    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            s : &ArrayType => {
                if (self._len != s._len) return false;
                if (self._inners [0us] != s._inners [0us]) return false;

                true
            }
            _ => { false }
        }
    }

    pub over isCompatible (self, o : &Type)-> bool {
        match o {
            s : &ArrayType => {
                if (self._len != s._len) return false;
                if (!self._inners [0us].isCompatible (s._inners [0us])) return false;

                true
            }
            _ => {
                false
            }
        }
    }

    pub over mutabilityFit (self, o : &Type, borrowed : bool)-> bool {
        match o {
            a : &ArrayType => {
                if (!a._isMutable) return true;
                if (borrowed && !self._isMutable) return false;

                return self._inners [0us].mutabilityFit (a._inners [0us], borrowed-> borrowed);
            }
            _ => panic;
        }
    } 

    pub over mutabilityDistance (self, o : &Type, borrowed : bool)-> u32 {
        match o {
            a : &ArrayType => {
                if (!self._isMutable) return 0u32;
                let inner = self._inners [0us].mutabilityDistance (a._inners [0us], borrowed-> borrowed);
                if (borrowed && !a._isMutable) {
                    1u32 + inner
                } else {
                    inner
                }
            }
            _ => {
                std::io::eprintln (o);
                panic;
            }
        }
    }

    pub over maxMutabilityDistance (self, borrowed : bool)-> u32 {
        let inner = self._inners [0us].maxMutabilityDistance (borrowed-> borrowed);
        if (borrowed) {
            1u32 + inner
        } else {
            inner
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
 */

    /**
     * @returns: the size of the array
     */
    pub fn getLen (self)-> usize {
        self._len
    }

    pub over isDeeplyMutable (self)-> bool {
        if (!self._isMutable) {
            false
        } else {
            self._inners [0].isDeeplyMutable ()
        }
    }

    pub over needExplicitAlias (self)-> bool {
        if (!self._isMutable) {
            false
        } else {
            self._inners [0us].needExplicitAlias ()
        }
    }

    pub over borrowDatas (self)-> bool {
        self._inners [0us].borrowDatas ()
    }

    pub over sameMutability (self, o : &Type)-> bool {
        match o {
            a : &ArrayType => {
                if (self._isMutable != o._isMutable) return false;
                return self._inners [0us].sameMutability (a._inners [0us]);
            }
        }

        panic;;
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           CLONING            =========================
     * ================================================================================
     * ================================================================================
 */

    pub over clone (self, mutLevel : u32)-> &Type {
        let innerLevel = if (mutLevel >= 1u32) { mutLevel - 1u32 } else { 0u32 };
        {
            copy ArrayType (self._loc, self._inners [0us].clone (innerLevel), self._len, isMutable-> (mutLevel != 0u32))
        } catch {
            _ => panic;
        }
    }

    pub over clone (self, pattern : &Type)-> &Type {
        match pattern {
            a : &ArrayType => {
                let inner = self._inners [0us].clone (a._inners [0us]);
                {
                    copy ArrayType (self._loc, inner, self._len, isMutable-> a._isMutable)
                } catch {
                    _ => panic;
                }
            }
            _ => {
                let inner = self._inners [0us].clone (0);
                {
                    copy ArrayType (self._loc, inner, self._len, isMutable-> pattern._isMutable)
                } catch {
                    _ => panic;
                }
            }
        }
    }

    pub over toDeeplyMutable (self)-> &Type {
        {
            copy ArrayType (self._loc, self._inners [0us].toDeeplyMutable (), self._len, isMutable-> true)
        } catch {
            _ => panic;
        }
    }


    pub over cloneMutableTilBorrow (self)-> &Type {
        let inner = self._inners [0].cloneMutableTilBorrow ();
        {
            copy ArrayType (self._loc, inner, self._len, isMutable-> true)
        } catch {
            _ => panic;
        }
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
 */

    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            if (self._isMutable) stream:.write (Decorators::MUT, ' ');
            stream:.write (Tokens::LCRO);
            stream:.entabing ();
            self._inners [0us].format (alias stream);
            stream:.write (" ; "s8);
            stream:.write (self._len, "us"s8);
            stream:.detabing ();
            stream:.write (Tokens::RCRO);
        }                
    }
    
}
