mod ymirc::semantic::generator::type::native::compound::pointer;

import ymirc::semantic::generator::type::native::compound::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::base;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::utils::format;
import ymirc::syntax::keys;

import std::io;


/**
 * Pointer type
 */
pub class PointerType over CompoundType {

    /**
     * @params: 
     *    - loc: the location of the type
     *    - inner: the inner type of the pointer
     */
    pub self (loc : &Word, inner : &Type, isMutable : bool = false) with super (loc, inners-> [inner], isMutable-> isMutable) {
        self._mutabilityLevel = if (isMutable) {
            1u32 + inner._mutabilityLevel
        } else { 0u32 };

        self._needExplicitAlias = true;
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            s : &PointerType => { self._inners [0us] == s._inners [0us] }
            _ => { false }
        }
    }

    pub over isCompatible (self, o : &Type)-> bool {
        match o {
            s : &PointerType => { self._inners [0us].isCompatible (s._inners [0us]) }
            _ => { false }
        }
    }
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================           CLONING            =========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self, isMutable : bool)-> &Type {
        PointerType::new (self._loc, self._inners [0us].clone (false), isMutable-> isMutable)
    }

    pub over toDeeplyMutable (self)-> &Type {
        PointerType::new (self._loc, self._inners [0us].toDeeplyMutable (), isMutable-> true)
    }
    

    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (self._isMutable) stream:.write (Decorators::MUT, ' 'c8);
            stream:.write (Tokens::AND, Tokens::LPAR);
            self._inners [0us].format (alias stream);
            stream:.write (Tokens::RPAR);
        }                
    }
    
}
