in float;

use ymirc::lexing::word;
use ymirc::utils::format;
use ymirc::lexing::keys;

use std::stream;

/**
 * Float type
 */
@final
pub class FloatType over ScalarType {

    // The size of the float in bits
    let _size : u16;
    
    /**
     * @params: 
     *    - loc: the location of the type
     */
    pub self (loc : &Word, size : u16 = 32u16, isMutable : bool = false)
        with super (loc, isMutable-> isMutable)
        , _size = size
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            x : &FloatType => { x._size == self._size }
            _ => { false }
        }
    }
    
    pub over isCompatible (self, o : &Type)-> bool {
        self == o
    }


    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the size of the float in bits
     */
    pub fn getSize (self)-> u16 {
        self._size
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           CLONING            =========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self, mutLevel : u32)-> &Type {
        if (self._isMutable && mutLevel != 0u32) {
            self
        } else {
            copy FloatType (self._loc, size-> self._size, isMutable-> (mutLevel != 0u32))
        }
    }
    
    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (self._isMutable) stream:.write (Decorators::MUT, ' ');
            if self._size == 32u16 {
                stream:.write (NativeTypes::F32);
            } else if (self._size == 64u16) {
                stream:.write (NativeTypes::F64);
            } else if (self._size == 80u16) {
                stream:.write (NativeTypes::F80);
            } else {
                stream:.write (NativeTypes::REAL);
            }
        }                
    }
    
}
