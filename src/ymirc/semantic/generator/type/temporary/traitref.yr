mod ymirc::semantic::generator::type::temporary::traitref;

import ymirc::semantic::generator::type::temporary::base;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::value::base;
import ymirc::semantic::generator::value::literal::none;
import ymirc::semantic::symbol::trait_;
import ymirc::syntax::keys;
import ymirc::lexing::word;

import std::io;
import ymirc::utils::format;


/**
 * A trait ref type is the type used to refer to a trait symbol 
 * it is not a usable type, meaning that a variable cannot be of type Trait
 */
pub class @abstract TraitRefType over TemporaryType {

    /// The traitrefure defining the traitref
    let _sym : &TraitSymbol;
    
    /**
     * @params:
     *   - loc: the location of the class definition
     *   - sym: the trait symbol 
     */
    pub self (loc : &Word, sym : &TraitSymbol)
        with super (loc, isMutable-> false), _sym = sym
    {}

    
    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */
    
    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &TraitRefType => {
                self._sym == p._sym
            }
            _ => { false }
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================           CLONING            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @warning: isMutable is ignored, trait ref are always immutable
     */
    pub over clone (self, _ : bool)-> &Type {
        self
    }

    pub over toDeeplyMutable (self)-> &Type {
        self
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the reference to the class symbol 
     */
    pub def getSymbol (self)-> &TraitSymbol {
        self._sym
    }
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write ("trait ", self._sym);
        }                
    }

        
}
