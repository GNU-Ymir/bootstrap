in classfield;

use ymirc::lexing::word;

use ymirc::semantic::generator::value;
use ymirc::semantic::generator::type::{custom::classptr,
                                       temporary::{traitref, classref}};

use ymirc::semantic::generator::value::{vardecl,
                                        template::_,
                                        construct::typeinfo,
                                        literal::unit,
                                        prototypes::{ctorproto, methodproto}};

use ymirc::semantic::symbol::class_;
use ymirc::semantic::validator::template::mapper;
use ymirc::syntax::keys;

use std::stream;
use ymirc::utils::format;


/**
 * Class temporay type used to access the field of a class during a class construction
 * */
@final
pub class ClassFieldAccType over TemporaryType {

    let _cref : &ClassRefType;

    let dmut _validatedFields : [[c8] => ()] = copy [];

    pub self (loc : &Word, cref : &ClassRefType, full : bool = false)
        with super (loc, isMutable-> false)
        , _cref = cref
    {
        if full {
            for i in cref.getLocalFields () {
                self._validatedFields [i.getLoc ().str] = ();
            }
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          GETTERS          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub fn getInner (self)-> &ClassRefType {
        self._cref
    }

    pub fn isFieldValidated (self, name : [c8])-> bool {
        name in self._validatedFields
    }

    pub fn insertValidated (mut self, name : [c8]) {
        self._validatedFields [name] = ();
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ==================================          OVER GETTERS          ==================================
     * ====================================================================================================
     * ====================================================================================================
     */


    pub over isDeeplyMutable (self)-> bool {
        true
    }

    pub over needExplicitAlias (self)-> bool {
        false
    }

    pub over borrowDatas (self)-> bool {
        false
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &ClassFieldAccType => {
                self._cref == p._cref
            }
            _ => { false }
        }
    }

    pub over isCompatible (self, o : &Type)-> bool {
        self == o
    }



    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          CLONING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub over clone (self, _ : u32)-> &Type {
        self
    }

    pub over clone (self, _ : &Type)-> &Type {
        self
    }

    pub over toDeeplyMutable (self)-> &Type {
        self
    }

    pub over cloneMutableTilBorrow (self)-> &Type {
        self
    }


    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ======================================          MISC          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    impl Streamable;
    impl Formattable {
        pub over format (self, dmut stream : &Formatter) {
            stream:.write (format ("&(%)::fields", self._cref));
        }
    }

}
