in closure;

use ymirc::semantic::generator::type::native::scalar::none_;
use ymirc::semantic::generator::value;
use ymirc::semantic::validator::visitor;

use ymirc::lexing::word;
use ymirc::utils::{format, list};
use std::stream;

/**
 * Expand type 
 */
@final
pub class ClosureType over TemporaryType {

    // The inner type of the closure
    let _innerTypes : [&Type];

    // The inner names of the closure
    let _innerNames : [[c8]];

    // The enclosed values
    let _innerValues : [&Value];

    // The list of variables passed by reference
    let _innerRefs : [bool];
    
    // The uniq id of the variable creating the closure value (parameter)
    let _refId : usize;

    // The symbols that are enclosed
    let _closureSymbols : &List!{dmut &DeclaredVars};

    // The list of used symbpls
    let dmut _used : &List!{dmut &UsedVars};

    let _finalized : bool = false;

    /**
     * @params: 
     *    - loc: the location of the type
     */
    pub self (loc : &Word, innerTypes : [&Type],
              innerNames : [[c8]],
              innerValues : [&Value],
              innerRefs : [bool],
              refId : usize,
              symbols : &List!{dmut &DeclaredVars},
              dmut used : &List!{dmut &UsedVars},
              finalized : bool = false)

        with super (loc, isMutable-> false)
        ,_innerNames = innerNames
        ,_innerTypes = innerTypes
        ,_innerValues = innerValues
        ,_innerRefs = innerRefs
        ,_refId = refId
        ,_closureSymbols = symbols
        ,_used = alias used
        ,_finalized = finalized
    {}

    pub self empty (loc : &Word, refId : usize)
        with super (loc, isMutable-> false)
        , _innerNames = []
        , _innerTypes = []
        , _innerValues = []
        , _innerRefs = []
        , _refId = refId
        , _closureSymbols = copy List!{dmut &DeclaredVars} ()
        , _used = copy List!{dmut &UsedVars} ()
    {}

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the list of inner types
     */
    pub fn getInnerTypes (self)-> [&Type] {
        self._innerTypes
    }

    /**
     * @returns: the name of the enclosed values
     */
    pub fn getInnerNames (self)-> [[c8]] {
        self._innerNames
    }

    /**
     * @returns: the list of parameters passed by reference in the closure
     * */
    pub fn getInnerRefs (self)-> [bool] {
        self._innerRefs
    }

    /**
     * The enclosed values
     */
    pub fn getInnerValues (self)-> [&Value] {
        self._innerValues
    }
    
    /**
     * @params: 
     *   - name: the name of the field to search
     * @returns: the type of the field (NONE_TYPE if not found)
     */
    pub fn getField (self, name : [c8])-> (&Type, bool, usize) {
        for i in 0us .. self._innerNames.len {
            if (self._innerNames [i] == name) return (self._innerTypes [i], self._innerRefs [i], i);
        }

        (NONE_TYPE, false, 0us)
    }
    
    /**
     * @returns: the index of the frame scope that is enclosed (cf:
     */
    pub fn getClosureSymbols (self)-> &List!{dmut &DeclaredVars} {
        self._closureSymbols
    }

    /**
     * @returns: the list of used symbols in the enclosed scopes
     */
    pub fn getClosureUsed (mut self)-> dmut &List!{dmut &UsedVars} {
        alias self._used
    }

    /**
     * @returns: the uniq id of the closure for the varref creation
     */
    pub fn getRefId (self)-> usize {
        self._refId
    }

    /**
     * @returns: true if values are enclosed by reference
     * */
    pub fn isFinalized (self)-> bool {
        self._finalized
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */    

    pub over opEquals (self, _ : &Generator)-> bool {
        false
    }

    pub over isCompatible (self, _ : &Type)-> bool {
        false
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           CLONING            =========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self, _ : u32)-> &Type {
        self
    }
    
    /*!
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */


    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            stream:.write ('{');
            for i, j in self._innerTypes {
                if (i != 0us) stream:.write (", ");
                stream:.write (self._innerNames [i], " : ", j);
            }
            stream:.write ('}');
        }                
    }
    
}
