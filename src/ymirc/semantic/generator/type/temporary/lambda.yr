mod ymirc::semantic::generator::type::temporary::lambda;

import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::type::temporary::base;
import ymirc::semantic::generator::type::native::scalar::none;
import ymirc::semantic::generator::base;
import ymirc::lexing::word;
import ymirc::utils::format;
import ymirc::syntax::keys;

import std::io;
import std::collection::vec;

/**
 * Lambda type 
 */
pub class LambdaType over TemporaryType {

    // The types of the parameters (might be None)
    let _params : [&Type];

    // The type of the return type (might be None)
    let _retType : &Type;

    /**
     * @params: 
     *    - loc: the location of the type
     */
    pub self (loc : &Word, params : [&Type], retType : &Type, isMutable : bool = false)
        with super (loc, isMutable-> isMutable)
        , _params = params
        , _retType = retType
    {}

    /**
     * ================================================================================
     * ================================================================================
     * ===========================          GETTERS          ==========================
     * ================================================================================
     * ================================================================================
     */

    pub fn getParameters (self)-> [&Type] {
        self._params
    }

    pub fn getRetType (self)-> &Type {
        self._retType
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */


    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            l : &LambdaType => {
                if (self._retType != l._retType) return false;
                if (self._params != l._params) return false;
                true

            }
            _ => { false }
        }
    }

    pub over isCompatible (self, o : &Type)-> bool {
        self == o
    }


    /**
     * ================================================================================
     * ================================================================================
     * =========================           CLONING            =========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self, _ : u32)-> &Type {
        self
    }

    
    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */


    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (self._isMutable) stream:.write (Decorators::MUT, ' 'c8);
            stream:.write ("Î» ("s8);
            for i, j in self._params {
                if (i != 0us) stream:.write (", "s8);
                if (!j.isOf!{&NoneType}()) { stream:.write (j); }
                else stream:.write ("_"s8);
            }
            stream:.write (")-> "s8);
            if (self._retType.isOf!{&NoneType} ()) stream:.write ("_");
            else stream:.write (self._retType);
        }                
    }
    
}
