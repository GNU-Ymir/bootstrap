mod ymirc::semantic::generator::type::custom::classptr;

import ymirc::semantic::generator::type::custom::base;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::type::temporary::classref;
import ymirc::syntax::keys;
import ymirc::lexing::word;
import ymirc::lexing::tokens;

import std::io;
import ymirc::utils::format;

/**
 * A class ptr type is the type that can be used (&A), instead of just (A) (assuming A is a class)
 */
pub class @abstract ClassPtrType over CustomType {

    /// The content of the class ptr
    let _inner : &ClassRefType;

    /// True if the type was created from a self::super, or things like that
    let _isProxy : bool;
       
    /**
     * 
     */
    pub self (loc : &Word, type : &ClassRefType, isProxy : bool = false, isMutable : bool = false)
        with super (loc, isMutable-> isMutable), _inner = type, _isProxy = isProxy
    {
        self._needExplicitAlias = true;
        self._mutabilityLevel = if (isMutable) {
            1u32 + type.mutabilityLevel ()
        } else { 0u32 };
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */
    
    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &ClassPtrType => {
                self._inner == p._inner
            }
            _ => { false }
        }
    }

    pub over isCompatible (self, o : &Type)-> bool {
        match o {
            p : &ClassPtrType => {
                self._inner.isCompatible (p._inner)
            }
            _ => { false }
        }
    }
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================           CLONING            =========================
     * ================================================================================
     * ================================================================================
     */

    pub over clone (self, isMutable : bool)-> &Type {
        self._inner.createInstance (loc-> self._loc, isMutable, self._inner.isMutable () && isMutable)
    }

    /**
     * Clone the class type
     * @params:
     *   - mutLevel: 1 = mut, 2 = dmut
     */
    pub fn clone (self, mutLevel : u32)-> &Type {
        match self._inner.clone (isMutable-> (mutLevel >= 2u32)) {
            c : &ClassRefType => {
                ClassPtrType::new (self._loc, isMutable-> mutLevel >= 1u32, isProxy-> self._isProxy, c)
            }
            _ => __pragma!panic ()
        }        
    }

    pub over toDeeplyMutable (self)-> &Type {
        if (self._isMutable && self._inner.isMutable ()) {
            self
        } else {
            self.clone (mutLevel-> 2u32)
        }
    }
    
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the class type contained in the ptr
     */
    pub fn getInner (self)-> &ClassRefType {
        self._inner
    }
    
    /**
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (self._isMutable) stream:.write (Decorators::MUT, ' 'c8);
            stream:.write (Tokens::AND, '('c8);
            stream:.entabing ();
            stream:.write (self._inner);
            stream:.detabing ();
            stream:.write (')'c8);
        }                
    }

        
}
