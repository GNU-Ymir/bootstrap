mod ymirc::semantic::generator::type::custom::classptr;

import ymirc::semantic::generator::type::custom::base;
import ymirc::semantic::generator::base;
import ymirc::semantic::generator::type::base;
import ymirc::semantic::generator::type::temporary::classref;
import ymirc::syntax::keys;
import ymirc::lexing::word;
import ymirc::lexing::tokens;

import std::io;
import ymirc::utils::format;

/**
 * A class ptr type is the type that can be used (&A), instead of just (A) (assuming A is a class)
 */
pub class @abstract ClassPtrType over CustomType {

    /// The content of the class ptr
    let _inner : &ClassRefType;

    /// True if the type was created from a self::super, or things like that
    let _isProxy : bool;

    /**
     * 
     */
    pub self (loc : &Word, type : &ClassRefType, isProxy : bool = false, isMutable : bool = false)
        with super (loc, isMutable-> isMutable), _inner = type, _isProxy = isProxy
    {}


    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    pub over isDeeplyMutable (self)-> bool {
        self._isMutable && self._inner.isMutable ()
    }

    pub over needExplicitAlias (self)-> bool {
        self.isDeeplyMutable ()
    }

    pub over borrowDatas (self)-> bool {
        true
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    pub over opEquals (self, o : &Generator)-> bool {
        match o {
            p : &ClassPtrType => {
                self._inner == p._inner
            }
            _ => { false }
        }
    }

    pub over isCompatible (self, o : &Type)-> bool {
        match o {
            p : &ClassPtrType => {
                self._inner.isCompatible (p._inner)
            }
            _ => { false }
        }
    }

    pub over mutabilityFit (self, o : &Type, borrowed : bool = false)-> bool {
        match o {
            p : &ClassPtrType => {
                if (!p._isMutable) return true;
                if (borrowed && !self._isMutable) return false;

                return self._inner.mutabilityFit (p._inner, borrowed-> true);
            }
            _ => __pragma!panic ();
        }
    }

    pub over mutabilityDistance (self, o : &Type, borrowed : bool = false)-> u32 {
        match o {
            p : &ClassPtrType => {
                if (!self._isMutable) return 0u32;
                let inner = self._inner.mutabilityDistance (p._inner, borrowed-> true);

                if (borrowed && !p._isMutable) { 1u32 + inner }
                else {
                    inner
                }
            }
            _ => __pragma!panic ();
        }
    }

    pub over maxMutabilityDistance (self, borrowed : bool = false)-> u32 {
        let inner = self._inner.maxMutabilityDistance (borrowed-> true);
        if (borrowed) {
            1u32 + inner
        } else {
            inner
        }
    }


    pub over sameMutability (self, o : &Type)-> bool {
        match o {
            p : &ClassPtrType => {
                if (self._isMutable != p._isMutable) return false;
                return self._inner.sameMutability (p._inner);
            }
        }

        __pragma!panic ();
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =========================           CLONING            =========================
     * ================================================================================
     * ================================================================================
     */


    /**
     * Clone the class type
     * @params:
     *   - mutLevel: 1 = mut, 2 = dmut
     */
    pub over clone (self, mutLevel : u32)-> &Type {
        let innerLevel = if (mutLevel >= 1u32) { mutLevel - 1u32 } else { 0u32 };

        match self._inner.clone (innerLevel) {
            c : &ClassRefType => {
                ClassPtrType::new (self._loc, isMutable-> (mutLevel != 0u32), isProxy-> self._isProxy, c)
            }
            _ => __pragma!panic ()
        }        
    }

    pub over clone (self, pattern : &Type)-> &Type {
        match pattern {
            p : &ClassPtrType => {
                let inner = self._inner.clone (p._inner);
                return ClassPtrType::new (self._loc, isMutable-> p._isMutable, isProxy-> self._isProxy, inner.asOf!{&ClassRefType} ());
            }
            _ => __pragma!panic ();
        }
    }

    pub over toDeeplyMutable (self)-> &Type {
        if (self._isMutable && self._inner.isMutable ()) {
            self
        } else {
            self.clone (mutLevel-> 2u32)
        }
    }

    
    /*!
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: the class type contained in the ptr
     */
    pub fn getInner (self)-> &ClassRefType {
        self._inner
    }
    
    /*!
     * ================================================================================
     * ================================================================================
     * =========================             MISC             =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {

        pub over format (self, dmut stream : &Formatter) {
            if (self._isMutable) stream:.write (Decorators::MUT, ' 'c8);
            stream:.write (Tokens::AND, '('c8);
            stream:.entabing ();
            stream:.write (self._inner);
            stream:.detabing ();
            stream:.write (')'c8);
        }                
    }


}
