mod ymirc::semantic::symbol::module;

import ymirc::utils::format;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::semantic::symbol::_;
import ymirc::semantic::generator::_;

import ymirc::syntax::declaration::_;
import ymirc::syntax::expression::_;
import ymirc::syntax::declaration::protection;

import std::io;

/**
 *
 */
pub class ModuleSymbol over Symbol {

    /// True iif the module was imported
    let _isExtern : bool;

    /// True iif the module is global
    let _isGlobal : bool;

    /// The location of the module if it is a sub module declared throw an importation
    let _importLoc : &Word;


    /**
     * @params: 
     *   - loc: the location of the module
     *   - comm: the comments about the module
     *   - isExtern: is the module external ?
     *   - isGlobal: is the module global ?
     */
    pub self (loc : &Word, importLoc : &Word, comm : [c8], protection : Protection = Protection::PUBLIC, isExtern : bool = false, isGlobal : bool = false)
        with super (loc, comm, protection-> protection), _importLoc = importLoc, _isExtern = isExtern, _isGlobal = isGlobal
    {}

    /**
     * ================================================================================
     * ================================================================================
     * =========================           GETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * @returns: true iif the module is global
     */
    pub fn isGlobal (self)-> bool {
        self._isGlobal
    }

    /**
     * @returns: true iif the module was imported
     */
    pub fn isExtern (self)-> bool {
        self._isExtern
    }    

    /**
     * @returns: the location of the import (for global sub modules)
     * */
    pub fn getImportLoc (self)-> &Word {
        self._importLoc
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================        MISCELLANEOUS         =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable;
    impl Formattable {


        pub over format (self, dmut stream : &Formatter) {
            stream:.write ("Module : [", self._loc.str (), " (", self._fullName.toStr (), ")", "]");
            let inner = self._table.getContent ();
            stream:.entabing ();
            for n, i in inner {
                for s in i {
                    stream:.write ("\n- ", n, " : ");
                    s.format (alias stream);
                }
            }
            stream:.detabing (); 
        }        
    }    
    
}
