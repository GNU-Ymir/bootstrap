in sym_list;

use std::stream;

/**
 * A symbol list is a container of mutable symbols
 * It ensures that symbols appears only once in the list
 */
@final
pub class SymbolList {

    // The hashset to make sure the symbols appears only once
    let dmut _set : [(*void) => &Symbol] = copy [];
    
    /**
     * Create an empty symbol list
     */
    pub self () {}

    /**
     * Insert a symbol in the list
     * Ensure that is it not already in it
     */
    pub fn insert (mut self, dmut sym : &Symbol) {
        self._set [ymirc::utils::memory::Runtime!{&Symbol, *void}::_yrt_unsafe_cast (sym)] = alias sym;        
    }

    /**
     * @returns: the list of symbols
     */
    pub fn opIndex (mut self)-> dmut [&Symbol] {
        copy [alias s for _, dmut s in alias self._set]        
    }

    /**
     * @returns: the list of symbols
     */
    pub fn opIndex (self)-> [&Symbol] {
        copy [s for _, s in self._set]        
    }

    impl Streamable;
}


/**
 * A symbol list is a container of immutable symbols
 * It ensures that symbols appears only once in the list
 */
@final
pub class ImutSymbolList {

    // The hashset to make sure the symbols appears only once
    let mut _set : [(*void) => mut &Symbol] = copy [];
    
    /**
     * Create an empty symbol list
     */
    pub self () {}

    /**
     * Insert a symbol in the list
     * Ensure that is it not already in it
     */
    pub fn insert (mut self, sym : &Symbol) {
        self._set [ymirc::utils::memory::Runtime!{&Symbol, *void}::_yrt_unsafe_cast (sym)] = sym;          
    }

    /**
     * @returns: the list of symbols
     */
    pub fn opIndex (self)-> [&Symbol] {
        copy [s for _, s in self._set]
    }

    impl Streamable;
}
