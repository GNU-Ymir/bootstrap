in solver;

use ymirc::semantic::validator::macro_::{mapper, rewrite};
use ymirc::semantic::{symbol::{_, macro_::_},
                      generator::{value::_, type::_}};

use ymirc::semantic::validator::{utf, visitor, errors, scores};
use ymirc::errors::_;
use ymirc::utils::format;
use ymirc::syntax::{_,
                    declaration::_,
                    expression::{_, literal::_, macro_::_, operator::_, template::_}};

use ymirc::lexing::{word, tokens, keys};
use std::{io, stream};

use ymirc::semantic::generator::type::{_,
                                       custom::_,
                                       native::{_, scalar::_, compound::_},
                                       temporary::_};

use ymirc::semantic::generator::value::{_,
                                        construct::{_, delegate::_},
                                        literal::_,
                                        memory::_,
                                        operator::{_, binary::_, call::_, fields::_, map::_, slice::_, unary::_},
                                        operator::map::iteration::_,
                                        prototypes::_,
                                        scope::_,
                                        syntax::_,
                                        template::_};

/**
 * Validator used to validate macro calls
 */
pub record MacroSolver {

    /**
     * Create an empty validator
     * */
    pub self () {}

    /**
     * Validate a macro call expression
     * @params:
     *    - context: the context of the validation
     *    - m: the call expression to validate
     * @returns: the validated value
     * @throws:
     *    - &ErrorMsg: if the validation failed
     *  */
    pub fn validate (self, dmut context : &Validator, m : &MacroCallExpr)-> &Value
        throws ErrorMsg
    {            
        let left = context:.validateValue (m.getMacro ());
        let value : &MultSymValue = match left {
            mu : &MultSymValue => { mu }
            tmp : _ => { copy MultSymValue (tmp.getLoc (), copy [tmp], prettyStr-> format ("%", tmp)) }
        };

        self.validateMultSym (alias context, m.getLoc (), value, m)
    }


    /**
     * Validate a macro call expression
     * @params:
     *    - context: the context of the validation
     *    - loc: the location of the call
     *    - value: the multsym containing the symbol referenced by 'm.getLeft ()'
     *    - m: the call expression to validate
     * @returns: the validated value
     * @throws:
     *    - &ErrorMsg: if the validation failed
     * */
    fn validateMultSym (self, dmut context : &Validator, loc : &Word, value : &MultSymValue, m : &MacroCallExpr)-> &Value
        throws ErrorMsg
    {
        let mut results : [[c8]] = []; // list of generated macro contents
        let mut lefts : [(&MacroRefValue, &Word)] = []; // list of macros used to generate values
        let mut errors : [&ErrorMsg] = [];

        for it in value.getValues () {
            {
                match it {
                    mac : &MacroRefValue => {
                        let (localLoc, gen) = self.validateCall (alias context, loc, mac, m.getContent ());
                        results ~= [gen];
                        lefts ~= [(mac, localLoc)];
                    }
                    _ => {
                        let prettyName = context:.getCallOpValidator ().prettyName (it);
                        throw copy ErrorMsg::fatal (loc, format (ValidateErrorMessage::UNDEFINED_MACRO_CALL, prettyName));
                    }
                }
            } catch {
                err : &ErrorMsg => { // either not a macro, or call failed
                    errors ~= [err];
                }
            }
        }

        if (results.len == 0us) { // no candidates succeeded
            throw copy ErrorMsg::fatal (loc, format (ValidateErrorMessage::MACRO_CALL_VALIDATION_FAILED),
                                        notes-> errors);
        }
        else if (results.len != 1us) { // multiple candidates succeeded to the macro call
            let mut candidates = copy [copy ErrorMsg::note (i._0.getLoc (), format (ValidateErrorMessage::CANDIDATE_ARE, i._0))
                                       for i in lefts];

            throw copy ErrorMsg::fatal (loc, format (ValidateErrorMessage::WORKS_WITH_BOTH), notes-> candidates);
        }

        // only one success
        self.finalizeMacro (alias context, loc, lefts [0]._1, lefts [0]._0, results [0])
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =====================            MACRO CALL CTOR            ====================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Validate a call to a macro using a macro call expression
     * @params:
     *    - context: the context of the validation
     *    - loc: the location of the call
     *    - value: the macro value being called
     *    - str: the string used to call the macro
     * @returns: the validated value (transformation of the macro content)
     * @throws:
     *    - &ErrorMsg: if the call failed, or the validation of the content failed
     * */
    fn validateCall (self, dmut context : &Validator, loc : &Word, value : &MacroRefValue, str : [c8])-> (&Word, [c8])
        throws ErrorMsg
    {
        let mut errors : [&ErrorMsg] = [];
        let mut lefts : [&MacroConstructorSymbol] = [];
        let mut values : [[c8]] = [];

        for it in value.getSymbol ().getAllLocalSymbols (protection-> Protection::PRIVATE) match it {
            mct : &MacroConstructorSymbol => {
                {
                    values ~= [self.validateCallCtor (alias context, loc, value, mct, str)];
                    lefts ~= [mct];
                } catch {
                    err : &ErrorMsg => { errors ~= [err]; }
                }
            }
        }

        if values.len == 0us {
            if (errors.len == 0us) {
                throw copy ErrorMsg::fatal (loc, format (ValidateErrorMessage::UNDEFINED_CTOR_MACRO, value));
            } else {
                throw copy ErrorMsg::list (errors);
            }
        } else if (values.len != 1us) {
            let mut candidates = copy [copy ErrorMsg::note (i.getLoc (), format (ValidateErrorMessage::CANDIDATE_ARE, i.getPath ()))
                                       for i in lefts];

            throw copy ErrorMsg::fatal (loc, format (ValidateErrorMessage::WORKS_WITH_BOTH), notes-> candidates);
        }

        (lefts [0].getLoc (), values [0])
    }

    /**
     * Validate the call to a macro constructor
     * @params:
     *   - context: the context of the validation
     *   - loc: the location of the call
     *   - msym: the macro symbol being called
     *   - ctor: the ctor to call
     *   - str: the string being used to call the ctor
     * @returns: the rewritten content of the ctor
     * @throws:
     *   - &ErrorMsg: if the call failed
     * */
    fn validateCallCtor (self, dmut context : &Validator, loc : &Word, msym : &MacroRefValue, ctor : &MacroConstructorSymbol, str : [c8])-> [c8]
        throws ErrorMsg
    {
        let mult = ctor.getContent ().getRule ();
        let skips = self.validateSkips (alias context, ctor.getContent ().getSkips ());
        {
            let (retMapper, mut rest) = self.validateMacroMult (alias context, loc, msym, mult, str, skips, str, 0us);
            match skips {
                sk : &MacroMultExpr => {
                    let (_, postSkip) = self.validateMacroRepeat (alias context, loc, msym, sk, rest, EMPTY_EXPR, str, 0us);
                    rest = postSkip;
                }
            }

            if (rest.len != 0us) {
                let realLoc = self.computeLoc (loc, str, rest);
                let centering = utils::string::center ("", utils::string::maxLineLength (rest), "=");
                throw copy ErrorMsg::fatal (ctor.getLoc (), end-> realLoc, format (ValidateErrorMessage::MACRO_REST, centering, rest, centering));
            }

            self.rewriteMapper (ctor.getContent ().getContent (), retMapper, 0us)
        } catch {
            err : &ErrorMsg => {
                throw copy ErrorMsg::note (loc, end-> ctor.getLoc (), format (ValidateErrorMessage::MACRO_IN_CTOR), notes-> copy [err]);
            }
        }
    }

    /**
     * Validate the call to a macro rule
     * @params:
     *   - context: the context of the validation
     *   - loc: the location of the call
     *   - msym: the macro symbol being called
     *   - ctor: the ctor to call
     *   - str: the string being used to call the ctor
     * @returns: the rewritten content of the ctor
     * @throws:
     *   - &ErrorMsg: if the call failed
     * */
    fn validateCallRule (self, dmut context : &Validator, loc : &Word, msym : &MacroRefValue, rule : &MacroRuleSymbol, str : [c8], full : [c8], ruleIndex : usize)-> (MacroMapper, [c8])
        throws ErrorMsg
    {
        let mult = rule.getContent ().getRule ();
        let skips = self.validateSkips (alias context, rule.getContent ().getSkips ());

        {
            let (retMapper, rest) = self.validateMacroMult (alias context, loc, msym, mult, str, skips, full, ruleIndex);
            let content = self.rewriteMapper (rule.getContent ().getContent (), retMapper, ruleIndex);

            (MacroMapper (content), rest)
        } catch {
            err : &ErrorMsg => {
                let eLoc = self.computeLoc (loc, full, str);
                throw copy ErrorMsg::note (eLoc, end-> rule.getLoc (), format (ValidateErrorMessage::MACRO_IN_RULE, rule.getLoc ().str),
                                           notes-> copy [err]);
            }
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =======================            MULTIPLIERS            ======================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Validate a macro mult expression
     * @params:
     *    - context: the context of the validation
     *    - loc: the location of the call
     *    - msym: the macro symbol being called
     *    - mult: the mult expression being extracted
     *    - str: the string used to validate the mult expression
     *    - skips: the expression used to remove skip tokens
     * @returns:
     *    - .0: the macro mapper generated from that validation
     *    - .1: the rest of the string that does not work with the macro mult
     * @throws:
     *    - &ErrorMsg: if the validation failed
     * */
    fn validateMacroMult (self, dmut context : &Validator, loc : &Word, msym : &MacroRefValue, mult : &MacroMultExpr, str : [c8], skips : &Expression, full : [c8], ruleIndex : usize)-> (MacroMapper, [c8])
        throws ErrorMsg
    {
        if mult.getMultiplier ().isEof {
            return self.validateMacroList (alias context, loc, msym, mult, str, skips, full, ruleIndex);
        }

        match mult.getMultiplier ().str {
            Tokens::INTEG => { self.validateMacroOneOrNone (alias context, loc, msym, mult, str, skips, full, ruleIndex) }
            Tokens::STAR => {
                let (vecs, rest) = self.validateMacroRepeat (alias context, loc, msym, mult, str, skips, full, ruleIndex);
                let dmut result = copy StringStream ();
                for i in vecs { result:.write (i.getValue ()); }

                return (MacroMapper (result[]), rest);
            }
            Tokens::PLUS => {
                let (vecs, rest) = self.validateMacroOneOrMore (alias context, loc, msym, mult, str, skips, full, ruleIndex);
                let dmut result = copy StringStream ();
                for i in vecs { result:.write (i.getValue ()); }

                return (MacroMapper (result[]), rest);
            }
            _ => { self.validateMacroList (alias context, loc, msym, mult, str, skips, full, ruleIndex) }
        }
    }

    /**
     * Validate a macro mult that is composed of a list without repetition
     * @params:
     *    - context: the context of the validation
     *    - loc: the location of the call
     *    - msym: the macro symbol being called
     *    - mult: the mult expression being extracted
     *    - str: the string used to validate the mult expression
     *    - skips: the expression used to remove skip tokens
     * @returns:
     *    - .0: the macro mapper generated from that validation
     *    - .1: the rest of the string that does not work with the macro mult
     * @throws:
     *    - &ErrorMsg: if the validation failed
     * */
    fn validateMacroList (self, dmut context : &Validator, loc : &Word, msym : &MacroRefValue, mult : &MacroMultExpr, str : [c8], skips : &Expression, full : [c8], ruleIndex : usize)-> (MacroMapper, [c8])
        throws ErrorMsg
    {
        let mut retMapper = MacroMapper ("");
        let mut rest = str;

        for rule in mult.getContent () {
            match skips {
                sk : &MacroMultExpr => {
                    let (_, postSkip) = self.validateMacroRepeat (alias context, loc, msym, sk, rest, EMPTY_EXPR, full, ruleIndex);
                    rest = postSkip;
                }
            }

            let (localMapper, postRule) = self.validateMacroRuleExpr (alias context, loc, msym, rule, rest, skips, full, ruleIndex);
            retMapper:.merge (localMapper);

            rest = postRule;
        }


        (retMapper, rest)
    }

    /**
     * Validate a macro mult that is composed of a list with one or no repetition
     * @params:
     *    - context: the context of the validation
     *    - loc: the location of the call
     *    - msym: the macro symbol being called
     *    - mult: the mult expression being extracted
     *    - str: the string used to validate the mult expression
     *    - skips: the expression used to remove skip tokens
     * @returns:
     *    - .0: the macro mapper generated from that validation
     *    - .1: the rest of the string that does not work with the macro mult
     * @throws:
     *    - &ErrorMsg: if the validation failed
     * */
    fn validateMacroOneOrNone (self, dmut context : &Validator, loc : &Word, msym : &MacroRefValue, mult : &MacroMultExpr, str : [c8], skips : &Expression, full : [c8], ruleIndex : usize)-> (MacroMapper, [c8]) {
        {
            return self.validateMacroList (alias context, loc, msym, mult, str, skips, full, ruleIndex);
        } catch { // One or none, thus no problem if fail
            _ => {}
        }

        (MacroMapper (""), str)
    }

    /**
     * Validate a macro mult that is composed of a list with none, one or more repetitions
     * @params:
     *    - context: the context of the validation
     *    - loc: the location of the call
     *    - msym: the macro symbol being called
     *    - mult: the mult expression being extracted
     *    - str: the string used to validate the mult expression
     *    - skips: the expression used to remove skip tokens
     * @returns:
     *    - .0: the macro mapper generated from that validation
     *    - .1: the rest of the string that does not work with the macro mult
     * @throws:
     *    - &ErrorMsg: if the validation failed
     * */
    fn validateMacroRepeat (self, dmut context : &Validator, loc : &Word, msym : &MacroRefValue, mult : &MacroMultExpr, str : [c8], skips : &Expression, full : [c8], ruleIndex : usize)-> ([MacroMapper], [c8]) {
        let mut res : [MacroMapper] = [];
        let mut rest = str;
        loop {
            {
                let (localMap, postRead) = self.validateMacroList (alias context, loc, msym, mult, rest, skips, full, ruleIndex);
                res ~= [localMap];
                rest = postRead;
            } catch { // repeat is not necessary, thus an error is not a real problem
                _ => { break; }
            }
        }

        (res, rest)
    }

    /**
     * Validate a macro mult that is composed of a list with at least one repetition
     * @params:
     *    - context: the context of the validation
     *    - loc: the location of the call
     *    - msym: the macro symbol being called
     *    - mult: the mult expression being extracted
     *    - str: the string used to validate the mult expression
     *    - skips: the expression used to remove skip tokens
     * @returns:
     *    - .0: the macro mapper generated from that validation
     *    - .1: the rest of the string that does not work with the macro mult
     * @throws:
     *    - &ErrorMsg: if the validation failed
     * */
    fn validateMacroOneOrMore (self, dmut context : &Validator, loc : &Word, msym : &MacroRefValue, mult : &MacroMultExpr, str : [c8], skips : &Expression, full : [c8], ruleIndex : usize)-> ([MacroMapper], [c8])
        throws ErrorMsg
    {
        let mut res : [MacroMapper] = [];
        let mut rest = str;
        let mut error : (&ErrorMsg)? = none;
        loop {
            {
                let (localMap, postRead) = self.validateMacroList (alias context, loc, msym, mult, rest, skips, full, ruleIndex);
                res ~= [localMap];
                rest = postRead;
            } catch { // repeat is not necessary, thus an error is not a real problem
                err : &ErrorMsg => { error = err?; break; }
            }
        }

        if (res.len == 0us) {
            let eLoc = self.computeLoc (loc, full, rest);
            if let Ok (x) = error {
                throw copy ErrorMsg::fatal (eLoc,
                                            end-> mult.getLoc (),
                                            format (ValidateErrorMessage::MACRO_DOES_NOT_MATCH, self.shorten (rest), mult),
                                            notes-> copy [x]);
            }

            throw copy ErrorMsg::fatal (eLoc,
                                        end-> mult.getLoc (),
                                        format (ValidateErrorMessage::MACRO_DOES_NOT_MATCH, self.shorten (rest), mult));
        }

        (res, rest)
    }

    /**
     * Validate a macro rule
     * @params:
     *    - context: the context of the validation
     *    - loc: the location of the call
     *    - msym: the macro symbol being called
     *    - expr: the or expression being extracted
     *    - str: the string used to validate the mult expression
     *    - skips: the expression used to remove skip tokens
     * @returns:
     *    - .0: the macro mapper generated from that validation
     *    - .1: the rest of the string that does not work with the macro mult
     * @throws:
     *    - &ErrorMsg: if the validation failed
     * */
    fn validateMacroRuleExpr (self, dmut context : &Validator, loc : &Word, msym : &MacroRefValue, expr : &MacroRuleExpression, str : [c8], skips : &Expression, full : [c8], ruleIndex : usize)-> (MacroMapper, [c8])
        throws ErrorMsg
    {
        match expr {
            mlt : &MacroMultExpr => { self.validateMacroMult (alias context, loc, msym, mlt, str, skips, full, ruleIndex) }
            mor : &MacroOrExpr => { self.validateMacroOr (alias context, loc, msym, mor, str, skips, full, ruleIndex) }
            mva : &MacroVarExpr => { self.validateMacroVar (alias context, loc, msym, mva, str, skips, full, ruleIndex) }
            mto : &MacroTokenExpr => { self.validateMacroToken (alias context, loc, msym, mto, str, skips, full) }
            mid : &MacroIdentExpr => { self.validateMacroIdent (alias context, loc, msym, mid, str, skips, full, ruleIndex) }
            rule : _ => {
                println ("??", rule.__typeinfo__.name);
                panic;
            }
        }
    }

    /**
     * Validate a macro or
     * @params:
     *    - context: the context of the validation
     *    - loc: the location of the call
     *    - msym: the macro symbol being called
     *    - or: the or expression being extracted
     *    - str: the string used to validate the mult expression
     *    - skips: the expression used to remove skip tokens
     * @returns:
     *    - .0: the macro mapper generated from that validation
     *    - .1: the rest of the string that does not work with the macro mult
     * @throws:
     *    - &ErrorMsg: if the validation failed
     * */
    fn validateMacroOr (self, dmut context : &Validator, loc : &Word, msym : &MacroRefValue, mor : &MacroOrExpr, str : [c8], skips : &Expression, full : [c8], ruleIndex : usize)-> (MacroMapper, [c8])
        throws ErrorMsg
    {
        let mut errors : [&ErrorMsg] = [];
        {
            return self.validateMacroRuleExpr (alias context, loc, msym, mor.getLeft (), str, skips, full, ruleIndex);
        } catch {
            err : &ErrorMsg => { errors ~= [err]; }
        }

        {
            return self.validateMacroRuleExpr (alias context, loc, msym, mor.getRight (), str, skips, full, ruleIndex);
        } catch {
            err : &ErrorMsg => { errors ~= [err]; }
        }

        let eLoc = self.computeLoc (loc, full, str);
        throw copy ErrorMsg::fatal (eLoc, end-> mor.getLoc (),
                                    format (ValidateErrorMessage::MACRO_DOES_NOT_MATCH, self.shorten (str), mor),
                                    notes-> errors);
    }

    /**
     * Validate a macro var expr
     * @params:
     *    - context: the context of the validation
     *    - loc: the location of the call
     *    - msym: the macro symbol being called
     *    - or: the or expression being extracted
     *    - str: the string used to validate the mult expression
     *    - skips: the expression used to remove skip tokens
     * @returns:
     *    - .0: the macro mapper generated from that validation
     *    - .1: the rest of the string that does not work with the macro mult
     * @throws:
     *    - &ErrorMsg: if the validation failed
     * */
    fn validateMacroVar (self, dmut context : &Validator, loc : &Word, msym : &MacroRefValue, mva : &MacroVarExpr, str : [c8], skips : &Expression, full : [c8], ruleIndex : usize)-> (MacroMapper, [c8])
        throws ErrorMsg
    {
        let (vec, rest) = match mva.getContent () {
            mlt : &MacroMultExpr => {
                if (mlt.getMultiplier () == Tokens::STAR) {
                    self.validateMacroRepeat (alias context, loc, msym, mlt, str, skips, full, ruleIndex)
                } else if (mlt.getMultiplier () == Tokens::PLUS) {
                    self.validateMacroOneOrMore (alias context, loc, msym, mlt, str, skips, full, ruleIndex)
                } else {
                    let (localMap, rest) = self.validateMacroMult (alias context, loc, msym, mlt, str, skips, full, ruleIndex);
                    (copy [localMap], rest)
                }
            }
            rule : _ => {
                let (localMapper, rest) = self.validateMacroRuleExpr (alias context, loc, msym, rule, str, skips, full, ruleIndex);
                (copy [localMapper], rest)
            }
        };

        let dmut stream = copy StringStream ();
        for i in vec { stream:.write (i.getValue ()); }

        let mut res = MacroMapper (stream[]);
        res:.insert (mva.getLoc ().str, vec);

        (res, rest)
    }

    /**
     * Validate a macro token
     * @params:
     *    - context: the context of the validation
     *    - loc: the location of the call
     *    - msym: the macro symbol being called
     *    - or: the or expression being extracted
     *    - str: the string used to validate the mult expression
     *    - skips: the expression used to remove skip tokens
     * @returns:
     *    - .0: the macro mapper generated from that validation
     *    - .1: the rest of the string that does not work with the macro mult
     * @throws:
     *    - &ErrorMsg: if the validation failed
     * */
    fn validateMacroToken (self, dmut context : &Validator, loc : &Word, _ : &MacroRefValue, mtok : &MacroTokenExpr, str : [c8], _ : &Expression, full : [c8])-> (MacroMapper, [c8])
        throws ErrorMsg
    {
        let (size, strV) = context:.getCompileTimeInterpreter ():.computeString (context:.validateValue (mtok.getContent ()));
        let strCont = utf::instance ().decode (size, strV);

        if (strCont.len > str.len) {
            let eLoc = self.computeLoc (loc, full, str);
            throw copy ErrorMsg::fatal (eLoc, end-> mtok.getLoc (), format (ValidateErrorMessage::MACRO_DOES_NOT_MATCH, self.shorten (str), mtok));
        }

        for i in 0us .. strCont.len {
            if strCont [i] != str [i] {
                let eLoc = self.computeLoc (loc, full, str [i .. $]);
                throw copy ErrorMsg::fatal (eLoc, end-> mtok.getLoc (), format (ValidateErrorMessage::MACRO_DOES_NOT_MATCH, self.shorten (str [i .. $]), mtok));
            }
        }

        let rest = str [strCont.len .. $];
        (MacroMapper (strCont), rest)
    }

    /**
     * Validate a macro identifier rule
     * @params:
     *    - context: the context of the validation
     *    - loc: the location of the call
     *    - msym: the macro symbol being called
     *    - or: the or expression being extracted
     *    - str: the string used to validate the mult expression
     *    - skips: the expression used to remove skip tokens
     * @returns:
     *    - .0: the macro mapper generated from that validation
     *    - .1: the rest of the string that does not work with the macro mult
     * @throws:
     *    - &ErrorMsg: if the validation failed
     * */
    fn validateMacroIdent (self, dmut context : &Validator, loc : &Word, msym : &MacroRefValue, mid : &MacroIdentExpr, str : [c8], _ : &Expression, full : [c8], ruleIndex : usize)-> (MacroMapper, [c8])
        throws ErrorMsg
    {
        let mut values : [MacroMapper] = [];
        let mut lefts : [&MacroRuleSymbol] = [];
        let mut rests : [[c8]] = [];
        let mut errors : [&ErrorMsg] = [];

        for it in msym.getSymbol ().getAllLocalSymbols (protection-> Protection::PRIVATE) match it {
            mct : &MacroRuleSymbol => {
                if (mct.getPath ().file () == mid.getLoc ().str) {
                    {
                        let (localMapper, rest) = self.validateCallRule (alias context, loc, msym, mct, str, full, ruleIndex + 1us);
                        values ~= [localMapper];
                        rests ~= [rest];
                        lefts ~= [mct];
                    }
                } catch {
                    err : &ErrorMsg => { errors ~= [err]; }
                }
            }
        }


        if (values.len == 0us) {
            if (errors.len == 0us) return self.validateMacroKnwonIdent (alias context, loc, msym, mid, str, full);
            else {
                throw copy ErrorMsg::list (errors);
            }
        } else if (values.len != 1us) {
            let mut candidates = copy [copy ErrorMsg::note (i.getLoc (), format (ValidateErrorMessage::CANDIDATE_ARE, i.getPath ()))
                                       for i in lefts];

            let eLoc = self.computeLoc (loc, full, str);
            throw copy ErrorMsg::fatal (eLoc, end-> mid.getLoc (), format (ValidateErrorMessage::WORKS_WITH_BOTH),
                                        notes-> candidates);
        }

        (values [0], rests [0])
    }

    /**
     * Validate a pre constructed macro identifier rule
     * @params:
     *    - context: the context of the validation
     *    - loc: the location of the call
     *    - msym: the macro symbol being called
     *    - or: the or expression being extracted
     *    - str: the string used to validate the mult expression
     *    - skips: the expression used to remove skip tokens
     * @returns:
     *    - .0: the macro mapper generated from that validation
     *    - .1: the rest of the string that does not work with the macro mult
     * @throws:
     *    - &ErrorMsg: if the validation failed
     * */
    fn validateMacroKnwonIdent (self, dmut context : &Validator, loc : &Word, msym : &MacroRefValue, mid : &MacroIdentExpr, str : [c8], full : [c8])-> (MacroMapper, [c8])
        throws ErrorMsg
    {
        let ident = context:.getSubTypeValidator ().removeUnders (mid.getLoc ().str);
        match ident {
            MacroKnownRuleKeys::ANY => {
                return self.validateMacroIdentAny (alias context, loc, msym, mid, str, full);
            }
            MacroKnownRuleKeys::CHAR => {
                return self.validateMacroIdentChar (alias context, loc, msym, mid, str, full);
            }
        }

        let mut found = false;
        for f in MacroKnownRuleKeys::__members__ {
            if (f == ident) { found = true; break; }
        }

        if (!found) {
            let eLoc = self.computeLoc (loc, full, str);
            throw copy ErrorMsg::fatal (eLoc, end-> mid.getLoc (),
                                        format (ValidateErrorMessage::UNDEFINED_RULE_MACRO, msym, mid.getLoc ().str));
        }

                
        // let strLoc = self.computeLoc (loc, full, str);
        let dmut visitor = copy SyntaxVisitor::fromStr (loc.filename, str);

        match ident {
            MacroKnownRuleKeys::IDENT => {
                visitor:.readIdentifier ();
            }
            MacroKnownRuleKeys::STRING => {
                visitor:.readStringTokens (Tokens::DQUOTE, suffix-> false);                                
            }
            MacroKnownRuleKeys::TOKEN => {
                visitor:.readTokens (tokens::__TOKEN_LIST__);
            }
            MacroKnownRuleKeys::LITERAL => {
                let (token, _) = visitor:.readToken ();
                visitor:.readLiteral (token);
            }
            MacroKnownRuleKeys::OPERAND => {
                visitor:.readOperand0 ();
            }
            MacroKnownRuleKeys::EXPR => {
                visitor:.readExpression ();
            }
            _ => panic;
        }

        let seek = visitor:.getLexer ().getSeek ();
        let (inMap, rest) = if (seek < str.len) {
             (str [0 .. seek],
              str [seek .. $])
        } else {
            (str, "")                         
        };
                
        (MacroMapper (inMap), rest)
    }


    /**
     * Validate a pre constructed macro identifier rule that read a char
     * @params:
     *    - context: the context of the validation
     *    - loc: the location of the call
     *    - msym: the macro symbol being called
     *    - or: the or expression being extracted
     *    - str: the string used to validate the mult expression
     *    - skips: the expression used to remove skip tokens
     * @returns:
     *    - .0: the macro mapper generated from that validation
     *    - .1: the rest of the string that does not work with the macro mult
     * @throws:
     *    - &ErrorMsg: if the validation failed
     * */
    fn validateMacroIdentChar (self, dmut _ : &Validator, loc : &Word, _ : &MacroRefValue, mid : &MacroIdentExpr, str : [c8], full : [c8])-> (MacroMapper, [c8])
        throws ErrorMsg
    {
        if (str.len == 0us) {
            let eLoc = self.computeLoc (loc, full, str);
            throw copy ErrorMsg::fatal (eLoc, end-> mid.getLoc (), format (ValidateErrorMessage::MACRO_DOES_NOT_MATCH, str, MacroKnownRuleKeys::ANY));
        }

        let ch = str [0];
        let res = if (ch == '\"') { "\\\"" }
        else if ch == '\'' { "\\\'" }
        else { copy [ch] };

        (MacroMapper (res), str [1us .. $])
    }

    /**
     * Validate a pre constructed macro identifier rule that read anything
     * @params:
     *    - context: the context of the validation
     *    - loc: the location of the call
     *    - msym: the macro symbol being called
     *    - or: the or expression being extracted
     *    - str: the string used to validate the mult expression
     *    - skips: the expression used to remove skip tokens
     * @returns:
     *    - .0: the macro mapper generated from that validation
     *    - .1: the rest of the string that does not work with the macro mult
     * @throws:
     *    - &ErrorMsg: if the validation failed
     * */
    fn validateMacroIdentAny (self, dmut _ : &Validator, loc : &Word, _ : &MacroRefValue, mid : &MacroIdentExpr, str : [c8], full : [c8])-> (MacroMapper, [c8])
        throws ErrorMsg
    {
        if (str.len == 0us) {
            let eLoc = self.computeLoc (loc, full, str);
            throw copy ErrorMsg::fatal (eLoc, end-> mid.getLoc (), format (ValidateErrorMessage::MACRO_DOES_NOT_MATCH, str, MacroKnownRuleKeys::ANY));
        }

        (MacroMapper (str [0 .. 1]), str [1us .. $])
    }

    /**
     * Transform a list of string into a macro mult expression
     * @params:
     *    - context: the context of the validation
     *    - skips: the list of skip tokens
     * @returns: a macro mult expression (or empty if no skips)
     * @throws:
     *    - &ErrorMsg: if the skip tokens are invalid
     * */
    fn validateSkips (self, dmut context : &Validator, skips : [&Expression])-> &Expression
        throws ErrorMsg
    {
        let mut next : &Expression = EMPTY_EXPR;
        for i, it in skips {
            if i == 0us { next = it; }
            else {
                match (next, it) {
                    (n : &MacroRuleExpression, p : &MacroTokenExpr) => {
                        context:.validateValue (p.getContent ());
                        next = copy MacroOrExpr (it.getLoc (), n, p);
                    }
                    _ => panic;
                }
            }
        }

        match next {
            n : &MacroRuleExpression => {
                copy MacroMultExpr (n.getLoc (),
                                    end-> n.getLoc (),
                                    copy [n],
                                    mult-> next.getLoc ().locate (Tokens::STAR))
            }
            _ => next
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * =======================            REWRITTING            =======================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Rewrite the content of a macro using a mapper and return it
     * @params:
     *    - loc: the location of the call
     *    - content: the content of the macro to rewrite
     *    - mapper: the mapper used to rewrite
     * */
    fn rewriteMapper (self, content : [c8], mapper : MacroMapper, ruleIndex : usize)-> [c8]
        throws ErrorMsg
    {
        MacroRewriter (mapper, ruleIndex).rewrite (content)
    }

    /**
     * Compute the location of the end of content, where loc is located at the beginning of it
     * */
    fn computeLoc (self, loc : &Word, content : [c8], end : [c8])-> &Word {
        let mut line = loc.line, mut col = loc.col + 2u64; // "#{, #[, #("
        let mut lineSeek = loc.lineSeek;
        for i in 0us .. (content.len - end.len) {
            if (content [i] == '\n') {
                line += 1u64;
                lineSeek += cast!usize (col);
                col = 1u64;
            } else col += 1u64;
        }

        loc.locate (end, line-> line, col-> col, lineSeek-> lineSeek)
    }

    /**
     * Shorten the text used to
     * */
    fn shorten (self, str : [c8])-> [c8] {
        let maxLen = if (str.len < 10us) { str.len } else { 10us };
        let mut curr = maxLen;

        for i in 0us .. maxLen {
            let br = if str [i] == '\n' { true }
            else { false };

            if (br) {
                curr = i;
                break;
            }
        }

        if (curr != str.len) {
            str [0us .. curr] ~ " ..."
        } else {
            str
        }
    }

    /*!
     * ================================================================================
     * ================================================================================
     * ======================            FINALIZATION            ======================
     * ================================================================================
     * ================================================================================
     */


    /**
     * Finalize a macro content syntax, and validate it into a value
     * @params:
     *   - context: the context of the validation
     *   - content: the string ymir code to validate
     * @returns: the validated value
     * @throws:
     *    - &ErrorMsg: if the validation failed
     * */
    fn finalizeMacro (self, dmut context : &Validator, loc : &Word, ctorLoc : &Word, macVal : &MacroRefValue, content : [c8])-> &Value
        throws ErrorMsg
    {
        let centering = utils::string::center ("", utils::string::maxLineLength (content), "=");
        let dmut visitor = copy SyntaxVisitor::fromStr (loc.filename, "{" ~ content ~ "}");
        let expr = {
            visitor:.readExpression ()
        } catch {
            err : &ErrorMsg => {
                throw copy ErrorMsg::fatal (loc, format (ValidateErrorMessage::IN_MACRO_FINALIZATION, centering, content, centering),
                                            notes-> copy [copy ErrorMsg::note (ctorLoc, format (ValidateErrorMessage::DEFINED_HERE)), err]);
            }
        }

        let dmut macroRef = context:.getReferent ();
        context:.pushReferent (alias context:.getSymbolFromRef (macVal.getSymbol ()), macroSym-> alias macroRef);
        // context:.enterForeign (isFnBody-> true);
        let value = {
            context:.validateValue (expr)
        } catch {
            err : &ErrorMsg => {
                throw copy ErrorMsg::fatal (loc, format (ValidateErrorMessage::IN_MACRO_FINALIZATION, centering, content, centering),
                                            notes-> copy [copy ErrorMsg::note (ctorLoc, format (ValidateErrorMessage::DEFINED_HERE)), err]);
            }
        }

        // context:.exitForeign ();
        context:.popReferent ();

        value
    }


}
