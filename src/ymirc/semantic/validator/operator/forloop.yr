mod ymirc::semantic::validator::operator::forloop;

import std::io;
import std::collection::vec;

import ymirc::semantic::validator::visitor;
import ymirc::semantic::validator::errors;
import ymirc::semantic::validator::template::_;

import ymirc::errors::_;
import ymirc::utils::format;
import ymirc::semantic::generator::value::_;
import ymirc::syntax::expression::_;
import ymirc::syntax::keys;
import ymirc::lexing::word;

import ymirc::semantic::generator::type::_;

/**
 * The For loop validator is used to validate ForLoopExpr
 * It is a class validator because for loop operations are complex
 */
pub class ForLoopValidator {

    /**
     * Create an empty validator     
     */
    pub self () {}
    
    /**
     * Validate a for loop expression
     * @params: 
     *  - context: the context of the validation
     *  - f: the for loop to validate
     * @returns: a value
     * @throws:
     *   - &ErrorMsg: if the validation failed
     */
    pub fn validate (self, dmut context : &Validator, f : &ForLoopExpr) -> &Value
        throws &ErrorMsg
    {
        let value = context:.validateValue (f.getValue ());
        match value.getType () {
            t : &TupleType => { self.validateTupleLoop (alias context, t, value, f) }
            s : &SliceType => { self.validateSliceLoop (alias context, s, value, f) }
            a : &ArrayType => { self.validateArrayLoop (alias context, a, value, f) }
            r : &RangeType => { self.validateRangeLoop (alias context, r, value, f) }
            _ => {
                throw ErrorMsg::fatal (f.getLoc (), format (ValidateErrorMessage::UNDEF_FOR_LOOP_OPERATOR, f.getVariables ().len, value.getType ()));
            }
        }
    }

    /**
     * Validate a for loop expression that is unrolled at cte
     * @params:
     *    - context: the context of the compilation
     *    - f: the for loop to validate
     * @returns: a block containing the unrolled for loop
     * @throws:
     *    - &ErrorMsg: if the validation failed
     * */
    pub fn validateCteForLoop (self, dmut context : &Validator, f : &ForLoopExpr)-> &Value
        throws &ErrorMsg
    {
        let value = context:.validateValue (f.getValue ());
        match value.getType () {
            t : &TupleType => { self.validateTupleLoop (alias context, t, value, f) }
            r : &RangeType => { self.validateCteRangeLoop (alias context, r, value, f) }
            _ => {
                throw ErrorMsg::fatal (f.getLoc (), format (ValidateErrorMessage::UNDEF_CTE_FOR_LOOP_OPERATOR, f.getVariables ().len, value.getType ()));
            }
        }
    }

    /**
     * Validate a slice for loop expression
     * @params:
     *    - context: the context of the validation
     *    - all: the slice allocator to validate
     * @returns: a value
     * @throws:
     *    - &ErrorMsg, if the validation failed
     * */
    pub fn validateListComprehension (self, dmut context : &Validator, all : &SliceForAllocatorExpr)-> &Value
        throws &ErrorMsg
    {
        let value = context:.validateValue (all.getIter ());
        match value.getType () {
            t : &TupleType => { self.validateComprTuple (alias context, t, value, all) }
            s : &SliceType => { self.validateComprSlice (alias context, s, value, all) }
            r : &RangeType => { self.validateComprRange (alias context, r, value, all) }
            _ => {
                throw ErrorMsg::fatal (all.getForLoc (), format (ValidateErrorMessage::UNDEF_FOR_LOOP_OPERATOR, all.getVariables ().len, value.getType ()));
            }
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================        LOOP ON TUPLES        =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Validate a for loop on a tuple
     * @params: 
     *    - context: the context of the validation
     *    - type: the type of the tuple
     *    - value: the value to iterate
     *    - f: the for loop to validate
     * @throws: 
     *    - &ErrorMsg: if the validation failed
     * @returns: the validated loop
     */
    fn validateTupleLoop (self, dmut context : &Validator, type : &TupleType, value : &Value, f : &ForLoopExpr)-> &Value
        throws &ErrorMsg
    {
        let vars = f.getVariables ();
        let uniqV = context:.createUniqValue (value);
        match vars.len {
            1us => {
                return self.validateTupleIteration (alias context, type, uniqV, f, vars[0], EmptyExpression::new ());
            }
            2us => {
                return self.validateTupleIteration (alias context, type, uniqV, f, vars[0], vars[1]);
            }
            _ => {
                throw ErrorMsg::fatal (f.getLoc (), format (ValidateErrorMessage::UNDEF_FOR_LOOP_OPERATOR, vars.len, type));
            }
        }
    }

    /**
     * Validate a for loop iteration on a tuple
     * @params:
     *     - context: the context of the validation
     *     - type: the type of the tuple
     *     - value: the value of the tuple (already uniq)
     *     - f: the for loop
     *     - iter1: the declaration of the first iteration variable
     *     - iter2: the declaration of the second iteration variable (might be EmptyExpression)
     * @throws:
     *     - &ErrorMsg: if the validation failed
     * @returns: the validated loop
     * */
    fn validateTupleIteration (self, dmut context : &Validator, type : &TupleType, value : &Value, f : &ForLoopExpr, iter1 : &Expression, iter2 : &Expression)-> &Value
        throws &ErrorMsg
    {
        let dmut content = Vec!{&Value}::new ();
        context:.enterLoop (); // Even if it is unwinded at compile time, it should behave like a loop (so we can break in it)
        context:.setCurrentLoopType (VoidType::new (f.getLoc ())); // it has no type, (because there is no else part for the moment ?, in theory it is no necessary if we check that there is at least one value in the tuple?)
        {
            for i in 0us .. type.getInners ().len { // we validate the content of the loop for each type of the tuple
                content:.push (self.validateTupleInnerLoop (alias context, i, iter1, iter2, type, value, f.getBlock ()));
            }
        } catch {
            err : &ErrorMsg => {
                context:.quitLoop (); // we must not forget to quit the loop
                throw err;
            }
        }

        context:.quitLoop ();
        let inner = BlockValue::new (f.getLoc (), VoidType::new (f.getLoc ()), content[]);
        LoopValue::new (f.getLoc (), VoidType::new (f.getLoc ()), NONE_VALUE, inner, false, isCteFor-> true)
    }

    /**
     * Validate the inner content of a loop iteration over a tuple value
     * @params: 
     *     - context: the context of the validation
     *     - i: the index of the current iteration 
     *     - vdecl: the variable iterator to declare
     *     - type: the type of the tuple to iterate
     *     - value: the content of the tuple value
     *     - blk: the content of the for loop
     * @returns: the content of the for loop validated for th i-th iteration
     * @throws:
     *    - &ErrorMsg: if the validation failed
     */
    fn validateTupleInnerLoop (self, dmut context : &Validator, i : usize, iter1 : &Expression, iter2 : &Expression, type : &TupleType, value : &Value, blk : &Expression)-> &Value
        throws &ErrorMsg
    {
        let dmut errors = Vec!{&ErrorMsg}::new ();        
        context:.enterBlock ();
        let res = { // Simply validate the content of a single iteration
            match (iter1, iter2) {
                (vdecl : &VarDeclExpr, EmptyExpression ()) => {
                    let (_, iterValue) = context:.getCompileTimeInterpreter ().simplify (TupleFieldAccessValue::new (vdecl.getLoc (), type.getInners ()[i], value, i));
                    let var = self.validateIterDecl (alias context, value.getLoc (), vdecl, type.getInners ()[i], value-> iterValue, globType-> type, canCte-> true);
                    if (vdecl.getLoc () != Keys::UNDER) context:.insertLocal (vdecl.getLoc ().str (), var); // we declare the variable so we can retreive it inside the loop

                    let content = context:.validateValue (blk); // we validate the content of the loop
                    BlockValue::new (blk.getLoc (), VoidType::new (blk.getLoc ()), [var, content]) // and we return the loop iteration block
                }
                (idecl : &VarDeclExpr, vdecl : &VarDeclExpr) => {
                    let (_, iterValue) = context:.getCompileTimeInterpreter ().simplify (TupleFieldAccessValue::new (vdecl.getLoc (), type.getInners ()[i], value, i));
                    let var = self.validateIterDecl (alias context, value.getLoc (), vdecl, type.getInners ()[i], value-> iterValue, globType-> type, canCte-> true);
                    if (vdecl.getLoc () != Keys::UNDER) context:.insertLocal (vdecl.getLoc ().str (), var);

                    let index = self.validateIterDecl (alias context, value.getLoc (), idecl, IntType::new (idecl.getLoc (), size-> 0u16, signed-> false), value-> usize (idecl.getLoc (), i), globType-> type, canCte-> true);
                    if (idecl.getLoc () != Keys::UNDER) context:.insertLocal (idecl.getLoc ().str (), index);

                    let content = context:.validateValue (blk); // we validate the content of the loop
                    BlockValue::new (blk.getLoc (), VoidType::new (blk.getLoc ()), [var, index, content])
                }
                _ => __pragma!panic ();
            }
        } catch {
            err: &ErrorMsg => {
                errors:.push (err);
                NONE_VALUE
            }
        }

        {
            context:.quitBlock (warnUnused-> errors.len () == 0us); // if there was an error, we don't add the warn unused
        } catch { 
            err : &ErrorMsg => errors:.push (err); 
        }

        if (errors.len () != 0us) throw ErrorMsg::list (errors[]);
        res
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================        LOOP ON ARRAYS        =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Validate a for loop operation on an array value
     * @params:
     *    - context: the context of the validation
     *    - arrType: the type of the value
     *    - value: the value being iterated
     *    - content: the expression to validate
     * @returns: a validate value
     * @throws:
     *     - &ErrorMsg: if the validation failed
     * */
    fn validateArrayLoop (self, dmut context : &Validator, arrType : &ArrayType, value : &Value, content : &ForLoopExpr)-> &Value
        throws &ErrorMsg
    {
        let vars = content.getVariables ();
        let loc = content.getLoc ();

        let ptr = PointerType::new (loc, arrType.getInners ()[0], isMutable-> false);
        let slcT = SliceType::new (loc, arrType.getInners ()[0], isMutable-> false);

        let uniqV = SliceCtorValue::new (content.getLoc (), slcT, AddressValue::new (loc, ptr, value), arrType.getSize ());
        match vars.len {
            1us => // only the value of the array
                return self.validateSliceIteration (alias context, slcT, uniqV, content, vars[0], EmptyExpression::new ());
            2us => // The value of the array and the iterator
                return self.validateSliceIteration (alias context, slcT, uniqV, content, vars[0], vars[1]);
            _ => { // no iteration is possible with three variables
                throw ErrorMsg::fatal (content.getLoc (), format (ValidateErrorMessage::UNDEF_FOR_LOOP_OPERATOR, vars.len, arrType));
            }
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================        LOOP ON SLICES        =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Validate a for loop iterating through a slice value
     * @params: 
     *     - context: the context of the validation
     *     - slcType: the type of the iteration
     *     - value: the value that is iterated
     *     - content: the content of the iteration to validate
     */
    fn validateSliceLoop (self, dmut context : &Validator, slcType : &SliceType, value : &Value, content : &ForLoopExpr)-> &Value
        throws &ErrorMsg
    {
        let vars = content.getVariables ();
        match vars.len {
            1us => // only the value of the array
                return self.validateSliceIteration (alias context, slcType, value, content, vars[0], EmptyExpression::new ());
            2us => // The value of the array and the iterator
                return self.validateSliceIteration (alias context, slcType, value, content, vars[0], vars[1]);
            _ => { // no iteration is possible with three variables
                throw ErrorMsg::fatal (content.getLoc (), format (ValidateErrorMessage::UNDEF_FOR_LOOP_OPERATOR, vars.len, slcType));
            }
        }
    }

    /**
     * Validate the iteration over the slice
     * @params: 
     *    - context: the context of the validation
     *    - slcType: the type of the slice of the iteration
     *    - value: the value of the slice of the iteration
     *    - content: the for loop containing the ocntent of the iteration
     *    - iter1: the first iteration variable (never empty)
     *    - iter2: the second iteration variable (might be empty)
     * @returns: the value of the iteration
     * @throws:
     *    - &ErrorMsg: if the iteration cannot be validated
     */
    fn validateSliceIteration (self, dmut context : &Validator, slcType : &SliceType, sliceValue : &Value, content : &ForLoopExpr, iter1 : &Expression, iter2 : &Expression)-> &Value
        throws &ErrorMsg
    {
        let dmut errors = Vec!{&ErrorMsg}::new ();
        let dmut innerValues = Vec!{&Value}::new ();
        let dmut values = Vec!{&Value}::new ();
        let loc = content.getLoc ();
        let mut test : &Value = NONE_VALUE;

        context:.enterBlock ();
        {
            // First we create the iteration variables, that will store the value at the current array index, and the value of the index
            let vars = self.validateSliceIteratorVars (alias context, sliceValue.getLoc (), slcType, iter1, iter2);
            let sliceDecl = VarDeclValue::new (Word::new ("#_value"s8, sliceValue.getLoc ()), slcType, sliceValue, isMutable-> false, isPure-> false);
            let sliceRef = VarRefValue::new (sliceDecl.getLoc (), sliceDecl.getLoc (), slcType, false, sliceDecl.getUniqId (), isSelf-> false, isReference-> false, canCte-> false);

            let array = vars._0; // the value inside the array
            let iter = vars._1; // the value of the current index
            values:.push (sliceDecl);
            values:.push (vars._2);
            values:.push (vars._3);
            
            let one = usize (loc, 1us); // used to increment the iterator
            let len = StructFieldAccessValue::new (loc, one.getType (), sliceRef, SliceKeys::LEN); // access the size of the array to verify test when to exit the loop
            
            test = CmpIntOperatorValue::new (loc, BinaryOperators::INF, iter, len); // if the iterator is greater than the value of the array the loop is stopped
            let affect = AffectValue::new (loc, array.getType (), array, SliceAccessValue::new (loc, array.getType (), sliceRef, iter)); // affect the current value of the array to the index
            let increment = AffectValue::new (loc, iter.getType (), iter, BinaryMathIntOperatorValue::new (loc, iter.getType (), BinaryOperators::PLUS, iter, one)); // increment the iterator for the next iteration

            // the loop has three steps: 
            innerValues:.push (affect); // 1. get the current value of the array
            innerValues:.push (context:.validateValue (content.getBlock ())); // execute the content of the loop
            innerValues:.push (increment); // go to the next iteration
        } catch {
            err : &ErrorMsg => { errors:.push (err); } // we catch everything because we need to exit the block
        }
    
        let blkContent = BlockValue::new (loc, VoidType::new (loc), innerValues[]); // create the loop 
        let _loop = LoopValue::new (loc, VoidType::new (loc), test-> test, blkContent, isDo-> false);
        values:.push (_loop);

        {
            context:.quitBlock (warnUnused-> errors.len () == 0us); // we only warn if there was no error in the loop content
        } catch {
            err : &ErrorMsg => errors:.push (err); // some warn unused
        }

        if (errors.len () != 0us) throw ErrorMsg::list (errors[]);
        BlockValue::new (loc, VoidType::new (loc), values[])
    }

    /**
     * Create the iteration variable (and verify that they are correctly defined)
     * @params: 
     *    - context: the context of the validaiton
     *    - loc: the location of the value iterated
     *    - slcType: the type of the iteration
     *    - iter1: the iterator (value if iter2 is empty, index otherwise)
     *    - iter2: value if not empty
     * @returns:
     *    - .0: the ref to the value variable 
     *    - .1: the ref to the index variable (even if iter2 is empty)
     *    - .2: the vardecl of the value variable
     *    - .3: the vardecl of the index variable (even if iter1 is empty)
     * @throws:
     *    - &ErrorMsg: if the variables are uncorrectly defined
     */
    fn validateSliceIteratorVars (self, dmut context : &Validator, loc : &Word, slcType : &SliceType, iter1 : &Expression, iter2 : &Expression)-> (&Value, &Value, &Value, &Value)
        throws &ErrorMsg
    {
        // This function can seem a bit complex but isn't
        match (iter1, iter2) { // we have two cases, (with one variable, or with two)            
            (v : &VarDeclExpr, EmptyExpression ()) => { // 1. with one
                let array = self.validateIterDecl (alias context, loc, v, slcType.getInners ()[0].clone (0u32), slcType, canCte-> false);

                // And the variable for the iterator, with an name that cannot be taken elsewhere, but this is not important
                let iter = VarDeclValue::new (Word::new ("#_iter"s8, v.getLoc ()), IntType::new (v.getLoc (), size-> 0u16, signed-> false), usize (v.getLoc (), 0us), isMutable-> false, isPure-> false);
                if (v.getLoc () != Keys::UNDER) context:.insertLocal (v.getLoc ().str (), array); // The variable must be retreivable, so we insert it inside the context, (but not the iterator that is hidden)               

                let vrefArray = VarRefValue::new (v.getLoc (), array.getLoc (), array.getVarType (), false, array.getUniqId (), isSelf-> false, isReference-> false, canCte-> false);
                let vrefIter = VarRefValue::new (iter.getLoc (), iter.getLoc (), iter.getVarType (), false, iter.getUniqId (), isSelf-> false, isReference-> false, canCte-> false);
                
                return (vrefArray, vrefIter, array, iter); // we return the variable for the loop validation
            }
            (i : &VarDeclExpr, v : &VarDeclExpr) => {
                let array = self.validateIterDecl (alias context, loc, v, slcType.getInners ()[0].clone (0u32), slcType, canCte-> false);
                let iter = self.validateIterDecl (alias context, loc, i, IntType::new (i.getLoc (), size-> 0u16, signed-> false), value-> usize (i.getLoc (), 0us), slcType, canCte-> false);

                if (v.getLoc () != Keys::UNDER) context:.insertLocal (v.getLoc ().str (), array); // insert them, so we can retreive them
                if (i.getLoc () != Keys::UNDER) context:.insertLocal (i.getLoc ().str (), iter);

                let vrefArray = VarRefValue::new (v.getLoc (), array.getLoc (), array.getVarType (), false, array.getUniqId (), isSelf-> false, isReference-> false, canCte-> false);
                let vrefIter = VarRefValue::new (iter.getLoc (), iter.getLoc (), iter.getVarType (), false, iter.getUniqId (), isSelf-> false, isReference-> false, canCte-> false);
                
                return (vrefArray, vrefIter, array, iter); // and we return them for the loop valiation
            }
            _ => __pragma!panic (); // panic if we get something else
        }
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================        LOOP ON RANGES        =========================
     * ================================================================================
     * ================================================================================
     */


    /**
     * Validate a for loop on a range value
     * @params: 
     *    - context: the context of the validation
     *    - rngType: the type of the range 
     *    - value: the value to iterate
     *    - f: the for loop to validate
     * @returns: the validated loop
     * @throws:
     *    - &ErrorMsg: if the validation fails
     */
    fn validateRangeLoop (self, dmut context : &Validator, rngType : &RangeType, value : &Value, f : &ForLoopExpr)-> &Value
        throws &ErrorMsg
    {        
        let vars = f.getVariables ();

        // We can iterate over range with only one variable
        if (vars.len != 1us) throw ErrorMsg::fatal (f.getLoc (), format (ValidateErrorMessage::UNDEF_FOR_LOOP_OPERATOR, vars.len, rngType));

        let dmut errors = Vec!{&ErrorMsg}::new ();
        let dmut innerValues = Vec!{&Value}::new ();
        let dmut values = Vec!{&Value}::new ();
        let loc = f.getLoc ();
        let mut test : &Value = NONE_VALUE;

        context:.enterBlock ();
        {
            let rangeVar = VarDeclValue::new (Word::new ("#_range"s8, value.getLoc ()), value.getType (), value, isMutable-> false, isPure-> false);
            let mut uniqV = VarRefValue::new (rangeVar.getLoc (), rangeVar.getLoc (), rangeVar.getVarType (), value-> value, false, rangeVar.getUniqId (), isSelf-> false, isReference-> false);

            // First we create the variable that will store the iterator
            let (needVar, varRef, varDecl) = self.validateRangeIteratorVar (alias context, rngType, vars[0], uniqV);
            if (needVar) {
                values:.push (rangeVar);
            }

            values:.push (varDecl); // push the var decl before the loop 
            
            // Validate the increment of the iterator, and the test of the loop
            let (increment, _test) = self.validateRangeIteratorIncrement (alias context, rngType, varRef, if (needVar) { uniqV } else { value });

            innerValues:.push (context:.validateValue (f.getBlock ())); // the loop content 
            innerValues:.push (increment); // the loop increment
            test = _test;
        } catch {
            err : &ErrorMsg => { errors:.push (err); }
        }

        let blkContent = BlockValue::new (loc, VoidType::new (loc), innerValues[]); // The content of the loop
        let _loop = LoopValue::new (loc, VoidType::new (loc), test-> test, blkContent, isDo-> false); // The loop

        values:.push (_loop); // push the loop after the declaration of the iterator variable
        {
            context:.quitBlock (warnUnused-> errors.len () == 0us);
        } catch {
            err : &ErrorMsg => errors:.push (err); // Some warn unused
        }

        if (errors.len () != 0us) throw ErrorMsg::list (errors[]);
        BlockValue::new (loc, VoidType::new (loc), values[])
    }

    /**
     * Validate the iterator variable
     * @params: 
     *    - context: the context of the validation
     *    - rngType: the type of the range value
     *    - iterDecl: the declaration of the iterator
     *    - rngValue: the value of the range
     * @returns:
     *    - .0: true iif cannot use only compile time values
     *    - .1: the variable declaration
     *    - .2: the variable ref value
     * @throws:
     *    - &ErrorMsg: if the validation fails
     */
    fn validateRangeIteratorVar (self, dmut context : &Validator, rngType : &RangeType, iterDecl : &Expression, rngValue : &Value)-> (bool, &Value, &Value)
        throws &ErrorMsg
    {
        match iterDecl {
            v : &VarDeclExpr => {
                // We create the variable for the iterator
                let iterType = rngType.getInners ()[0].clone (0u32);
                let (isCte, iterContent) = context:.getCompileTimeInterpreter ():.simplify (StructFieldAccessValue::new (v.getLoc (), iterType, rngValue, RangeKeys::FST));
                let iterValue = self.validateIterDecl (alias context, rngValue.getLoc (), v, iterType, value-> iterContent, rngType, canCte-> false);

                if (v.getLoc () != Keys::UNDER) context:.insertLocal (v.getLoc ().str (), iterValue);
                // And the variable ref so we can use it
                let vRefIter = VarRefValue::new (v.getLoc (), iterValue.getLoc (), iterType, false, iterValue.getUniqId (), isSelf-> false, isReference-> false, canCte-> false);

                return (!isCte, vRefIter, iterValue);
            }
            _ => __pragma!panic ();
        }
    }

    /**
     * Validate the increment of the iterator during an iteration on a range value
     * @params: 
     *    - context: the context of the validation
     *    - rngType: the type of the range
     *    - varRef: the variable containing the iterator
     *    - value: the range value
     * @returns:
     *    - .0: the increment value
     *    - .1: the test for the loop
     */
    fn validateRangeIteratorIncrement (self, dmut context : &Validator, rngType : &RangeType, varRef : &Value, value : &Value)-> (&Value, &Value) {
        let loc = varRef.getLoc ();
        
        let (_, step) = context:.getCompileTimeInterpreter ().simplify (StructFieldAccessValue::new (loc, rngType.getInners ()[1], value, RangeKeys::STEP));
        let (_, scd)  = context:.getCompileTimeInterpreter ().simplify (StructFieldAccessValue::new (loc, rngType.getInners ()[0], value, RangeKeys::SCD));
        let (_, contains) = context:.getCompileTimeInterpreter ().simplify (StructFieldAccessValue::new (loc, BoolType::new (loc), value, RangeKeys::CONTAIN));
       
        let (inf, infEq, sup, supEq) = match rngType.getInners ()[0] {
            IntType () => {
                (cast!{&Value} (CmpIntOperatorValue::new (loc, BinaryOperators::INF, varRef, scd)),
                 cast!{&Value} (CmpIntOperatorValue::new (loc, BinaryOperators::INF_EQUAL, varRef, scd)),
                 cast!{&Value} (CmpIntOperatorValue::new (loc, BinaryOperators::SUP, varRef, scd)),
                 cast!{&Value} (CmpIntOperatorValue::new (loc, BinaryOperators::SUP_EQUAL, varRef, scd)))
            }
            FloatType () => {
                (cast!{&Value} (CmpFloatOperatorValue::new (loc, BinaryOperators::INF, varRef, scd)),
                 cast!{&Value} (CmpFloatOperatorValue::new (loc, BinaryOperators::INF_EQUAL, varRef, scd)),
                 cast!{&Value} (CmpFloatOperatorValue::new (loc, BinaryOperators::SUP, varRef, scd)),
                 cast!{&Value} (CmpFloatOperatorValue::new (loc, BinaryOperators::SUP_EQUAL, varRef, scd)))
            }
            CharType () => {
                (cast!{&Value} (CmpCharOperatorValue::new (loc, BinaryOperators::INF, varRef, scd)),
                 cast!{&Value} (CmpCharOperatorValue::new (loc, BinaryOperators::INF_EQUAL, varRef, scd)),
                 cast!{&Value} (CmpCharOperatorValue::new (loc, BinaryOperators::SUP, varRef, scd)),
                 cast!{&Value} (CmpCharOperatorValue::new (loc, BinaryOperators::SUP_EQUAL, varRef, scd)))
            }
            _ => __pragma!panic ();
        };
                
        // step < 0
        let (_, stepCmp) = context:.getCompileTimeInterpreter ():.simplify (match rngType.getInners ()[1] {
            i : &IntType => {
                CmpIntOperatorValue::new (loc, BinaryOperators::INF, step, intValue (loc, 0us, i.getSize (), true))
            }
            f : &FloatType => {
                CmpFloatOperatorValue::new (loc, BinaryOperators::INF, step, floatValue (loc, d-> 0.0, f.getSize ()))
            }
            c : &CharType => {
                CmpCharOperatorValue::new (loc, BinaryOperators::INF, step, charValue (loc, c-> 0u32, c.getSize ()))
            }
            _ => __pragma!panic ()
        });
        
        // varRef = varRef + cast!(typeof (varRef)) (step)
        let increment = match varRef.getType () {
            i : &IntType => {
                AffectValue::new (loc, i, varRef, BinaryMathIntOperatorValue::new (loc, varRef.getType (), BinaryOperators::PLUS, varRef, CastValue::new (loc, i, step)))
            }
            f : &FloatType => {
                AffectValue::new (loc, f, varRef, BinaryMathFloatOperatorValue::new (loc, varRef.getType (), BinaryOperators::PLUS, varRef, CastValue::new (loc, f, step)))
            }
            c : &CharType => {
                AffectValue::new (loc, c, varRef, BinaryMathCharOperatorValue::new (loc, varRef.getType (), BinaryOperators::PLUS, varRef, CastValue::new (loc, c, step)))
            }
            _ => __pragma!panic ();
        };

        let (innerCondInf, innerCondSup) = match contains {
            b : &BoolValue => {
                if b.isTrue () {
                    (infEq, supEq)
                } else {
                    (inf, sup)
                }
            }
            _ => {
                (cast!{&Value} (ConditionalValue::new (loc, BoolType::new (loc), contains, infEq, elsev-> inf, isComplete-> true)),
                 cast!{&Value} (ConditionalValue::new (loc, BoolType::new (loc), contains, supEq, elsev-> sup, isComplete-> true)))
            }
        }
        
        let test = match (stepCmp) {
            b : &BoolValue => {
                if (b.isTrue ()) {
                    innerCondSup
                } else {
                    innerCondInf
                }
            }
            _ => {
                cast!{&Value} (ConditionalValue::new (loc, BoolType::new (loc), stepCmp, innerCondSup, innerCondInf, isComplete-> true))
            }
        }
                        
        (increment, test)
    }

    /**
     * ================================================================================
     * ================================================================================
     * ========================        COMPREHENSION SLICE        =====================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Validate a list comprehension from a slice value
     * @params:
     *   - context: the context of the validation
     *   - slc: the slice type being iterated
     *   - iter: the value being iterated
     *   - all: the list comprehension to validate
     * @returns: the generated slice/array
     * @throws:
     *   - &ErrorMsg: if the validation failes
     * */
    fn validateComprSlice (self, dmut context : &Validator, slc : &SliceType, iter : &Value, all : &SliceForAllocatorExpr)-> &Value
        throws &ErrorMsg
    {
        context;
        slc;
        iter;
        all;
        __pragma!fake_throw (&ErrorMsg);
        __pragma!panic ();
    }

    /**
     * ================================================================================
     * ================================================================================
     * ========================        COMPREHENSION TUPLE        =====================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Validate a list comprehension from a tuple value
     * @params:
     *   - context: the context of the validation
     *   - tu: the tuple type being iterated
     *   - iter: the value being iterated
     *   - all: the list comprehension to validate
     * @returns: the generated slice/array
     * @throws:
     *   - &ErrorMsg: if the validation failes
     * */
    fn validateComprTuple (self, dmut context : &Validator, tu : &TupleType, iter : &Value, all : &SliceForAllocatorExpr)-> &Value
        throws &ErrorMsg
    {
        context;
        tu;
        iter;
        all;
        __pragma!fake_throw (&ErrorMsg);
        __pragma!panic ();
    }



    /**
     * ================================================================================
     * ================================================================================
     * ========================        COMPREHENSION RANGE        =====================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Validate a list comprehension from a range value
     * @params:
     *   - context: the context of the validation
     *   - ra: the range type being iterated
     *   - iter: the value being iterated
     *   - all: the list comprehension to validate
     * @returns: the generated slice/array
     * @throws:
     *   - &ErrorMsg: if the validation failes
     * */
    fn validateComprRange (self, dmut context : &Validator, ra : &RangeType, iter : &Value, all : &SliceForAllocatorExpr)-> &Value
        throws &ErrorMsg
    {
        context;
        ra;
        iter;
        all;
        __pragma!fake_throw (&ErrorMsg);
        __pragma!panic ();
    }

    /**
     * ================================================================================
     * ================================================================================
     * =============================        CTE RANGE        ==========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Validate a cte for loop operation on an range value
     * @params:
     *    - context: the context of the validation
     *    - rngType: the type of the value
     *    - value: the value being iterated
     *    - content: the expression to validate
     * @returns: a validate value
     * @throws:
     *     - &ErrorMsg: if the validation failed
     * */
    fn validateCteRangeLoop (self, dmut context : &Validator, rngType : &RangeType, value : &Value, f : &ForLoopExpr)-> &Value
        throws &ErrorMsg
    {
        let vars = f.getVariables ();
        if (vars.len != 1us) throw ErrorMsg::fatal (f.getLoc (), format (ValidateErrorMessage::UNDEF_CTE_FOR_LOOP_OPERATOR, vars.len, rngType));

        let dmut errors = Vec!{&ErrorMsg}::new ();
        let dmut innerValues = Vec!{&Value}::new ();
        let loc = f.getLoc ();

        context:.enterBlock ();
        {
            let start = context:.getCompileTimeInterpreter ().compute (StructFieldAccessValue::new (loc, rngType.getInners ()[0], value, RangeKeys::FST));
            let step = context:.getCompileTimeInterpreter ().compute (StructFieldAccessValue::new (loc, rngType.getInners ()[1], value, RangeKeys::STEP));
            let contains = context:.getCompileTimeInterpreter ().computeBool (StructFieldAccessValue::new (loc, BoolType::new (loc), value, RangeKeys::CONTAIN));
            let end = context:.getCompileTimeInterpreter ().compute (StructFieldAccessValue::new (loc, rngType.getInners ()[0], value, RangeKeys::SCD));

            let mut current = start;

            loop {
                let (isEnding, next) = self.checkEndCteRangeLoop (alias context, f.getLoc (), contains, current, end, step, rngType);
                if (!isEnding) {
                    let block = self.createCurrentCteBlock (alias context, f.getBlock (), rngType, vars, [current]);
                    innerValues:.push (context:.validateValue (block));
                    current = next;
                    if (current.isOf!{&NoneValue} ()) break {}
                } else break {}
            }
        } catch {
            err : &ErrorMsg => { errors:.push (err); }
        }

        {
            context:.quitBlock (warnUnused-> errors.len () == 0us);
        } catch {
            err : &ErrorMsg => errors:.push (err); // Some warn unused
        }

        if (errors.len () != 0us) throw ErrorMsg::list (errors[]);
        BlockValue::new (loc, VoidType::new (loc), innerValues[])
    }

    /**
     * Check wether the current iteration is past the end of the range value
     * @params:
     *    - context: the context of the validation
     *    - loc: the location of the loop
     *    - contains: true iif the end value is contained in the range
     *    - current: the current iteration value
     *    - end: the end of the range value
     *    - rngType: the type of the range
     * */
    fn checkEndCteRangeLoop (self, dmut context : &Validator, loc : &Word, contains : bool, current : &Value, end : &Value, step : &Value, rngType : &RangeType)-> (bool, &Value)
        throws &ErrorMsg
    {
        let (inf, infEq, sup, supEq) = match rngType.getInners ()[0] {
            IntType () => {
                (cast!{&Value} (CmpIntOperatorValue::new (loc, BinaryOperators::INF, current, end)),
                 cast!{&Value} (CmpIntOperatorValue::new (loc, BinaryOperators::INF_EQUAL, current, end)),
                 cast!{&Value} (CmpIntOperatorValue::new (loc, BinaryOperators::SUP, current, end)),
                 cast!{&Value} (CmpIntOperatorValue::new (loc, BinaryOperators::SUP_EQUAL, current, end)))
            }
            FloatType () => {
                (cast!{&Value} (CmpFloatOperatorValue::new (loc, BinaryOperators::INF, current, end)),
                 cast!{&Value} (CmpFloatOperatorValue::new (loc, BinaryOperators::INF_EQUAL, current, end)),
                 cast!{&Value} (CmpFloatOperatorValue::new (loc, BinaryOperators::SUP, current, end)),
                 cast!{&Value} (CmpFloatOperatorValue::new (loc, BinaryOperators::SUP_EQUAL, current, end)))
            }
            CharType () => {
                (cast!{&Value} (CmpCharOperatorValue::new (loc, BinaryOperators::INF, current, end)),
                 cast!{&Value} (CmpCharOperatorValue::new (loc, BinaryOperators::INF_EQUAL, current, end)),
                 cast!{&Value} (CmpCharOperatorValue::new (loc, BinaryOperators::SUP, current, end)),
                 cast!{&Value} (CmpCharOperatorValue::new (loc, BinaryOperators::SUP_EQUAL, current, end)))
            }
            _ => __pragma!panic ();
        };

        let stepCmp = context:.getCompileTimeInterpreter ():.computeBool (match rngType.getInners ()[1] {
            i : &IntType => {
                CmpIntOperatorValue::new (loc, BinaryOperators::INF, step, intValue (loc, 0us, i.getSize (), true))
            }
            f : &FloatType => {
                CmpFloatOperatorValue::new (loc, BinaryOperators::INF, step, floatValue (loc, d-> 0.0, f.getSize ()))
            }
            c : &CharType => {
                CmpCharOperatorValue::new (loc, BinaryOperators::INF, step, charValue (loc, c-> 0u32, c.getSize ()))
            }
            _ => __pragma!panic ()
        });

        let isLooping = context:.getCompileTimeInterpreter ().computeBool (match (contains, stepCmp) {
            (true, true)  => { supEq }
            (true, false) => { infEq }
            (false, true) => { sup }
            _   => { inf }
        });

        {
            // varRef = varRef + cast!(typeof (varRef)) (step)
            let increment = context:.getCompileTimeInterpreter ().compute (match rngType.getInners ()[0] {
                i : &IntType => {
                    cast!{&Value} (CastValue::new (loc, i, BinaryMathIntOperatorValue::new (loc, i, BinaryOperators::PLUS, CastValue::new (loc, step.getType (), current), step)))
                }
                f : &FloatType => {
                    cast!{&Value} (BinaryMathFloatOperatorValue::new (loc, f, BinaryOperators::PLUS, current, step))
                }
                c : &CharType => {
                    cast!{&Value} (CastValue::new (loc, c, BinaryMathIntOperatorValue::new (loc, step.getType (), BinaryOperators::PLUS, CastValue::new (loc, step.getType (), current), step)))
                }
                _ => __pragma!panic ();
            });

            (!isLooping, increment)
        } catch {
            err : _ => { // can fail if it reach the end of the encoding capacities of the step type, thus there is no next value
                throw err;
                // (!isLooping, NoneValue::new ())
            }
        }
    }



    /**
     * ================================================================================
     * ================================================================================
     * =============================        COMMON        =============================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Validate a variable decl from iteration variable
     * @params:
     *     - context: the context of the validation
     *     - loc: the location of the iteration
     *     - vdecl: the variable being declared
     *     - type: the expected type of the resutl value
     *     - globType: the type being iterated
     *     - withDeco: true if the variable can have decorators
     * @returns: a vardecl value
     * */
    fn validateIterDecl (self, dmut context : &Validator, loc : &Word, vdecl : &VarDeclExpr, type : &Type, value : &Value = NONE_VALUE, globType : &Type, withDeco : bool = false, canCte : bool = true)-> &VarDeclValue
        throws &ErrorMsg
    {
        if (!withDeco) {
            if (context.findDecos (vdecl.getDecos (), Decorators::__members__)) {
                throw ErrorMsg::fatal (vdecl.getLoc (), format (ValidateErrorMessage::FORBID_DECO_FOR_LOOP, globType));
            }
        }

        let isPure = context.findDecos (vdecl.getDecos (), [Decorators::PURE]);
        let isMutable = context.findDecos (vdecl.getDecos (), [Decorators::MUT, Decorators::DMUT]);

        let vType = if (!vdecl.getType ().isOf!{&EmptyExpression} ()) {
            let vtype = context:.validateType (vdecl.getType (), decos-> vdecl.getDecos (), canBeRef-> false)._1;
            context:.verifyCompatibleType (vdecl.getType ().getLoc (), loc, vtype, type);
            vtype
        } else {
            type
        }

        if (vdecl.getLoc () != Keys::UNDER) context:.verifyShadow (vdecl.getLoc ());

        VarDeclValue::new (vdecl.getLoc (), vType, value, isMutable-> isMutable, isPure-> isPure, isCte-> canCte)
    }

    /**
     * Create the block of a for loop that has to be validated at cte time
     * @params:
     *    - context: the context of the validation
     *    - block: the block template
     *    - globType: the type being iterated
     *    - vars: the list of variables used to iterate (assuming its len is equal to the len of values)
     *    - values: the list of values to assign to vars
     * @returns: an expression
     * */
    fn createCurrentCteBlock (self, dmut context : &Validator, block : &Expression, globType : &Type, vars : [&Expression], values : [&Value])-> &Expression
        throws &ErrorMsg
    {
        let dmut mapper = TemplateMapper::new ();
        for i in 0us .. vars.len match vars [i] {
            v : &VarDeclExpr => {
                if (context.findDecos (v.getDecos (), Decorators::__members__)) {
                    throw ErrorMsg::fatal (v.getLoc (), format (ValidateErrorMessage::FORBID_DECO_FOR_LOOP, globType));
                }

                if (!v.getType ().isOf!{&EmptyExpression} ()) {
                    let vtype = context:.validateType (v.getType (), decos-> v.getDecos (), canBeRef-> false)._1;
                    context:.verifyCompatibleType (v.getType ().getLoc (), values [i].getLoc (), vtype, values [i].getType ());
                }

                mapper:.insert (v.getLoc ().str (), ValueWrapperExpr::new (values [i].getLoc (), values [i]));
            }
            _ => __pragma!panic ();
        }

        let rewriter = TemplateRewriter::new (mapper);
        rewriter.rewrite (block)
    }


}
