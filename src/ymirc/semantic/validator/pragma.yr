mod ymirc::semantic::validator::pragma;

import ymirc::syntax::expression::_;
import ymirc::semantic::validator::visitor;
import ymirc::semantic::validator::errors;

import ymirc::semantic::generator::_;
import ymirc::errors::_;
import ymirc::utils::format;
import ymirc::lexing::word;
import ymirc::lexing::tokens;
import ymirc::syntax::keys;

import std::io;

/**
 * The validator responsible for the validation of '__pragma' expressions
 */
pub class PragmaValidator {

    /**
     * Create a new empty validator
     */
    pub self () {}


    /**
     * Validate a pragma expression 
     * @params:
     *    - context: the context of the validation
     *    - p: the pragma expression to validate
     * @returns: the validated expression
     */
    pub def validate (self, dmut context : &Validator, p : &PragmaExpr)-> &Value
        throws &ErrorMsg
    {
        match p.getLoc ().str () {
            PragmaKeys::COMPILE => {
                self.validateCompile (alias context, p)
            }
            PragmaKeys::MSG => {
                self.validateMessage (alias context, p)
            }
            PragmaKeys::OPERATOR => {
                self.validateOperator (alias context, p)
            }
            _ => {
                throw ErrorMsg::fatal (p.getLoc (), format (ValidateErrorMessage::UNKNOWN_PRAGMA, p.getLoc ().str ()));
            }
        }
    }
    
    /**
     * Tries to validate the content of the pragma 
     * @params:
     *    - context: the context of the validation
     *    - p: the pragma to validate
     * @returns: a bool value containing true iif the inner values compiles, containing false otherwise
     */
    def validateCompile (self, dmut context : &Validator, p : &PragmaExpr)-> &Value {    
        
        for v in p.getParameters ()
        {            
            let _ =
                (cast!{&Generator} (context:.validateValue (v))?)
                .getOr (context:.validateType (v));            
        } catch {
            _ => {
                    return BoolValue::new (p.getLoc (), false)
                }
        }
        
        BoolValue::new (p.getLoc (), true)
    }

    /**
     * Validate a message printing at compilation time
     * @params:
     *    - context: the context of the validation
     *    - p: the expression to validate
     * @returns: en none value
     */
    def validateMessage (self, dmut context : &Validator, p : &PragmaExpr)-> &Value
        throws &ErrorMsg
    {
        for v in p.getParameters () {
            let mut msg = context:.getCompileTimeInterpreter ().compute (context:.validateValue (v));
            match msg {
                a : &AliaserValue => { msg = a.getInnerValue (); }
            }
            
            match msg {
                s : &StringValue => {
                    println (s.getUtf8 (), s.getUtf32 ());
                }             
                m : _ => {
                    println (format ("%", m));
                }
            }
        }

        NoneValue::new ()
    }    


    /**
     * Validate a pragma 'operator' to check the existence of an operator between two types (e.g. __pragma!"operator" ("==", i32, i32))
     * @params:
     *     - context: the context of the validation
     *     - p: the pragma to validate
     * @returns: a bool value
     */
    def validateOperator (self, dmut context : &Validator, p : &PragmaExpr)-> &Value
        throws &ErrorMsg
    {
        if (p.getParameters ().len != 2us && p.getParameters ().len != 3us) {
            throw ErrorMsg::fatal (p.getLoc (), format (ValidateErrorMessage::MALFORMED_PRAGMA, p.getLoc ().str ()));
        }
        
        context;

        // let op = context:.getCompileTimeInterpreter ().computeString (context:.validateValue (p.getParameters ()[0]));
        NoneValue::new ()
    }
    
}
