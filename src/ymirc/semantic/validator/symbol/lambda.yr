mod ymirc::semantic::validator::symbol::lambda;

import ymirc::semantic::validator::visitor;
import ymirc::semantic::validator::errors;
import ymirc::syntax::declaration::protection;
import ymirc::syntax::expression::_;
import ymirc::syntax::declaration::_;

import ymirc::syntax::keys;
import ymirc::semantic::generator::_;
import ymirc::errors::_;
import ymirc::utils::format;
import ymirc::lexing::word;

import std::collection::vec;
import std::collection::map;
import std::io, std::fs::path;


/**
 * The lambda validator is the class responsible for lambda symbol validation
 */
pub class LambdaValidator {

    /// The already validated lambdas
    let dmut _validated = HashMap!{[c8], &Value}::new ();
    
    /**
     * Create a new empty validator
     */
    pub self () {}

    /**
     * Validate a lambda symbol
     * @params: 
     *    - context: the context of the validation
     *    - sym: the symbol to validate
     * @returns: the value of the lambda
     * @throws: &ErrorMsg, if the lambda is malformed
     */
    pub fn validate (mut self, dmut context : &Validator, sym : &LambdaValueExpr, isClosure : bool = false)-> &Value
        throws &ErrorMsg
    {
        let name = format ("_%_%", sym.getLoc ().line (), sym.getLoc ().col ());
        let frameName = context:.getReferent ().getPath ().push (name);
        match self._validated.find (frameName.toStr (sep-> "::"s8)) {
            Ok (x : &Value) => return x;
        };
        
        // We start by validating the prototype of the lambda
        // Unlike function prototype it can be uncomplete (missing some var decl type)
        let (paramVars, paramTypes, retType, uncomplete) = self.validateLambdaPrototype (alias context, sym.getFunctionProtoDecl ());

        // Each lambda has a uniq index, to be identified for closure in addition to there name
        let index = context:.getCurrentClosingIndex ();
        let proto = LambdaValue::new (sym.getLoc (), LambdaType::new (sym.getLoc ()),
                                      frameName.toStr (sep-> "::"s8),
                                      paramVars,
                                      retType,
                                      sym.getValue (),
                                      isClosure,
                                      index);
        
        if (uncomplete) { // Missing some types
            proto
        } else { // Have all the type we can validate it as a function
            self.validateLambdaBody (alias context, frameName, proto, paramTypes)
        }
    }

    /**
     * Validate the prototype of a lambda expression
     * @params: 
     *   - context: the context of the validation
     *   - proto: the prototype of the lambda expression
     * @returns:
     *    - .0: the list of parameters of the lambda (ParamVar)
     *    - .1: the list of the types of the parameters (might be full of NoneType, if the lambda is not complete)
     *    - .2: the return type of the lambda (NoneType is possible even if the lambda is complete)
     *    - .3: true iif the lambda is complete and that the body can be validated
     * @throws: &ErrorMsg: if the lambda is malformed
     */
    fn validateLambdaPrototype (self, dmut context : &Validator, proto : &FunctionProtoDecl)-> ([&Value], [&Type], &Type, bool)
        throws &ErrorMsg
    {
        let dmut errors = Vec!{&ErrorMsg}::new ();
        let mut uncomplete = false;
        let dmut parameters = Vec!{&Value}::new ();
        let dmut types = Vec!{&Type}::new ();        

        // Enter a foreign because we don't want to pollute the current function scope (but this is only for the proto)
        context:.enterForeign (isFnBody-> false, clContext-> context:.getCurrentClassContext ()); 
        context:.enterBlock ();
        
        for param in proto.getParameters () {   
            {
                match param {
                    v : &VarDeclExpr => {
                        if (v.getType ().isOf!{&EmptyExpression} ()) { // If no type has been given in the definition of the lambda, then it is validated by calling it
                            uncomplete = true;
                            parameters:.push (ExprWrapperValue::new (v.getLoc (), v));
                            types:.push (NoneType::new (v.getLoc ()));
                        } else {
                            let (isRef, type) = self.validateTypeParam (alias context, v); // Otherwise we can already make some validation
                            
                            let paramGen = ProtoVarValue::new (v.getLoc (), type, NoneValue::new (), type.isMutable (), isSelf-> false, isReference-> isRef);
                            parameters:.push (paramGen);
                            types:.push (type);
                            
                            if (param.getLoc ().str () != Keys::UNDER) {
                                context:.verifyShadow (paramGen.getLoc ());
                                context:.insertLocal (paramGen.getLoc ().str (), paramGen);
                            }
                        }
                    }
                    _ => __pragma!panic (); // There is only VarDeclExpr in lambda prototypes
                }
            } catch {
                err : &ErrorMsg => errors:.push (err);
            }
        }
                
        let r : &Type = {        
            match proto.getRetType () {
                EmptyExpression () => {
                    cast!{&Type} (NoneType::new (proto.getLoc ())) // If no type is given as return type, it will take the type of the body (unlike normal function definition)
                }
                x : _ => { context:.validateType (x) }
            }
        } catch {
            err : &ErrorMsg => {
                errors:.push (err);
                cast!{&Type} (NONE_TYPE)
            }           
        };
        
        context:.quitBlock (warnUnused-> false);
        context:.exitForeign (); // Of course we exit the foreign a get back to the parent function definition

        if (errors.len () != 0us) {
            throw ErrorMsg::list (errors[]);
        }
        return (parameters [], types[], r, uncomplete);
    }


    /**
     * Validate the type composant of a VarDeclExpr assuming it is not an EmptyExpression
     * @params: 
     *   - context: the context of the validation
     *   - v: the var decl whose type we have to validate
     * @returns: the type of the vardecl with correct mutable level
     * @throws: 
     *  - &ErrorMsg: if the type is not valid
     */
    fn validateTypeParam (self, dmut context : &Validator, vdecl : &VarDeclExpr)-> (bool, &Type)
        throws &ErrorMsg
    {
        let (isRef, type) = {
            context:.validateType (vdecl.getType (), decos-> vdecl.getDecos (), canBeRef-> true)
        } catch {
            err : &ErrorMsg => throw ErrorMsg::note (vdecl.getLoc (), ValidateErrorMessage::VALIDATING, notes-> [err]);
        }                
        
        if (type.isMutable () && !type.needExplicitAlias () && !isRef) { // Verification that the value is mutable iif it is aliasable
            throw ErrorMsg::fatal (vdecl.getLoc (), ValidateErrorMessage::MUTABLE_CONST_PARAM);
        }

        match type { // Verification that the type of the var decl is usable
            VoidType () => throw ErrorMsg::fatal (vdecl.getLoc (), format (ValidateErrorMessage::DECLARE_VOID_VAR, type));
            NoneType () => throw ErrorMsg::fatal (vdecl.getLoc (), format (ValidateErrorMessage::DECLARE_VOID_VAR, type));
            ClassRefType () => throw ErrorMsg::fatal (vdecl.getType ().getLoc (), format (ValidateErrorMessage::INCOMPLETE_TYPE, type),
                                                      notes-> [ErrorMsg::note (vdecl.getType ().getLoc (), ValidateErrorMessage::FORGET_AND_CLASS, oneLine-> true)]);
            TemporaryType () => throw ErrorMsg::fatal (vdecl.getType ().getLoc (), format (ValidateErrorMessage::INCOMPLETE_TYPE, type));
        }
        
        (isRef, type)
    }
    

    /**
     * Validate the body of a lambda expression, assuming that the lambda is complete
     * @params: 
     *    - context: the context of the validation
     *    - proto: the prototype of the lambda to validate
     *    - paramTypes: the type of the parameters of the lambda
     * @throws: &ErrorMsg: if the lambda is malformed
     */
    pub fn validateLambdaBody (self, dmut context : &Validator, frameName : &Path, lmbd : &LambdaValue, paramTypes : [&Type])-> &Value
        throws &ErrorMsg        
    {
        context:.enterForeign (isLambda-> true, isFnBody-> true, clContext-> context:.getCurrentClassContext ()); // Enter a foreign because we don't want to pollute the current function scope
        context:.enterBlock ();
        {
            let mut error = ((&ErrorMsg)?)::__err__;
            let proto = {            
                // Validate the parameters with the correct types
                // insert them in context, and set the return type of the lambda
                let (references, types, params) = self.validateFinalParameters (alias context, lmbd, paramTypes);

                // Validate the body of the lambda
                let (body, closure, retType, needFinalReturn) = self.validateLambdaBody (alias context, generator::base::generateUniqID (), lmbd);
                
                let _ = (!body.isReturner () && !retType.isOf!{&VoidType} ());
                
                match closure {
                    cl : &ClosureType => {
                        let closureType = TupleType::new (cl.getLoc (), cl.getInnerTypes ());
                        let ptrType = PointerType::new (cl.getLoc (), closureType, isMutable-> false);
                        let fstParam : &Value = ProtoVarValue::new (cl.getLoc (), ptrType, NoneValue::new (), false, isSelf-> false, isReference-> false);

                        let frame = FrameGenerator::new (lmbd.getLoc (), frameName, parameters-> ([fstParam] ~ params), retType, body, needFinalReturn-> needFinalReturn, isWeak-> true);
                        context:.insertNewGenerator (frame);
                        
                        let closureValue = AddressValue::new (cl.getLoc (),
                                                              ptrType,
                                                              TupleValue::new (cl.getLoc (), closureType, cl.getInnerValues ()));

                        DelegateValue::new (lmbd.getLoc (), frameName, closure-> closureValue, references, types, retType)                        
                    }
                    _ => {
                        let frame = FrameGenerator::new (lmbd.getLoc (), frameName, parameters-> params, retType, body, needFinalReturn-> needFinalReturn, isWeak-> true);
                        context:.insertNewGenerator (frame);
                        
                        FuncPtrValue::new (lmbd.getLoc (), frameName, references, types, retType)
                    }
                }
            } catch {
                err : &ErrorMsg => {                
                    throw err;
                }
            } exit {
                {
                    context:.quitBlock (warnUnused-> match error { Ok () => { false }
                                                                   _ => { true }});
                } catch {
                    err : &ErrorMsg => {
                        error = err?;
                    }
                }
            }
            
            match error {
                Ok (msg : _) => throw msg;
            }
            
            proto
        } exit {        
            context:.exitForeign (); // Of course we exit the foreign a get back to the parent function definition
        }
    }     


    /**
     * Validate the uncomplete parameters of the lambda, with the given paramTypes
     * @warning: complete var decl must have the same types as the corresponding paramTypes
     * @info: update the context by inserting param var decl in the current scope and setting current return type of function
     * @assume: a new foreign is entered, and there is at least one scope to fill
     * @params: 
     *   - context: the context of the validation
     *   - proto: the proto lambda to validate
     *   - paramTypes: the types of the var decls 
     * @returns: the list of parameters
     * @throws: 
     *   - &ErrorMsg: if there is a incompatibility between paramTypes and proto
     */
    fn validateFinalParameters (self, dmut context : &Validator, proto : &LambdaValue, paramTypes : [&Type])-> ([bool], [&Type], [&Value])
        throws &ErrorMsg
    {
        if (proto.getParameters ().len != paramTypes.len) __pragma!panic ();
        
        let dmut parameters = Vec!{&Value}::new ();
        let dmut types = Vec!{&Type}::new ();
        let dmut references = Vec!{bool}::new ();
        let dmut errors = Vec!{&ErrorMsg}::new ();
        
        for i, param in proto.getParameters () {
            {
                match param {
                    v : &ProtoVarValue => {
                        context:.verifyCompatibleType (v.getLoc (), paramTypes[i].getLoc (), v.getType (), paramTypes[i]);                        
                        parameters:.push (v);
                        references:.push (v.isReference ());
                        types:.push (v.getType ());
                        
                        if (v.getLoc ().str () != Keys::UNDER) {
                            // Useless ? Is there a way for the var to avoid the shadowing verif made by the lambda validation ?
                            context:.verifyShadow (v.getLoc ()); 
                            context:.insertLocal (v.getLoc ().str (), v);
                        }                    
                    }
                    w : &ExprWrapperValue => { // prototype value is not finished, we need to validate it
                        match w.getContent () {
                            inner : &VarDeclExpr => {
                                let vdecl = VarDeclExpr::new (w.getLoc (), inner.getName (), inner.getValue (), TypeWrapperExpr::new (inner.getLoc (), paramTypes [i]), decos-> inner.getDecos ());
                                let (isRef, type) = self.validateTypeParam (alias context, vdecl); 
                                context:.verifyCompatibleType (vdecl.getLoc (), paramTypes[i].getLoc (), type, paramTypes[i]);
                                let paramGen = ProtoVarValue::new (inner.getLoc (), type, NoneValue::new (), type.isMutable (), isSelf-> false, isReference-> isRef);

                                parameters:.push (paramGen);
                                types:.push (type);
                                references:.push (isRef);
                                
                                if (param.getLoc ().str () != Keys::UNDER) {
                                    context:.verifyShadow (paramGen.getLoc ());
                                    context:.insertLocal (paramGen.getLoc ().str (), paramGen);
                                }
                            }
                            _ => __pragma!panic ();
                        }
                    }
                    _ => __pragma!panic ();
                }
            } catch {
                err : &ErrorMsg => errors:.push (err);
            }
        }

        if (errors.len () != 0us) throw ErrorMsg::list (errors[]);
        context:.setFunctionReturn (proto.getRetType ());


        (references[], types [], parameters[])
    }

    /**
     * Validate the body of a lambda value
     * @assume: a new foreign is entered, and there is at least one scope to fill
     * @params: 
     *   - context: the context of the validation
     *   - lmbd: the lambda to finish validating
     *   - params: the list of parameters of the lambda proto
     * @returns: 
     *   - .0: the body of the lambda
     *   - .1: the closure type
     *   - .2: the return type of the lambda
     * @throws:
     *   - &ErrorMsg: if the lambda was no valid
     */
    fn validateLambdaBody (self, dmut context : &Validator, refId : usize, lmbd : &LambdaValue)-> (&Value, &Type, &Type, bool)
        throws &ErrorMsg
    {
        if (lmbd.isClosure ()) {
            context:.enterClosure (lmbd.getLoc (), refId, lmbd.getClosureIndex ());
        }
        
        let mut body = {
            context:.validateValue (lmbd.getContent ())
        } catch {
            err : &ErrorMsg => {
                if (lmbd.isClosure ()) context:.exitClosure ();
                throw err;
            }
        }
        
        let mut retType = context:.getFunctionReturn ();
        
        let closure = if (lmbd.isClosure ()) {
            context:.exitClosure ()
        } else {
            cast!{&Type} (NoneType::new (lmbd.getLoc ()))
        }

        let mut need = false;
        if (!body.isReturner ()) { // If there is a path in the body that leads to an exit with no return
            match retType {
                NoneType () => { // There is no return in the body
                    retType = body.getType ();
                    need = true;
                }
                _ => { // There is a return, and we need to check the type mutability
                    body = context:.verifyCompatibleTypeWithValue (retType.getLoc (), retType, body);
                    context:.verifyMemoryOwner (retType.getLoc (), retType, body, construct-> true, byReference-> false);
                }
            }
        }

        if (body.getThrowers ().len != 0us) {
            let dmut list = Vec!{&ErrorMsg}::new ();
            for it in body.getThrowers () {
                list:.push (ErrorMsg::note (it._0, format ("%", it._1)));
            }

            throw ErrorMsg::fatal (lmbd.getLoc (), notes-> list[], ValidateErrorMessage::THROWS_IN_LAMBDA);
        }

        (body, closure, retType, need)
    }
    
}

