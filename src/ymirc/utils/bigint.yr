mod ymirc::utils::bigint;

import std::io;
import std::traits, std::conv;
import ymirc::utils::gmp;

pub class BigIntError over Exception {

    pub let msg : [c32];

    pub self (msg : [c32]) with msg = msg {}

    impl stream::Streamable {
        pub over toStream (self, dmut stream : &StringStream) {
            self::super.toStream (alias stream);
        }
    }
    
}

pub class @final BigInt {

    /// lib gmp content
    let dmut _content = mpz_t ();

    /**
     * ================================================================================
     * ================================================================================
     * =========================             CTOR             =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Private ctor that init a big from an already init content
     */
    prv self (content : mpz_t)
        with _content = content
    {}
    
    /**
     * Create a big int containing the value 0
     */
    pub self ()
        with self (0u64)
    {}    

    /**
     * Create a big int from an unsigned integer
     */
    pub self (x : u32)
        with self (cast!u64 (x))
    {}

    /**
     * Create a big int from an signed integer
     */
    pub self (x : i32)
        with self (cast!i64 (x))
    {}
    
    /**
     * Create a big int from an unsigned long
     */
    pub self (x : u64) {
        __gmpz_init (alias &self._content);
        __gmpz_set_ui (alias &self._content, x);
    }

    /**
     * Create a big int from an signed long
     */
    pub self (x : i64) {
        __gmpz_init (alias &self._content);
        __gmpz_set_si (alias &self._content, x);
    }

    /**
     * Create a big int from an unsigned long
     */
    pub self (x : usize) {
        __gmpz_init (alias &self._content);
        __gmpz_set_ui (alias &self._content, cast!u64 (x));
    }

    /**
     * Create a big int from an signed long
     */
    pub self (x : isize) {
        __gmpz_init (alias &self._content);
        __gmpz_set_si (alias &self._content, cast!i64 (x));
    }

    /**
     * Create a big int from a string
     */
    pub self (x : [c8], base : u32 = 10u32) {
        __gmpz_init (alias &self._content);
        __gmpz_set_str (alias &self._content, x.toStringZ (), base);
    }

    /**
     * @returns: the mpz_t content
     * */
    pub def getZ (self)-> mpz_t {
        self._content
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================             ADD              =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Add two bigint
     * @example:
     * ===========================
     * let a = BigInt::new (1230), b = BigInt::new (-230489);
     * println (a + b);
     * ===========================
     */
    pub def opBinary {"+"} (self, o : &BigInt)-> &BigInt {
        let dmut res = mpz_t ();
        __gmpz_init (alias &res);

        __gmpz_add (alias &res, &self._content, &o._content);

        BigInt::new (alias res)
    }

    /**
     * Add a bigint and a signed long
     * @example:
     * ===========================
     * let a = BigInt::new (1230);
     * println (a + 124i64);
     * ===========================
     */
    pub def opBinary {"+", T} (self, o : T)-> &BigInt {
        self + BigInt::new (o)
    }

    /**
     * Add a bigint and a signed long
     * @example:
     * ===========================
     * let a = BigInt::new (1230);
     * println (124i64 + a);
     * ===========================
     */
    pub def opBinaryRight {"+", T} (self, o : T)-> &BigInt {
        BigInt::new (o) + self
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================             SUB              =========================
     * ================================================================================
     * ================================================================================
     */
    
    /**
     * Subtract two bigint
     * @example: 
     * ===========================
     * let a = BigInt::new (1230), b = BigInt::new (-230489);
     * println (a - b);
     * ===========================
     */
    pub def opBinary {"-"} (self, o : &BigInt)-> &BigInt {
        let dmut res = mpz_t ();
        __gmpz_init (alias &res);

        __gmpz_sub (alias &res, &self._content, &o._content);
        
        BigInt::new (alias res)
    }

    /**
     * Subtract a bigint and an int
     * @example: 
     * ===========================
     * let a = BigInt::new (1230);
     * println (a - 124);
     * ===========================
     */
    pub def opBinary {"-", T} (self, o : T)-> &BigInt {
        self - BigInt::new (o)
    }

    /**
     * Subtract a bigint and an int
     * @example: 
     * ===========================
     * let a = BigInt::new (1230);
     * println (124 - a);
     * ===========================
     */
    pub def opBinaryRight {"-", T} (self, o : T)-> &BigInt {
        BigInt::new (o) - self
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================             MUL              =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Multiple two bigint
     * @example: 
     * ===========================
     * let a = BigInt::new (1230), b = BigInt::new (-230489);
     * println (a * b);
     * ===========================
     */
    pub def opBinary {"*"} (self, o : &BigInt)-> &BigInt {
        let dmut res = mpz_t ();
        __gmpz_init (alias &res);

        __gmpz_mul (alias &res, &self._content, &o._content);
        
        BigInt::new (alias res)
    }

    /**
     * Multiple a bigint and an int
     * @example: 
     * ===========================
     * let a = BigInt::new (1230);
     * println (a * 124);
     * ===========================
     */
    pub def opBinary {"*", T} (self, o : T)-> &BigInt {
        self * BigInt::new (o)
    }

    /**
     * Multiple a bigint and an int
     * @example: 
     * ===========================
     * let a = BigInt::new (1230);
     * println (124 * a);
     * ===========================
     */
    pub def opBinaryRight {"*", T} (self, o : T)-> &BigInt {
        BigInt::new (o) * self
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================            DIVIDE            =========================
     * ================================================================================
     * ================================================================================
     */
    
    /**
     * Divide two bigint
     * @example: 
     * ===========================
     * let a = BigInt::new (1230), b = BigInt::new (-230489);
     * println (a / b);
     * ===========================
     */
    pub def opBinary {"/"} (self, o : &BigInt)-> &BigInt
        throws &BigIntError
    {
        if (__gmpz_cmp_ui (&o._content, 0u64) == 0) {
            throw BigIntError::new ("Dividing by 0");
        }
        
        let dmut res = mpz_t ();
        __gmpz_init (alias &res);

        __gmpz_divexact (alias &res, &self._content, &o._content);
        
        BigInt::new (alias res)
    }

    /**
     * Multiple a bigint and an int
     * @example: 
     * ===========================
     * let a = BigInt::new (1230);
     * println (a / 124);
     * ===========================
     */
    pub def opBinary {"/", T} (self, o : T)-> &BigInt
        throws &BigIntError
    {
        self / BigInt::new (o)
    }

    /**
     * Multiple a bigint and an int
     * @example: 
     * ===========================
     * let a = BigInt::new (1230);
     * println (124 / a);
     * ===========================
     */
    pub def opBinaryRight {"/", T} (self, o : T)-> &BigInt
        throws &BigIntError
    {
        BigInt::new (o) / self
    }

    /**
     * Modulo two bigint
     * @example: 
     * ===========================
     * let a = BigInt::new (1230), b = BigInt::new (-230489);
     * println (a % b);
     * ===========================
     * @throws:
     *    - &BigIntError: if b is equal to 0
     */
    pub def opBinary {"%"} (self, o : &BigInt)-> &BigInt
        throws &BigIntError
    {
        if (__gmpz_cmp_ui (&o._content, 0u64) == 0) {
            throw BigIntError::new ("Dividing by 0");
        }
        
        let dmut res = mpz_t ();
        __gmpz_init (alias &res);

        __gmpz_mod (alias &res, &self._content, &o._content);
        
        BigInt::new (alias res)
    }

    /**
     * Multiple a bigint and an int
     * @example: 
     * ===========================
     * let a = BigInt::new (1230);
     * println (a % 124);
     * ===========================
     * @throws:
     *    - &BigIntError: if b is equal to 0
     */
    pub def opBinary {"%", T} (self, o : T)-> &BigInt
        throws &BigIntError
    {
        self % BigInt::new (o)
    }

    /**
     * Multiple a bigint and an int
     * @example: 
     * ===========================
     * let a = BigInt::new (1230);
     * println (124 % a);
     * ===========================
     * @throws:
     *    - &BigIntError: if b is equal to 0
     */
    pub def opBinaryRight {"%", T} (self, o : T)-> &BigInt
        throws &BigIntError
    {
        BigInt::new (o) % self
    }


    /**
     * Power two bigint
     * @example: 
     * ===========================
     * let a = BigInt::new (1230), b = BigInt::new (-230489);
     * println (a ^^ b);
     * ===========================
     * @throws:
     *    - &BigIntError: if o does not fit in a u64
     */
    pub def opBinary {"^^"} (self, o : &BigInt)-> &BigInt
        throws &BigIntError
    {                
        let dmut res = mpz_t ();
        __gmpz_init (alias &res);
        __gmpz_pow_ui (alias &res, &self._content, o.to!u64 ());
        
        BigInt::new (alias res)
    }

    /**
     * Power a bigint and an int
     * @example: 
     * ===========================
     * let a = BigInt::new (1230);
     * println (a ^^ 124);
     * ===========================
     * @throws:
     *    - &BigIntError: if o does not fit in a u64
     */
    pub def opBinary {"^^", T} (self, o : T)-> &BigInt
        throws &BigIntError
    {
        self ^^ BigInt::new (o)
    }

    /**
     * Power a bigint and an int
     * @example: 
     * ===========================
     * let a = BigInt::new (1230);
     * println (124 ^^ a);
     * ===========================
     * @throws:
     *    - &BigIntError: if o does not fit in a u64
     */
    pub def opBinaryRight {"^^", T} (self, o : T)-> &BigInt
        throws &BigIntError
    {
        BigInt::new (o) ^^ self
    }

    
    /**
     * ================================================================================
     * ================================================================================
     * =========================          COMPARISON          =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Compare two big ints
     */
    pub def opCmp (self, o : &BigInt)-> i32 {
        __gmpz_cmp (&self._content, &o._content)
    }

    /**
     * Compare a big int and an integer
     */
    pub def opCmp {T} (self, o : T)-> i32 {
        self.opCmp (BigInt::new (o))
    }    

    /**
     * ================================================================================
     * ================================================================================
     * =========================          CONVERTION          =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Convert a big int into a simple int
     * @example : 
     * ============================
     * let a = BigInt::new (u32::max) + 198;
     * let b = a.to!u64 ();
     * ============================
     * @throws:
     *     - &BigIntError: if the int does not fit in the result var (< T::min, or > T::max)
     */
    pub def to {T} (self)-> T
        throws &BigIntError
    {
        if (self < T::min) throw BigIntError::new ("Fail to convert");
        if (self > T::max) throw BigIntError::new ("Fail to convert");
        cte if (!isSigned!{T} ()) {
            let ui = __gmpz_get_ui (&self._content);
            cast!T (ui)
        } else {
            let si = __gmpz_get_si (&self._content);
            cast!T (si)
        }
    }
    

    
    /**
     * ================================================================================
     * ================================================================================
     * =========================             DTOR             =========================
     * ================================================================================
     * ================================================================================
     */

    __dtor (mut self) {
        __gmpz_clear (alias &self._content);    
    }

    /**
     * ================================================================================
     * ================================================================================
     * =========================            TRAITS            =========================
     * ================================================================================
     * ================================================================================
     */

    impl Streamable {

        pub over toStream (self, dmut stream : &StringStream) {
            let len = __gmp_snprintf (null, 0u32, "%Zd"s8.ptr, &self._content);
            let dmut res = ['\u{0}'c8 ; new len + 1us];

            __gmp_snprintf (alias res.ptr, len + 1u32, "%Zd"s8.ptr, &self._content);
            stream:.write (res[0us .. $ - 1us]);
        }        

    }

    
}
