mod ymirc::utils::format;

import std::collection::vec;
import std::collection::set;
import ymirc::utils::colors;
import std::io;


static mut __WITH_COLOR__ = true;

pub fn doNotFormatColor (b : bool) {
    __WITH_COLOR__ = !b;
}

pub trait Formattable {
    pub fn format (self, dmut stream : &Formatter);
}

pub trait ErrorFormattable {
    pub fn format (self, dmut stream : &Formatter, root : bool, depth : u32);
}


/**
 * A formatter is used to write content to a string in a more efficient manner than concatenation
 * It is close to a StringStream, but with special format methods
 * @example: 
 * =============
 * class X {
 *  // ...
 *  impl Formattable;
 * }
 *
 * fn foo () -> i32 {
 *    // ... 
 * }
 *
 * fn bar () -> [c8] {
 *   // ...
 * }
 * 
 * let dmut stream = Formatter::new ();
 * stream:.write ("Foo");
 * stream:.write (X::new ();
 * stream:.writef ("Foo %, Bar %", foo (), bar ()); // Formatting
 * =============
 */
pub class @final Formatter {

    /// The content of the formatter
    let dmut _content = Vec!(c8)::new ();

    /// The color currently used in the formatter
    let mut _currentColor = ([c8]?)::__err__;

    /// The level of entabing
    let dmut _entabing = Vec!([c8])::new ();

    /// If _willEntab, apply entabing
    let mut _willEntab = false;

    // The list of error ids that were already formatted by the formatter
    let dmut _errorIds = HashSet!{usize}::new ();

    // The list of value ids that were already formatter by the formatter
    let dmut _valueIds = HashSet!{usize}::new ();

    // True if the formatted should print variable ids
    let mut _withVarIds = false;

    // True if the formatter write the ids of the errors
    let mut _withErrorIds = true;

    // True if the formatter is used to format call parameters
    let mut _forParameters = false;

    // True if the formatter format verbose error, no matter the global state
    let mut _forceVerbose = false;

    // The list of custom format info (e.g. "withParams" for prototypes)
    let dmut _customFormatInfo = HashSet!{[c8]}::new ();

    /**
     * Create an empty stringstream
     */
    pub self () {}


    /*!
     * =============================================================================
     * =============================================================================
     * =========================          ENTABING        ==========================
     * =============================================================================
     * =============================================================================
     */

    
    pub fn entabing (mut self, txt : [c8] = "    "s8) {
        self._entabing:.push (txt);
    }

    pub fn detabing (mut self) {
        self._entabing:.pop ()?;
    }
    
    pub fn getEntabing (self)-> [[c8]] {
        self._entabing[]
    }

    /*!
     * =============================================================================
     * =============================================================================
     * ===========================          CUSTOM        ==========================
     * =============================================================================
     * =============================================================================
     */

    pub fn insertCustomFormatTag (mut self, name : [c8]) {
        self._customFormatInfo:.insert (name);
    }

    pub fn hasCustomFormatTag (self, name : [c8])-> bool {
        name in self._customFormatInfo
    }

    /*!
     * =============================================================================
     * =============================================================================
     * ============================          IDS        ============================
     * =============================================================================
     * =============================================================================
     */

    pub fn hasWrittenError (self, id : usize)-> bool {
        id in self._errorIds 
    }

    pub fn hasWrittenValue (self, id : usize)-> bool {
        id in self._valueIds
    }

    pub fn registerError (mut self, id : usize) {
        self._errorIds:.insert (id);
    }

    pub fn registerValue (mut self, id : usize) {
        self._valueIds:.insert (id);
    }

    pub fn withErrorIds (self)-> bool {
        self._withErrorIds
    }
    
    pub fn withVarIds (self)-> bool {
        self._withVarIds
    }

    pub fn forParams (mut self, b : bool) {
        self._forParameters = b;
    }

    pub fn forParams (self)-> bool {
        self._forParameters
    }

    pub fn withErrorIds (mut self, b : bool) {
        self._withErrorIds = b;
    }   

    pub fn withVarIds (mut self, b : bool) {
        self._withVarIds = b;
    }

    pub fn forceVerbose (mut self, b : bool) {
        self._forceVerbose = b;
    }

    pub fn forceVerbose (self)-> bool {
        self._forceVerbose
    }
    
    /*!
     * =============================================================================
     * =============================================================================
     * =========================          WRITTING        ==========================
     * =============================================================================
     * =============================================================================
     */
    
    /**
     * Write a c8 into the stream
     */
    pub fn write (mut self, c : c8) -> dmut &Formatter {
        if self._willEntab {
            for j in self._entabing {
                for z in j {
                    self._content:.push (z);
                }
            }
            self._willEntab = false;
        }
        
        self._content:.push (c);
        if (c == '\n'c8) {
            self._willEntab = true;
        }
        alias self
    }

    /**
     * Write a string in utf8 into the stream
     */
    pub fn write (mut self, c : [c8]) -> dmut &Formatter {
        for i in c {
            if self._willEntab {
                for j in self._entabing {
                    for z in j {
                        self._content:.push (z);
                    }
                }
                self._willEntab = false;
            }
            
            self._content:.push (i);
            if (i == '\n'c8) {
                self._willEntab = true;
            }
        };
        alias self
    }

    /**
     * Write a element in the stream
     * @info: the element must be convertible to a [c8]
     */
    pub fn if (!is!{T}{class U}) write {T} (mut self, c : T) -> dmut &Formatter {
        import std::conv;
        self:.write (c.to![c8] ());
        alias self
    }

    /**
     * Write a string into the stream
     */
    pub fn write (mut self, c : [c32]) -> dmut &Formatter {
        import std::conv;
        self:.write (c.to![c8] ());
        alias self
    }
    
    /**
     * Write an array of element into the stream
     */
    pub fn write {T of [U], U} (mut self, c : T) -> dmut &Formatter {
        let mut j = 0u64;
        match (self._currentColor, __WITH_COLOR__) {
            (Ok (x : _), true) => {
                self:.write (Colors::RESET, '{'c8, x);
            }
            _ => {
                self:.write ('{'c8);
            }
        }
        for i in c {
            if (j != 0u64) {
                match (self._currentColor, __WITH_COLOR__) {
                    (Ok (x : _), true) => {
                        self:.write (Colors::RESET, ", "s8, x);
                    }
                    _ => { self:.write (", "s8); }
                }
            }
            j += 1u64;
            self:.write (i);            
        }
        match (self._currentColor, __WITH_COLOR__) {
            (Ok (x : _), true) => {
                self:.write (Colors::RESET, '}'c8, x);
            }
            _ => {
                self:.write ('}'c8);
            }
        }
        alias self
    }

    /**
     * Write multiple element into the stream
     */
    pub fn write {F, T...} (mut self, f : F, r : T) -> dmut &Formatter {
        self:.write (f);
        cte if (is!T {Z of (U,), U...}) {
            for i in r
                self:.write (i);
        } else {
            self:.write (r);
        }
        alias self
    }

    /**
     * Write a tuple into the stream
     */
    pub fn write {T of (U,), U...} (mut self, a : T) -> dmut &Formatter {
        self:.write ('('c8);
        let mut j = 0;
        for i in a {
            if j != 0 {
                self:.write (','c8);
            }
            self:.write (i);
            j += 1;
        }        
        if (j == 1) self:.write (','c8);
        self:.write (')'c8);
        alias self
    }

    /**
     * Write an option type into the stream
     */
    pub fn write {T} (mut self, a : T?) -> dmut &Formatter {
        match a {
            Ok  (x:_)   => self:.write ("Ok("s8, x, ')'c8);
            Err (msg:_) => self:.write ("Err("s8, msg, ')'c8);
            Err ()      => self:.write ("Err()"s8);
        }
        alias self
    }

    /**
     * Write a struct into the stream
     */
    pub fn write {struct T} (mut self, a : T) -> dmut &Formatter {
        self:.write (T::typeid, '('c8);
        cte if (typeof (__pragma!tupleof (a))::arity > 0us) {
            let mut j = 0;
            for i in __pragma!tupleof (a) {
                if (j != 0) self:.write (", "s8);
                self:.write (i);
                j += 1;
            }
        }
        self:.write (')'c8);
        alias self
    }
    
    /**
     * Write an object that implement the trait formattable in the stream
     */
    pub fn write {class T} (mut self, c : T) -> dmut &Formatter {
        cte if (is!{T}{U impl Formattable}) {
            c.format (alias self);
        } else cte if (is!{T}{U impl ErrorFormattable}) {
            c.format (alias self, root-> true, depth-> 0u32);
        } else cte if (is!{T}{U impl Streamable}) {
            let dmut stream = StringStream::new ();
            c.toStream (alias stream);
            self:.write (stream[]);
        } else {
            self:.write (T::typeid);
        }
        alias self
    }
    
    /*!
     * ===============================================================================
     * ===============================================================================
     * =========================          FORMATTING        ==========================
     * ===============================================================================
     * ===============================================================================
     */

    /**
     * Write a format into the stream
     * @params: 
     *    - format: the format 
     *    - t: a list of elements that will be written at the proper place according to format
     * @example: 
     * =================
     * let dmut stream = Formatter::new ();
     * stream:.writef ("Format (f = %, j = %)", 1, 2);
     * assert (stream.str () == "Format (f = 1, j = 2)"s8);
     * =================
     */   
    pub fn writef {T...} (mut self, format : [c32], t : T) -> dmut &Formatter {
        import std::conv;
        self:.writef (format.to![c8](), expand t);
        alias self
    }
    
    /**
     * Write a format into the stream
     * @params: 
     *    - format: the format 
     *    - f: an element that will be written at the proper place according to format
     *    - args: a list of element that will be written at the proper place according to format
     * @example: 
     * =================
     * let dmut stream = Formatter::new ();
     * stream:.writef ("Format (f = %, j = %)"s8, 1, 2);
     * assert (stream.str () == "Format (f = 1, j = 2)"s8);
     * =================
     */   
    pub fn writef {F, T...} (mut self, format : [c8], f : F, args : T) -> dmut &Formatter {
        let mut i = 0us;
        while i < format.len {
            if (format [i] == '\\'c8 && i < format.len - 1us) {
                i += 1us;
            } else if (format [i] == '%'c8) {
                i += 1us;
                if (format.len > 2us && i < format.len - 2us && format [i] == '('c8) {
                    let col = match format [i + 1us] {
                        'r'c8 => { Colors::RED }
                        'b'c8 => { Colors::BLUE }
                        'y'c8 => { Colors::YELLOW }
                        'g'c8 => { Colors::GREEN }
                        'B'c8 => { Colors::BOLD }
                        _ => { Colors::RESET }
                    };
                    if (__WITH_COLOR__) {
                        self._currentColor = (col)?;
                        self:.write (col);
                    }
                    self:.write (f);
                    if (__WITH_COLOR__) {
                        self:.write (Colors::RESET);
                        self._currentColor = ([c8]?)::__err__;
                    }
                    self:.writef (format [i + 3us .. $], expand args);
                } else {
                    self:.write (f);
                    self:.writef (format [i .. $], expand args);
                }
                
                break{}
            } else if (format [i] == '@'c8) {
                i += 1us;
                let col = match format [i + 1us] {
                    'r'c8 => { Colors::RED }
                    'b'c8 => { Colors::BLUE }
                    'y'c8 => { Colors::YELLOW }
                    'g'c8 => { Colors::GREEN }
                    'B'c8 => { Colors::BOLD }
                    _ => { Colors::RESET }
                };

                i += 3us;
                if (__WITH_COLOR__) {
                    self._currentColor = (col)?;
                    self:.write (col);
                }
                while format [i] != ')'c8 {
                    self:.write (format [i]);
                    i += 1us;
                }

                i += 1us;
                if (__WITH_COLOR__) {
                    self:.write (Colors::RESET);
                    self._currentColor = ([c8]?)::__err__;
                }
            } else {
                self:.write (format [i]);
                i += 1us;
            }
        }
        alias self
    } 

    /**
     * Write a format into the stream
     * @params: 
     *    - format: the format 
     *    - f: an element that will be written at the proper place according to format
     * @example: 
     * =================
     * let dmut stream = Formatter::new ();
     * stream:.writef ("Format (f = %)"s8, 1);
     * assert (stream.str () == "Format (f = 1)"s8);
     * =================
     */   
    pub fn writef {F} (mut self, format : [c8], f : F) -> dmut &Formatter {
        let mut i = 0us;
        while i < format.len {
            if (format [i] == '\\'c8 && i < format.len - 1u64) {
                i += 1us;
            } else if (format [i] == '%'c8) {
                i += 1us;
                if (format.len > 2us && i < format.len - 2us && format [i] == '('c8) {
                    let col = match format [i + 1us] {
                        'r'c8 => { Colors::RED }
                        'b'c8 => { Colors::BLUE }
                        'y'c8 => { Colors::YELLOW }
                        'g'c8 => { Colors::GREEN }
                        'B'c8 => { Colors::BOLD }
                        _ => { Colors::RESET }
                    };
                    if (__WITH_COLOR__) {
                        self._currentColor = (col)?;
                        self:.write (col);
                    }
                    self:.write (f);
                    if (__WITH_COLOR__) {
                        self:.write (Colors::RESET);                        
                        self._currentColor = ([c8]?)::__err__;
                    }
                    i += 3us;
                } else {
                    self:.write (f);
                }                
            } else if (format [i] == '@'c8) {
                i += 1us; // skip (
                let col = match format [i + 1us] {
                    'r'c8 => { Colors::RED }
                    'b'c8 => { Colors::BLUE }
                    'y'c8 => { Colors::YELLOW }
                    'g'c8 => { Colors::GREEN }
                    'B'c8 => { Colors::BOLD }
                    _ => { Colors::RESET }
                };

                i += 3us; // skip y=
                if (__WITH_COLOR__) {
                    self._currentColor = (col)?;
                    self:.write (col);
                }
                while format [i] != ')'c8 {
                    self:.write (format [i]);
                    i += 1us;
                }

                i += 1us; // skip )
                if (__WITH_COLOR__) {
                    self:.write (Colors::RESET);
                    self._currentColor = ([c8]?)::__err__;
                }
            } else {
                self:.write (format [i]);
                i += 1us;
            }
        }
        alias self
    }

    /*!
     * ===============================================================================
     * ===============================================================================
     * =========================          GETTERS        =============================
     * ===============================================================================
     * ===============================================================================
     */
    
    /**
     * The content of the stream, in a [c8]
     */
    pub fn opIndex (self) -> [c8] {
        self._content []
    }

    /**
     * @returns: the writting buffer
     */
    pub fn getContent (mut self)-> dmut &Vec!{c8} {
        alias self._content
    }

    /**
     * @returns: the current writting color 
     */
    pub fn getCurrentColor (self)-> ([c8])? {
        self._currentColor
    }

    /**
     * @returns: the entabing buffer
     */
    pub fn getEntabing (mut self)-> dmut &Vec!{[c8]} {
        alias self._entabing
    }

    /**
     * @returns: true if the next write call will be entabed     
     */
    pub fn getWillEntab (self)-> bool {
        self._willEntab
    }

     /*!
     * ================================================================================
     * ================================================================================
     * =========================           SETTERS            =========================
     * ================================================================================
     * ================================================================================
     */

    /**
     * Change the writting buffer
     */
    pub fn setContent (mut self, dmut content : &Vec!{c8}) {
        self._content = alias content;
    }

    /**
     * Change the current writting color 
     */
    pub fn setCurrentColor (mut self, color : ([c8])?) {
        self._currentColor = color;
    }

    /**
     * Change the entabing buffer
     */
    pub fn setEntabing (mut self, dmut entabing : &Vec!{[c8]}) {
        self._entabing = alias entabing;
    }

    /**
     * @returns: true if the next write call will be entabed     
     */
    pub fn setWillEntab (mut self, will : bool) {
        self._willEntab = will;
    }
    
    
    impl Streamable {
        
        /**
         * Print the content of the stream to stdout
         */
        pub over toStream (self, dmut stream : &StringStream) {
            stream:.write (self._content []);
        }
    }
    
}

/**
 * Simple string formatting
 * @example: 
 * ================
 * assert (format ("Foo %"s8, 12) == "Foo 12"s8);
 * ================
 */
pub fn format {T...} (txt : [c8], withTags : [[c8]] = [], args : T) -> [c8] {
    let dmut buf = Formatter::new ();
    for i in withTags match i {
        "withVarIds"s8 => { buf:.withVarIds (true); }
        "withErrorIds"s8 => { buf:.withErrorIds (true); }
        "forParams"s8 => { buf:.forParams (true); }
        "verbose"s8 => { buf:.forceVerbose (true); }
        fl : _ => {
            buf:.insertCustomFormatTag (fl);
        }
    }

    buf:.writef (txt, expand args);
    buf[]
}


/**
 * Simple string formatting
 * @example:
 * ================
 * format ("Foo @(y=true)"s8);
 * ================
 */
pub fn format (txt : [c8], withTags : [[c8]] = []) -> [c8] {
    let dmut buf = Formatter::new ();
    for i in withTags match i {
        "withVarIds"s8 => { buf:.withVarIds (true); }
        "withErrorIds"s8 => { buf:.withErrorIds (true); }
        "forParams"s8 => { buf:.forParams (true); }
        "verbose"s8 => { buf:.forceVerbose (true); }
        fl : _ => {
            buf:.insertCustomFormatTag (fl);
        }
    }

    buf:.writef (txt, ""s8);
    buf[]
}


/**
 * Simple string formatting
 * @info: converted into a utf8 string
 * @example:
 * ================
 * assert (format ("Foo %", 12) == "Foo 12"s8);
 * ================
 */
pub fn format {T...} (txt : [c32], withTags : [[c8]] = [], args : T) -> [c8] {
    let dmut buf = Formatter::new ();
    for i in withTags match i {
        "withVarIds"s8 => { buf:.withVarIds (true); }
        "withErrorIds"s8 => { buf:.withErrorIds (true); }
        "forParams"s8 => { buf:.forParams (true); }
        "verbose"s8 => { buf:.forceVerbose (true); }
        fl : _ => {
            buf:.insertCustomFormatTag (fl);
        }
    }

    buf:.writef (txt, expand args);
    buf []
}

/**
 * Simple string formatting
 * @info: converted into a utf8 string
 * @example:
 * ================
 * format ("Foo @(y=true)");
 * ================
 */
pub fn format (txt : [c32], withTags : [[c8]] = []) -> [c8] {
    let dmut buf = Formatter::new ();
    for i in withTags match i {
        "withVarIds"s8 => { buf:.withVarIds (true); }
        "withErrorIds"s8 => { buf:.withErrorIds (true); }
        "forParams"s8 => { buf:.forParams (true); }
        "verbose"s8 => { buf:.forceVerbose (true); }
        fl : _ => {
            buf:.insertCustomFormatTag (fl);
        }
    }

    buf:.writef (txt, ""s8);
    buf []
}
